CREATE PROCEDURE [common].[USP_CMCODE_SEARCH] (
@SEARCH AS nvarchar(50) = '',
@PAGE_NUMBER AS int = 1,
@PAGE_SIZE AS int = 10,
@IS_ASCENDING AS bit = 1)
AS
  WITH RAW_DATA
  AS (SELECT
    [dbo].[CMCODE].[CMCODEID],
    [dbo].[CMCODE].[CODENUMBER],
    [dbo].[CMCODE].[CMCODECATEGORYID],
    [dbo].[CMCODECATEGORY].[NAME],
    MIN([dbo].[CMCODEREVISION].[CODETEXT]) AS [CODETEXT],
    MIN([dbo].[CMCODEREVISION].[REVISIONNUMBER]) AS [REVISIONNUMBER],
    [dbo].[CMCODE].[ROWVERSION],
    CASE @IS_ASCENDING
      WHEN 1 THEN ROW_NUMBER() OVER (ORDER BY [CODENUMBER])
      ELSE ROW_NUMBER() OVER (ORDER BY [CODENUMBER] DESC)
    END AS RowNumber,
    COUNT(1) OVER () AS TotalRows
  FROM [dbo].[CMCODE]
  INNER JOIN [dbo].[CMCODEREVISION]
    ON [dbo].[CMCODEREVISION].[CMCODEID] = [dbo].[CMCODE].[CMCODEID]
  LEFT JOIN [dbo].[CMCODECATEGORY]
    ON [dbo].[CMCODECATEGORY].[CMCODECATEGORYID] = [dbo].[CMCODE].[CMCODECATEGORYID]

  WHERE ([dbo].[CMCODE].[CODENUMBER] LIKE '%' + @SEARCH + '%'
  OR [dbo].[CMCODECATEGORY].[NAME] LIKE '%' + @SEARCH + '%'
  OR [dbo].[CMCODEREVISION].[CODETEXT] LIKE '%' + @SEARCH + '%'
  OR [dbo].[CMCODEREVISION].[REVISIONNUMBER] LIKE '%' + @SEARCH + '%'
  )
  AND [dbo].[CMCODEREVISION].[CURRENTREVISION] = 1
  GROUP BY [dbo].[CMCODE].[CMCODEID],
           [dbo].[CMCODE].[CODENUMBER],
           [dbo].[CMCODE].[CMCODECATEGORYID],
           [dbo].[CMCODECATEGORY].[NAME],
           [dbo].[CMCODE].[ROWVERSION])

  SELECT
    *
  FROM RAW_DATA
  WHERE RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1)
  AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
  ORDER BY RowNumber