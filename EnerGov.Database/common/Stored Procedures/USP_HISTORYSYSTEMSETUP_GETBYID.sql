CREATE PROCEDURE [common].[USP_HISTORYSYSTEMSETUP_GETBYID]
(	
	@PAGE_NUMBER AS INT	= 1,
	@PAGE_SIZE AS INT =	10,
	@IS_ASCENDING AS BIT =	1,
	@RECORDID AS CHAR(36),
	@SORTBYCOLUMNNAME AS NVARCHAR(50) = 'CHANGEDON',
	@TIMEZONE_DIFFERENCE_IN_MINUTES AS FLOAT = 0,
	@CHANGEDONLIST AS [dbo].[HISTORYSYSTEMSETUP_COLUMNFILTERS] READONLY,
	@CHANGEDBYLIST AS [dbo].[RecordIDs] READONLY,
	@FIELDNAMELIST AS [dbo].[LONG_NAMES] READONLY
)
AS
WITH RAW_DATA AS (
	SELECT
		[dbo].[HISTORYSYSTEMSETUP].[ID],
		DATEADD(MINUTE, @TIMEZONE_DIFFERENCE_IN_MINUTES, [dbo].[HISTORYSYSTEMSETUP].[CHANGEDON]) AS CHANGEDON,
		[dbo].[HISTORYSYSTEMSETUP].[CHANGEDBY],
		[dbo].[HISTORYSYSTEMSETUP].[FIELDNAME],
		[dbo].[HISTORYSYSTEMSETUP].[OLDVALUE],
		[dbo].[HISTORYSYSTEMSETUP].[NEWVALUE],
		[dbo].[HISTORYSYSTEMSETUP].[ADDITIONALINFO],
		[dbo].[USERS].[LNAME] + COALESCE(', ' + [dbo].[USERS].[FNAME], '') AS USERNAME,
		CASE @IS_ASCENDING WHEN 1 THEN
			CASE WHEN @SORTBYCOLUMNNAME = 'CHANGEDON' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[HISTORYSYSTEMSETUP].[CHANGEDON] ASC)
				ELSE ROW_NUMBER() OVER
				 (
					ORDER BY
						CASE
							WHEN @SORTBYCOLUMNNAME = 'CHANGEDBY' THEN ([dbo].[USERS].[LNAME] + COALESCE(', ' + [dbo].[USERS].[FNAME], ''))							
							WHEN @SORTBYCOLUMNNAME = 'FIELDNAME' THEN [dbo].[HISTORYSYSTEMSETUP].[FIELDNAME]
							WHEN @SORTBYCOLUMNNAME = 'OLDVALUE' THEN [dbo].[HISTORYSYSTEMSETUP].[OLDVALUE]
							WHEN @SORTBYCOLUMNNAME = 'NEWVALUE' THEN [dbo].[HISTORYSYSTEMSETUP].[NEWVALUE]
						END
					ASC,
					[dbo].[HISTORYSYSTEMSETUP].[CHANGEDON] ASC
				)
			END
		ELSE
			CASE WHEN @SORTBYCOLUMNNAME = 'CHANGEDON' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[HISTORYSYSTEMSETUP].[CHANGEDON] DESC)
				 ELSE ROW_NUMBER() OVER
				 (
					ORDER BY
						CASE
							WHEN @SORTBYCOLUMNNAME = 'CHANGEDBY' THEN ([dbo].[USERS].[LNAME] + COALESCE(', ' + [dbo].[USERS].[FNAME], ''))							
							WHEN @SORTBYCOLUMNNAME = 'FIELDNAME' THEN [dbo].[HISTORYSYSTEMSETUP].[FIELDNAME]
							WHEN @SORTBYCOLUMNNAME = 'OLDVALUE' THEN [dbo].[HISTORYSYSTEMSETUP].[OLDVALUE]
							WHEN @SORTBYCOLUMNNAME = 'NEWVALUE' THEN [dbo].[HISTORYSYSTEMSETUP].[NEWVALUE]							
						END
					DESC,
					[dbo].[HISTORYSYSTEMSETUP].[CHANGEDON] DESC
				)
			END
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[HISTORYSYSTEMSETUP]
	INNER JOIN [dbo].[USERS] ON [dbo].[USERS].[SUSERGUID] = [dbo].[HISTORYSYSTEMSETUP].[CHANGEDBY]
	WHERE
		[dbo].[HISTORYSYSTEMSETUP].[ID] = @RECORDID
		AND ((SELECT COUNT(1) FROM @CHANGEDBYLIST) = 0  OR [dbo].[HISTORYSYSTEMSETUP].[CHANGEDBY] IN (SELECT [RECORDID] FROM @CHANGEDBYLIST))
		AND ((SELECT COUNT(1) FROM @FIELDNAMELIST) = 0  OR [dbo].[HISTORYSYSTEMSETUP].[FIELDNAME] IN (SELECT [NAME] FROM @FIELDNAMELIST))
		AND ((SELECT COUNT(1) FROM @CHANGEDONLIST) = 0  OR CONVERT(NVARCHAR(MAX), DATEADD(MINUTE, @TIMEZONE_DIFFERENCE_IN_MINUTES, [dbo].[HISTORYSYSTEMSETUP].[CHANGEDON]), 101) IN (SELECT [FILTERVALUES] FROM @CHANGEDONLIST))
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber