CREATE PROCEDURE [globalsearch].[USP_ZONE_SEARCH_SELECT_LOOKUP]
(
  @SEARCH AS NVARCHAR(MAX) = '',
  @PAGE_NUMBER AS INT = 1,
  @PAGE_SIZE AS INT = 10,
  @IS_ASCENDING AS BIT = 1,
  @ZONE_SORT_BY VARCHAR(50) -- Expected Parameters:ZONECODE, DESCRIPTION
)
AS
BEGIN
SET NOCOUNT ON;
WITH RAW_DATA AS(
SELECT
   [ZONE].[ZONEID],
   [ZONE].[NAME],
   [ZONE].[DESCRIPTION],
   [ZONE].[ZONECODE],
   CASE WHEN @IS_ASCENDING = 1 THEN
            (CASE WHEN @ZONE_SORT_BY = 'ZONECODE' THEN  ROW_NUMBER() OVER(ORDER BY [ZONE].[ZONECODE])
                  WHEN  @ZONE_SORT_BY = 'DESCRIPTION' THEN ROW_NUMBER() OVER(ORDER BY [ZONE].[DESCRIPTION])
                  ELSE  ROW_NUMBER() OVER(ORDER BY [ZONE].[NAME]) END)
         ELSE
            (CASE WHEN @ZONE_SORT_BY = 'ZONECODE' THEN  ROW_NUMBER() OVER(ORDER BY [ZONE].[ZONECODE] DESC)
                  WHEN  @ZONE_SORT_BY = 'DESCRIPTION' THEN ROW_NUMBER() OVER(ORDER BY [ZONE].[DESCRIPTION] DESC)
                  ELSE  ROW_NUMBER() OVER(ORDER BY [ZONE].[NAME] DESC) END)    
       END AS RowNumber,
   COUNT(1) OVER() AS TotalRows
FROM [ZONE] WITH (NOLOCK)
WHERE  [ZONE].[ZONECODE] like '%' + @SEARCH + '%'
   OR [ZONE].[DESCRIPTION] like '%' + @SEARCH + '%'
   OR [ZONE].[NAME]  like '%' + @SEARCH + '%'
)
SELECT *
FROM RAW_DATA
WHERE
  RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND
  RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY
  RowNumber
END