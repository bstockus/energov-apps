CREATE PROCEDURE [globalsearch].[USP_USERS_GETBY_CRITERIA_SELECT_LOOKUP]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@APPLICATIONID INT=1,-- By Default => EnerGov |  EnerGov = 1, CitizenAccess = 2
	@TYPEID INT=2, -- By Default => Service | User = 1, Service = 2
	@IMINSPECTIONTYPEID CHAR(36) = NUll
)
AS
BEGIN
SET NOCOUNT ON;

IF @IMINSPECTIONTYPEID IS NULL OR  @IMINSPECTIONTYPEID =''
	BEGIN
		WITH RAW_DATA AS(
			SELECT	
				[USERS].[SUSERGUID],
          		COALESCE([dbo].[USERS].[FNAME]+ ' ', '' ) + [dbo].[USERS].[LNAME] AS [NAME],
				[USERS].[FNAME],
				[USERS].[LNAME],
				ROW_NUMBER() OVER(ORDER BY [USERS].[FNAME]) AS RowNumber,
				COUNT(1) OVER() AS [TOTALROWS]
			FROM [dbo].[USERS] WITH (NOLOCK)
				INNER JOIN [dbo].[APPLICATIONALLOWED] WITH (NOLOCK) ON [USERS].[SUSERGUID]=[APPLICATIONALLOWED].[USERID]
				INNER JOIN [dbo].[APPLICATION] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATION].[APPLICATIONID]
				INNER JOIN [dbo].[APPLICATIONUSERTYPE] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATIONUSERTYPE].[APPLICATIONID]	
			WHERE
				 [USERS].[BACTIVE]=1 
				 AND  [APPLICATION].[APPLICATIONID]=@APPLICATIONID 
				 AND [APPLICATIONUSERTYPE].[APPLICATIONID]=@APPLICATIONID 
				 AND [APPLICATIONUSERTYPE].[TYPEID]!=@TYPEID 		
				 AND ([USERS].[FNAME] LIKE '%'+@SEARCH+'%' 
					OR [USERS].[LNAME] LIKE '%'+@SEARCH+'%' 
					OR [USERS].[FNAME]+' '+[USERS].[LNAME] LIKE '%'+@SEARCH+'%')	
		)
		SELECT [RAW_DATA].[SUSERGUID],
				[RAW_DATA].[NAME],
				[RAW_DATA].[FNAME],
				[RAW_DATA].[LNAME], 
				[RAW_DATA].[TOTALROWS]
		FROM RAW_DATA
		WHERE
			RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
			RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
		ORDER BY 
			RowNumber
	END
ELSE
	BEGIN
		;WITH USERS_CTE([SUSERGUID],[NAME],[FNAME],[LNAME]) AS
			(
		SELECT	
			[USERS].[SUSERGUID],
			[USERS].[FNAME] +' '+[USERS].[LNAME] AS [NAME],
			[USERS].[FNAME],
			[USERS].[LNAME]
		FROM [dbo].[USERS] WITH (NOLOCK)
			INNER JOIN [dbo].[APPLICATIONALLOWED] WITH (NOLOCK) ON [USERS].[SUSERGUID]=[APPLICATIONALLOWED].[USERID]
			INNER JOIN [dbo].[APPLICATION] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATION].[APPLICATIONID]
			INNER JOIN [dbo].[APPLICATIONUSERTYPE] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATIONUSERTYPE].[APPLICATIONID]	
		WHERE
			 [USERS].[BACTIVE]=1 
			 AND  [APPLICATION].[APPLICATIONID]=@APPLICATIONID 
			 AND [APPLICATIONUSERTYPE].[APPLICATIONID]=@APPLICATIONID 
			 AND [APPLICATIONUSERTYPE].[TYPEID]!=@TYPEID 		
			 AND ([USERS].[FNAME] LIKE '%'+@SEARCH+'%' 
				OR [USERS].[LNAME] LIKE '%'+@SEARCH+'%' 
				OR [USERS].[FNAME]+' '+[USERS].[LNAME] LIKE '%'+@SEARCH+'%')
				),
		RAW_DATA AS(SELECT  [USERS_CTE].[SUSERGUID], [USERS_CTE].[NAME], [USERS_CTE].[FNAME], [USERS_CTE].[LNAME],
				ROW_NUMBER() OVER(ORDER BY [USERS_CTE].[NAME]) AS RowNumber,
				COUNT(1) OVER() AS [TOTALROWS]
			FROM [USERS_CTE]  INNER JOIN [globalsearch].[UFN_GET_USERID_BY_IMINSPECTIONTYPEID](@IMINSPECTIONTYPEID) AS IMINSPECTIONTYPEUSER
			ON [USERS_CTE].[SUSERGUID] = [IMINSPECTIONTYPEUSER].[USERID]
		)
		SELECT [RAW_DATA].[SUSERGUID],
				[RAW_DATA].[NAME],
				[RAW_DATA].[FNAME],
				[RAW_DATA].[LNAME], 
				[RAW_DATA].[TOTALROWS]
		FROM RAW_DATA		
		WHERE
			RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
			RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
		ORDER BY 
			RowNumber
	END
END