CREATE PROCEDURE [globalsearch].[USP_BUSINESS_TYPE_SEARCH_SELECT_LOOKUP]
(
  @SEARCH AS NVARCHAR(MAX) = '',
  @PAGE_NUMBER AS INT = 1,
  @PAGE_SIZE AS INT = 10,
  @IS_ASCENDING AS BIT = 1,
  @BUSINESS_TYPE_SORT_BY VARCHAR(50) = ''
)
AS 
BEGIN
SET NOCOUNT ON;
WITH RAW_DATA AS(
SELECT 
		[BLEXTBUSINESSTYPE].[BLEXTBUSINESSTYPEID],
		[BLEXTBUSINESSTYPE].[CODENUMBER],
		[BLEXTBUSINESSTYPE].[NAME],
		[BLEXTBUSINESSCATEGORY].[NAME] AS CATEGORY_NAME,
		CASE WHEN @IS_ASCENDING = 1 THEN
            (CASE WHEN @BUSINESS_TYPE_SORT_BY = 'NAME' THEN  ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[NAME])
                  WHEN  @BUSINESS_TYPE_SORT_BY = 'CODENUMBER' THEN ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[CODENUMBER])
				  WHEN  @BUSINESS_TYPE_SORT_BY = 'CATEGORY_NAME' THEN ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSCATEGORY].[NAME])
                  ELSE  ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[NAME]) END)
         ELSE
            (CASE WHEN @BUSINESS_TYPE_SORT_BY = 'NAME' THEN  ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[NAME] DESC)
                  WHEN  @BUSINESS_TYPE_SORT_BY = 'CODENUMBER' THEN ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[CODENUMBER] DESC)
				  WHEN  @BUSINESS_TYPE_SORT_BY = 'CATEGORY_NAME' THEN ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSCATEGORY].[NAME] DESC)
                  ELSE  ROW_NUMBER() OVER(ORDER BY [BLEXTBUSINESSTYPE].[NAME] DESC) END)
       END AS RowNumber,
	COUNT(1) OVER() AS TotalRows
FROM
	[BLEXTBUSINESSTYPE] WITH (NOLOCK)
	INNER JOIN [BLEXTBUSINESSCATEGORY] WITH (NOLOCK) ON [BLEXTBUSINESSTYPE].[BLEXTBUSINESSCATEGORYID] = [BLEXTBUSINESSCATEGORY].[BLEXTBUSINESSCATEGORYID]
	WHERE [BLEXTBUSINESSTYPE].[CODENUMBER] like '%' + @SEARCH + '%'
	   OR [BLEXTBUSINESSTYPE].[NAME] like '%' + @SEARCH + '%'
	   OR [BLEXTBUSINESSCATEGORY].[NAME]  like '%' + @SEARCH + '%'
	)

SELECT *
FROM RAW_DATA
WHERE
  RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND
  RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY
  RowNumber
END