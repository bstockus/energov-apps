CREATE PROCEDURE [globalsearch].[USP_TEAM_MEMBER_WITH_DEPARTMENT_SEARCH_SELECT_LOOKUP]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@APPLICATIONID INT=1,-- By Default => EnerGov |  EnerGov = 1, CitizenAccess = 2 
	@TYPEID INT=2,-- By Default => Service | User = 1, Service = 2
	@IDS RECORDIDS READONLY -- DEPARTMENT IDS
)
AS
BEGIN
SET NOCOUNT ON;

WITH RAW_DATA AS(
SELECT DISTINCT -- Get multiple record becuse of department join 
	[USERS].[SUSERGUID],
	[USERS].[FNAME],
	[USERS].[MIDDLENAME],
	[USERS].[LNAME],
	[USERS].[TITLE],
	[USERS].[PHONE],
	[USERS].[EMAIL]
FROM 
	[dbo].[USERS] WITH (NOLOCK)
	INNER JOIN [dbo].[APPLICATIONALLOWED] WITH (NOLOCK) ON [USERS].[SUSERGUID]=[APPLICATIONALLOWED].[USERID]
	INNER JOIN [dbo].[APPLICATION] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATION].[APPLICATIONID]
	INNER JOIN [dbo].[APPLICATIONUSERTYPE] WITH (NOLOCK) ON [APPLICATIONALLOWED].[APPLICATIONID]=[APPLICATIONUSERTYPE].[APPLICATIONID]
	INNER JOIN [dbo].[USERDEPARTMENT] WITH (NOLOCK) ON [USERDEPARTMENT].[SUSERGUID] =[USERS].[SUSERGUID]
	INNER JOIN [dbo].[DEPARTMENT] WITH(NOLOCK) ON [DEPARTMENT].[DEPARTMENTID]=[USERDEPARTMENT].DEPARTMENTID
	WHERE 
	 [USERS].[BACTIVE]=1 
	 AND  [APPLICATION].[APPLICATIONID]=@APPLICATIONID 
	 AND [APPLICATIONUSERTYPE].[APPLICATIONID]=@APPLICATIONID 
	 AND [APPLICATIONUSERTYPE].[TYPEID]!=@TYPEID 
	 AND ([dbo].[USERS].[LNAME] LIKE '%'+ @SEARCH +'%'
		OR [dbo].[USERS].[FNAME] LIKE '%'+ @SEARCH +'%'
		OR [dbo].[USERS].[FNAME]+' '+[dbo].[USERS].[LNAME] LIKE '%'+ @SEARCH +'%'
		OR [dbo].[DEPARTMENT].[NAME] LIKE '%'+ @SEARCH +'%')
	AND (
			((SELECT COUNT(1) FROM  @IDS)=0) 
		 OR (((SELECT COUNT(1) FROM  @IDS)>0) AND  ([DEPARTMENT].[DEPARTMENTID] IN (SELECT [RECORDID] FROM @IDS)))		
		)
	)

	SELECT *,
		ROW_NUMBER() OVER(ORDER BY [LNAME],[FNAME]) RowNumber,
		COUNT(1) OVER() AS TotalRows
	INTO #RESULT_DATA 
		FROM RAW_DATA;

SELECT * FROM #RESULT_DATA
WHERE
		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
		RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
		RowNumber

DECLARE @DEPTIDS RECORDIDS
DECLARE @USERIDS RECORDIDS

IF ((SELECT COUNT(RECORDID) FROM @IDS) <= 0)
	INSERT INTO @DEPTIDS
	SELECT DEPARTMENTID FROM USERDEPARTMENT WHERE SUSERGUID IN (SELECT DISTINCT SUSERGUID FROM #RESULT_DATA)
ELSE
	INSERT INTO @DEPTIDS
	SELECT RECORDID FROM @IDS

INSERT INTO @USERIDS
SELECT DISTINCT SUSERGUID FROM #RESULT_DATA

EXEC [globalsearch].[USP_DEPARTMENT_GETBYDEPTIDS_GETBYUSERIDS] @USERIDS, @DEPTIDS
END