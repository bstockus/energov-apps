CREATE PROCEDURE [taxremittanceaccount].[USP_TAXBILLPERIOD_SEARCH]
(
	@REPORTPERIODID AS CHAR(36),
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS int = 1,
	@PAGE_SIZE AS int = 10
)
AS
BEGIN
;WITH RAW_DATA
  AS (
		SELECT 
			[dbo].[TXRPTPERIOD].[TXRPTPERIODID] AS REPORTPERIODID, 
			[dbo].[TXRPTPERIOD].[NAME] AS REPORTPERIODNAME, 
			[dbo].[TXBILLPERIOD].[BILLPERIODID] AS BILLPERIODID, 
			[dbo].[TXBILLPERIOD].[PERIODNAME] AS BILLPERIODNAME, 
			[dbo].[TXBILLPERIOD].[YEAR], 
			[dbo].[TXBILLPERIOD].[DUEDATE],
			ROW_NUMBER() OVER (ORDER BY [dbo].[TXBILLPERIOD].[PERIODNAME]) AS RowNumber,
			COUNT(1) OVER() AS TotalRows
		FROM [dbo].[TXRPTPERIOD]
		INNER JOIN [dbo].[TXBILLPERIOD] 
			ON [TXBILLPERIOD].[RPTPERIODREF] = [TXRPTPERIOD].[TXRPTPERIODID]
		WHERE [dbo].[TXRPTPERIOD].[TXRPTPERIODID] = @REPORTPERIODID
			AND ([TXRPTPERIOD].[NAME] LIKE '%'+ @SEARCH +'%' 
			  OR [TXBILLPERIOD].[PERIODNAME] LIKE '%'+ @SEARCH +'%'
			  OR [TXBILLPERIOD].[YEAR] LIKE '%'+ @SEARCH +'%'
			  OR CONVERT(VARCHAR(10), [TXBILLPERIOD].[DUEDATE], 101) LIKE '%'+ @SEARCH +'%')
	)
SELECT
	REPORTPERIODID,
	REPORTPERIODNAME,
	BILLPERIODID,
	BILLPERIODNAME,
	YEAR,
	DUEDATE,
	RowNumber,
	TotalRows
FROM RAW_DATA  
  WHERE RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1)
  AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
  ORDER BY RowNumber
END