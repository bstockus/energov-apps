CREATE PROCEDURE [communitydevelopmentsetup].[USP_PLPLANTYPE_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
WITH RAW_DATA AS (
	SELECT
		[dbo].[PLPLANTYPE].[PLPLANTYPEID],
		[dbo].[PLPLANTYPE].[PLANNAME],
		[dbo].[PLPLANTYPE].[PREFIX],
		[dbo].[PLPLANTYPE].[DAYSTOEXPIRE],
		[dbo].[PLPLANTYPE].[DAYSTOPLANAPPROVALEXPIRE],
		[dbo].[PLPLANSTATUS].NAME AS [DEFAULTSTATUS],
		[dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') AS USERNAME,
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [dbo].[PLPLANTYPE].[PLANNAME])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [dbo].[PLPLANTYPE].[PLANNAME] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[PLPLANTYPE]
	LEFT JOIN [dbo].[PLPLANSTATUS] ON [dbo].[PLPLANSTATUS].[PLPLANSTATUSID] = [dbo].[PLPLANTYPE].[DEFAULTSTATUS]
	LEFT JOIN [dbo].[USERS] ON [dbo].[USERS].[SUSERGUID] = [dbo].[PLPLANTYPE].[ASSIGNEDUSER]
	WHERE ([dbo].[PLPLANTYPE].[PLANNAME] LIKE '%'+ @SEARCH +'%')
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber