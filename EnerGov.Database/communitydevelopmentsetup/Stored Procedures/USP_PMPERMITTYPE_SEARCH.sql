CREATE PROCEDURE [communitydevelopmentsetup].[USP_PMPERMITTYPE_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
WITH RAW_DATA AS (
	SELECT
		[dbo].[PMPERMITTYPE].[PMPERMITTYPEID],
		[dbo].[PMPERMITTYPE].[NAME],
		[dbo].[PMPERMITTYPE].[PERMITPREFIX],
		[dbo].[PMPERMITTYPE].[DAYSUNTILEXPIRE],
		[dbo].[PMPERMITTYPE].[DAYSUNTILEXPIREFROMLASTINSPECT],
		[dbo].[PMPERMITSTATUS].NAME AS [DEFAULTSTATUS],
		[dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') AS USERNAME,
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [dbo].[PMPERMITTYPE].[NAME])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [dbo].[PMPERMITTYPE].[NAME] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[PMPERMITTYPE]
	LEFT JOIN [dbo].[PMPERMITSTATUS] ON [dbo].[PMPERMITSTATUS].[PMPERMITSTATUSID] = [dbo].[PMPERMITTYPE].[DEFAULTPMPERMITSTATUSID]
	LEFT JOIN [dbo].[USERS] ON [dbo].[USERS].[SUSERGUID] = [dbo].[PMPERMITTYPE].[ASSIGNEDUSER]
	WHERE ([dbo].[PMPERMITTYPE].[NAME] LIKE '%'+ @SEARCH +'%')
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber