CREATE PROCEDURE [systemsetup].[USP_EXTERNAL_USERS_GETALL_BY_CRITERIA]
(
  	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
  	@IS_APPROVED AS BIT = 0,
  	@EXTERNALUSERSORTBY VARCHAR(50) = ''
)
AS
SET NOCOUNT ON;
WITH RAW_DATA AS (
	SELECT 
		[dbo].[USERS].[SUSERGUID],
		[dbo].[USERS].[ID],
		[dbo].[USERS].[FNAME],
		[dbo].[USERS].[LNAME],
		[dbo].[USERS].[MIDDLENAME],
		[dbo].[USERS].[PHONE],
		[dbo].[USERS].[EMAIL],
		[dbo].[USERS].[BACTIVE],
		[dbo].[USERS].[GLOBALENTITYID],
		CASE WHEN [dbo].[GLOBALENTITY].[GLOBALENTITYNAME] != ''
				THEN [dbo].[GLOBALENTITY].[GLOBALENTITYNAME]
			  ELSE [dbo].[GLOBALENTITY].[LASTNAME] + COALESCE(', ' + [dbo].[GLOBALENTITY].[FIRSTNAME], '')
		END	AS GLOBALENTITYNAME,
		[dbo].[USERS].[MAILINGADDRESSID],
		[dbo].[USERS].[COMPANY],
		[dbo].[USERS].[CREATEDON],
		[dbo].[USERS].[LNAME] + COALESCE(', ' + [dbo].[USERS].[FNAME], '') AS [FULLNAME],
		[dbo].[APPLICATIONALLOWED].[APPROVED],
		[dbo].[USERS].[GLOBALPREFFERCOMMID],
		[dbo].[USERS].[LASTCHANGEDON],
		[dbo].[USERS].[LASTCHANGEDBY],
		[dbo].[USERS].[ROWVERSION],
		[dbo].[APPLICATIONALLOWED].[APPLICATIONALLOWEDID],
		[dbo].[USERS].[TITLE],
		[dbo].[MAILINGADDRESS].[MAILINGADDRESSID] AS MAILINGADDRESS,
		[dbo].[MAILINGADDRESS].[ADDRESSLINE1],
		[dbo].[MAILINGADDRESS].[ADDRESSLINE2],
		[dbo].[MAILINGADDRESS].[ADDRESSLINE3],
		[dbo].[MAILINGADDRESS].[CITY],
		[dbo].[MAILINGADDRESS].[STATE],
		[dbo].[MAILINGADDRESS].[POSTALCODE],
		[dbo].[MAILINGADDRESS].[COUNTRYTYPE],
		[dbo].[MAILINGADDRESS].[POSTDIRECTION],
		[dbo].[MAILINGADDRESS].[PREDIRECTION],
		[dbo].[MAILINGADDRESS].[ADDRESSID],
		[dbo].[MAILINGADDRESS].[ADDRESSTYPE],
		[dbo].[MAILINGADDRESS].[STREETTYPE],
		[dbo].[MAILINGADDRESS].[UNITORSUITE],
		[dbo].[MAILINGADDRESS].[PROVINCE],
		[dbo].[MAILINGADDRESS].[RURALROUTE],
		[dbo].[MAILINGADDRESS].[STATION],
		[dbo].[MAILINGADDRESS].[COMPSITE],
		[dbo].[MAILINGADDRESS].[POBOX],
		[dbo].[MAILINGADDRESS].[ATTN],
		[dbo].[GLOBALENTITY].[CONTACTID],
		CASE WHEN @IS_ASCENDING = 1 THEN
		        (CASE WHEN 	@EXTERNALUSERSORTBY = 'FNAME' THEN  ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[FNAME])
		              WHEN  @EXTERNALUSERSORTBY = 'LNAME' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME])
					  WHEN  @EXTERNALUSERSORTBY = 'EMAIL' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[EMAIL])
					  WHEN  @EXTERNALUSERSORTBY = 'COMPANY' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[COMPANY])
					  WHEN  @EXTERNALUSERSORTBY = 'TITLE' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[TITLE])
					  WHEN  @EXTERNALUSERSORTBY = 'CONTACTID' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[GLOBALENTITY].[CONTACTID])
		              ELSE  ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME])
				END)
		     ELSE
		        (CASE WHEN 	@EXTERNALUSERSORTBY = 'FNAME' THEN  ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[FNAME] DESC)
		              WHEN  @EXTERNALUSERSORTBY = 'LNAME' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME] DESC)
					  WHEN  @EXTERNALUSERSORTBY = 'EMAIL' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[EMAIL] DESC)
					  WHEN  @EXTERNALUSERSORTBY = 'COMPANY' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[COMPANY] DESC)
					  WHEN  @EXTERNALUSERSORTBY = 'TITLE' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[TITLE] DESC)
					  WHEN  @EXTERNALUSERSORTBY = 'CONTACTID' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[GLOBALENTITY].[CONTACTID] DESC)
		              ELSE  ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME] DESC)
				END)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[USERS]
		LEFT JOIN [APPLICATIONALLOWED] ON [APPLICATIONALLOWED].[USERID] = [USERS].[SUSERGUID]
		LEFT JOIN [GLOBALENTITY] ON [GLOBALENTITY].[GLOBALENTITYID] = [USERS].[GLOBALENTITYID]
		LEFT JOIN [MAILINGADDRESS] ON [MAILINGADDRESS].[MAILINGADDRESSID] = [USERS].[MAILINGADDRESSID]
	WHERE
		[dbo].[APPLICATIONALLOWED].[APPLICATIONID] = 2 AND [dbo].[USERS].[EMAIL] NOT LIKE '%@energovdeleteduser.com%'
		AND ( [dbo].[APPLICATIONALLOWED].[APPROVED] = 0 OR [dbo].[APPLICATIONALLOWED].[APPROVED] = @IS_APPROVED)
		AND ([dbo].[USERS].[COMPANY] like '%' + @SEARCH + '%'
			OR [dbo].[USERS].[FNAME] like '%' + @SEARCH + '%'
			OR [dbo].[USERS].[LNAME]  like '%' + @SEARCH + '%'
			OR [dbo].[USERS].[EMAIL] like '%' + @SEARCH + '%')
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber