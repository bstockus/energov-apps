CREATE PROCEDURE [project].[USP_PRPROJECT_GETBYIDS]
(
	@PRPROJECTLIST RecordIDs READONLY,
	@USERID AS CHAR(36) = NULL
)
AS
BEGIN
	SELECT	PRPROJECT.PRPROJECTID,
			PROJECTNAME = PRPROJECT.NAME,
			PRPROJECT.PROJECTNUMBER,
			PRPROJECT.EXPECTEDENDDATE,
			TYPENAME = PRPROJECTTYPE.NAME,
			STATUSNAME = PRPROJECTSTATUS.NAME,
			PARCELNUMBER =  ISNULL((SELECT TOP 1 PARCEL.PARCELNUMBER
							FROM PRPROJECTPARCEL
							INNER JOIN PARCEL ON PARCEL.PARCELID = PRPROJECTPARCEL.PARCELID
							WHERE PRPROJECTPARCEL.PRPROJECTID = PRPROJECT.PRPROJECTID
								  AND PRPROJECTPARCEL.MAIN = 1), ''),
			DISTRICT = DISTRICT.NAME,
			PARENTPROJECTNUMBER = PARENTPROJECT.PROJECTNUMBER,
			PARENTPROJECTID = PARENTPROJECT.PRPROJECTID,
			PRPROJECT.STARTDATE,
			PRPROJECT.COMPLETEDATE,
			PRPROJECT.DESCRIPTION,
			PRPROJECT.LASTCHANGEDON,
			PRPROJECT.ROWVERSION,
			NUMBEROFCONDITIONS = (SELECT COUNT(1) FROM PRPROJECTCONDITION WHERE PRPROJECTID = PRPROJECT.PRPROJECTID),
			[project].[UFN_GET_PROJECT_BALANCE_DUE](PRPROJECT.PRPROJECTID) AS BALANCEDUE
	FROM	PRPROJECT 
			JOIN PRPROJECTTYPE ON PRPROJECT.PRPROJECTTYPEID = PRPROJECTTYPE.PRPROJECTTYPEID
			JOIN DISTRICT ON PRPROJECT.DISTRICTID = DISTRICT.DISTRICTID
			JOIN PRPROJECTSTATUS ON PRPROJECT.PRPROJECTSTATUSID = PRPROJECTSTATUS.PRPROJECTSTATUSID
			JOIN @PRPROJECTLIST PRPROJECTLIST ON PRPROJECT.PRPROJECTID = PRPROJECTLIST.RECORDID
			LEFT JOIN RECENTHISTORYPROJECT ON RECENTHISTORYPROJECT.PROJECTID = PRPROJECT.PRPROJECTID AND RECENTHISTORYPROJECT.USERID = @USERID
			LEFT JOIN PRPROJECT AS PARENTPROJECT ON PARENTPROJECT.PRPROJECTID = PRPROJECT.PRPROJECTPARENTID
	ORDER	BY RECENTHISTORYPROJECT.LOGGEDDATETIME DESC

	EXEC [project].[USP_PRPROJECTADDRESS_GETBYIDS] @PRPROJECTLIST
	EXEC [project].[USP_PRPROJECTCONTACT_GETBYIDS] @PRPROJECTLIST

END