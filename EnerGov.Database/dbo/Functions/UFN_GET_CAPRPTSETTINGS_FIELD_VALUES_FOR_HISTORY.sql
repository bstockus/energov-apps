CREATE FUNCTION [dbo].[UFN_GET_CAPRPTSETTINGS_FIELD_VALUES_FOR_HISTORY]
(
	@MODULETYPEID CHAR(36), 
	@MODULECLASSID CHAR(36), 
	@CAPMODULESID INT, 
	@RPTREPORTID CHAR(36)
)

RETURNS @CAPRPTSETTINGSHISTORYVALUES TABLE
(
	MODULETYPENAME		NVARCHAR(250)	NOT NULL,
	WORKCLASSFIELDLABEL	NVARCHAR(50)	NOT NULL,
	WORKCLASSNAME		NVARCHAR(50)	NOT NULL,
	MODULENAME			NVARCHAR(100)	NOT NULL,
	REPORTFRIENDLYNAME	NVARCHAR(255)	NOT NULL
)

AS
BEGIN
	DECLARE	@MODULETYPENAME			NVARCHAR(250)
	DECLARE @WORKCLASSFIELDLABEL	NVARCHAR(50)	=	''
	DECLARE @WORKCLASSNAME			NVARCHAR(50)
	DECLARE @MODULENAME				NVARCHAR(100)	=	(SELECT [CAPMODULES].[NAME] FROM [CAPMODULES] WITH (NOLOCK) WHERE [CAPMODULES].[CAPMODULESID] = @CAPMODULESID)
	DECLARE @REPORTFRIENDLYNAME		NVARCHAR(255)
	DECLARE @FORMNAME				NVARCHAR(255)

	IF (@MODULENAME = 'Application')
	BEGIN
		SELECT	@MODULETYPENAME = [PLAPPLICATIONTYPE].[APPLICATIONTYPENAME]
		FROM	[PLAPPLICATIONTYPE] WITH (NOLOCK) 
		WHERE	[PLAPPLICATIONTYPE].[PLAPPLICATIONTYPEID] = @MODULETYPEID

		SET		@FORMNAME = 'Application Documents'
	END
	
	ELSE IF (@MODULENAME = 'Business')
	BEGIN
		SELECT	@MODULETYPENAME = [BLEXTCOMPANYTYPE].[NAME]
		FROM	[BLEXTCOMPANYTYPE] WITH (NOLOCK) 
		WHERE	[BLEXTCOMPANYTYPE].[BLEXTCOMPANYTYPEID] = @MODULETYPEID

		SET		@FORMNAME = 'Business License Documents'
	END
	
	ELSE IF (@MODULENAME = 'Cashier')
	BEGIN
		SELECT	@MODULETYPENAME = CASE	@MODULETYPEID  WHEN '1' THEN 'Receipt' ELSE 'Invoice' END

		SET		@FORMNAME = 'Transaction Documents'
	END
	
	ELSE IF (@MODULENAME = 'Code')
	BEGIN
		SELECT	@MODULETYPENAME = [CMCASETYPE].[NAME]
		FROM	[CMCASETYPE] WITH (NOLOCK) 
		WHERE	[CMCASETYPE].[CMCASETYPEID] = @MODULETYPEID

		SET		@FORMNAME = 'Code Documents'
	END

	ELSE IF (@MODULENAME = 'Inspection')
	BEGIN
		SELECT	@MODULETYPENAME = [IMINSPECTIONTYPE].[NAME]
		FROM	[IMINSPECTIONTYPE] WITH (NOLOCK) 
		WHERE	[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID] = @MODULETYPEID

		SET		@FORMNAME = 'Inspection Documents'
	END

	ELSE IF (@MODULENAME = 'License')
	BEGIN
		SELECT	@MODULETYPENAME = [BLLICENSETYPE].[NAME]
		FROM	[BLLICENSETYPE] WITH (NOLOCK) 
		WHERE	[BLLICENSETYPE].[BLLICENSETYPEID] = @MODULETYPEID

		SELECT	@WORKCLASSNAME = [BLLICENSECLASS].[NAME]
		FROM	[BLLICENSECLASS] WITH (NOLOCK)
		WHERE	[BLLICENSECLASS].[BLLICENSECLASSID] = @MODULECLASSID

		SET		@WORKCLASSFIELDLABEL = 'Classifications'
		SET		@FORMNAME = 'Business License Documents'
	END
	
	ELSE IF (@MODULENAME = 'Permit')
	BEGIN
		SELECT	@MODULETYPENAME = [PMPERMITTYPE].[NAME]
		FROM	[PMPERMITTYPE] WITH (NOLOCK) 
		WHERE	[PMPERMITTYPE].[PMPERMITTYPEID] = @MODULETYPEID

		SELECT	@WORKCLASSNAME = [PMPERMITWORKCLASS].[NAME]
		FROM	[PMPERMITWORKCLASS] WITH (NOLOCK)
		WHERE	[PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = @MODULECLASSID

		SET		@WORKCLASSFIELDLABEL = 'Work Class'
		SET		@FORMNAME = 'Permit Documents'
	END
	
	ELSE IF (@MODULENAME = 'Plan')
	BEGIN
		SELECT	@MODULETYPENAME = [PLPLANTYPE].[PLANNAME]
		FROM	[PLPLANTYPE] WITH (NOLOCK) 
		WHERE	[PLPLANTYPE].[PLPLANTYPEID] = @MODULETYPEID

		SELECT	@WORKCLASSNAME = [PLPLANWORKCLASS].[NAME]
		FROM	[PLPLANWORKCLASS] WITH (NOLOCK)
		WHERE	[PLPLANWORKCLASS].[PLPLANWORKCLASSID] = @MODULECLASSID

		SET		@WORKCLASSFIELDLABEL = 'Work Class'
		SET		@FORMNAME = 'Plan Documents'
	END
	
	ELSE IF (@MODULENAME = 'Prof License')
	BEGIN
		SELECT	@MODULETYPENAME = [ILLICENSETYPE].[NAME]
		FROM	[ILLICENSETYPE] WITH (NOLOCK) 
		WHERE	[ILLICENSETYPE].[ILLICENSETYPEID] = @MODULETYPEID

		SELECT	@WORKCLASSNAME = [ILLICENSECLASSIFICATION].[NAME]
		FROM	[ILLICENSECLASSIFICATION] WITH (NOLOCK)
		WHERE	[ILLICENSECLASSIFICATION].[ILLICENSECLASSIFICATIONID] = @MODULECLASSID

		SET		@WORKCLASSFIELDLABEL = 'Classifications'
		SET		@FORMNAME = 'Professional License Documents'
	END
	
	ELSE IF (@MODULENAME = 'Request')
	BEGIN
		SELECT	@MODULETYPENAME = [CITIZENREQUESTTYPE].[NAME]
		FROM	[CITIZENREQUESTTYPE] WITH (NOLOCK) 
		WHERE	[CITIZENREQUESTTYPE].[CITIZENREQUESTTYPEID] = @MODULETYPEID

		SET		@FORMNAME = 'Request Documents'
	END
	
	ELSE IF (@MODULENAME = 'TaxRemittance')
	BEGIN
		SELECT	@MODULETYPENAME = [TXREMITTANCETYPE].[NAME]
		FROM	[TXREMITTANCETYPE] WITH (NOLOCK) 
		WHERE	[TXREMITTANCETYPE].[TXREMITTANCETYPEID] = @MODULETYPEID
		
		SET		@FORMNAME = 'Tax Remittance Documents'
	END

	SELECT	@REPORTFRIENDLYNAME = [RPTREPORT].[FRIENDLYNAME]
	FROM	[RPTREPORT]
	JOIN	[RPTFORMTOBUSINESSOBJECTMAPPING] ON [RPTFORMTOBUSINESSOBJECTMAPPING].[RPTFRMTOBUSOBJMAPID] = [RPTREPORT].[RPTFRMTOBUSOBJMAPID]
	WHERE	[RPTFORMTOBUSINESSOBJECTMAPPING].[GENRICREPORT] = 0
			AND [RPTREPORT].[RPTREPORTID] = @RPTREPORTID
			AND ([RPTFORMTOBUSINESSOBJECTMAPPING].[FORMNAME] = @FORMNAME
				OR (@FORMNAME = 'Transaction Documents' AND [RPTFORMTOBUSINESSOBJECTMAPPING].[FORMNAME] = 'Invoice Documents'))

	SET @MODULETYPENAME		=	ISNULL(@MODULETYPENAME, '[none]')
	SET @WORKCLASSNAME		=	ISNULL(@WORKCLASSNAME, '[none]')
	SET @MODULENAME			=	ISNULL(@MODULENAME, '[none]')
	SET @REPORTFRIENDLYNAME	=	ISNULL(@REPORTFRIENDLYNAME, '[none]')
	

	INSERT INTO @CAPRPTSETTINGSHISTORYVALUES (MODULETYPENAME, WORKCLASSFIELDLABEL, WORKCLASSNAME, MODULENAME, REPORTFRIENDLYNAME)
	VALUES (@MODULETYPENAME, @WORKCLASSFIELDLABEL, @WORKCLASSNAME, @MODULENAME, @REPORTFRIENDLYNAME)
	
	RETURN
END