/*
Where condition for multi-jurisdiction (finance system) support.
Online payments don't have an office/jurisdiction attached to them (no physical office for the transaction). We have to look at fee to determine finance system.
For in-house payments, we look at the office to determine finance system. If no office for in-house payment, still include for the default finance system.
If somehow a batch isn't associated with a finance system at all (maybe old batches prior to upgrade?), everything will be included as it should be.
*/

CREATE PROCEDURE [dbo].[USP_INTEGRATION_GET_BATCH_FOR_GL]
	@BATCH_ID AS NVARCHAR(50),
	@IS_PREVIEW AS BIT,
	@ENABLE_MULTI_JURISDICTION AS BIT,
	@INCLUDE_REFUND AS BIT,
	@INCLUDE_NSF AS BIT,
	@INCLUDE_CATRANSACTIONID AS RecordIDs READONLY,
	@INCLUDE_CACOMPUTEDFEEID AS RecordIDs READONLY,
	@INCLUDE_RECEIPTNUMBER AS RecordNames READONLY
AS

BEGIN


DECLARE @HAVE_REJECTED_CATRANSACTIONID AS BIT = (SELECT CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END FROM @INCLUDE_CATRANSACTIONID);
DECLARE @HAVE_REJECTED_CACOMPUTEDFEEID AS BIT = (SELECT CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END FROM @INCLUDE_CACOMPUTEDFEEID);
DECLARE @HAVE_REJECTED_RECEIPTNUMBER AS BIT = (SELECT CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END FROM @INCLUDE_RECEIPTNUMBER);


DECLARE @Finance_Receipt AS TABLE (
	FINANCEBATCHID NVARCHAR(50) NOT NULL,
	BATCH_NUMBER NVARCHAR(50) NOT NULL,
	CATRANSACTIONID CHAR(36) NOT NULL,
	RECEIPTNUMBER NVARCHAR(50) NOT NULL,
	TRANSACTIONDATE DATE NULL,
	GLOBALENTITYNAME NVARCHAR(MAX) NULL,
	DEPOSITREFNBR NVARCHAR(50) NULL,
	GLOBALENTITYID CHAR(36) NULL,
	CATRANSACTIONTYPEID INT NULL,
	CAPAYMENTTYPEID INT NULL,
	PAYMENTAMOUNT MONEY NULL, 
	SUPPLEMENTALDATA NVARCHAR(2200) NULL, 
	PAYMENTNOTE NVARCHAR(MAX) NULL,
	ISONLINE BIT NULL,
	TYLERFINANCIALOFFICENAME NVARCHAR(100) NULL, 
	USERID NVARCHAR(255) NULL,
	VENDORNUMBER NVARCHAR(100) NULL,
	CAFINANCIALINTEGRATIONSETUPID CHAR(36) NULL,
	ISDEFAULT BIT NULL,
	CATRANSACTIONACCOUNT_ENTITYACCOUNTNUMBER NVARCHAR(500) NULL,
	ACCOUNTAMOUNT MONEY NULL,
	GLOBALENTITYACCOUNTTYPE_CHARGECODE NVARCHAR(100) NULL,
	OFFICE_FINANCIALINTEGRATIONSETUPID CHAR(36) NULL,
	WITHDRAWALACCOUNTNUMBER NVARCHAR(500) NULL, 
	BATCHSOURCE INT NULL,
	NSFDATE DATE NULL
);

INSERT INTO @Finance_Receipt
SELECT
CAINTEGRATIONBATCHPROCESS.FINANCEBATCHID, 
CAINTEGRATIONBATCHPROCESS.BATCH_NUMBER, 
CATRANSACTION.CATRANSACTIONID,
CATRANSACTION.RECEIPTNUMBER, 
CATRANSACTION.TRANSACTIONDATE, 
CATRANSACTION.GLOBALENTITYNAME, 
CATRANSACTION.DEPOSITREFNBR, 
CATRANSACTION.GLOBALENTITYID,
CATRANSACTION.CATRANSACTIONTYPEID,
CATRANSACTIONPAYMENT.CAPAYMENTTYPEID, 
CATRANSACTIONPAYMENT.PAYMENTAMOUNT,
ISNULL(CATRANSACTIONPAYMENT.SUPPLEMENTALDATA, '') SUPPLEMENTALDATA, 
ISNULL(CATRANSACTIONPAYMENT.PAYMENTNOTE, '') PAYMENTNOTE, 
CATRANSACTIONPAYMENT.ISONLINE,
ISNULL(OFFICE.TYLERFINANCIALOFFICENAME, '') TYLERFINANCIALOFFICENAME, 
USERS.ID USERID, 
CAFINANCIALINTEGRATIONSETUP.VENDORNUMBER,
CAFINANCIALINTEGRATIONSETUP.CAFINANCIALINTEGRATIONSETUPID,
CAFINANCIALINTEGRATIONSETUP.ISDEFAULT,
CATRANSACTIONACCOUNT.ENTITYACCOUNTNUMBER CATRANSACTIONACCOUNT_ENTITYACCOUNTNUMBER,
CATRANSACTIONACCOUNT.AMOUNT,
GLOBALENTITYACCOUNTTYPE.CHARGECODE,
JURISDICTION.CAFINANCIALINTEGRATIONSETUPID OFFICE_FINANCIALINTEGRATIONSETUPID,
CASE WHEN CATRANSACTION.CATRANSACTIONTYPEID In (3,6) THEN COALESCE(CATRANSACTIONACCOUNT.ENTITYACCOUNTNUMBER, '') ELSE N'' END AS WITHDRAWALACCOUNTNUMBER,
COALESCE((SELECT 2 FROM CAINTEGRATIONBATCHGLTYPE WHERE NAME = LEFT(CAINTEGRATIONBATCHPROCESS.BATCH_NUMBER,3) OR NAME = LEFT(CAINTEGRATIONBATCHPROCESS.BATCH_NUMBER,2)), 1) AS BATCHSOURCE,
COALESCE(CATRANSACTION.NSFDATE, CATRANSACTION.TRANSACTIONDATE) NSFDATE 
FROM CAINTEGRATIONBATCHPROCESS
JOIN CATRANSACTION On CAINTEGRATIONBATCHPROCESS.BATCH_NUMBER = CATRANSACTION.EXTERNALBATCHNBR
JOIN CATRANSACTIONPAYMENT On CATRANSACTION.CATRANSACTIONID = CATRANSACTIONPAYMENT.CATRANSACTIONID 
JOIN CATILLSESSION on CATILLSESSION.CATILLSESSIONID = CATRANSACTION.CATILLSESSIONID
JOIN USERS on USERS.SUSERGUID = CATILLSESSION.USERID
LEFT OUTER JOIN CAFINANCIALINTEGRATIONSETUP on CAFINANCIALINTEGRATIONSETUP.CAFINANCIALINTEGRATIONSETUPID = CAINTEGRATIONBATCHPROCESS.CAFINANCIALINTEGRATIONSETUPID
LEFT OUTER JOIN OFFICE ON CATRANSACTION.OFFICEID = OFFICE.OFFICEID 
LEFT OUTER JOIN JURISDICTION on JURISDICTION.JURISDICTIONID = OFFICE.JURISDICTIONID
LEFT OUTER JOIN CATRANSACTIONACCOUNT On CATRANSACTION.CATRANSACTIONID = CATRANSACTIONACCOUNT.CATRANSACTIONID 
LEFT OUTER JOIN GLOBALENTITYACCOUNT On CATRANSACTIONACCOUNT.ENTITYACCOUNTID = GLOBALENTITYACCOUNT.GLOBALENTITYACCOUNTID 
LEFT OUTER JOIN GLOBALENTITYACCOUNTTYPE On GLOBALENTITYACCOUNT.GLOBALENTITYACCOUNTTYPEID = GLOBALENTITYACCOUNTTYPE.GLOBALENTITYACCOUNTTYPEID 
WHERE ((@BATCH_ID != '' AND ((@IS_PREVIEW = 1 AND CAINTEGRATIONBATCHPROCESS.FINANCEBATCHID = @BATCH_ID) 
                          OR (@IS_PREVIEW = 0 AND CAINTEGRATIONBATCHPROCESS.FINANCEBATCHID = @BATCH_ID AND CAINTEGRATIONBATCHPROCESS.SUCCESS = 0)))
    OR (@BATCH_ID = '' AND CAINTEGRATIONBATCHPROCESS.SUCCESS = 0 AND CAINTEGRATIONBATCHPROCESS.FAILED_COUNT < 1))
AND ((@INCLUDE_REFUND = 1 AND CATRANSACTION.CATRANSACTIONTYPEID = 4 AND CATRANSACTION.CATRANSACTIONSTATUSID != 2)
  OR (@INCLUDE_REFUND = 0 AND CATRANSACTION.CATRANSACTIONTYPEID != 4))
AND ((@INCLUDE_NSF = 1)
  OR (@INCLUDE_NSF = 0 AND CATRANSACTION.CATRANSACTIONTYPEID != 5))
AND ((@HAVE_REJECTED_CATRANSACTIONID = 0)
  OR (@HAVE_REJECTED_CATRANSACTIONID = 1 AND EXISTS (SELECT 'X' FROM @INCLUDE_CATRANSACTIONID A WHERE CATRANSACTION.CATRANSACTIONID = A.RECORDID)))
AND ((@HAVE_REJECTED_RECEIPTNUMBER = 0)
  OR (@HAVE_REJECTED_RECEIPTNUMBER = 1 AND EXISTS (SELECT 'X' FROM @INCLUDE_RECEIPTNUMBER A WHERE CATRANSACTION.RECEIPTNUMBER = A.Name)))
;


DECLARE @Finance_Receipt_Fee AS TABLE (
	FINANCEBATCHID NVARCHAR(50) NOT NULL,
	BATCH_NUMBER NVARCHAR(50) NOT NULL,
	CATRANSACTIONID CHAR(36) NOT NULL,
	RECEIPTNUMBER NVARCHAR(50) NOT NULL,
	TRANSACTIONDATE DATE NULL,
	GLOBALENTITYNAME NVARCHAR(MAX) NULL,
	DEPOSITREFNBR NVARCHAR(50) NULL,
	GLOBALENTITYID CHAR(36) NULL,
	CATRANSACTIONTYPEID INT NULL,
	CAPAYMENTTYPEID INT NULL,
	PAYMENTAMOUNT MONEY NULL, 
	SUPPLEMENTALDATA NVARCHAR(2200) NULL, 
	PAYMENTNOTE NVARCHAR(MAX) NULL,
	ISONLINE BIT NULL,
	TYLERFINANCIALOFFICENAME NVARCHAR(100) NULL, 
	USERID NVARCHAR(255) NULL,
	VENDORNUMBER NVARCHAR(100) NULL,
	CAFINANCIALINTEGRATIONSETUPID CHAR(36) NULL,
	ISDEFAULT BIT NULL,
	CATRANSACTIONACCOUNT_ENTITYACCOUNTNUMBER NVARCHAR(500) NULL,
	ACCOUNTAMOUNT MONEY NULL,
	GLOBALENTITYACCOUNTTYPE_CHARGECODE NVARCHAR(100) NULL,
	OFFICE_FINANCIALINTEGRATIONSETUPID CHAR(36) NULL,
	WITHDRAWALACCOUNTNUMBER NVARCHAR(500) NULL, 
	BATCHSOURCE INT NULL,
	NSFDATE DATE NULL,

	FEE_FINANCIALINTEGRATIONSETUPID CHAR(36) NULL,
	INVOICENUMBER NVARCHAR(500) NULL, 
	CACOMPUTEDFEEID CHAR(36) NULL,
	EXPORTEDON DATE NULL,
	ENTITYID INT NULL,
	CHARGECODE NVARCHAR(100) NULL, 
	FEECHARGECODE NVARCHAR(100) NULL, 
	CHARGEDETAILS NVARCHAR(30) NULL,
	ACTIVITYTYPE NVARCHAR(50) NULL,
	PAIDAMOUNT MONEY NULL
);


INSERT INTO @Finance_Receipt_Fee
SELECT 
FINANCERECEIPT.*,
PRIMEFEE.CAFINANCIALINTEGRATIONSETUPID FEE_FINANCIALINTEGRATIONSETUPID,
ISNULL(PRIMEFEE.INVOICENUMBER,'') INVOICENUMBER, 
PRIMEFEE.CACOMPUTEDFEEID,
PRIMEFEE.EXPORTEDON,
PRIMEFEE.ENTITYID,
COALESCE(FINANCERECEIPT.GLOBALENTITYACCOUNTTYPE_CHARGECODE, PRIMEFEE.ARFEECODE, '') AS CHARGECODE, 
COALESCE(PRIMEFEE.ARFEECODE, '') AS FEECHARGECODE,
CASE 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 2 THEN N'EscrowAccount' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 7 THEN N'BondAccount' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 8 THEN N'BondAccount' 
    WHEN PRIMEFEE.ENTITYID = 1 THEN N'PermitNumber' 
    WHEN PRIMEFEE.ENTITYID = 2 THEN N'CodeCaseNumber' 
    WHEN PRIMEFEE.ENTITYID = 3 THEN N'Violation' 
    WHEN PRIMEFEE.ENTITYID = 4 THEN N'ApplicationNumber' 
    WHEN PRIMEFEE.ENTITYID = 5 THEN N'PlanNumber' 
    WHEN PRIMEFEE.ENTITYID = 6 THEN N'ProjectNumber'
    WHEN PRIMEFEE.ENTITYID = 7 THEN N'BusinessLicenseNumber' 
    WHEN PRIMEFEE.ENTITYID = 8 THEN N'ProfessionalLicenseNumber' 
    WHEN PRIMEFEE.ENTITYID = 9 THEN N'TaxRemittance' 
    WHEN PRIMEFEE.ENTITYID = 10 THEN N'RentalPropertyNumber' 
    WHEN PRIMEFEE.ENTITYID = 11 THEN N'LandlordLicenseNumber' 
    WHEN PRIMEFEE.ENTITYID = 12 THEN N'InspectionNumber' 
    WHEN PRIMEFEE.ENTITYID = 13 THEN N'ImpactCondition'
	WHEN PRIMEFEE.ENTITYID = 14 THEN N'Business'
    ELSE N'Miscellaneous' 
END AS CHARGEDETAILS,
CASE 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 1 AND PRIMEFEE.ISARPOSTING = 'False' THEN N'Payment' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 1 AND PRIMEFEE.ISARPOSTING = 'True' THEN N'BillPayment' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 2 THEN N'EscrowDeposit' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 3 THEN N'AccountWithdrawal' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 4 THEN N'Refund' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 5 THEN N'NSF' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 6 THEN N'Void' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 7 THEN N'BondDeposit' 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID = 8 THEN N'BondRelease' 
    ELSE N'' 
END AS ACTIVITYTYPE,
CASE 
    WHEN FINANCERECEIPT.CATRANSACTIONTYPEID In(2,7,8) THEN FINANCERECEIPT.ACCOUNTAMOUNT 
	WHEN PRIMEFEE.POSTINGAMOUNT IS NOT NULL AND PRIMEFEE.POSTINGAMOUNT <> 0 THEN PRIMEFEE.POSTINGAMOUNT 
    WHEN PRIMEFEE.PAIDAMOUNT IS NOT NULL AND PRIMEFEE.PAIDAMOUNT <> 0 THEN PRIMEFEE.PAIDAMOUNT 
    WHEN FINANCERECEIPT.ACCOUNTAMOUNT IS NOT NULL AND FINANCERECEIPT.ACCOUNTAMOUNT <> 0 THEN FINANCERECEIPT.ACCOUNTAMOUNT 
    ELSE FINANCERECEIPT.PAYMENTAMOUNT 
END AS PAIDAMOUNT
FROM @Finance_Receipt FINANCERECEIPT
LEFT OUTER JOIN (
	SELECT DISTINCT CACOMPUTEDFEE.CACOMPUTEDFEEID,
	CAFEE.NAME as FEENAME, CAFEE.ARFEECODE as ARFEECODE, 
	CAENTITY.CAENTITYID as ENTITYID, CAENTITY.NAME AS ENTITY,
	CATRANSACTIONFEE.CATRANSACTIONID, CATRANSACTIONFEE.PAIDAMOUNT,
	CAINVOICEFEE.CAINVOICEID, CAINVOICE.INVOICENUMBER,
	CATRANSACTIONFEEPOSTING.CATRANSACTIONGLPOSTINGID AS GLPOSTINGID, 
	CATRANSACTIONGLPOSTING.POSTINGAMOUNT, CATRANSACTIONGLPOSTING.EXPORTEDON, 
	JURISDICTION.CAFINANCIALINTEGRATIONSETUPID,
	CASE WHEN CAINVOICEFEEARPOSTING.CATRANSACTIONARPOSTINGID IS NULL THEN 'False' ELSE 'True' END AS ISARPOSTING
	FROM CAFEE
	JOIN CAFEETEMPLATEFEE ON CAFEE.CAFEEID = CAFEETEMPLATEFEE.CAFEEID 
	JOIN CAFEETEMPLATE ON CAFEETEMPLATEFEE.CAFEETEMPLATEID = CAFEETEMPLATE.CAFEETEMPLATEID 
	JOIN CAENTITY ON CAFEETEMPLATE.CAENTITYID = CAENTITY.CAENTITYID 
	JOIN CACOMPUTEDFEE ON CAFEETEMPLATEFEE.CAFEETEMPLATEFEEID = CACOMPUTEDFEE.CAFEETEMPLATEFEEID 
	JOIN CATRANSACTIONFEE ON CACOMPUTEDFEE.CACOMPUTEDFEEID = CATRANSACTIONFEE.CACOMPUTEDFEEID 
	LEFT OUTER JOIN CAINVOICEFEE ON CACOMPUTEDFEE.CACOMPUTEDFEEID = CAINVOICEFEE.CACOMPUTEDFEEID 
	LEFT OUTER JOIN CAINVOICE ON CAINVOICE.CAINVOICEID = CAINVOICEFEE.CAINVOICEID
	LEFT OUTER JOIN CATRANSACTIONFEEPOSTING ON CATRANSACTIONFEE.CATRANSACTIONFEEID = CATRANSACTIONFEEPOSTING.CATRANSACTIONFEEID
	LEFT OUTER JOIN CATRANSACTIONGLPOSTING ON CATRANSACTIONFEEPOSTING.CATRANSACTIONGLPOSTINGID = CATRANSACTIONGLPOSTING.CATRANSACTIONGLPOSTINGID
	LEFT OUTER JOIN CAINVOICEFEEARPOSTING ON CAINVOICEFEEARPOSTING.CAINVOICEFEEID = CAINVOICEFEE.CAINVOICEFEEID
	LEFT OUTER JOIN JURISDICTION ON JURISDICTION.JURISDICTIONID = CAFEE.JURISDICTIONID
	UNION ALL
	SELECT DISTINCT CAMISCFEE.CAMISCFEEID AS CACOMPUTEDFEEID,
	CAFEE.NAME AS FEENAME, CAFEE.ARFEECODE as ARFEECODE,
	99 AS ENTITYID, 'Misc' AS ENTITY,
	CATRANSACTIONMISCFEE.CATRANSACTIONID, CATRANSACTIONMISCFEE.PAIDAMOUNT,
	CAINVOICEMISCFEE.CAINVOICEID, CAINVOICE.INVOICENUMBER,
	CATRANSACTIONMISCFEEPOSTING.CATRANSACTIONGLPOSTINGID as GLPOSTINGID,
	CATRANSACTIONGLPOSTING.POSTINGAMOUNT, CATRANSACTIONGLPOSTING.EXPORTEDON,
	JURISDICTION.CAFINANCIALINTEGRATIONSETUPID,
	CASE WHEN CAINVMISCFEEARPOSTING.CATRANSACTIONARPOSTINGID IS NULL THEN 'False' ELSE 'True' END AS ISARPOSTING
	FROM CAMISCFEE 
	JOIN CAFEE ON CAMISCFEE.CAFEEID = CAFEE.CAFeeID 
	JOIN CATRANSACTIONMISCFEE ON CAMISCFEE.CAMISCFEEID = CATRANSACTIONMISCFEE.CAMISCFEEID 
	LEFT OUTER JOIN CAINVOICEMISCFEE ON CAMISCFEE.CAMISCFEEID = CAINVOICEMISCFEE.CAMISCFEEID
	LEFT OUTER JOIN CAINVOICE ON CAINVOICE.CAINVOICEID = CAINVOICEMISCFEE.CAINVOICEID
	LEFT OUTER JOIN CATRANSACTIONMISCFEEPOSTING ON CATRANSACTIONMISCFEE.CATRANSACTIONMISCFEEID = CATRANSACTIONMISCFEEPOSTING.CATRANSACTIONMISCFEEID 
	LEFT OUTER JOIN CATRANSACTIONGLPOSTING ON CATRANSACTIONMISCFEEPOSTING.CATRANSACTIONGLPOSTINGID = CATRANSACTIONGLPOSTING.CATRANSACTIONGLPOSTINGID
	LEFT OUTER JOIN CAINVMISCFEEARPOSTING ON CAINVMISCFEEARPOSTING.CAINVOICEMISCFEEID = CAINVOICEMISCFEE.CAINVOICEMISCFEEID
	LEFT OUTER JOIN JURISDICTION ON JURISDICTION.JURISDICTIONID = CAFEE.JURISDICTIONID
) PRIMEFEE ON FINANCERECEIPT.CATRANSACTIONID = PRIMEFEE.CATRANSACTIONID 
WHERE ((@ENABLE_MULTI_JURISDICTION = 0) 
    OR (@ENABLE_MULTI_JURISDICTION = 1 AND FINANCERECEIPT.CAFINANCIALINTEGRATIONSETUPID =
CASE
WHEN FINANCERECEIPT.ISONLINE = 0 AND FINANCERECEIPT.ISDEFAULT = 1 THEN COALESCE(FINANCERECEIPT.OFFICE_FINANCIALINTEGRATIONSETUPID ,FINANCERECEIPT.CAFINANCIALINTEGRATIONSETUPID)
WHEN FINANCERECEIPT.ISONLINE = 0 AND FINANCERECEIPT.ISDEFAULT = 0 THEN FINANCERECEIPT.OFFICE_FINANCIALINTEGRATIONSETUPID
WHEN FINANCERECEIPT.ISONLINE = 1 AND FINANCERECEIPT.ISDEFAULT = 1 THEN COALESCE(PRIMEFEE.CAFINANCIALINTEGRATIONSETUPID ,FINANCERECEIPT.CAFINANCIALINTEGRATIONSETUPID)
WHEN FINANCERECEIPT.ISONLINE = 1 AND FINANCERECEIPT.ISDEFAULT = 0 THEN PRIMEFEE.CAFINANCIALINTEGRATIONSETUPID
ELSE NULL
END ))
AND ((@HAVE_REJECTED_CACOMPUTEDFEEID = 0)
  OR (@HAVE_REJECTED_CACOMPUTEDFEEID = 1 AND EXISTS (SELECT 'X' FROM @INCLUDE_CACOMPUTEDFEEID A WHERE PRIMEFEE.CACOMPUTEDFEEID = A.RECORDID)))
;


SELECT TB.*, TB.CATRANSACTIONACCOUNT_ENTITYACCOUNTNUMBER ENTITYACCOUNTNUMBER
FROM @Finance_Receipt_Fee TB WHERE TB.CATRANSACTIONTYPEID IN (2,7,8)
UNION ALL
SELECT TB.*, N'Miscellaneous'
FROM @Finance_Receipt_Fee TB WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND (TB.ENTITYID IS NULL OR TB.ENTITYID NOT IN (1,2,3,4,5,6,7,8,9,10,11,12,13,14))
UNION ALL
SELECT TB.*, PMPERMIT.PERMITNUMBER
FROM @Finance_Receipt_Fee TB
JOIN PMPERMITFEE ON TB.CACOMPUTEDFEEID = PMPERMITFEE.CACOMPUTEDFEEID
JOIN PMPERMIT ON PMPERMITFEE.PMPERMITID = PMPERMIT.PMPERMITID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 1
UNION ALL
SELECT TB.*, CMCODECASE.CASENUMBER
FROM @Finance_Receipt_Fee TB 
JOIN CMCODECASEFEE ON TB.CACOMPUTEDFEEID = CMCODECASEFEE.CACOMPUTEDFEEID
JOIN CMCODECASE ON CMCODECASEFEE.CMCODECASEID = CMCODECASE.CMCODECASEID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 2
UNION ALL
SELECT TB.*, CMCODECASE.CASENUMBER
FROM @Finance_Receipt_Fee TB 
JOIN CMVIOLATIONFEE ON TB.CACOMPUTEDFEEID = CMVIOLATIONFEE.CACOMPUTEDFEEID
JOIN CMVIOLATION ON CMVIOLATIONFEE.CMVIOLATIONID = CMVIOLATION.CMVIOLATIONID
JOIN CMCODEWFSTEP ON CMVIOLATION.CMCODEWFSTEPID = CMCODEWFSTEP.CMCODEWFSTEPID
JOIN CMCODECASE ON CMCODEWFSTEP.CMCODECASEID = CMCODECASE.CMCODECASEID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 3
UNION ALL
SELECT TB.*, PLAPPLICATION.APPNUMBER
FROM @Finance_Receipt_Fee TB 
JOIN PLAPPLICATIONFEE ON TB.CACOMPUTEDFEEID = PLAPPLICATIONFEE.CACOMPUTEDFEEID
JOIN PLAPPLICATION ON PLAPPLICATIONFEE.PLAPPLICATIONID = PLAPPLICATION.PLAPPLICATIONID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 4
UNION ALL
SELECT TB.*, PLPLAN.PLANNUMBER
FROM @Finance_Receipt_Fee TB 
JOIN PLPLANFEE ON TB.CACOMPUTEDFEEID = PLPLANFEE.CACOMPUTEDFEEID
JOIN PLPLAN ON PLPLANFEE.PLPLANID = PLPLAN.PLPLANID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 5
UNION ALL
SELECT TB.*, PRPROJECT.PROJECTNUMBER
FROM @Finance_Receipt_Fee TB 
JOIN PRPROJECTFEE ON TB.CACOMPUTEDFEEID = PRPROJECTFEE.CACOMPUTEDFEEID
JOIN PRPROJECT ON PRPROJECTFEE.PRPROJECTID = PRPROJECT.PRPROJECTID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 6
UNION ALL
SELECT TB.*, BLLICENSE.LICENSENUMBER
FROM @Finance_Receipt_Fee TB 
JOIN BLLICENSEFEE ON TB.CACOMPUTEDFEEID = BLLICENSEFEE.CACOMPUTEDFEEID
JOIN BLLICENSE ON BLLICENSEFEE.BLLICENSEID = BLLICENSE.BLLICENSEID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 7
UNION ALL
SELECT TB.*, ILLICENSE.LICENSENUMBER 
FROM @Finance_Receipt_Fee TB 
JOIN ILLICENSEFEE ON TB.CACOMPUTEDFEEID = ILLICENSEFEE.CACOMPUTEDFEEID
JOIN ILLICENSE ON ILLICENSEFEE.ILLICENSEID = ILLICENSE.ILLICENSEID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 8
UNION ALL
SELECT TB.*, TXREMITTANCEACCOUNT.REMITTANCEACCOUNTNUMBER 
FROM @Finance_Receipt_Fee TB 
JOIN TXREMITTANCEFEE ON TB.CACOMPUTEDFEEID = TXREMITTANCEFEE.CACOMPUTEDFEEID
JOIN TXREMITTANCE ON TXREMITTANCEFEE.TXREMITTANCEID = TXREMITTANCE.TXREMITTANCEID
JOIN TXREMITTANCEACCOUNT ON TXREMITTANCE.TXREMITTANCEACCOUNTID = TXREMITTANCEACCOUNT.TXREMITTANCEACCOUNTID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 9
UNION ALL
SELECT TB.*, GLOBALENTITY.CONTACTID 
FROM @Finance_Receipt_Fee TB 
JOIN RPPROPERTYFEE ON TB.CACOMPUTEDFEEID = RPPROPERTYFEE.CACOMPUTEDFEEID
JOIN RPPROPERTY ON RPPROPERTYFEE.RPPROPERTYID = RPPROPERTY.RPPROPERTYID
JOIN RPLANDLORDLICENSE ON RPPROPERTY.RPLANDLORDLICENSEID = RPLANDLORDLICENSE.RPLANDLORDLICENSEID
JOIN GLOBALENTITY ON RPLANDLORDLICENSE.GLOBALENTITYID = GLOBALENTITY.GLOBALENTITYID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 10
UNION ALL
SELECT TB.*, GLOBALENTITY.CONTACTID 
FROM @Finance_Receipt_Fee TB 
JOIN RPLANDLORDLICENSEFEE ON TB.CACOMPUTEDFEEID = RPLANDLORDLICENSEFEE.CACOMPUTEDFEEID
JOIN RPLANDLORDLICENSE ON RPLANDLORDLICENSEFEE.RPLANDLORDLICENSEID = RPLANDLORDLICENSE.RPLANDLORDLICENSEID
JOIN GLOBALENTITY ON RPLANDLORDLICENSE.GLOBALENTITYID = GLOBALENTITY.GLOBALENTITYID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 11
UNION ALL
SELECT TB.*, IMINSPECTION.INSPECTIONNUMBER
FROM @Finance_Receipt_Fee TB 
JOIN IMINSPECTIONFEE ON TB.CACOMPUTEDFEEID = IMINSPECTIONFEE.CACOMPUTEDFEEID
JOIN IMINSPECTION ON IMINSPECTIONFEE.IMINSPECTIONID = IMINSPECTION.IMINSPECTIONID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 12
UNION ALL
SELECT TB.*, IPCONDITION.CONDITIONNUMBER 
FROM @Finance_Receipt_Fee TB 
JOIN IPCONDITIONFEE ON TB.CACOMPUTEDFEEID = IPCONDITIONFEE.CACOMPUTEDFEEID
JOIN IPCONDITION ON IPCONDITIONFEE.IPCONDITIONID = IPCONDITION.IPCONDITIONID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 13
UNION ALL
SELECT TB.*, BLGLOBALENTITYEXTENSION.REGISTRATIONID 
FROM @Finance_Receipt_Fee TB 
JOIN BLGLOBALENTITYEXTENSIONFEE ON TB.CACOMPUTEDFEEID = BLGLOBALENTITYEXTENSIONFEE.CACOMPUTEDFEEID
JOIN BLGLOBALENTITYEXTENSION  ON BLGLOBALENTITYEXTENSIONFEE.BLGLOBALENTITYEXTENSIONID = BLGLOBALENTITYEXTENSION.BLGLOBALENTITYEXTENSIONID
WHERE TB.CATRANSACTIONTYPEID NOT IN (2,7,8) AND TB.ENTITYID = 14

ORDER BY TB.FINANCEBATCHID, TB.RECEIPTNUMBER
;

END