
CREATE PROCEDURE rpt_TX_SR_Tax_Remittance_Report_Invoice_Aging
	--@STARTDATE AS DATETIME,
	--@ENDDATE AS DATETIME
AS

--SET @STARTDATE=DATEADD(DD,DATEDIFF(DD,0,@STARTDATE),0)
--SET @ENDDATE=DATEADD(SS,-1,DATEDIFF(DD,0,@ENDDATE) + 1) 

BEGIN

DECLARE @CURRENTDATE AS DATETIME = DATEADD(DD,DATEDIFF(DD,0,GETDATE()),0);

IF OBJECT_ID('tempdb.dbo.#FILTER') IS NOT NULL
	DROP TABLE #FILTER;
SELECT DISTINCT R.TXREMITTANCEID, R.TXREMITTANCEACCOUNTID
	, BP.PERIODNAME + ' ' + CAST(BP.[YEAR] AS VARCHAR(4)) PERIODNAME
	, BP.STARTDATE, BP.ENDDATE
INTO #FILTER
FROM TXREMITTANCE R
INNER JOIN TXBILLPERIOD BP ON R.TXBILLPERIODID = BP.BILLPERIODID
INNER JOIN TXREMITTANCEFEE RF ON R.TXREMITTANCEID = RF.TXREMITTANCEID
INNER JOIN CACOMPUTEDFEE CF ON RF.CACOMPUTEDFEEID = CF.CACOMPUTEDFEEID
	AND CF.CASTATUSID <> 10 -- DELETED
INNER JOIN CAINVOICEFEE CIF ON CF.CACOMPUTEDFEEID = CIF.CACOMPUTEDFEEID
INNER JOIN CAINVOICE I ON CIF.CAINVOICEID = I.CAINVOICEID
	AND I.CASTATUSID NOT IN (4, 10) --PAID IN FULL, DELETED
;CREATE INDEX TEMP1 ON #FILTER(TXREMITTANCEID)
 CREATE INDEX TEMP2 ON #FILTER(TXREMITTANCEACCOUNTID);
 
IF OBJECT_ID('tempdb.dbo.#INVOICESUMMARY') IS NOT NULL
	DROP TABLE #INVOICESUMMARY;
SELECT F.TXREMITTANCEID
	, I.CAINVOICEID, I.INVOICENUMBER, I.INVOICEDATE, I.INVOICEDUEDATE, I.CASTATUSID
	, DATEDIFF(DD,I.INVOICEDATE,@CURRENTDATE) INVOICEAGE
	, SUM(CF.COMPUTEDAMOUNT) TOTALAMOUNT
	, SUM(CF.AMOUNTPAIDTODATE) PAIDAMOUNT
INTO #INVOICESUMMARY
FROM #FILTER F
INNER JOIN TXREMITTANCEFEE RF ON F.TXREMITTANCEID = RF.TXREMITTANCEID
INNER JOIN CACOMPUTEDFEE CF ON RF.CACOMPUTEDFEEID = CF.CACOMPUTEDFEEID
	AND CF.CASTATUSID <> 10 -- DELETED
INNER JOIN CAINVOICEFEE CIF ON CF.CACOMPUTEDFEEID = CIF.CACOMPUTEDFEEID
INNER JOIN CAINVOICE I ON CIF.CAINVOICEID = I.CAINVOICEID
	AND I.CASTATUSID NOT IN (4, 10) --PAID IN FULL, DELETED
GROUP BY F.TXREMITTANCEID, F.PERIODNAME, I.CAINVOICEID, I.INVOICENUMBER, I.INVOICEDATE, I.INVOICEDUEDATE, DATEDIFF(DD,I.INVOICEDATE,@CURRENTDATE), I.CASTATUSID
;CREATE INDEX TEMP1 ON #INVOICESUMMARY(TXREMITTANCEID);

IF OBJECT_ID('tempdb.dbo.#INVOICEDETAIL') IS NOT NULL
	DROP TABLE #INVOICEDETAIL;
SELECT PVT.TXREMITTANCEID, PVT.CAINVOICEID, PVT.INVOICENUMBER, PVT.INVOICEDATE, PVT.INVOICEDUEDATE
	, COALESCE(PVT.LT31,$0) LT31, COALESCE(PVT.LT61,$0) LT61, COALESCE(PVT.LT91,$0) LT91, COALESCE(PVT.LT121,$0) LT121, COALESCE(PVT.GT120,$0) GT120
INTO #INVOICEDETAIL
FROM (
	SELECT INS.TXREMITTANCEID, INS.CAINVOICEID, INS.INVOICENUMBER, INS.INVOICEDATE, INS.INVOICEDUEDATE, INS.TOTALAMOUNT-INS.PAIDAMOUNT AMTDUE
		, CASE WHEN INS.INVOICEAGE < 31 THEN 'LT31'
		   WHEN INS.INVOICEAGE < 61 THEN 'LT61'
		   WHEN INS.INVOICEAGE < 91 THEN 'LT91'
		   WHEN INS.INVOICEAGE < 121 THEN 'LT121'
		   ELSE 'GT120'
		  END AGEGROUP
	FROM #INVOICESUMMARY INS
	WHERE INS.TOTALAMOUNT > INS.PAIDAMOUNT
) X
PIVOT
	(SUM(X.AMTDUE)
		FOR X.AGEGROUP IN ([LT31],[LT61],[LT91],[LT121],[GT120])
	) PVT
;CREATE INDEX TEMP1 ON #INVOICEDETAIL(TXREMITTANCEID);

SELECT F.TXREMITTANCEID, F.PERIODNAME, F.STARTDATE
	, RA.TXREMITTANCEACCOUNTID, RA.TXREMITTANCETYPEID, RA.REMITTANCEACCOUNTNUMBER, RA.ACCOUNTNUMBER
	, RS.[NAME] ACCOUNTSTATUS
	, RT.[NAME] REMITTANCETYPE, RT.FRIENDLYNAME REMITTANCETYPEFRIENDLY
	, GE.GLOBALENTITYNAME
	, ID.CAINVOICEID, ID.INVOICENUMBER, ID.INVOICEDATE, ID.INVOICEDUEDATE
	, ID.LT31, ID.LT61, ID.LT91, ID.LT121, ID.GT120
	, (SELECT CASE WHEN R.[IMAGE] IS NULL THEN 'N' ELSE 'Y' END FROM RPTIMAGELIB R WHERE R.IMAGENAME = 'Municipality_Logo') SHOWLOGO
	, (SELECT R.REPORTTEXT FROM RPTTEXTLIB R WHERE R.TEXTNAME = 'Municipality_Name') Municipality_Name
	, (SELECT R.REPORTTEXT FROM RPTTEXTLIB R WHERE R.TEXTNAME = 'Municipality_Page_Footer') Municipality_Page_Footer
	, ROW_NUMBER () OVER (
		ORDER BY RA.REMITTANCEACCOUNTNUMBER ASC, ID.INVOICEDATE ASC, F.STARTDATE ASC
	  ) SORTORD
FROM #FILTER F
INNER JOIN TXREMITTANCEACCOUNT RA ON F.TXREMITTANCEACCOUNTID = RA.TXREMITTANCEACCOUNTID
INNER JOIN TXREMITSTATUS RS ON RA.TXREMITTANCESTATUSID = RS.TXREMITSTATUSID
INNER JOIN TXREMITTANCETYPE RT ON RA.TXREMITTANCETYPEID = RT.TXREMITTANCETYPEID
INNER JOIN BLGLOBALENTITYEXTENSION GEE ON RA.BLGLOBALENTITYEXTENSIONID = GEE.BLGLOBALENTITYEXTENSIONID
INNER JOIN GLOBALENTITY GE ON GEE.GLOBALENTITYID = GE.GLOBALENTITYID
INNER JOIN #INVOICEDETAIL ID ON F.TXREMITTANCEID = ID.TXREMITTANCEID
ORDER BY SORTORD



END
