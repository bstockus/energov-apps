CREATE PROCEDURE [dbo].[USP_ADVANCEDSEARCHCRITERIA_SEARCH]
(
	@USER_ID AS CHAR(36),
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
	@FILTERVALUE as nvarchar(100)
)
AS
WITH RAW_DATA AS (
	SELECT
 	    [ADVANCEDSEARCHCRITERIAID],
		[ADVANCEDSEARCHCRITERIA].[NAME],
		[DESCRIPTION],
		[USERID],
		[SHARED],
		[SEARCHMODULE],
		[ADVANCEDSEARCHCRITERIA].[CREATEDON],
		[USERS].[FNAME] AS [CREATEDBYFIRSTNAME],
		[USERS].[LNAME] AS [CREATEDBYLASTNAME],
		[LASTCHANGEDBY].[FNAME] AS [LASTCHANGEDBYFIRSTNAME],
		[LASTCHANGEDBY].[LNAME] AS [LASTCHANGEDBYLASTNAME],
		[ADVANCEDSEARCHCRITERIA].[LASTCHANGEDON],
		[ADVANCEDSEARCHCRITERIA].[ROWVERSION],
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [ADVANCEDSEARCHCRITERIA].[LASTCHANGEDON])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [ADVANCEDSEARCHCRITERIA].[LASTCHANGEDON] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[ADVANCEDSEARCHCRITERIA]
	INNER JOIN [dbo].[USERS] on [dbo].[USERS].SUSERGUID = [dbo].[ADVANCEDSEARCHCRITERIA].USERID
	INNER JOIN [dbo].[USERS] AS LASTCHANGEDBY on LASTCHANGEDBY.SUSERGUID = [dbo].[ADVANCEDSEARCHCRITERIA].LASTCHANGEDBY
	WHERE (([USERID] = @USER_ID) OR ([SHARED] = 1)) AND (([ADVANCEDSEARCHCRITERIA].[NAME] LIKE '%'+ @FILTERVALUE +'%') OR ([ADVANCEDSEARCHCRITERIA].[DESCRIPTION] LIKE '%'+ @FILTERVALUE + '%'))
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber