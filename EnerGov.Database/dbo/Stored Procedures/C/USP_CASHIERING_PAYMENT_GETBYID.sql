CREATE PROCEDURE [dbo].[USP_CASHIERING_PAYMENT_GETBYID]
	@ID				AS CHAR(36) ='',
	@PAGE_NUMBER	AS INT		= 1,
	@PAGE_SIZE		AS INT		= 10,
	@IS_ASCENDING	AS BIT		= 1,
	@SORT_COLUMN	AS CHAR(50)	= ''
AS
BEGIN
	WITH RAW_DATA AS (
		SELECT 
			InvoiceId,
			InvoiceTransactionId,
			TransactionPaymentId,
			ReceiptNumber,
			Status,
			TransactionType,
			PaymentType,
			PaymentAmount,
			RefundAmount,
			PaymentReceived,
			ChangeBackAmount,
			PaymentDate,
			TransactionDate,
			TotalRows,
			CASE @IS_ASCENDING WHEN 1 THEN
				CASE @SORT_COLUMN WHEN 'RECEIPTNUMBER' THEN ROW_NUMBER() OVER(ORDER BY ReceiptNumber ASC)
								 WHEN 'TRANSACTIONTYPE' THEN ROW_NUMBER() OVER(ORDER BY TransactionType ASC)
								 WHEN 'STATUS' THEN ROW_NUMBER() OVER(ORDER BY Status ASC)
								 WHEN 'PAYMENTTYPE' THEN ROW_NUMBER() OVER(ORDER BY PaymentType ASC)
								 WHEN 'REFUNDAMOUNT' THEN ROW_NUMBER() OVER(ORDER BY RefundAmount ASC)
								 WHEN 'PAYMENTAMOUNT' THEN ROW_NUMBER() OVER(ORDER BY PaymentAmount ASC)
								 WHEN 'PAYMENTRECEIVED' THEN ROW_NUMBER() OVER(ORDER BY PaymentReceived ASC)
								 WHEN 'CHANGEBACK' THEN ROW_NUMBER() OVER(ORDER BY ChangeBackAmount ASC)
								 WHEN 'PAYMENTAPPLIED' THEN ROW_NUMBER() OVER(ORDER BY PaymentReceived ASC)
								 WHEN 'TRANSACTIONDATE' THEN ROW_NUMBER() OVER(ORDER BY TransactionDate ASC)
								 WHEN 'PAYMENTDATE' THEN ROW_NUMBER() OVER(ORDER BY PaymentDate ASC)
								 ELSE ROW_NUMBER() OVER(ORDER BY ReceiptNumber, TransactionDate, PaymentDate ASC)
				END
			ELSE
				CASE @SORT_COLUMN WHEN 'RECEIPTNUMBER' THEN ROW_NUMBER() OVER(ORDER BY ReceiptNumber DESC)
								 WHEN 'TRANSACTIONTYPE' THEN ROW_NUMBER() OVER(ORDER BY TransactionType DESC)
								 WHEN 'STATUS' THEN ROW_NUMBER() OVER(ORDER BY Status DESC)
								 WHEN 'PAYMENTTYPE' THEN ROW_NUMBER() OVER(ORDER BY PaymentType DESC)
								 WHEN 'REFUNDAMOUNT' THEN ROW_NUMBER() OVER(ORDER BY RefundAmount DESC)
								 WHEN 'PAYMENTAMOUNT' THEN ROW_NUMBER() OVER(ORDER BY PaymentAmount DESC)
								 WHEN 'PAYMENTRECEIVED' THEN ROW_NUMBER() OVER(ORDER BY PaymentReceived DESC)
								 WHEN 'CHANGEBACK' THEN ROW_NUMBER() OVER(ORDER BY ChangeBackAmount DESC)
								 WHEN 'PAYMENTAPPLIED' THEN ROW_NUMBER() OVER(ORDER BY PaymentReceived DESC)
								 WHEN 'TRANSACTIONDATE' THEN ROW_NUMBER() OVER(ORDER BY TransactionDate DESC)
								 WHEN 'PAYMENTDATE' THEN ROW_NUMBER() OVER(ORDER BY PaymentDate DESC)
								 ELSE ROW_NUMBER() OVER(ORDER BY ReceiptNumber, TransactionDate, PaymentDate DESC)
				END
			END AS RowNumber
		FROM (
			SELECT	[CAINVOICE].[CAINVOICEID] AS InvoiceId,
					[CATRANSACTION].[CATRANSACTIONID] AS InvoiceTransactionId,
					[CATRANSACTIONPAYMENT].[CATRANSACTIONPAYMENTID] AS TransactionPaymentId,
					[CATRANSACTION].[RECEIPTNUMBER] AS ReceiptNumber,
					[CATRANSACTIONSTATUS].[NAME] AS Status,
					[CATRANSACTIONTYPE].[NAME] AS TransactionType,
					[CAPAYMENTMETHOD].[NAME] AS PaymentType,
					CASE
						 WHEN ([CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 4 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 5 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 6)
							THEN 0
							ELSE [CATRANSACTIONPAYMENT].[PAYMENTAMOUNT]
					END AS PaymentAmount,
					CASE
						 WHEN ([CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 4 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 5 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 6)
							THEN [CATRANSACTIONPAYMENT].[PAYMENTAMOUNT]
							ELSE 0
					END AS RefundAmount,
					CASE
						 WHEN ([CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 4 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 5 OR [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID] = 6)
							THEN [CATRANSACTION].[CHANGEBACK]
							ELSE [CATRANSACTIONPAYMENT].[PAYMENTAMOUNT] + [CATRANSACTION].[CHANGEBACK]
					END AS PaymentReceived,
					[CATRANSACTION].[CHANGEBACK] AS ChangeBackAmount,
					[CATRANSACTIONPAYMENT].[PAYMENTDATE] AS PaymentDate,
					[CATRANSACTION].[TRANSACTIONDATE] AS TransactionDate,
					COUNT(1) OVER() AS TotalRows
			FROM [CATRANSACTION]
			JOIN [CATRANSACTIONINVOICE] ON [CATRANSACTION].[CATRANSACTIONID] = [CATRANSACTIONINVOICE].[CATRANSACTIONID]
			JOIN [CAINVOICE] ON [CATRANSACTIONINVOICE].[CAINVOICEID] = [CAINVOICE].[CAINVOICEID]
			JOIN [CATRANSACTIONSTATUS] ON [CATRANSACTION].[CATRANSACTIONSTATUSID] = [CATRANSACTIONSTATUS].[CATRANSACTIONSTATUSID]
			JOIN [CATRANSACTIONTYPE] ON [CATRANSACTION].[CATRANSACTIONTYPEID] = [CATRANSACTIONTYPE].[CATRANSACTIONTYPEID]
			JOIN [CATRANSACTIONPAYMENT] ON [CATRANSACTION].[CATRANSACTIONID] = [CATRANSACTIONPAYMENT].[CATRANSACTIONID]
			JOIN [CAPAYMENTMETHOD] ON [CATRANSACTIONPAYMENT].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD].[CAPAYMENTMETHODID]
			WHERE [CAINVOICE].[CAINVOICEID] = @ID
		) AS Payments
	)
	SELECT 
		InvoiceId,
		InvoiceTransactionId,
		TransactionPaymentId,
		ReceiptNumber,
		Status,
		TransactionType,
		PaymentType,
		PaymentAmount,
		RefundAmount,
		PaymentReceived,
		ChangeBackAmount,
		PaymentDate,
		TransactionDate,
		RowNumber, 
		TotalRows
	FROM RAW_DATA
	WHERE
		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
		RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
	ORDER BY  RowNumber 
END;