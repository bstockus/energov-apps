CREATE TABLE [dbo].[WORKFLOWACTIONTASKUSER] (
    [WORKFLOWACTIONTASKUSERID] CHAR (36) NOT NULL,
    [WORKFLOWACTIONTASKID]     CHAR (36) NOT NULL,
    [USERID]                   CHAR (36) NOT NULL,
    CONSTRAINT [PK_WorkflowActionTaskUser] PRIMARY KEY CLUSTERED ([WORKFLOWACTIONTASKUSERID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_WFActionTaskUser_Users] FOREIGN KEY ([USERID]) REFERENCES [dbo].[USERS] ([SUSERGUID]),
    CONSTRAINT [FK_WTaskUser_WTask] FOREIGN KEY ([WORKFLOWACTIONTASKID]) REFERENCES [dbo].[WORKFLOWACTIONTASK] ([WORKFLOWACTIONTASKID])
);


GO
CREATE NONCLUSTERED INDEX [IX_WorkflowActionTaskUser]
    ON [dbo].[WORKFLOWACTIONTASKUSER]([WORKFLOWACTIONTASKID] ASC) WITH (FILLFACTOR = 90);


GO
CREATE NONCLUSTERED INDEX [WORKFLOWACTIONTASKUSER_USERID]
    ON [dbo].[WORKFLOWACTIONTASKUSER]([USERID] ASC);


GO

CREATE TRIGGER [dbo].[TG_WORKFLOWACTIONTASKUSER_INSERT]
   ON  [dbo].[WORKFLOWACTIONTASKUSER]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )	
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			[WORKFLOW].[LASTCHANGEDBY],
			'Workflow Action Task User Added',
			'',
			'',
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + '), User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'')  + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			1,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[inserted]
			INNER JOIN [WORKFLOWACTIONTASK] ON [WORKFLOWACTIONTASK].[WORKFLOWACTIONTASKID] = [inserted].[WORKFLOWACTIONTASKID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [WORKFLOWACTIONTASK].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
			INNER JOIN [USERS] WITH (NOLOCK) ON [USERS].[SUSERGUID] = [inserted].[USERID]

END
GO

CREATE TRIGGER [dbo].[TG_WORKFLOWACTIONTASKUSER_DELETE]
   ON  [dbo].[WORKFLOWACTIONTASKUSER]
   AFTER DELETE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )	
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Workflow Action Task User Deleted',
			'',
			'',
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + '), User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'')  + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			3,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[deleted]
			INNER JOIN [WORKFLOWACTIONTASK] ON [WORKFLOWACTIONTASK].[WORKFLOWACTIONTASKID] = [deleted].[WORKFLOWACTIONTASKID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [WORKFLOWACTIONTASK].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
			INNER JOIN [USERS] WITH (NOLOCK) ON [USERS].[SUSERGUID] = [deleted].[USERID]

END