CREATE TABLE [dbo].[WORKFLOWACTIONFTP] (
    [WORKFLOWACTIONFTPID] CHAR (36)      NOT NULL,
    [WORKFLOWACTIONID]    CHAR (36)      NOT NULL,
    [FTPSETUPID]          CHAR (36)      NOT NULL,
    [SUBPATH]             NVARCHAR (255) NULL,
    [ATTACHMENTGROUPID]   CHAR (36)      NOT NULL,
    [FTPFILENAMEOPTIONID] INT            NULL,
    CONSTRAINT [PK_WORKFLOWACTIONFTP] PRIMARY KEY CLUSTERED ([WORKFLOWACTIONFTPID] ASC),
    CONSTRAINT [FK_WORKFLOWACTIONFTP_ATTACHMENTGROUP] FOREIGN KEY ([ATTACHMENTGROUPID]) REFERENCES [dbo].[ATTACHMENTGROUP] ([ATTACHMENTGROUPID]),
    CONSTRAINT [FK_WORKFLOWACTIONFTP_FTPFILENAMEOPTION] FOREIGN KEY ([FTPFILENAMEOPTIONID]) REFERENCES [dbo].[FTPFILENAMEOPTION] ([FTPFILENAMEOPTIONID]),
    CONSTRAINT [FK_WORKFLOWACTIONFTP_FTPSETUP] FOREIGN KEY ([FTPSETUPID]) REFERENCES [dbo].[FTPSETUP] ([FTPSETUPID]),
    CONSTRAINT [FK_WORKFLOWACTIONFTP_WORKFLOWACTION] FOREIGN KEY ([WORKFLOWACTIONID]) REFERENCES [dbo].[WORKFLOWACTION] ([WORKFLOWACTIONID])
);


GO
CREATE NONCLUSTERED INDEX [WORKFLOWACTIONFTP_WORKFLOWACTIONID]
    ON [dbo].[WORKFLOWACTIONFTP]([WORKFLOWACTIONID] ASC);


GO
CREATE NONCLUSTERED INDEX [WORKFLOWACTIONFTP_FTPFILENAMEOPTIONID]
    ON [dbo].[WORKFLOWACTIONFTP]([FTPFILENAMEOPTIONID] ASC);


GO
CREATE NONCLUSTERED INDEX [WORKFLOWACTIONFTP_FTPSETUPID]
    ON [dbo].[WORKFLOWACTIONFTP]([FTPSETUPID] ASC);


GO
CREATE NONCLUSTERED INDEX [WORKFLOWACTIONFTP_ATTACHMENTGROUPID]
    ON [dbo].[WORKFLOWACTIONFTP]([ATTACHMENTGROUPID] ASC);


GO

CREATE TRIGGER [dbo].[TG_WORKFLOWACTIONFTP_UPDATE]
   ON  [dbo].[WORKFLOWACTIONFTP]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )	
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			[WORKFLOW].[LASTCHANGEDBY],
			'FTP',
			DELETEFTPSETUP.[NAME],
			INSERTFTPSETUP.[NAME],
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			2,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
			INNER JOIN [FTPSETUP] AS DELETEFTPSETUP WITH (NOLOCK) ON DELETEFTPSETUP.[FTPSETUPID] = [deleted].[FTPSETUPID]
			INNER JOIN [FTPSETUP] AS INSERTFTPSETUP WITH (NOLOCK) ON INSERTFTPSETUP.[FTPSETUPID] = [inserted].[FTPSETUPID]
	WHERE	[deleted].[FTPSETUPID] <> [inserted].[FTPSETUPID]

	UNION ALL
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			[WORKFLOW].[LASTCHANGEDBY],
			'Attachment Group',
			DELETEATTACHMENTGROUP.[NAME],
			INSERTATTACHMENTGROUP.[NAME],
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			2,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
			INNER JOIN [ATTACHMENTGROUP] AS DELETEATTACHMENTGROUP WITH (NOLOCK) ON DELETEATTACHMENTGROUP.[ATTACHMENTGROUPID] = [deleted].[ATTACHMENTGROUPID]
			INNER JOIN [ATTACHMENTGROUP] AS INSERTATTACHMENTGROUP WITH (NOLOCK) ON INSERTATTACHMENTGROUP.[ATTACHMENTGROUPID] = [inserted].[ATTACHMENTGROUPID]
	WHERE	[deleted].[ATTACHMENTGROUPID] <> [inserted].[ATTACHMENTGROUPID]

	UNION ALL
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			[WORKFLOW].[LASTCHANGEDBY],
			'Sub-path',
			ISNULL([deleted].[SUBPATH],'[none]'),
			ISNULL([inserted].[SUBPATH],'[none]'),
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			2,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
	WHERE	ISNULL([deleted].[SUBPATH],'') <> ISNULL([inserted].[SUBPATH],'')

	UNION ALL
	SELECT
			[WORKFLOW].[WORKFLOWID], 
			[WORKFLOW].[ROWVERSION],
			GETUTCDATE(),
			[WORKFLOW].[LASTCHANGEDBY],
			'Modify Name With Case Number',
			ISNULL(DELETEFTPFILENAMEOPTION.[NAME],'[none]'),
			ISNULL(INSERTFTPFILENAMEOPTION.[NAME],'[none]'),
			'Intelligent Object (' + [WORKFLOW].[WORKFLOWNAME] + '), Action (' + [WORKFLOWACTION].[ACTIONNAME] +'), Action Type (' + [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPENAME] + ')',
			'F053A756-19BE-4A42-A70D-185D5B01C31A',
			2,
			0,
			[WORKFLOWACTION].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOWACTION] ON [WORKFLOWACTION].[WORKFLOWACTIONID] = [inserted].[WORKFLOWACTIONID]
			INNER JOIN [WORKFLOW] ON [WORKFLOW].[WORKFLOWID] = [WORKFLOWACTION].[WORKFLOWID]
			INNER JOIN [WORKFLOWACTIONTYPE] WITH (NOLOCK) ON [WORKFLOWACTIONTYPE].[WORKFLOWACTIONTYPEID] = [WORKFLOWACTION].[WORKFLOWACTIONTYPEID]
			LEFT JOIN [FTPFILENAMEOPTION] AS DELETEFTPFILENAMEOPTION WITH (NOLOCK) ON DELETEFTPFILENAMEOPTION.[FTPFILENAMEOPTIONID] = [deleted].[FTPFILENAMEOPTIONID]
			LEFT JOIN [FTPFILENAMEOPTION] AS INSERTFTPFILENAMEOPTION WITH (NOLOCK) ON INSERTFTPFILENAMEOPTION.[FTPFILENAMEOPTIONID] = [inserted].[FTPFILENAMEOPTIONID]
	WHERE	ISNULL([deleted].[FTPFILENAMEOPTIONID],'') <> ISNULL([inserted].[FTPFILENAMEOPTIONID],'')
END