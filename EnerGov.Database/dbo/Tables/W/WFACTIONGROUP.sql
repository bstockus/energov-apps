CREATE TABLE [dbo].[WFACTIONGROUP] (
    [WFACTIONGROUPID] CHAR (36)      NOT NULL,
    [NAME]            NVARCHAR (50)  NOT NULL,
    [DESCRIPTION]     NVARCHAR (MAX) NULL,
    [WFENTITYID]      INT            NOT NULL,
    [WFSTEPTYPEID]    INT            NOT NULL,
    [LASTCHANGEDBY]   CHAR (36)      NULL,
    [LASTCHANGEDON]   DATETIME       CONSTRAINT [DF_WFACTIONGROUP_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]      INT            CONSTRAINT [DF_WFACTIONGROUP_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_WFActionGroup] PRIMARY KEY CLUSTERED ([WFACTIONGROUPID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_WFActionGroup_WFEntity] FOREIGN KEY ([WFENTITYID]) REFERENCES [dbo].[WFENTITY] ([WFENTITYID]),
    CONSTRAINT [FK_WFActionGroup_WFStepType] FOREIGN KEY ([WFSTEPTYPEID]) REFERENCES [dbo].[WFSTEPTYPE] ([WFSTEPTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [WFACTIONGROUP_IX_QUERY]
    ON [dbo].[WFACTIONGROUP]([WFACTIONGROUPID] ASC, [NAME] ASC);


GO
CREATE TRIGGER [dbo].[TG_WFACTIONGROUP_INSERT] ON [dbo].[WFACTIONGROUP]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[WFACTIONGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Workflow Action Group Added',
			'',
			'',
			'Workflow Action Group (' + [inserted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]	
END
GO
CREATE TRIGGER [dbo].[TG_WFACTIONGROUP_UPDATE] ON [dbo].[WFACTIONGROUP] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[WFACTIONGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Workflow Action Group (' + [inserted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WFACTIONGROUPID] = [inserted].[WFACTIONGROUPID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]
	UNION ALL

	SELECT
			[inserted].[WFACTIONGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Description',
			ISNULL([deleted].[DESCRIPTION],'[none]'),
			ISNULL([inserted].[DESCRIPTION],'[none]'),
			'Workflow Action Group (' + [inserted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WFACTIONGROUPID] = [inserted].[WFACTIONGROUPID]
	WHERE	ISNULL([deleted].[DESCRIPTION],'') <> ISNULL([inserted].[DESCRIPTION],'')
	UNION ALL

	SELECT
			[inserted].[WFACTIONGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Entity',
			[WFENTITY_DELETED].[NAME],
			[WFENTITY_INSERTED].[NAME],
			'Workflow Action Group (' + [inserted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WFACTIONGROUPID] = [inserted].[WFACTIONGROUPID]			
			INNER JOIN [WFENTITY] [WFENTITY_INSERTED] WITH (NOLOCK) ON [WFENTITY_INSERTED].[WFENTITYID] = [inserted].[WFENTITYID]
			INNER JOIN [WFENTITY] [WFENTITY_DELETED] WITH (NOLOCK) ON [WFENTITY_DELETED].[WFENTITYID] = [deleted].[WFENTITYID]
	WHERE	[deleted].[WFENTITYID] <> [inserted].[WFENTITYID]
	UNION ALL

	SELECT
			[inserted].[WFACTIONGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Step Type',
			[WFSTEPTYPE_DELETED].[NAME],
			[WFSTEPTYPE_INSERTED].[NAME],
			'Workflow Action Group (' + [inserted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[WFACTIONGROUPID] = [inserted].[WFACTIONGROUPID]			
			INNER JOIN [WFSTEPTYPE] [WFSTEPTYPE_INSERTED] WITH (NOLOCK) ON [WFSTEPTYPE_INSERTED].[WFSTEPTYPEID] = [inserted].[WFSTEPTYPEID]
			INNER JOIN [WFSTEPTYPE] [WFSTEPTYPE_DELETED] WITH (NOLOCK) ON [WFSTEPTYPE_DELETED].[WFSTEPTYPEID] = [deleted].[WFSTEPTYPEID]
	WHERE	[deleted].[WFSTEPTYPEID] <> [inserted].[WFSTEPTYPEID]
END
GO
CREATE TRIGGER [dbo].[TG_WFACTIONGROUP_DELETE] ON [dbo].[WFACTIONGROUP]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[deleted].[WFACTIONGROUPID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Workflow Action Group Deleted',
			'',
			'',
			'Workflow Action Group (' + [deleted].[NAME] + ')',
			'B2E65396-BF99-4411-9498-E21F42CD80FD',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END