CREATE TABLE [dbo].[OFFICEADDRESS] (
    [OFFICEID]          CHAR (36) NOT NULL,
    [MAILLINGADDRESSID] CHAR (36) NOT NULL,
    CONSTRAINT [PK_OFFICEADDRESS_1] PRIMARY KEY CLUSTERED ([OFFICEID] ASC, [MAILLINGADDRESSID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_OFFICEADDRESS_MAILINGADDRESS] FOREIGN KEY ([MAILLINGADDRESSID]) REFERENCES [dbo].[MAILINGADDRESS] ([MAILINGADDRESSID]),
    CONSTRAINT [FK_OFFICEADDRESS_OFFICE] FOREIGN KEY ([OFFICEID]) REFERENCES [dbo].[OFFICE] ([OFFICEID])
);


GO
CREATE NONCLUSTERED INDEX [OFFICEADDRESS_IX_OFFICEID]
    ON [dbo].[OFFICEADDRESS]([OFFICEID] ASC);


GO
CREATE TRIGGER [TG_OFFICEADDRESS_INSERT] ON OFFICEADDRESS
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[OFFICE].[OFFICEID],
			[OFFICE].[ROWVERSION],
			GETUTCDATE(),
			[OFFICE].[LASTCHANGEDBY],
			'Office Mailing Address Added',
			'',
			'',
			'Office (' + [OFFICE].[NAME] + '), Office Mailing Address (' + [MAILINGADDRESS].[ADDRESSLINE1] + ' ' + ISNULL([MAILINGADDRESS].[ADDRESSLINE2], '') + ')',
			'A4A2A7E4-FB08-47AA-80D9-51C0E68DEBC7',
			1,
			0,
			[MAILINGADDRESS].[ADDRESSLINE1] + ' ' + ISNULL([MAILINGADDRESS].[ADDRESSLINE2], '')
	FROM	[inserted]
	INNER JOIN OFFICE ON [inserted].[OFFICEID] = [OFFICE].[OFFICEID]
	INNER JOIN MAILINGADDRESS ON MAILINGADDRESS.[MAILINGADDRESSID] = [inserted].[MAILLINGADDRESSID]
END
GO


CREATE TRIGGER [TG_OFFICEADDRESS_DELETE] ON OFFICEADDRESS
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[OFFICE].[OFFICEID],
			[OFFICE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Office Mailing Address Deleted',
			'',
			'',
			'Office (' + [OFFICE].[NAME] + '), Office Mailing Address (' + [MAILINGADDRESS].[ADDRESSLINE1] + ' ' + ISNULL([MAILINGADDRESS].[ADDRESSLINE2], '') + ')',
			'A4A2A7E4-FB08-47AA-80D9-51C0E68DEBC7',
			3,
			0,
			[MAILINGADDRESS].[ADDRESSLINE1] + ' ' + ISNULL([MAILINGADDRESS].[ADDRESSLINE2], '')
	FROM	[deleted]	
	INNER JOIN OFFICE ON [OFFICE].[OFFICEID] = [deleted].[OFFICEID]
	INNER JOIN MAILINGADDRESS ON  [deleted].[MAILLINGADDRESSID] = [MAILINGADDRESS].[MAILINGADDRESSID]
END