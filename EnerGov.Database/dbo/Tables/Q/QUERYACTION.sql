CREATE TABLE [dbo].[QUERYACTION] (
    [QUERYACTIONID]     CHAR (36)      NOT NULL,
    [ACTIONNAME]        NVARCHAR (50)  NOT NULL,
    [CUSTOMMESSAGE]     NVARCHAR (MAX) NOT NULL,
    [ACTIONCLASSNAME]   NVARCHAR (MAX) NOT NULL,
    [PRIORITY]          INT            NOT NULL,
    [QUERYID]           CHAR (36)      NOT NULL,
    [QUERYACTIONTYPEID] INT            NOT NULL,
    CONSTRAINT [PK_QUERYACTION] PRIMARY KEY CLUSTERED ([QUERYACTIONID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_QUERYACTION_ACTIONTYPE] FOREIGN KEY ([QUERYACTIONTYPEID]) REFERENCES [dbo].[QUERYACTIONTYPE] ([QUERYACTIONTYPEID]),
    CONSTRAINT [FK_QUERYACTION_QUERY] FOREIGN KEY ([QUERYID]) REFERENCES [dbo].[QUERY] ([QUERYID])
);


GO
CREATE NONCLUSTERED INDEX [QUERYACTION_QUERYACTIONTYPEID]
    ON [dbo].[QUERYACTION]([QUERYACTIONTYPEID] ASC);


GO
CREATE NONCLUSTERED INDEX [QUERYACTION_QUERYID]
    ON [dbo].[QUERYACTION]([QUERYID] ASC);


GO

CREATE TRIGGER [dbo].[TG_QUERYACTION_INSERT]
   ON  [dbo].[QUERYACTION]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [QUERY].[QUERYID], 
        [QUERY].[ROWVERSION],
        GETUTCDATE(),
        [QUERY].[LASTCHANGEDBY],	
        'Intelligent Query Action: Action Added',
        '',
        '',       
		'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +')',
		'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
		1,
		0,
		[inserted].[ACTIONNAME]
    FROM [inserted]
	INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
END
GO

CREATE TRIGGER [dbo].[TG_QUERYACTION_UPDATE]
   ON  [dbo].[QUERYACTION]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )	
	SELECT
			[QUERY].[QUERYID], 
			[QUERY].[ROWVERSION],
			GETUTCDATE(),
			[QUERY].[LASTCHANGEDBY],
			'Action Name',
			[deleted].[ACTIONNAME],
			[inserted].[ACTIONNAME],
			'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +'), Action Type (' + [QUERYACTIONTYPE].[NAME] + ')',
			'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
			2,
			0,
			[inserted].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[QUERYACTIONID] = [inserted].[QUERYACTIONID]
			INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
			INNER JOIN [QUERYACTIONTYPE] WITH (NOLOCK) ON [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = [inserted].[QUERYACTIONTYPEID]
	WHERE	[deleted].[ACTIONNAME] <> [inserted].[ACTIONNAME]

	UNION ALL
	SELECT
			[QUERY].[QUERYID], 
			[QUERY].[ROWVERSION],
			GETUTCDATE(),
			[QUERY].[LASTCHANGEDBY],
			CASE WHEN [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = 9 THEN 'Stored Procedure Name' ELSE 'Custom Message' END,
			[deleted].[CUSTOMMESSAGE],
			[inserted].[CUSTOMMESSAGE],
			'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +'), Action Type (' + [QUERYACTIONTYPE].[NAME] + ')',
			'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
			2,
			0,
			[inserted].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[QUERYACTIONID] = [inserted].[QUERYACTIONID]
			INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
			INNER JOIN [QUERYACTIONTYPE] WITH (NOLOCK) ON [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = [inserted].[QUERYACTIONTYPEID]
	WHERE	[deleted].[CUSTOMMESSAGE] <> [inserted].[CUSTOMMESSAGE] AND [QUERYACTIONTYPE].[QUERYACTIONTYPEID] <> 15

	UNION ALL
	SELECT
			[QUERY].[QUERYID], 
			[QUERY].[ROWVERSION],
			GETUTCDATE(),
			[QUERY].[LASTCHANGEDBY],
			'Geo Rule',
			ISNULL(DELETEGEORULE.[RULENAME],'[None]'),
			ISNULL(INSERTGEORULE.[RULENAME],'[None]'),
			'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +'), Action Type (' + [QUERYACTIONTYPE].[NAME] + ')',
			'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
			2,
			0,
			[inserted].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[QUERYACTIONID] = [inserted].[QUERYACTIONID]
			INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
			INNER JOIN [QUERYACTIONTYPE] WITH (NOLOCK) ON [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = [inserted].[QUERYACTIONTYPEID]
			LEFT JOIN [GEORULE] AS DELETEGEORULE WITH (NOLOCK) ON DELETEGEORULE.[GEORULEID] = [deleted].[CUSTOMMESSAGE]
			LEFT JOIN [GEORULE] AS INSERTGEORULE WITH (NOLOCK) ON INSERTGEORULE.[GEORULEID] = [inserted].[CUSTOMMESSAGE]
	WHERE	[deleted].[CUSTOMMESSAGE] <> [inserted].[CUSTOMMESSAGE] AND [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = 15

	UNION ALL
	SELECT
			[QUERY].[QUERYID], 
			[QUERY].[ROWVERSION],
			GETUTCDATE(),
			[QUERY].[LASTCHANGEDBY],
			'Action Class Name',
			[deleted].[ACTIONCLASSNAME],
			[inserted].[ACTIONCLASSNAME],
			'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +'), Action Type (' + [QUERYACTIONTYPE].[NAME] + ')',
			'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
			2,
			0,
			[inserted].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[QUERYACTIONID] = [inserted].[QUERYACTIONID]
			INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
			INNER JOIN [QUERYACTIONTYPE] WITH (NOLOCK) ON [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = [inserted].[QUERYACTIONTYPEID]
	WHERE	[deleted].[ACTIONCLASSNAME] <> [inserted].[ACTIONCLASSNAME]

	UNION ALL
	SELECT
			[QUERY].[QUERYID], 
			[QUERY].[ROWVERSION],
			GETUTCDATE(),
			[QUERY].[LASTCHANGEDBY],
			'Priority',
			CONVERT(NVARCHAR(MAX), [deleted].[PRIORITY]),
			CONVERT(NVARCHAR(MAX), [inserted].[PRIORITY]),
			'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [inserted].[ACTIONNAME] +'), Action Type (' + [QUERYACTIONTYPE].[NAME] + ')',
			'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
			2,
			0,
			[inserted].[ACTIONNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[QUERYACTIONID] = [inserted].[QUERYACTIONID]
			INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [inserted].[QUERYID]
			INNER JOIN [QUERYACTIONTYPE] WITH (NOLOCK) ON [QUERYACTIONTYPE].[QUERYACTIONTYPEID] = [inserted].[QUERYACTIONTYPEID]
	WHERE	[deleted].[PRIORITY] <> [inserted].[PRIORITY]
END
GO

CREATE TRIGGER [dbo].[TG_QUERYACTION_DELETE]
   ON  [dbo].[QUERYACTION]
   AFTER DELETE
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [QUERY].[QUERYID], 
        [QUERY].[ROWVERSION],
        GETUTCDATE(),
        (SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),	
        'Intelligent Query Action: Action Deleted',
        '',
        '',       
		'Intelligent Query Action (' + [QUERY].[NAME] + '), Action (' + [deleted].[ACTIONNAME] +')',
		'C08776DA-046E-4E9B-9DE4-4CC2005A98CD',
		3,
		0,
		[deleted].[ACTIONNAME]
    FROM [deleted]
	INNER JOIN [QUERY] ON [QUERY].[QUERYID] = [deleted].[QUERYID]
END