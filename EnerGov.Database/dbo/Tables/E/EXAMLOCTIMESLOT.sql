CREATE TABLE [dbo].[EXAMLOCTIMESLOT] (
    [EXAMLOCTIMESLOTID] CHAR (36) NOT NULL,
    [EXAMTIMESLOTID]    CHAR (36) NOT NULL,
    [EXAMLOCATIONID]    CHAR (36) NOT NULL,
    [ISDEFAULT]         BIT       NOT NULL,
    CONSTRAINT [PK_EXAMLOCTIMESLOT] PRIMARY KEY CLUSTERED ([EXAMLOCTIMESLOTID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_EXAMLOCTIMESLOT_EXAMLOC] FOREIGN KEY ([EXAMLOCATIONID]) REFERENCES [dbo].[EXAMLOCATION] ([EXAMLOCATIONID]),
    CONSTRAINT [FK_EXAMLOCTIMESLOT_TIMESLOT] FOREIGN KEY ([EXAMTIMESLOTID]) REFERENCES [dbo].[EXAMTIMESLOT] ([EXAMTIMESLOTID])
);


GO
CREATE NONCLUSTERED INDEX [EXAMLOCTIMESLOT_IX_EXAMLOCATIONID]
    ON [dbo].[EXAMLOCTIMESLOT]([EXAMLOCATIONID] ASC);


GO
CREATE NONCLUSTERED INDEX [EXAMLOCTIMESLOT_IX_EXAMTIMESLOTID]
    ON [dbo].[EXAMLOCTIMESLOT]([EXAMTIMESLOTID] ASC);


GO

CREATE TRIGGER [dbo].[TG_EXAMLOCTIMESLOT_DELETE] ON [dbo].[EXAMLOCTIMESLOT]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[EXAMLOCATION].[EXAMLOCATIONID],
			[EXAMLOCATION].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Exam Location Exam Time Slot Deleted',
			'',
			'',
			'Exam Location (' + [EXAMLOCATION].[NAME] + '), Exam Time Slot (' + [EXAMTIMESLOT].[NAME] + ')',
			'140B0DF3-942B-404D-BEB8-FEB6904289FC',
			3,
			0,
			[EXAMTIMESLOT].[NAME]
	FROM	[deleted]
			INNER JOIN [EXAMLOCATION] ON [EXAMLOCATION].[EXAMLOCATIONID] = [deleted].[EXAMLOCATIONID]
			INNER JOIN [EXAMTIMESLOT] ON [EXAMTIMESLOT].[EXAMTIMESLOTID] = [deleted].[EXAMTIMESLOTID]
END
GO

CREATE TRIGGER [dbo].[TG_EXAMLOCTIMESLOT_UPDATE] ON [dbo].[EXAMLOCTIMESLOT]
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
		
    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[EXAMLOCATION].[EXAMLOCATIONID],
			[EXAMLOCATION].[ROWVERSION],
			GETUTCDATE(),
			[EXAMLOCATION].[LASTCHANGEDBY],
			'Default Flag',
			CASE [deleted].[ISDEFAULT] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			CASE [inserted].[ISDEFAULT] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			'Exam Location (' + [EXAMLOCATION].[NAME] + '), Exam Time Slot (' + [EXAMTIMESLOT].[NAME] + ')',
			'140B0DF3-942B-404D-BEB8-FEB6904289FC',
			2,
			0,
			[EXAMTIMESLOT].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[EXAMLOCTIMESLOTID] = [inserted].[EXAMLOCTIMESLOTID]
			INNER JOIN [EXAMLOCATION] ON [EXAMLOCATION].[EXAMLOCATIONID] = [deleted].[EXAMLOCATIONID]
			INNER JOIN [EXAMTIMESLOT] ON [EXAMTIMESLOT].[EXAMTIMESLOTID] = [deleted].[EXAMTIMESLOTID]
	WHERE	[deleted].[ISDEFAULT] <> [inserted].[ISDEFAULT]
END
GO

CREATE TRIGGER [dbo].[TG_EXAMLOCTIMESLOT_INSERT] ON [dbo].[EXAMLOCTIMESLOT]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[EXAMLOCATION].[EXAMLOCATIONID],
			[EXAMLOCATION].[ROWVERSION],
			GETUTCDATE(),
			[EXAMLOCATION].[LASTCHANGEDBY],
			'Exam Location Exam Time Slot Added',
			'',
			'',
			'Exam Location (' + [EXAMLOCATION].[NAME] + '), Exam Time Slot (' + [EXAMTIMESLOT].[NAME] + ')',
			'140B0DF3-942B-404D-BEB8-FEB6904289FC',
			1,
			0,
			[EXAMTIMESLOT].[NAME]
	FROM	[inserted]
			INNER JOIN [EXAMLOCATION] ON [EXAMLOCATION].[EXAMLOCATIONID] = [inserted].[EXAMLOCATIONID]
			INNER JOIN [EXAMTIMESLOT] ON [EXAMTIMESLOT].[EXAMTIMESLOTID] = [inserted].[EXAMTIMESLOTID]
END