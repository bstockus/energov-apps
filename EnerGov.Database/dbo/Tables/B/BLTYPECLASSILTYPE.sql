CREATE TABLE [dbo].[BLTYPECLASSILTYPE] (
    [BLTYPECLASSILTYPEID]  CHAR (36) NOT NULL,
    [BLLICENSETYPECLASSID] CHAR (36) NOT NULL,
    [ILLICENSETYPEID]      CHAR (36) NOT NULL,
    CONSTRAINT [PK_BLTYPECLASSILTYPE] PRIMARY KEY CLUSTERED ([BLTYPECLASSILTYPEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_BLTYPECLASSILTYPE_BLTC] FOREIGN KEY ([BLLICENSETYPECLASSID]) REFERENCES [dbo].[BLLICENSETYPECLASS] ([BLLICENSETYPECLASSID]),
    CONSTRAINT [FK_BLTYPECLASSILTYPE_ILTYPE] FOREIGN KEY ([ILLICENSETYPEID]) REFERENCES [dbo].[ILLICENSETYPE] ([ILLICENSETYPEID])
);


GO
CREATE NONCLUSTERED INDEX [IX_BLTYPECLASSILTYPE_BLLICENSETYPECLASSID]
    ON [dbo].[BLTYPECLASSILTYPE]([BLLICENSETYPECLASSID] ASC);


GO

CREATE TRIGGER [dbo].[TG_BLTYPECLASSILTYPE_INSERT] ON  [dbo].[BLTYPECLASSILTYPE]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [BLLICENSETYPE].[BLLICENSETYPEID], 
        [BLLICENSETYPE].[ROWVERSION],
        GETUTCDATE(),
        [BLLICENSETYPE].[LASTCHANGEDBY],	
        'Business License Type Valid Professional License Type Added',
        '',
        '',       
		'Business License Type (' + [BLLICENSETYPE].[NAME] + '), Valid Professional License Type Name (' + [ILLICENSETYPE].[NAME] +')',
		'8C9D2A3A-2F18-45CE-B059-33A98C1EA148',
		1,
		0,
		[ILLICENSETYPE].[NAME]
    FROM [inserted]
	INNER JOIN [BLLICENSETYPECLASS] ON [BLLICENSETYPECLASS].[BLLICENSETYPECLASSID]=[inserted].[BLLICENSETYPECLASSID]
	INNER JOIN [BLLICENSETYPE] ON [BLLICENSETYPE].[BLLICENSETYPEID] = [BLLICENSETYPECLASS].[BLLICENSETYPEID]
	INNER JOIN [ILLICENSETYPE] WITH (NOLOCK) ON [ILLICENSETYPE].[ILLICENSETYPEID] = [inserted].[ILLICENSETYPEID]
END
GO
CREATE TRIGGER [dbo].[TG_BLTYPECLASSILTYPE_DELETE]  ON  [dbo].[BLTYPECLASSILTYPE]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[BLLICENSETYPE].[BLLICENSETYPEID],
		[BLLICENSETYPE].[ROWVERSION],
		GETUTCDATE(),			
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Business License Type Valid Professional License Type Deleted',
        '',
        '',       
		'Business License Type (' + [BLLICENSETYPE].[NAME] + '), Valid Professional License Type Name (' + [ILLICENSETYPE].[NAME] +')',
		'8C9D2A3A-2F18-45CE-B059-33A98C1EA148',
		3,
		0,
		[ILLICENSETYPE].[NAME]
    FROM [deleted]
	INNER JOIN [BLLICENSETYPECLASS] ON [BLLICENSETYPECLASS].[BLLICENSETYPECLASSID]=[deleted].[BLLICENSETYPECLASSID]
	INNER JOIN [BLLICENSETYPE] ON [BLLICENSETYPE].[BLLICENSETYPEID] = [BLLICENSETYPECLASS].[BLLICENSETYPEID]
	INNER JOIN [ILLICENSETYPE] WITH (NOLOCK) ON [ILLICENSETYPE].[ILLICENSETYPEID] = [deleted].[ILLICENSETYPEID]
END