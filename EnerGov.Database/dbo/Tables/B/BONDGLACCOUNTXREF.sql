CREATE TABLE [dbo].[BONDGLACCOUNTXREF] (
    [BONDGLACCOUNTXREFID]    CHAR (36)       NOT NULL,
    [BONDINTERESTSCHEDULEID] CHAR (36)       NOT NULL,
    [DEBITGLACCOUNTID]       CHAR (36)       NULL,
    [CREDITGLACCOUNTID]      CHAR (36)       NULL,
    [CAPAYMENTMETHODID]      CHAR (36)       NOT NULL,
    [PERCENTAGE]             DECIMAL (20, 4) NOT NULL,
    CONSTRAINT [PK_BONDGLACCOUNTXREF] PRIMARY KEY NONCLUSTERED ([BONDGLACCOUNTXREFID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_BOND_SCH_GLXREF] FOREIGN KEY ([BONDINTERESTSCHEDULEID]) REFERENCES [dbo].[BONDINTERESTSCHEDULE] ([BONDINTERESTSCHEDULEID]),
    CONSTRAINT [FK_BONDSCH_GLCREDIT] FOREIGN KEY ([CREDITGLACCOUNTID]) REFERENCES [dbo].[GLACCOUNT] ([GLACCOUNTID]),
    CONSTRAINT [FK_BONDSCH_GLDEBIT] FOREIGN KEY ([DEBITGLACCOUNTID]) REFERENCES [dbo].[GLACCOUNT] ([GLACCOUNTID]),
    CONSTRAINT [FK_BONDSCH_PMT_METHOD] FOREIGN KEY ([CAPAYMENTMETHODID]) REFERENCES [dbo].[CAPAYMENTMETHOD] ([CAPAYMENTMETHODID])
);


GO
CREATE NONCLUSTERED INDEX [BONDGLACCOUNTXREF_IX_BONDINTERESTSCHEDULEID]
    ON [dbo].[BONDGLACCOUNTXREF]([BONDINTERESTSCHEDULEID] ASC);


GO

CREATE TRIGGER [dbo].[TG_BONDGLACCOUNTXREF_UPDATE] ON [dbo].[BONDGLACCOUNTXREF] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;  

    INSERT INTO [HISTORYSYSTEMSETUP]
		(	[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			[BONDINTERESTSCHEDULE].[LASTCHANGEDBY],
			'GL Debit Account',
			ISNULL([GLACCOUNT_DELETED].[NAME], '[none]'),
			ISNULL([GLACCOUNT_INSERTED].[NAME], '[none]'),
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_INSERTED].[NAME] + ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			2,
			0,
			[CAPAYMENTMETHOD_INSERTED].[NAME]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[BONDGLACCOUNTXREFID] = [deleted].[BONDGLACCOUNTXREFID]
			INNER JOIN BONDINTERESTSCHEDULE ON [deleted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			LEFT JOIN GLACCOUNT GLACCOUNT_DELETED WITH (NOLOCK) ON [deleted].[DEBITGLACCOUNTID] = [GLACCOUNT_DELETED].[GLACCOUNTID]
			LEFT JOIN GLACCOUNT GLACCOUNT_INSERTED WITH (NOLOCK) ON [inserted].[DEBITGLACCOUNTID] = [GLACCOUNT_INSERTED].[GLACCOUNTID]
			INNER JOIN CAPAYMENTMETHOD CAPAYMENTMETHOD_INSERTED WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_INSERTED].[CAPAYMENTMETHODID]
	WHERE	ISNULL([deleted].[DEBITGLACCOUNTID], '') <> ISNULL([inserted].[DEBITGLACCOUNTID], '') 

	UNION ALL
	SELECT
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			[BONDINTERESTSCHEDULE].[LASTCHANGEDBY],
			'GL Credit Account',
			ISNULL([GLACCOUNT_DELETED].[NAME],'[none]'),
			ISNULL([GLACCOUNT_INSERTED].[NAME],'[none]'),
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_INSERTED].[NAME] + ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			2,
			0,
			[CAPAYMENTMETHOD_INSERTED].[NAME]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[BONDGLACCOUNTXREFID] = [deleted].[BONDGLACCOUNTXREFID]
			INNER JOIN BONDINTERESTSCHEDULE ON [deleted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			LEFT JOIN GLACCOUNT GLACCOUNT_DELETED WITH (NOLOCK) ON [deleted].[CREDITGLACCOUNTID] = [GLACCOUNT_DELETED].[GLACCOUNTID]
			LEFT JOIN GLACCOUNT GLACCOUNT_INSERTED WITH (NOLOCK) ON [inserted].[CREDITGLACCOUNTID] = [GLACCOUNT_INSERTED].[GLACCOUNTID]
			INNER JOIN CAPAYMENTMETHOD CAPAYMENTMETHOD_INSERTED WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_INSERTED].[CAPAYMENTMETHODID]
	WHERE	ISNULL([deleted].[CREDITGLACCOUNTID], '') <> ISNULL([inserted].[CREDITGLACCOUNTID], '') 

	UNION ALL
	SELECT
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			[BONDINTERESTSCHEDULE].[LASTCHANGEDBY],
			'Payment Method',
			[CAPAYMENTMETHOD_DELETED].[NAME],
			[CAPAYMENTMETHOD_INSERTED].[NAME],
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_INSERTED].[NAME] + ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			2,
			0,
			[CAPAYMENTMETHOD_INSERTED].[NAME]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[BONDGLACCOUNTXREFID] = [deleted].[BONDGLACCOUNTXREFID]
			INNER JOIN BONDINTERESTSCHEDULE ON [deleted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			INNER JOIN CAPAYMENTMETHOD CAPAYMENTMETHOD_DELETED WITH (NOLOCK) ON [deleted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_DELETED].[CAPAYMENTMETHODID]
			INNER JOIN CAPAYMENTMETHOD CAPAYMENTMETHOD_INSERTED WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_INSERTED].[CAPAYMENTMETHODID]
	WHERE [deleted].[CAPAYMENTMETHODID] <> [inserted].[CAPAYMENTMETHODID]

	UNION ALL
	SELECT
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			[BONDINTERESTSCHEDULE].[LASTCHANGEDBY],
			'Percentage',
			CONVERT(NVARCHAR(MAX), CAST([deleted].[PERCENTAGE] AS FLOAT), 128),
			CONVERT(NVARCHAR(MAX), CAST([inserted].[PERCENTAGE] AS FLOAT),128),
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_INSERTED].[NAME] + ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			2,
			0,
			[CAPAYMENTMETHOD_INSERTED].[NAME]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[BONDGLACCOUNTXREFID] = [deleted].[BONDGLACCOUNTXREFID]
			INNER JOIN BONDINTERESTSCHEDULE ON [deleted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			INNER JOIN CAPAYMENTMETHOD CAPAYMENTMETHOD_INSERTED WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_INSERTED].[CAPAYMENTMETHODID]
	WHERE	[deleted].[PERCENTAGE] <> [inserted].[PERCENTAGE]
END
GO

CREATE TRIGGER [TG_BONDGLACCOUNTXREF_INSERT] ON [BONDGLACCOUNTXREF]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
		(	[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT 
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			[BONDINTERESTSCHEDULE].[LASTCHANGEDBY],
			'Bond Interest Schedule Payment Method Added',
			'',
			'',
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_INSERTED].[NAME]+ ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			1,
			0,
			[CAPAYMENTMETHOD_INSERTED].[NAME]
	FROM	[inserted]
			INNER JOIN BONDINTERESTSCHEDULE ON [inserted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			INNER JOIN CAPAYMENTMETHOD [CAPAYMENTMETHOD_INSERTED] WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_INSERTED].[CAPAYMENTMETHODID]
END
GO

CREATE TRIGGER [TG_BONDGLACCOUNTXREF_DELETE] ON [BONDGLACCOUNTXREF]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
		(	[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT
			[BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID],
			[BONDINTERESTSCHEDULE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Bond Interest Schedule Payment Method Deleted',
			'',
			'',
			'Bond Interest Schedule (' + [BONDINTERESTSCHEDULE].[NAME] + '), Payment Method (' + [CAPAYMENTMETHOD_DELETED].[NAME]+ ')',
			'B13CA7AB-136B-4286-BFF6-223B71BE732E',
			3,
			0,
			[CAPAYMENTMETHOD_DELETED].[NAME]
	FROM	[deleted] 
			INNER JOIN BONDINTERESTSCHEDULE ON [deleted].[BONDINTERESTSCHEDULEID] = [BONDINTERESTSCHEDULE].[BONDINTERESTSCHEDULEID]
			INNER JOIN CAPAYMENTMETHOD [CAPAYMENTMETHOD_DELETED] WITH (NOLOCK) ON [deleted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD_DELETED].[CAPAYMENTMETHODID]
END