CREATE TABLE [dbo].[JURISDICTION] (
    [JURISDICTIONID]                CHAR (36)      NOT NULL,
    [NAME]                          NVARCHAR (100) NOT NULL,
    [DESCRIPTION]                   NVARCHAR (255) NULL,
    [CAFINANCIALINTEGRATIONSETUPID] CHAR (36)      NULL,
    [LASTCHANGEDBY]                 CHAR (36)      NULL,
    [LASTCHANGEDON]                 DATETIME       CONSTRAINT [DF_JURISDICTION_LASTCHANGEDON] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]                    INT            CONSTRAINT [DF_JURISDICTION_ROWVERSION] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_JURISDICTION] PRIMARY KEY CLUSTERED ([JURISDICTIONID] ASC),
    CONSTRAINT [FK_JURISDICTION_CAFINANCIALINTEGRATIONSETUP] FOREIGN KEY ([CAFINANCIALINTEGRATIONSETUPID]) REFERENCES [dbo].[CAFINANCIALINTEGRATIONSETUP] ([CAFINANCIALINTEGRATIONSETUPID]),
    CONSTRAINT [FK_JURISDICTION_USERS] FOREIGN KEY ([LASTCHANGEDBY]) REFERENCES [dbo].[USERS] ([SUSERGUID]),
    CONSTRAINT [UC_JURISDICTION_NAME] UNIQUE NONCLUSTERED ([NAME] ASC)
);


GO

CREATE TRIGGER [TG_JURISDICTION_INSERT]
    ON [dbo].[JURISDICTION]
    FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[JURISDICTIONID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Jurisdiction Added',
			'',
			'',
			'Jurisdiction (' + [inserted].[NAME] + ')',
			'354BD1E6-4C7B-466F-86C6-7A53056638C7',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]
END
GO
CREATE TRIGGER [dbo].[TG_JURISDICTION_UPDATE] 
    ON [dbo].[JURISDICTION]
    AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[inserted].[JURISDICTIONID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Jurisdiction (' + [inserted].[NAME] + ')',
			'354BD1E6-4C7B-466F-86C6-7A53056638C7',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[JURISDICTIONID] = [inserted].[JURISDICTIONID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]

	UNION ALL

	SELECT
			[inserted].[JURISDICTIONID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Description',
			ISNULL([deleted].[DESCRIPTION],'[none]'),
			ISNULL([inserted].[DESCRIPTION],'[none]'),
			'Jurisdiction (' + [inserted].[NAME] + ')',
			'354BD1E6-4C7B-466F-86C6-7A53056638C7',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[JURISDICTIONID] = [inserted].[JURISDICTIONID]	
	WHERE	ISNULL([deleted].[DESCRIPTION], '') <> ISNULL([inserted].[DESCRIPTION], '')

	UNION ALL

	SELECT
			[inserted].[JURISDICTIONID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Financial Integration Setup',
			ISNULL([DELETED_INT].[NAME],'[none]'),
			ISNULL([INSERTED_INT].[NAME],'[none]'),
			'Jurisdiction (' + [inserted].[NAME] + ')',
			'354BD1E6-4C7B-466F-86C6-7A53056638C7',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[JURISDICTIONID] = [inserted].[JURISDICTIONID]	
			JOIN [CAFINANCIALINTEGRATIONSETUP] AS DELETED_INT ON [deleted].[CAFINANCIALINTEGRATIONSETUPID] = [DELETED_INT].[CAFINANCIALINTEGRATIONSETUPID]
			JOIN [CAFINANCIALINTEGRATIONSETUP] AS INSERTED_INT ON [inserted].[CAFINANCIALINTEGRATIONSETUPID] = [INSERTED_INT].[CAFINANCIALINTEGRATIONSETUPID]
	WHERE	ISNULL([deleted].[CAFINANCIALINTEGRATIONSETUPID], '') <> ISNULL([inserted].[CAFINANCIALINTEGRATIONSETUPID], '')
END
GO

CREATE TRIGGER [TG_JURISDICTION_DELETE] 
    ON [dbo].[JURISDICTION]
    AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[deleted].[JURISDICTIONID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Jurisdiction Deleted',
			'',
			'',
			'Jurisdiction (' + [deleted].[NAME] + ')',
			'354BD1E6-4C7B-466F-86C6-7A53056638C7',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END