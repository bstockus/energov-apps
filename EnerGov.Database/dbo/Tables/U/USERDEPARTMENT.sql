CREATE TABLE [dbo].[USERDEPARTMENT] (
    [SUSERGUID]    CHAR (36) NOT NULL,
    [DEPARTMENTID] CHAR (36) NOT NULL,
    CONSTRAINT [PK_UserDepartment] PRIMARY KEY CLUSTERED ([SUSERGUID] ASC, [DEPARTMENTID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_UserDepartment_Department] FOREIGN KEY ([DEPARTMENTID]) REFERENCES [dbo].[DEPARTMENT] ([DEPARTMENTID]),
    CONSTRAINT [FK_UserDepartment_Users] FOREIGN KEY ([SUSERGUID]) REFERENCES [dbo].[USERS] ([SUSERGUID])
);


GO
CREATE TRIGGER [TG_USERDEPARTMENT_DELETE] ON USERDEPARTMENT 
AFTER DELETE
AS
BEGIN
       SET NOCOUNT ON; 
	  
	    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'User Assigned Department Deleted',
			'',
			'',
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Department (' + [DEPARTMENT_DELETED].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			3,
			0,
			[DEPARTMENT_DELETED].[NAME]
	FROM	[deleted]
	INNER JOIN USERS ON [USERS].[SUSERGUID] = [deleted].[SUSERGUID]
	INNER JOIN DEPARTMENT [DEPARTMENT_DELETED] WITH (NOLOCK) ON [DEPARTMENT_DELETED].[DEPARTMENTID] = [deleted].[DEPARTMENTID]
END
GO


CREATE TRIGGER [TG_USERDEPARTMENT_INSERT] ON [USERDEPARTMENT]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			[USERS].[LASTCHANGEDBY],
			'User Assigned Department Added',
			'',
			'',
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Department (' + [DEPARTMENT_INSERTED].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			1,
			0,
			[DEPARTMENT_INSERTED].[NAME]
	FROM	[inserted]
	INNER JOIN USERS ON [USERS].[SUSERGUID] = [inserted].[SUSERGUID]
	INNER JOIN DEPARTMENT [DEPARTMENT_INSERTED] WITH (NOLOCK) ON [DEPARTMENT_INSERTED].[DEPARTMENTID] = [inserted].[DEPARTMENTID]
END