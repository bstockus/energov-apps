CREATE TABLE [dbo].[RPTCUSTOMFIELD] (
    [RPTCUSTOMFIELDID]         CHAR (36) NOT NULL,
    [REPORTID]                 CHAR (36) NOT NULL,
    [CUSTOMFIELDOBJECTID]      CHAR (36) NOT NULL,
    [REPORTCUSTOMFIELDSECTION] INT       NOT NULL,
    [ORDER]                    INT       NOT NULL,
    CONSTRAINT [PK_RPTCUSTOMFIELD] PRIMARY KEY CLUSTERED ([RPTCUSTOMFIELDID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_RPTCUSTFIELD_REPORT] FOREIGN KEY ([REPORTID]) REFERENCES [dbo].[RPTREPORT] ([RPTREPORTID])
);


GO

CREATE TRIGGER [dbo].[TG_RPTCUSTOMFIELD_INSERT]
   ON  [dbo].[RPTCUSTOMFIELD]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[RPTREPORT].[RPTREPORTID],
			[RPTREPORT].[ROWVERSION],
			GETUTCDATE(),
			[RPTREPORT].[LASTCHANGEDBY],
			'Report Custom Field Added',
			'',
			'',
			'Report (' + ISNULL([RPTREPORT].[REPORTNAME],'[none]') + '), Display on Report Name (' + ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]') +')',
			'0BF524E7-8340-4265-B97F-F6A04C18ADFA',
			1,
			0,
			ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]')
	FROM	[inserted]
	INNER JOIN [RPTREPORT] ON [RPTREPORT].[RPTREPORTID] = [inserted].[REPORTID]
	LEFT JOIN [CUSTOMFIELDOBJECT] ON [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD] = [inserted].[CUSTOMFIELDOBJECTID]
	LEFT JOIN [CUSTOMFIELD] ON [CUSTOMFIELD].[GCUSTOMFIELD] = [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD]
END
GO

CREATE TRIGGER [dbo].[TG_RPTCUSTOMFIELD_UPDATE] 
	ON [dbo].[RPTCUSTOMFIELD]
    AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[RPTREPORT].[RPTREPORTID],
			[RPTREPORT].[ROWVERSION],
			GETUTCDATE(),
			[RPTREPORT].[LASTCHANGEDBY],
			'Order',
			CONVERT(NVARCHAR(MAX),[deleted].[ORDER]),
			CONVERT(NVARCHAR(MAX),[inserted].[ORDER]),
			'Report (' + ISNULL([RPTREPORT].[REPORTNAME],'[none]') + '), Display on Report Name (' + ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]') +')',
			'0BF524E7-8340-4265-B97F-F6A04C18ADFA',
			2,
			0,
			ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]')
	FROM	[deleted]
			INNER JOIN [inserted] ON [deleted].[RPTCUSTOMFIELDID] = [inserted].[RPTCUSTOMFIELDID]
			INNER JOIN [RPTREPORT] ON [RPTREPORT].[RPTREPORTID] = [inserted].[REPORTID]	
			LEFT JOIN [CUSTOMFIELDOBJECT] ON [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD] = [inserted].[CUSTOMFIELDOBJECTID]
			LEFT JOIN [CUSTOMFIELD] ON [CUSTOMFIELD].[GCUSTOMFIELD] = [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD]
	WHERE	[deleted].[ORDER] <> [inserted].[ORDER]
	UNION ALL

	SELECT
			[RPTREPORT].[RPTREPORTID],
			[RPTREPORT].[ROWVERSION],
			GETUTCDATE(),
			[RPTREPORT].[LASTCHANGEDBY],
			'Report Custom Field Selection Flag',
			CASE [deleted].[REPORTCUSTOMFIELDSECTION] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[REPORTCUSTOMFIELDSECTION] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'Report (' + ISNULL([RPTREPORT].[REPORTNAME],'[none]') + '), Display on Report Name (' + ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]') +')',
			'0BF524E7-8340-4265-B97F-F6A04C18ADFA',
			2,
			0,
			ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]')
	FROM	[deleted]
			INNER JOIN [inserted] ON [deleted].[RPTCUSTOMFIELDID] = [inserted].[RPTCUSTOMFIELDID]
			INNER JOIN [RPTREPORT] ON [RPTREPORT].[RPTREPORTID] = [inserted].[REPORTID]	
			LEFT JOIN [CUSTOMFIELDOBJECT] ON [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD] = [inserted].[CUSTOMFIELDOBJECTID]
			LEFT JOIN [CUSTOMFIELD] ON [CUSTOMFIELD].[GCUSTOMFIELD] = [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD]
	WHERE	[deleted].[REPORTCUSTOMFIELDSECTION] <> [inserted].[REPORTCUSTOMFIELDSECTION]
END
GO

CREATE TRIGGER [dbo].[TG_RPTCUSTOMFIELD_DELETE]
   ON  [dbo].[RPTCUSTOMFIELD]
   AFTER DELETE
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[RPTREPORT].[RPTREPORTID],
			[RPTREPORT].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Report Custom Field Deleted',
			'',
			'',
			'Report (' + ISNULL([RPTREPORT].[REPORTNAME],'[none]') + '), Display on Report Name (' + ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]') +')',
			'0BF524E7-8340-4265-B97F-F6A04C18ADFA',
			3,
			0,
			ISNULL([CUSTOMFIELD].[SFIELDNAME],'[none]')
	FROM	[deleted]
	INNER JOIN [RPTREPORT] ON [RPTREPORT].[RPTREPORTID] = [deleted].[REPORTID]	
	LEFT JOIN [CUSTOMFIELDOBJECT] ON [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD] = [deleted].[CUSTOMFIELDOBJECTID]
	LEFT JOIN [CUSTOMFIELD] ON [CUSTOMFIELD].[GCUSTOMFIELD] = [CUSTOMFIELDOBJECT].[FKGCUSTOMFIELD]
END