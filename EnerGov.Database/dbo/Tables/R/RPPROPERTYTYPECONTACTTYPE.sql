CREATE TABLE [dbo].[RPPROPERTYTYPECONTACTTYPE] (
    [RPPROPERTYTYPECONTACTTYPEID] CHAR (36) NOT NULL,
    [RPCONTACTTYPEID]             CHAR (36) NOT NULL,
    [RPPROPERTYTYPEID]            CHAR (36) NOT NULL,
    [CONTACTTYPEGROUP]            INT       NULL,
    [ISREQUIRED]                  BIT       NOT NULL,
    CONSTRAINT [PK_RPPROPERTYTYPECONTACTTYPE] PRIMARY KEY NONCLUSTERED ([RPPROPERTYTYPECONTACTTYPEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_RPPROP_RPCONTACTTYPE] FOREIGN KEY ([RPCONTACTTYPEID]) REFERENCES [dbo].[RPCONTACTTYPE] ([RPCONTACTTYPEID]),
    CONSTRAINT [FK_RPPROPTYPECONTACTTYPE_TYPE] FOREIGN KEY ([RPPROPERTYTYPEID]) REFERENCES [dbo].[RPPROPERTYTYPE] ([RPPROPERTYTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [RPPROPERTYTYPECONTACTTYPE_IX_RPPROPERTYTYPEID]
    ON [dbo].[RPPROPERTYTYPECONTACTTYPE]([RPPROPERTYTYPEID] ASC);


GO

CREATE TRIGGER [TG_RPPROPERTYTYPECONTACTTYPE_DELETE] ON [RPPROPERTYTYPECONTACTTYPE]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO]
	)
	SELECT
			[RPPROPERTYTYPE].[RPPROPERTYTYPEID],
			[RPPROPERTYTYPE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Property Type Contact Type Deleted',
			'',
			'',
			'Property Type (' + [RPPROPERTYTYPE].[NAME] + '), Contact Type (' + [RPCONTACTTYPE].[NAME] + ')'
	FROM	[deleted]	
	INNER JOIN [RPPROPERTYTYPE] ON [deleted].[RPPROPERTYTYPEID] = [RPPROPERTYTYPE].[RPPROPERTYTYPEID]	
	INNER JOIN [RPCONTACTTYPE] WITH (NOLOCK) ON [deleted].[RPCONTACTTYPEID] = [RPCONTACTTYPE].[RPCONTACTTYPEID]
END
GO

CREATE TRIGGER [dbo].[TG_RPPROPERTYTYPECONTACTTYPE_UPDATE] ON  [dbo].[RPPROPERTYTYPECONTACTTYPE]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;

	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO]
    )
	SELECT
			[RPPROPERTYTYPE].[RPPROPERTYTYPEID],
			[RPPROPERTYTYPE].[ROWVERSION],
			GETUTCDATE(),
			[RPPROPERTYTYPE].[LASTCHANGEDBY],
			'Property Type Contact Type Set',
			ISNULL(CONVERT(NVARCHAR(MAX), [deleted].[CONTACTTYPEGROUP]),'[none]'),
			ISNULL(CONVERT(NVARCHAR(MAX), [inserted].[CONTACTTYPEGROUP]),'[none]'),
			'Property Type (' + [RPPROPERTYTYPE].[NAME] + '), Contact Type (' + [RPCONTACTTYPE].[NAME] + ')'
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[RPPROPERTYTYPEID] = [inserted].[RPPROPERTYTYPEID]
			INNER JOIN [RPPROPERTYTYPE] ON [inserted].[RPPROPERTYTYPEID] = [RPPROPERTYTYPE].[RPPROPERTYTYPEID]
			INNER JOIN [RPCONTACTTYPE] WITH (NOLOCK) ON [RPCONTACTTYPE].[RPCONTACTTYPEID] = [inserted].[RPCONTACTTYPEID]
	WHERE	ISNULL([deleted].[CONTACTTYPEGROUP], '') <> ISNULL([inserted].[CONTACTTYPEGROUP], '')
	UNION ALL

	SELECT
			[RPPROPERTYTYPE].[RPPROPERTYTYPEID],
			[RPPROPERTYTYPE].[ROWVERSION],
			GETUTCDATE(),
			[RPPROPERTYTYPE].[LASTCHANGEDBY],
			'Property Type Contact Type Is Required Flag',
			CASE [deleted].[ISREQUIRED] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISREQUIRED] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'Property Type (' + [RPPROPERTYTYPE].[NAME] + '), Contact Type (' + [RPCONTACTTYPE].[NAME] + ')'
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[RPPROPERTYTYPEID] = [inserted].[RPPROPERTYTYPEID]
			INNER JOIN [RPPROPERTYTYPE] ON [inserted].[RPPROPERTYTYPEID] = [RPPROPERTYTYPE].[RPPROPERTYTYPEID]
			INNER JOIN [RPCONTACTTYPE] WITH (NOLOCK) ON [RPCONTACTTYPE].[RPCONTACTTYPEID] = [inserted].[RPCONTACTTYPEID]
	WHERE	[deleted].[ISREQUIRED] <> [inserted].[ISREQUIRED]
END
GO
CREATE TRIGGER [TG_RPPROPERTYTYPECONTACTTYPE_INSERT] ON [RPPROPERTYTYPECONTACTTYPE]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO]
	)
	SELECT 
			[RPPROPERTYTYPE].[RPPROPERTYTYPEID],
			[RPPROPERTYTYPE].[ROWVERSION],
			GETUTCDATE(),
			[RPPROPERTYTYPE].[LASTCHANGEDBY],
			'Property Type Contact Type Added',
			'',
			'',
			'Property Type (' + [RPPROPERTYTYPE].[NAME] + '), Contact Type (' + [RPCONTACTTYPE].[NAME] + ')'
	FROM	[inserted]
	INNER JOIN [RPPROPERTYTYPE] ON [inserted].[RPPROPERTYTYPEID] = [RPPROPERTYTYPE].[RPPROPERTYTYPEID]	
	INNER JOIN [RPCONTACTTYPE] WITH (NOLOCK) ON [inserted].[RPCONTACTTYPEID] = [RPCONTACTTYPE].[RPCONTACTTYPEID]
END