CREATE TABLE [dbo].[ROLESROLESSETTING] (
    [ROLESSETTINGID] CHAR (36)      NOT NULL,
    [ROLEID]         CHAR (36)      NOT NULL,
    [BITVALUE]       BIT            CONSTRAINT [DF_RolesRolesSetting_BitValue] DEFAULT ((0)) NULL,
    [STRINGVALUE]    NVARCHAR (150) CONSTRAINT [DF_RolesRolesSetting_StringValue] DEFAULT (N'') NULL,
    [INTVALUE]       INT            CONSTRAINT [DF_RolesRolesSetting_IntValue] DEFAULT ((0)) NULL,
    CONSTRAINT [PK_RolesRolesSetting] PRIMARY KEY CLUSTERED ([ROLESSETTINGID] ASC, [ROLEID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_RolesRolesSetting_Roles] FOREIGN KEY ([ROLEID]) REFERENCES [dbo].[ROLES] ([SROLEGUID]),
    CONSTRAINT [FK_RolesRolesSetting_RolesSett] FOREIGN KEY ([ROLESSETTINGID]) REFERENCES [dbo].[ROLESSETTING] ([ROLESSETTINGID])
);


GO
CREATE NONCLUSTERED INDEX [IDX_ROLESROLESETTING_ROLEID]
    ON [dbo].[ROLESROLESSETTING]([ROLEID] ASC);


GO

CREATE TRIGGER [TG_ROLESROLESSETTING_INSERT] ON [ROLESROLESSETTING]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'User Role - Security Option Added',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Security Option (' + [ROLESSETTING].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			1,
			0,
			[ROLESSETTING].[NAME]
	FROM	[inserted]
	INNER JOIN [ROLES] ON [inserted].[ROLEID] = [ROLES].[SROLEGUID]
	INNER JOIN [ROLESSETTING] WITH (NOLOCK) ON [ROLESSETTING].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]
END
GO

CREATE TRIGGER [dbo].[TG_ROLESROLESSETTING_UPDATE] ON  [dbo].[ROLESROLESSETTING]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			[ROLESSETTING].[NAME] +' Flag',
			CASE [deleted].[BITVALUE] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' ELSE '[none]' END,
			CASE [inserted].[BITVALUE] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' ELSE '[none]' END,			
			'User Role (' + [ROLES].[ID] + '), Security Option (' + [ROLESSETTING].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[ROLESSETTING].[NAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [ROLESSETTING] WITH (NOLOCK) ON [ROLESSETTING].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]	
	WHERE	[deleted].[BITVALUE] <> [inserted].[BITVALUE]
	OR ([deleted].[BITVALUE] IS NULL AND [inserted].[BITVALUE] IS NOT NULL)
	OR ([deleted].[BITVALUE] IS NOT NULL AND [inserted].[BITVALUE] IS NULL)
	UNION ALL

	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Map Url',
			ISNULL([deleted].[STRINGVALUE],'[none]'),
			ISNULL([inserted].[STRINGVALUE],'[none]'),
			'User Role (' + [ROLES].[ID] + '), Security Option (' + [ROLESSETTING].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[ROLESSETTING].[NAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [ROLESSETTING] WITH (NOLOCK) ON [ROLESSETTING].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]	
	WHERE	ISNULL([deleted].[STRINGVALUE],'') <> ISNULL([inserted].[STRINGVALUE],'')
	UNION ALL

	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Integer Value',
			ISNULL(CAST([deleted].[INTVALUE] AS NVARCHAR(MAX)),'[none]'),
			ISNULL(CAST([inserted].[INTVALUE] AS NVARCHAR(MAX)),'[none]'),
			'User Role (' + [ROLES].[ID] + '), Security Option (' + [ROLESSETTING].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[ROLESSETTING].[NAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [ROLESSETTING] WITH (NOLOCK) ON [ROLESSETTING].[ROLESSETTINGID] = [inserted].[ROLESSETTINGID]	
	WHERE	ISNULL([deleted].[INTVALUE],'') <> ISNULL([inserted].[INTVALUE],'')
END