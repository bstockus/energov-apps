CREATE TABLE [dbo].[ROLEPAYMENTMETHODXREF] (
    [ROLEPAYMENTMETHODXREFID] CHAR (36) NOT NULL,
    [SROLEGUID]               CHAR (36) NOT NULL,
    [CAPAYMENTMETHODID]       CHAR (36) NOT NULL,
    [DISABLEDFLAG]            BIT       NOT NULL,
    CONSTRAINT [PK_ROLEPAYMENTMETHODXREF] PRIMARY KEY NONCLUSTERED ([ROLEPAYMENTMETHODXREFID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_ROLEPAYMENT_PMYT] FOREIGN KEY ([CAPAYMENTMETHODID]) REFERENCES [dbo].[CAPAYMENTMETHOD] ([CAPAYMENTMETHODID]),
    CONSTRAINT [FK_ROLEPAYMENT_ROLES] FOREIGN KEY ([SROLEGUID]) REFERENCES [dbo].[ROLES] ([SROLEGUID])
);


GO
CREATE NONCLUSTERED INDEX [ROLEPAYMENTMETHODXREF_IX_SROLEGUID]
    ON [dbo].[ROLEPAYMENTMETHODXREF]([SROLEGUID] ASC);


GO

CREATE TRIGGER [dbo].[TG_ROLEPAYMENTMETHODXREF_UPDATE] ON  [dbo].[ROLEPAYMENTMETHODXREF]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
			
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Is Disabled Flag',			
			CASE [deleted].[DISABLEDFLAG] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[DISABLEDFLAG] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'User Role (' + [ROLES].[ID] + '), Payment Method Role Security (' + [CAPAYMENTMETHOD].[NAME] +')',			
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[CAPAYMENTMETHOD].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[SROLEGUID] = [inserted].[SROLEGUID]			
			INNER JOIN [ROLES] ON [inserted].[SROLEGUID] = [ROLES].[SROLEGUID]		
			INNER JOIN [CAPAYMENTMETHOD] WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD].[CAPAYMENTMETHODID]
	WHERE	[deleted].[DISABLEDFLAG] <> [inserted].[DISABLEDFLAG]	

END
GO

CREATE TRIGGER [TG_ROLEPAYMENTMETHODXREF_INSERT] ON [ROLEPAYMENTMETHODXREF]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'User Role - Payment Method Role Security Added',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Payment Method Role Security (' + [CAPAYMENTMETHOD].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			1,
			0,
			[CAPAYMENTMETHOD].[NAME]
	FROM	[inserted]
	INNER JOIN [ROLES] ON [inserted].[SROLEGUID] = [ROLES].[SROLEGUID]
	INNER JOIN [CAPAYMENTMETHOD] WITH (NOLOCK) ON [inserted].[CAPAYMENTMETHODID] = [CAPAYMENTMETHOD].[CAPAYMENTMETHODID]
END