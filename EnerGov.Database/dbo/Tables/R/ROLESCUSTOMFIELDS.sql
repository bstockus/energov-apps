CREATE TABLE [dbo].[ROLESCUSTOMFIELDS] (
    [ROLECUSTOMFIELDID]           CHAR (36) NOT NULL,
    [ROLEID]                      CHAR (36) NOT NULL,
    [CUSTOMFIELDID]               CHAR (36) NOT NULL,
    [ISHIDDEN]                    BIT       NOT NULL,
    [ISDISABLED]                  BIT       NOT NULL,
    [CUSTOMFIELDTABLECOLUMNREFID] CHAR (36) NULL,
    CONSTRAINT [PK_RolesCustomFields] PRIMARY KEY CLUSTERED ([ROLECUSTOMFIELDID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_RolesCustomFields_ColumnRef] FOREIGN KEY ([CUSTOMFIELDTABLECOLUMNREFID]) REFERENCES [dbo].[CUSTOMFIELDTABLECOLUMNREF] ([CUSTOMFIELDTABLECOLUMNREFID]),
    CONSTRAINT [FK_RolesCustomFields_CustomFie] FOREIGN KEY ([CUSTOMFIELDID]) REFERENCES [dbo].[CUSTOMFIELD] ([GCUSTOMFIELD]),
    CONSTRAINT [FK_RolesCustomFields_Roles] FOREIGN KEY ([ROLEID]) REFERENCES [dbo].[ROLES] ([SROLEGUID])
);


GO
CREATE UNIQUE NONCLUSTERED INDEX [IX_RolesCustomFields]
    ON [dbo].[ROLESCUSTOMFIELDS]([ROLEID] ASC, [CUSTOMFIELDID] ASC, [CUSTOMFIELDTABLECOLUMNREFID] ASC);


GO

CREATE TRIGGER [dbo].[TG_ROLESCUSTOMFIELDS_UPDATE] ON  [dbo].[ROLESCUSTOMFIELDS]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Hidden Flag',
			CASE [deleted].[ISHIDDEN] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISHIDDEN] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'User Role (' + [ROLES].[ID] + '), Custom Field (' + [CUSTOMFIELD].[SFIELDNAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[CUSTOMFIELD].[SFIELDNAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLECUSTOMFIELDID] = [inserted].[ROLECUSTOMFIELDID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [CUSTOMFIELD] WITH (NOLOCK) ON [CUSTOMFIELD].[GCUSTOMFIELD] = [inserted].[CUSTOMFIELDID]	
	WHERE	[deleted].[ISHIDDEN] <> [inserted].[ISHIDDEN]
	UNION ALL

	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Disable Flag',
			CASE [deleted].[ISDISABLED] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISDISABLED] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'User Role (' + [ROLES].[ID] + '), Custom Field (' + [CUSTOMFIELD].[SFIELDNAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[CUSTOMFIELD].[SFIELDNAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLECUSTOMFIELDID] = [inserted].[ROLECUSTOMFIELDID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [CUSTOMFIELD] WITH (NOLOCK) ON [CUSTOMFIELD].[GCUSTOMFIELD] = [inserted].[CUSTOMFIELDID]	
	WHERE	[deleted].[ISDISABLED] <> [inserted].[ISDISABLED]
	UNION ALL

	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Custom Field Reference Id',
			[CUSTOMFIELDTABLECOLUMNREF_DELETED].[DISPLAYNAME],
			[CUSTOMFIELDTABLECOLUMNREF_INSERTED].[DISPLAYNAME],
			'User Role (' + [ROLES].[ID] + '), Custom Field (' + [CUSTOMFIELD].[SFIELDNAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[CUSTOMFIELD].[SFIELDNAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLECUSTOMFIELDID] = [inserted].[ROLECUSTOMFIELDID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [CUSTOMFIELD] WITH (NOLOCK) ON [CUSTOMFIELD].[GCUSTOMFIELD] = [inserted].[CUSTOMFIELDID]	
	LEFT JOIN [CUSTOMFIELDTABLECOLUMNREF] CUSTOMFIELDTABLECOLUMNREF_DELETED WITH (NOLOCK) ON [CUSTOMFIELDTABLECOLUMNREF_DELETED].[CUSTOMFIELDTABLECOLUMNREFID] = [deleted].[CUSTOMFIELDTABLECOLUMNREFID]
	LEFT JOIN [CUSTOMFIELDTABLECOLUMNREF] CUSTOMFIELDTABLECOLUMNREF_INSERTED WITH (NOLOCK) ON [CUSTOMFIELDTABLECOLUMNREF_INSERTED].[CUSTOMFIELDTABLECOLUMNREFID] = [inserted].[CUSTOMFIELDTABLECOLUMNREFID]
	WHERE	ISNULL([deleted].[CUSTOMFIELDTABLECOLUMNREFID], '') <> ISNULL([inserted].[CUSTOMFIELDTABLECOLUMNREFID], '')
END
GO

CREATE TRIGGER [TG_ROLESCUSTOMFIELDS_INSERT] ON [ROLESCUSTOMFIELDS]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'User Role - Custom Field Added',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Custom Field (' + [CUSTOMFIELD].[SFIELDNAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			1,
			0,
			[CUSTOMFIELD].[SFIELDNAME]
	FROM	[inserted]
	INNER JOIN [ROLES] ON [inserted].[ROLEID] = [ROLES].[SROLEGUID]
	INNER JOIN [CUSTOMFIELD] WITH (NOLOCK) ON [CUSTOMFIELD].[GCUSTOMFIELD] = [inserted].[CUSTOMFIELDID]
END
GO

CREATE TRIGGER [TG_ROLESCUSTOMFIELDS_DELETE] ON [ROLESCUSTOMFIELDS]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'User Role - Custom Field Deleted',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Custom Field (' + [CUSTOMFIELD].[SFIELDNAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			3,
			0,
			[CUSTOMFIELD].[SFIELDNAME]
	FROM	[deleted]	
	INNER JOIN [ROLES] ON [deleted].[ROLEID] = [ROLES].[SROLEGUID]	
	INNER JOIN [CUSTOMFIELD] WITH (NOLOCK) ON [CUSTOMFIELD].[GCUSTOMFIELD] = [deleted].[CUSTOMFIELDID]

END