CREATE TABLE [dbo].[RESOURCE] (
    [RESOURCEID]           CHAR (36)     NOT NULL,
    [USERID]               CHAR (36)     NOT NULL,
    [UNAVAILABLESTARTDATE] DATETIME      NOT NULL,
    [UNAVAILABLEENDDATE]   DATETIME      NULL,
    [REASON]               VARCHAR (MAX) NULL,
    [DELEGATEUSERID]       CHAR (36)     NULL,
    CONSTRAINT [PK_RESOURCE] PRIMARY KEY NONCLUSTERED ([RESOURCEID] ASC),
    CONSTRAINT [FK_RESOURCE_DELEGATEUSER] FOREIGN KEY ([DELEGATEUSERID]) REFERENCES [dbo].[USERS] ([SUSERGUID]),
    CONSTRAINT [FK_RESOURCE_USER] FOREIGN KEY ([USERID]) REFERENCES [dbo].[USERS] ([SUSERGUID])
);


GO
CREATE NONCLUSTERED INDEX [IX_RESOURCE_USER]
    ON [dbo].[RESOURCE]([USERID] ASC);


GO

CREATE TRIGGER TRG_RESOURCE_UPDATE_IMINSPECTORAVAILABILITY
ON [RESOURCE]
AFTER UPDATE  
AS

	BEGIN 
	 IF TRIGGER_NESTLEVEL() > 1
     RETURN;

		WITH UPDATES AS 
		(SELECT I.UNAVAILABLESTARTDATE, I.UNAVAILABLEENDDATE, I.USERID, D.UNAVAILABLESTARTDATE OLDSTARTDATE, D.UNAVAILABLEENDDATE OLDENDDATE, I.DELEGATEUSERID
		FROM INSERTED I
		INNER JOIN DELETED D ON I.RESOURCEID = D.RESOURCEID
		)

		UPDATE IMINSPECTORAVAILABILITY
		SET    
			IMINSPECTORAVAILABILITY.STARTDATE = U.UNAVAILABLESTARTDATE,
			IMINSPECTORAVAILABILITY.ENDDATE = U.UNAVAILABLEENDDATE,
			IMINSPECTORAVAILABILITY.DELEGATEINSPECTOR = U.DELEGATEUSERID
		FROM IMINSPECTORAVAILABILITY I
		INNER JOIN UPDATES U ON I.USERID = U.USERID AND I.STARTDATE = U.OLDSTARTDATE AND I.ENDDATE = U.OLDENDDATE
	END
GO

CREATE TRIGGER TRG_RESOURCE_INSERT_IMINSPECTORAVAILABILITY
ON [RESOURCE]
AFTER INSERT  
AS

	BEGIN 
	 IF TRIGGER_NESTLEVEL() > 1
     RETURN
		INSERT INTO IMINSPECTORAVAILABILITY (IMINSPECTORAVAILABILITYID, USERID, STARTDATE,ENDDATE, DELEGATEINSPECTOR)
		SELECT NEWID(), I.USERID, I.UNAVAILABLESTARTDATE, I.UNAVAILABLEENDDATE, I.DELEGATEUSERID
		FROM INSERTED I
	END
GO

CREATE TRIGGER TRG_RESOURCE_DELETE_IMINSPECTORAVAILABILITY
ON [RESOURCE]
AFTER DELETE
AS

	BEGIN 
	 IF TRIGGER_NESTLEVEL() > 1
     RETURN
		DELETE I FROM IMINSPECTORAVAILABILITY I
		INNER JOIN DELETED D ON I.USERID = D.USERID AND I.STARTDATE = D.UNAVAILABLESTARTDATE AND I.ENDDATE = D.UNAVAILABLEENDDATE
	END