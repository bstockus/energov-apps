CREATE TABLE [dbo].[ROLEBILLINGRATEXREF] (
    [ROLEBILLINGRATEXREFID] CHAR (36) NOT NULL,
    [ROLEID]                CHAR (36) NOT NULL,
    [BILLINGRATEID]         CHAR (36) NOT NULL,
    [ISDEFAULT]             BIT       CONSTRAINT [DF_RoleBillingRateXRef_IsDefault] DEFAULT ((0)) NOT NULL,
    CONSTRAINT [PK_RoleBillingRateXRef] PRIMARY KEY CLUSTERED ([ROLEBILLINGRATEXREFID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_RoleBillingRateXRef_BillingRate] FOREIGN KEY ([BILLINGRATEID]) REFERENCES [dbo].[BILLINGRATE] ([BILLINGRATEID]),
    CONSTRAINT [FK_RoleBillingRateXRef_Roles] FOREIGN KEY ([ROLEID]) REFERENCES [dbo].[ROLES] ([SROLEGUID]),
    CONSTRAINT [IX_RoleBillingRateXRef] UNIQUE NONCLUSTERED ([BILLINGRATEID] ASC, [ROLEID] ASC) WITH (FILLFACTOR = 90)
);


GO
CREATE NONCLUSTERED INDEX [ROLEBILLINGRATEXREF_IX_ROLEID]
    ON [dbo].[ROLEBILLINGRATEXREF]([ROLEID] ASC, [BILLINGRATEID] ASC);


GO

CREATE TRIGGER [TG_ROLEBILLINGRATEXREF_DELETE] ON [ROLEBILLINGRATEXREF]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'User Role - Billable Rate Deleted',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Billable Rate (' + [BILLINGRATE].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			3,
			0,
			[BILLINGRATE].[NAME]
	FROM	[deleted]	
	INNER JOIN [ROLES] ON [deleted].[ROLEID] = [ROLES].[SROLEGUID]	
	INNER JOIN [BILLINGRATE] WITH (NOLOCK) ON [BILLINGRATE].[BILLINGRATEID] = [deleted].[BILLINGRATEID]
END
GO

CREATE TRIGGER [TG_ROLEBILLINGRATEXREF_INSERT] ON [ROLEBILLINGRATEXREF]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'User Role - Billable Rate Added',
			'',
			'',
			'User Role (' + [ROLES].[ID] + '), Billable Rate (' + [BILLINGRATE].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			1,
			0,
			[BILLINGRATE].[NAME]
	FROM	[inserted]
	INNER JOIN [ROLES] ON [inserted].[ROLEID] = [ROLES].[SROLEGUID]
	INNER JOIN [BILLINGRATE] WITH (NOLOCK) ON [BILLINGRATE].[BILLINGRATEID] = [inserted].[BILLINGRATEID]
END
GO

CREATE TRIGGER [dbo].[TG_ROLEBILLINGRATEXREF_UPDATE] ON  [dbo].[ROLEBILLINGRATEXREF]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[ROLES].[SROLEGUID],
			[ROLES].[ROWVERSION],
			GETUTCDATE(),
			[ROLES].[LASTCHANGEDBY],
			'Default Flag',
			CASE [deleted].[ISDEFAULT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISDEFAULT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'User Role (' + [ROLES].[ID] + '), Billable Rate (' + [BILLINGRATE].[NAME] + ')',
			'801F270F-912F-420A-91D6-82EBC3F351F3',
			2,
			0,
			[BILLINGRATE].[NAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[ROLEBILLINGRATEXREFID] = [inserted].[ROLEBILLINGRATEXREFID]
	INNER JOIN [ROLES] ON [ROLES].[SROLEGUID] = [inserted].[ROLEID]
	INNER JOIN [BILLINGRATE] WITH (NOLOCK) ON [BILLINGRATE].[BILLINGRATEID] = [inserted].[BILLINGRATEID]	
	WHERE	[deleted].[ISDEFAULT] <> [inserted].[ISDEFAULT]
END