CREATE TABLE [dbo].[GISLLTRANSLATION] (
    [GISLLTRANSLATIONID] CHAR (36)     NOT NULL,
    [GISLLMAPPING]       CHAR (36)     NOT NULL,
    [ENERGOVVALUE]       VARCHAR (MAX) NULL,
    [EXTERNALVALUE]      VARCHAR (MAX) NULL,
    CONSTRAINT [PK_GISLLTranslation] PRIMARY KEY CLUSTERED ([GISLLTRANSLATIONID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_GISLLTrnsltion_GISLLMpng] FOREIGN KEY ([GISLLMAPPING]) REFERENCES [dbo].[GISLLMAPPING] ([GISLLMAPPINGID])
);


GO
CREATE NONCLUSTERED INDEX [GISLLTRANSLATION_IX_QUERY]
    ON [dbo].[GISLLTRANSLATION]([GISLLTRANSLATIONID] ASC);


GO

CREATE TRIGGER [TG_GISLLTRANSLATION_DELETE] ON [GISLLTRANSLATION]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[GISLLDATASOURCE].[GISLLDATASOURCEID],
			[GISLLDATASOURCE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Live Link Translation Deleted',
			'',
			'',
			'Live Link (' + ISNULL([GISLLDATASOURCE].[ARCGISLAYER],'[none]') + '), Translation (' + ISNULL([deleted].[ENERGOVVALUE],'[none]') + ')',
			'9B9CF7DC-8C6A-444A-B5B7-F50664942297',
			3,
			0,
			ISNULL([deleted].[ENERGOVVALUE],'[none]')
	FROM	[deleted]	
			JOIN	[GISLLMAPPING] AS [GISLLMAPPING] WITH (NOLOCK) ON [GISLLMAPPING].[GISLLMAPPINGID] = [deleted].[GISLLMAPPING]
			JOIN	[GISLLDATASOURCE] AS [GISLLDATASOURCE] WITH (NOLOCK) ON [GISLLDATASOURCE].[GISLLDATASOURCEID] = [GISLLMAPPING].[GISLLDATASOURCE]
END
GO

CREATE TRIGGER [TG_GISLLTRANSLATION_INSERT] ON [GISLLTRANSLATION]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[GISLLDATASOURCE].[GISLLDATASOURCEID],
			[GISLLDATASOURCE].[ROWVERSION],
			GETUTCDATE(),
			[GISLLDATASOURCE].[LASTCHANGEDBY],
			'Live Link Translation Added',
			'',
			'',
			'Live Link (' + ISNULL([GISLLDATASOURCE].[ARCGISLAYER],'[none]') + '), Translation (' + ISNULL([inserted].[ENERGOVVALUE],'[none]') + ')',
			'9B9CF7DC-8C6A-444A-B5B7-F50664942297',
			1,
			0,
			ISNULL([inserted].[ENERGOVVALUE],'[none]')
	FROM	[inserted]
			JOIN	[GISLLMAPPING] AS [GISLLMAPPING] WITH (NOLOCK) ON [GISLLMAPPING].[GISLLMAPPINGID] = [inserted].[GISLLMAPPING]
			JOIN	[GISLLDATASOURCE] AS [GISLLDATASOURCE] WITH (NOLOCK) ON [GISLLDATASOURCE].[GISLLDATASOURCEID] = [GISLLMAPPING].[GISLLDATASOURCE]
			
END
GO

CREATE TRIGGER [dbo].[TG_GISLLTRANSLATION_UPDATE] ON  [dbo].[GISLLTRANSLATION]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	
	SELECT 
			[GISLLDATASOURCE].[GISLLDATASOURCEID],
			[GISLLDATASOURCE].[ROWVERSION],
			GETUTCDATE(),
			[GISLLDATASOURCE].[LASTCHANGEDBY],
			'EnerGov Value',
			ISNULL([deleted].[ENERGOVVALUE],'[none]'),
			ISNULL([inserted].[ENERGOVVALUE],'[none]'),
			'Live Link (' + ISNULL([GISLLDATASOURCE].[ARCGISLAYER],'[none]') + '), Translation (' + ISNULL([inserted].[ENERGOVVALUE],'[none]') + ')',
			'9B9CF7DC-8C6A-444A-B5B7-F50664942297',
			2,
			0,
			ISNULL([inserted].[ENERGOVVALUE],'[none]')
	FROM	[deleted] JOIN [inserted] ON [deleted].[GISLLTRANSLATIONID] = [inserted].[GISLLTRANSLATIONID]
			JOIN	[GISLLMAPPING] AS [GISLLMAPPING] WITH (NOLOCK) ON [GISLLMAPPING].[GISLLMAPPINGID] = [inserted].[GISLLMAPPING]
			JOIN	[GISLLDATASOURCE] AS [GISLLDATASOURCE] WITH (NOLOCK) ON [GISLLDATASOURCE].[GISLLDATASOURCEID] = [GISLLMAPPING].[GISLLDATASOURCE]			
	WHERE	ISNULL([deleted].[ENERGOVVALUE],'') <> ISNULL([inserted].[ENERGOVVALUE],'')
	UNION ALL

	SELECT 
			[GISLLDATASOURCE].[GISLLDATASOURCEID],
			[GISLLDATASOURCE].[ROWVERSION],
			GETUTCDATE(),
			[GISLLDATASOURCE].[LASTCHANGEDBY],
			'External Value',
			ISNULL([deleted].[EXTERNALVALUE],'[none]'),
			ISNULL([inserted].[EXTERNALVALUE],'[none]'),
			'Live Link (' + ISNULL([GISLLDATASOURCE].[ARCGISLAYER],'[none]') + '), Translation (' + ISNULL([inserted].[ENERGOVVALUE],'[none]') + ')',
			'9B9CF7DC-8C6A-444A-B5B7-F50664942297',
			2,
			0,
			ISNULL([inserted].[ENERGOVVALUE],'[none]')
	FROM	[deleted] JOIN [inserted] ON [deleted].[GISLLTRANSLATIONID] = [inserted].[GISLLTRANSLATIONID]
			JOIN	[GISLLMAPPING] AS [GISLLMAPPING] WITH (NOLOCK) ON [GISLLMAPPING].[GISLLMAPPINGID] = [inserted].[GISLLMAPPING]
			JOIN	[GISLLDATASOURCE] AS [GISLLDATASOURCE] WITH (NOLOCK) ON [GISLLDATASOURCE].[GISLLDATASOURCEID] = [GISLLMAPPING].[GISLLDATASOURCE]			
	WHERE	ISNULL([deleted].[EXTERNALVALUE],'') <> ISNULL([inserted].[EXTERNALVALUE],'')
END