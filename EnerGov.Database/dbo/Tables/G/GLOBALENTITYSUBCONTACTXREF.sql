CREATE TABLE [dbo].[GLOBALENTITYSUBCONTACTXREF] (
    [GLOBALENTITYSUBCONTACTXREFID] CHAR (36) NOT NULL,
    [PARENTGLOBALENTITYID]         CHAR (36) NOT NULL,
    [SUBCONTACTGLOBALENTITYID]     CHAR (36) NOT NULL,
    CONSTRAINT [PK_GlobalEntitySubContactXRef] PRIMARY KEY CLUSTERED ([GLOBALENTITYSUBCONTACTXREFID] ASC),
    CONSTRAINT [FK_GlobalEntitySubContactXref_GlobalEntity] FOREIGN KEY ([PARENTGLOBALENTITYID]) REFERENCES [dbo].[GLOBALENTITY] ([GLOBALENTITYID]),
    CONSTRAINT [FK_GlobalEntitySubContactXref_GlobalEntity1] FOREIGN KEY ([SUBCONTACTGLOBALENTITYID]) REFERENCES [dbo].[GLOBALENTITY] ([GLOBALENTITYID]),
    CONSTRAINT [IX_GlobalEntitySubContactXref] UNIQUE NONCLUSTERED ([PARENTGLOBALENTITYID] ASC, [SUBCONTACTGLOBALENTITYID] ASC)
);


GO

CREATE TRIGGER [TG_GLOBALENTITYSUBCONTACTXREF_INSERT] ON  GLOBALENTITYSUBCONTACTXREF
   AFTER INSERT
AS 
BEGIN
	SET NOCOUNT ON;

	IF NOT EXISTS (SELECT * FROM [GLOBALENTITY] 
		INNER JOIN [Inserted] ON [GLOBALENTITY].[GLOBALENTITYID] = [Inserted].[SUBCONTACTGLOBALENTITYID] AND [GLOBALENTITY].[PARENTGLOBALENTITYID] = [Inserted].[PARENTGLOBALENTITYID])
	BEGIN
		UPDATE [GLOBALENTITY]
		SET [GLOBALENTITY].[PARENTGLOBALENTITYID] = [inserted].[PARENTGLOBALENTITYID],
			[GLOBALENTITY].[LASTCHANGEDON] = GETUTCDATE(),
			[GLOBALENTITY].[LASTCHANGEDBY] = PARENTGLOBALENTITY.[LASTCHANGEDBY]
		FROM [GLOBALENTITY] 
			INNER JOIN [Inserted] ON [GLOBALENTITY].[GLOBALENTITYID] = [Inserted].[SUBCONTACTGLOBALENTITYID]
			INNER JOIN [GLOBALENTITY] AS PARENTGLOBALENTITY ON [inserted].[PARENTGLOBALENTITYID] =  PARENTGLOBALENTITY.[GLOBALENTITYID]
	END
END
GO

CREATE TRIGGER [TG_GLOBALENTITYSUBCONTACTXREF_DELETE] ON  GLOBALENTITYSUBCONTACTXREF
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

	IF EXISTS (SELECT * FROM [GLOBALENTITY] 
		INNER JOIN [deleted]
		ON [GLOBALENTITY].[GLOBALENTITYID] = [deleted].[SUBCONTACTGLOBALENTITYID] AND [GLOBALENTITY].[PARENTGLOBALENTITYID] = [deleted].[PARENTGLOBALENTITYID])
	BEGIN
		UPDATE [GLOBALENTITY]
		SET [GLOBALENTITY].[PARENTGLOBALENTITYID] = (SELECT TOP 1 [PARENTGLOBALENTITYID] FROM [GLOBALENTITYSUBCONTACTXREF] WHERE [SUBCONTACTGLOBALENTITYID] = [deleted].[SUBCONTACTGLOBALENTITYID]),
			[GLOBALENTITY].[LASTCHANGEDON] = GETUTCDATE(),
			[GLOBALENTITY].[LASTCHANGEDBY] = PARENTGLOBALENTITY.[LASTCHANGEDBY]
		FROM [GLOBALENTITY] 
			INNER JOIN [deleted] 
			ON [GLOBALENTITY].[GLOBALENTITYID] = [deleted].[SUBCONTACTGLOBALENTITYID] AND [GLOBALENTITY].[PARENTGLOBALENTITYID] = [deleted].[PARENTGLOBALENTITYID]
			INNER JOIN [GLOBALENTITY] AS PARENTGLOBALENTITY ON [deleted].[PARENTGLOBALENTITYID] =  PARENTGLOBALENTITY.[GLOBALENTITYID]
	END

END