CREATE TABLE [dbo].[GISMAPLAYERXREF] (
    [MAPLAYERSXREFID] CHAR (36) NOT NULL,
    [GISMAPLAYERID]   CHAR (36) NOT NULL,
    [SUSERGUID]       CHAR (36) CONSTRAINT [DF_GISMapLayerXRef_sUserGUID] DEFAULT (newid()) NOT NULL,
    [SORTORDER]       INT       CONSTRAINT [DF_GISMAPLAYERXREF_SORTORDER] DEFAULT ((0)) NOT NULL,
    [DEFAULTACTIVE]   BIT       DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_GISMapLayersXRef] PRIMARY KEY CLUSTERED ([MAPLAYERSXREFID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_GISMapLayerXRef_GISMapLayer1] FOREIGN KEY ([GISMAPLAYERID]) REFERENCES [dbo].[GISMAPLAYER] ([GISMAPLAYERID]),
    CONSTRAINT [FK_GISMapLayerXRef_Users] FOREIGN KEY ([SUSERGUID]) REFERENCES [dbo].[USERS] ([SUSERGUID])
);


GO
CREATE NONCLUSTERED INDEX [GISMAPLAYERXREF_IX_MAPLAYERSXREFID]
    ON [dbo].[GISMAPLAYERXREF]([MAPLAYERSXREFID] ASC);


GO

CREATE TRIGGER [TG_GISMAPLAYERXREF_DELETE] ON [GISMAPLAYERXREF]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'User GIS Map Layer Deleted',
			'',
			'',
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Map Layer ('+ [GISMAPLAYER].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			3,
			0,
			[GISMAPLAYER].[NAME]
	FROM	[deleted]	
	INNER JOIN [USERS] ON [deleted].[SUSERGUID] = [USERS].[SUSERGUID]
	INNER JOIN [GISMAPLAYER] WITH (NOLOCK) ON [deleted].[GISMAPLAYERID] = [GISMAPLAYER].[GISMAPLAYERID]
END
GO

CREATE TRIGGER [TG_GISMAPLAYERXREF_INSERT] ON [GISMAPLAYERXREF]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			[USERS].[LASTCHANGEDBY],
			'User GIS Map Layer Added',
			'',
			'',
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Map Layer ('+ [GISMAPLAYER].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			1,
			0,
			[GISMAPLAYER].[NAME]
	FROM	[inserted]
	INNER JOIN [USERS] ON [inserted].[SUSERGUID] = [USERS].[SUSERGUID]
	INNER JOIN [GISMAPLAYER] WITH (NOLOCK) ON [inserted].[GISMAPLAYERID] = [GISMAPLAYER].[GISMAPLAYERID]
END
GO
CREATE TRIGGER [dbo].[TG_GISMAPLAYERXREF_UPDATE] ON  [dbo].[GISMAPLAYERXREF]
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			[USERS].[LASTCHANGEDBY],
			'Sort Order',
			CONVERT(NVARCHAR(MAX), [deleted].[SORTORDER]),
			CONVERT(NVARCHAR(MAX), [inserted].[SORTORDER]),
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Map Layer ('+ [GISMAPLAYER].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			2,
			0,
			[GISMAPLAYER].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[MAPLAYERSXREFID] = [inserted].[MAPLAYERSXREFID]
			INNER JOIN [USERS] ON [deleted].[SUSERGUID] = [USERS].[SUSERGUID]
			INNER JOIN [GISMAPLAYER] WITH (NOLOCK) ON [inserted].[GISMAPLAYERID] = [GISMAPLAYER].[GISMAPLAYERID]
	WHERE	([deleted].[SORTORDER] <> [inserted].[SORTORDER])
	UNION ALL

	SELECT
			[USERS].[SUSERGUID],
			[USERS].[ROWVERSION],
			GETUTCDATE(),
			[USERS].[LASTCHANGEDBY],
			'Default Active Flag',
			CASE [deleted].[DEFAULTACTIVE] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[DEFAULTACTIVE] WHEN 1 THEN 'Yes' ELSE 'No'  END,
			'User (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + '), Map Layer ('+ [GISMAPLAYER].[NAME] + ')',
			'4282BC67-F36D-41D4-9558-E8BD736B1204',
			2,
			0,
			[GISMAPLAYER].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[MAPLAYERSXREFID] = [inserted].[MAPLAYERSXREFID]
			INNER JOIN [USERS] ON [deleted].[SUSERGUID] = [USERS].[SUSERGUID]
			INNER JOIN [GISMAPLAYER] WITH (NOLOCK) ON [inserted].[GISMAPLAYERID] = [GISMAPLAYER].[GISMAPLAYERID]
	WHERE	([deleted].[DEFAULTACTIVE] <> [inserted].[DEFAULTACTIVE])	
END