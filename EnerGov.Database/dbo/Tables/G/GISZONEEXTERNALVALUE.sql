CREATE TABLE [dbo].[GISZONEEXTERNALVALUE] (
    [GISZONEEXTERNALVALUEID] CHAR (36)     NOT NULL,
    [GISZONEMAPPINGID]       CHAR (36)     NOT NULL,
    [VALUE]                  VARCHAR (250) NOT NULL,
    [IMINSPECTIONZONEID]     CHAR (36)     NULL,
    CONSTRAINT [PK_GISZoneExternalValue] PRIMARY KEY CLUSTERED ([GISZONEEXTERNALVALUEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_GISZoneExternalValue_GISZoneMapping] FOREIGN KEY ([GISZONEMAPPINGID]) REFERENCES [dbo].[GISZONEMAPPING] ([GISZONEMAPPINGID]),
    CONSTRAINT [FK_IMInspectionZone_GISZoneExternalValue] FOREIGN KEY ([IMINSPECTIONZONEID]) REFERENCES [dbo].[IMINSPECTIONZONE] ([IMINSPECTIONZONEID])
);


GO
CREATE NONCLUSTERED INDEX [GISZONEEXTERNALVALUE_IX_GISZONEMAPPINGID]
    ON [dbo].[GISZONEEXTERNALVALUE]([GISZONEMAPPINGID] ASC);


GO

CREATE TRIGGER [TG_GISZONEEXTERNALVALUE_DELETE] ON [GISZONEEXTERNALVALUE]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[GISZONEMAPPING].[GISZONEMAPPINGID],
			[GISZONEMAPPING].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Zone Inspector Mapping GIS Value Deleted',
			'',
			'',
			'Zone Inspector Mapping (' + [GISZONEMAPPING].[SOURCENAME] + '), GIS Value (' + [deleted].[VALUE] + ')',			
			'D2A6823F-93FB-45DF-8F63-BE59E04D0E23',
			3,
			0,
			[deleted].[VALUE]
	FROM	[deleted]
	INNER JOIN [GISZONEMAPPING]
      ON [GISZONEMAPPING].[GISZONEMAPPINGID] = [deleted].[GISZONEMAPPINGID]
END
GO

CREATE TRIGGER [TG_GISZONEEXTERNALVALUE_INSERT] ON [GISZONEEXTERNALVALUE]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[GISZONEMAPPING].[GISZONEMAPPINGID],
			[GISZONEMAPPING].[ROWVERSION],
			GETUTCDATE(),
			[GISZONEMAPPING].[LASTCHANGEDBY],
			'Zone Inspector Mapping GIS Value Added',
			'',
			'',
			'Zone Inspector Mapping (' + [GISZONEMAPPING].[SOURCENAME] + '), GIS Value (' + [inserted].[VALUE] + ')',			
			'D2A6823F-93FB-45DF-8F63-BE59E04D0E23',
			1,
			0,
			[inserted].[VALUE]
	FROM	[inserted]	
	INNER JOIN [GISZONEMAPPING]
      ON [GISZONEMAPPING].[GISZONEMAPPINGID] = [inserted].[GISZONEMAPPINGID]
END
GO

CREATE TRIGGER [TG_GISZONEEXTERNALVALUE_UPDATE] ON [GISZONEEXTERNALVALUE]
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[GISZONEMAPPING].[GISZONEMAPPINGID],
			[GISZONEMAPPING].[ROWVERSION],
			GETUTCDATE(),
			[GISZONEMAPPING].[LASTCHANGEDBY],
			'GIS Value',
			[deleted].[VALUE],
			[inserted].[VALUE],
			'Zone Inspector Mapping (' + [GISZONEMAPPING].[SOURCENAME] + '), GIS Value (' + [inserted].[VALUE] + ')',			
			'D2A6823F-93FB-45DF-8F63-BE59E04D0E23',
			2,
			0,
			[inserted].[VALUE]	
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GISZONEEXTERNALVALUEID] = [inserted].[GISZONEEXTERNALVALUEID]
			INNER JOIN [GISZONEMAPPING]
      ON [GISZONEMAPPING].[GISZONEMAPPINGID] = [inserted].[GISZONEMAPPINGID]
	WHERE	[deleted].[VALUE] <> [inserted].[VALUE]	
	UNION ALL
	SELECT
			[GISZONEMAPPING].[GISZONEMAPPINGID],
			[GISZONEMAPPING].[ROWVERSION],
			GETUTCDATE(),
			[GISZONEMAPPING].[LASTCHANGEDBY],
			'Mapped EnerGov Zone',
			ISNULL([IMINSPECTIONZONE_DELETED].[NAME], '[none]'),
			ISNULL([IMINSPECTIONZONE_INSERTED].[NAME], '[none]'),
			'Zone Inspector Mapping (' + [GISZONEMAPPING].[SOURCENAME] + '), GIS Value (' + [inserted].[VALUE] + ')',			
			'D2A6823F-93FB-45DF-8F63-BE59E04D0E23',
			2,
			0,
			[inserted].[VALUE]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GISZONEEXTERNALVALUEID] = [inserted].[GISZONEEXTERNALVALUEID]
			LEFT JOIN IMINSPECTIONZONE IMINSPECTIONZONE_DELETED WITH (NOLOCK) ON [deleted].[IMINSPECTIONZONEID] = [IMINSPECTIONZONE_DELETED].[IMINSPECTIONZONEID]
			LEFT JOIN IMINSPECTIONZONE IMINSPECTIONZONE_INSERTED WITH (NOLOCK) ON [inserted].[IMINSPECTIONZONEID] = [IMINSPECTIONZONE_INSERTED].[IMINSPECTIONZONEID]
			INNER JOIN [GISZONEMAPPING]
      ON [GISZONEMAPPING].[GISZONEMAPPINGID] = [inserted].[GISZONEMAPPINGID]
	WHERE ISNULL([deleted].[IMINSPECTIONZONEID],'') <> ISNULL([inserted].[IMINSPECTIONZONEID],'')	
END