CREATE TABLE [dbo].[GISMAPFEATUREXREF] (
    [GISMAPFEATUREXREFID] CHAR (36) NOT NULL,
    [GMAPID]              CHAR (36) CONSTRAINT [DF_GISMapFeatureXRef_gMapID] DEFAULT (newid()) NOT NULL,
    [GISFEATURECLASSID]   CHAR (36) NOT NULL,
    CONSTRAINT [PK_GISMapFeatureXRef] PRIMARY KEY CLUSTERED ([GISMAPFEATUREXREFID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_GISMapFeatureXRef_GISFeatureClass] FOREIGN KEY ([GISFEATURECLASSID]) REFERENCES [dbo].[GISFEATURECLASS] ([GISFEATURECLASSID]),
    CONSTRAINT [FK_GISMapFeatureXRef_GISMap] FOREIGN KEY ([GMAPID]) REFERENCES [dbo].[GISMAP] ([GMAPID])
);


GO
CREATE NONCLUSTERED INDEX [GISMAPFEATUREXREF_IX_GMAPID]
    ON [dbo].[GISMAPFEATUREXREF]([GMAPID] ASC);


GO

CREATE TRIGGER [dbo].[TG_GISMAPFEATUREXREF_DELETE] ON [dbo].[GISMAPFEATUREXREF]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[GISMAP].[GMAPID],
			[GISMAP].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'GIS Feature Deleted',
			'',
			'',
			'GIS Map (' + [GISMAP].[SMAPNAME] + '), GIS Feature (' + [GISFEATURECLASS].[ALIAS] + ')',
			'D85FB7D7-45FE-4FE0-88BE-FBD958E41153',
			3,
			0,
			[GISFEATURECLASS].[ALIAS]
	FROM	[deleted]
			JOIN [GISMAP] ON [deleted].[GMAPID] = [GISMAP].[GMAPID]
			JOIN [GISFEATURECLASS] WITH (NOLOCK) ON [deleted].[GISFEATURECLASSID] = [GISFEATURECLASS].[GISFEATURECLASSID]
END
GO

CREATE TRIGGER [dbo].[TG_GISMAPFEATUREXREF_INSERT] ON [dbo].[GISMAPFEATUREXREF]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP] 
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[GISMAP].[GMAPID],
			[GISMAP].[ROWVERSION],
			GETUTCDATE(),
			[GISMAP].[LASTCHANGEDBY],
			'GIS Feature Added',
			'',
			'',
			'GIS Map (' + [GISMAP].[SMAPNAME] + '), GIS Feature (' + [GISFEATURECLASS].[ALIAS] + ')',
			'D85FB7D7-45FE-4FE0-88BE-FBD958E41153',
			1,
			0,
			[GISFEATURECLASS].[ALIAS]
	FROM	[inserted]
			JOIN [GISMAP] ON [inserted].[GMAPID] = [GISMAP].[GMAPID]
			JOIN [GISFEATURECLASS] WITH (NOLOCK) ON [inserted].[GISFEATURECLASSID] = [GISFEATURECLASS].[GISFEATURECLASSID]
END