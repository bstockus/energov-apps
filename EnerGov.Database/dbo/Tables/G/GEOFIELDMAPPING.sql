CREATE TABLE [dbo].[GEOFIELDMAPPING] (
    [GEOFIELDMAPPINGID]    CHAR (36)      NOT NULL,
    [GEORULEPROCESSID]     CHAR (36)      NOT NULL,
    [ENTITYPROPERTY]       NVARCHAR (MAX) NOT NULL,
    [GISATTRIBUTENAME]     NVARCHAR (250) NOT NULL,
    [PROPERTYFRIENDLYNAME] NVARCHAR (MAX) NOT NULL,
    [ISCUSTOMFIELD]        BIT            NOT NULL,
    [CUSTOMFIELDID]        NVARCHAR (50)  CONSTRAINT [DF_GeoFieldMapping_CustomFieldID] DEFAULT ('') NULL,
    CONSTRAINT [PK_PLGeoFieldMapping] PRIMARY KEY CLUSTERED ([GEOFIELDMAPPINGID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_FieldMapping_Process] FOREIGN KEY ([GEORULEPROCESSID]) REFERENCES [dbo].[GEORULEPROCESS] ([GEORULEPROCESSID])
);


GO
CREATE NONCLUSTERED INDEX [GEOFIELDMAPPING_IX_GEORULEPROCESSID]
    ON [dbo].[GEOFIELDMAPPING]([GEORULEPROCESSID] ASC);


GO

CREATE TRIGGER [TG_GEOFIELDMAPPING_DELETE] ON [GEOFIELDMAPPING]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[GEORULE].[GEORULEID],
			[GEORULE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Geo Rule Process Mapping Deleted',
			'',
			'',
			'Geo Rule (' + [GEORULE].[RULENAME] + '), Process (' + [GEORULEPROCESS].[PROCESSNAME] + '), Mapping GIS Field (' + [deleted].[GISATTRIBUTENAME] + ') Entity Field (' + [deleted].[PROPERTYFRIENDLYNAME] + ')',
			'92E08D84-E807-4CC4-936A-04DCD65AF43A',
			3,
			0,
			'GIS Field (' + [deleted].[GISATTRIBUTENAME] + ') Entity Field (' + [deleted].[PROPERTYFRIENDLYNAME] + ')'
	FROM	[deleted]
			JOIN [GEORULEPROCESS] ON [deleted].[GEORULEPROCESSID] = [GEORULEPROCESS].[GEORULEPROCESSID]
			JOIN [GEORULE] ON [GEORULEPROCESS].[GEORULEID] = [GEORULE].[GEORULEID]
END
GO

CREATE TRIGGER [dbo].[TG_GEOFIELDMAPPING_UPDATE] ON [dbo].[GEOFIELDMAPPING] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
			
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[GEORULE].[GEORULEID],
			[GEORULE].[ROWVERSION],
			GETUTCDATE(),
			[GEORULE].[LASTCHANGEDBY],
			'GIS Field',
			[deleted].[GISATTRIBUTENAME],
			[inserted].[GISATTRIBUTENAME],
			'Geo Rule (' + [GEORULE].[RULENAME] + '), Process (' + [GEORULEPROCESS].[PROCESSNAME] + '), Mapping GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')',
			'92E08D84-E807-4CC4-936A-04DCD65AF43A',
			2,
			0,
			'GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')'
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GEOFIELDMAPPINGID] = [inserted].[GEOFIELDMAPPINGID]
			JOIN [GEORULEPROCESS] ON [inserted].[GEORULEPROCESSID] = [GEORULEPROCESS].[GEORULEPROCESSID]
			JOIN [GEORULE] ON [GEORULEPROCESS].[GEORULEID] = [GEORULE].[GEORULEID]
	WHERE	[deleted].[GISATTRIBUTENAME] <> [inserted].[GISATTRIBUTENAME]

	UNION ALL 
			SELECT 
			[GEORULE].[GEORULEID],
			[GEORULE].[ROWVERSION],
			GETUTCDATE(),
			[GEORULE].[LASTCHANGEDBY],
			'Entity Field',
			[deleted].[PROPERTYFRIENDLYNAME],
			[inserted].[PROPERTYFRIENDLYNAME],
			'Geo Rule (' + [GEORULE].[RULENAME] + '), Process (' + [GEORULEPROCESS].[PROCESSNAME] + '), Mapping GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')',
			'92E08D84-E807-4CC4-936A-04DCD65AF43A',
			2,
			0,
			'GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')'
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GEOFIELDMAPPINGID] = [inserted].[GEOFIELDMAPPINGID]
			JOIN [GEORULEPROCESS] ON [inserted].[GEORULEPROCESSID] = [GEORULEPROCESS].[GEORULEPROCESSID]
			JOIN [GEORULE] ON [GEORULEPROCESS].[GEORULEID] = [GEORULE].[GEORULEID]
	WHERE	[deleted].[PROPERTYFRIENDLYNAME] <> [inserted].[PROPERTYFRIENDLYNAME]
END
GO

CREATE TRIGGER [TG_GEOFIELDMAPPING_INSERT] ON [GEOFIELDMAPPING]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT 
			[GEORULE].[GEORULEID],
			[GEORULE].[ROWVERSION],
			GETUTCDATE(),
			[GEORULE].[LASTCHANGEDBY],
			'Geo Rule Process Mapping Added',
			'',
			'',
			'Geo Rule (' + [GEORULE].[RULENAME] + '), Process (' + [GEORULEPROCESS].[PROCESSNAME] + '), Mapping GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')',
			'92E08D84-E807-4CC4-936A-04DCD65AF43A',
			1,
			0,
			'GIS Field (' + [inserted].[GISATTRIBUTENAME] + ') Entity Field (' + [inserted].[PROPERTYFRIENDLYNAME] + ')'
	FROM	[inserted]
			JOIN [GEORULEPROCESS] ON [inserted].[GEORULEPROCESSID] = [GEORULEPROCESS].[GEORULEPROCESSID]
			JOIN [GEORULE] ON [GEORULEPROCESS].[GEORULEID] = [GEORULE].[GEORULEID]
END