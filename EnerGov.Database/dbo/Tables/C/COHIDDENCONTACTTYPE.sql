CREATE TABLE [dbo].[COHIDDENCONTACTTYPE] (
    [COHIDDENCONTACTTYPEID] CHAR (36) NOT NULL,
    [SYSTEMMODULEID]        INT       NOT NULL,
    [SROLEGUID]             CHAR (36) NOT NULL,
    [CONTACTTYPEID]         CHAR (36) NOT NULL,
    CONSTRAINT [PK_COHIDDENCONTACTTYPES] PRIMARY KEY NONCLUSTERED ([COHIDDENCONTACTTYPEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_COHIDCONTYPE_CODECASECONTYP] FOREIGN KEY ([CONTACTTYPEID]) REFERENCES [dbo].[CMCODECASECONTACTTYPE] ([CMCODECASECONTACTTYPEID]),
    CONSTRAINT [FK_COHIDCONTYPE_ROLE] FOREIGN KEY ([SROLEGUID]) REFERENCES [dbo].[ROLES] ([SROLEGUID]),
    CONSTRAINT [FK_COHIDCONTYPE_SYSMOD] FOREIGN KEY ([SYSTEMMODULEID]) REFERENCES [dbo].[SYSTEMMODULE] ([SYSTEMMODULEID])
);


GO
CREATE NONCLUSTERED INDEX [COHIDDENCONTACTTYPE_IX_SROLEGUID]
    ON [dbo].[COHIDDENCONTACTTYPE]([SROLEGUID] ASC);


GO

CREATE TRIGGER [TG_COHIDDENCONTACTTYPE_DELETE] ON [COHIDDENCONTACTTYPE]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
		   ([ID]
           ,[ROWVERSION]
           ,[CHANGEDON]
           ,[CHANGEDBY]
           ,[FIELDNAME]
           ,[OLDVALUE]
           ,[NEWVALUE]
           ,[ADDITIONALINFO]
           ,[FORMID]
           ,[ACTION]
           ,[ISROOT]
           ,[RECORDNAME])
	SELECT
			[ROLES].[SROLEGUID]
			,[ROLES].[ROWVERSION]
			,GETUTCDATE()
			,(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO())			
			,'User Role Code Management Hidden Contact Deleted'
			,''
			,''
			,'User Role (' + [ROLES].[ID] + '), Code Management Hidden Contact (' + [CMCODECASECONTACTTYPE].[NAME] + ')'
			,'801F270F-912F-420A-91D6-82EBC3F351F3'
			,3
			,0
			,[CMCODECASECONTACTTYPE].[NAME]
	FROM	[deleted]
	INNER JOIN [ROLES] ON [deleted].[SROLEGUID] = [ROLES].[SROLEGUID]
	INNER JOIN [CMCODECASECONTACTTYPE] ON [deleted].[CONTACTTYPEID] = [CMCODECASECONTACTTYPE].[CMCODECASECONTACTTYPEID]
END
GO

CREATE TRIGGER [TG_COHIDDENCONTACTTYPE_UPDATE] ON [COHIDDENCONTACTTYPE]
	AFTER UPDATE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
			([ID]
			,[ROWVERSION]
			,[CHANGEDON]
			,[CHANGEDBY]
			,[FIELDNAME]
			,[OLDVALUE]
			,[NEWVALUE]
			,[ADDITIONALINFO]
			,[FORMID]
			,[ACTION]
			,[ISROOT]
			,[RECORDNAME])
	SELECT
			[ROLES].[SROLEGUID]
			,[ROLES].[ROWVERSION]
			,GETUTCDATE()
			,[ROLES].[LASTCHANGEDBY]
			,'Code Contact Type to Hide'
			,[CMCODECASECONTACTTYPE_DELETED].[NAME]
			,[CMCODECASECONTACTTYPE_INSERTED].[NAME]
			,'User Role (' + [ROLES].[ID] + '), Code Management Hidden Contact (' + [CMCODECASECONTACTTYPE_INSERTED].[NAME] + ')'
			,'801F270F-912F-420A-91D6-82EBC3F351F3'
			,2
			,0
			,[CMCODECASECONTACTTYPE_INSERTED].[NAME]
	FROM	[deleted]
	JOIN [inserted] ON [deleted].[SROLEGUID] = [inserted].[SROLEGUID]
	INNER JOIN [ROLES] ON [inserted].[SROLEGUID] = [ROLES].[SROLEGUID]	
	INNER JOIN [CMCODECASECONTACTTYPE] CMCODECASECONTACTTYPE_DELETED WITH (NOLOCK) ON [deleted].[CONTACTTYPEID] = [CMCODECASECONTACTTYPE_DELETED].[CMCODECASECONTACTTYPEID]
	INNER JOIN [CMCODECASECONTACTTYPE] CMCODECASECONTACTTYPE_INSERTED WITH (NOLOCK) ON [inserted].[CONTACTTYPEID] = [CMCODECASECONTACTTYPE_INSERTED].[CMCODECASECONTACTTYPEID]
	WHERE	[deleted].[CONTACTTYPEID] <> [inserted].[CONTACTTYPEID]
END
GO

CREATE TRIGGER [TG_COHIDDENCONTACTTYPE_INSERT] ON [COHIDDENCONTACTTYPE]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
			([ID]
			,[ROWVERSION]
			,[CHANGEDON]
			,[CHANGEDBY]
			,[FIELDNAME]
			,[OLDVALUE]
			,[NEWVALUE]
			,[ADDITIONALINFO]
			,[FORMID]
			,[ACTION]
			,[ISROOT]
			,[RECORDNAME])	
	SELECT
			[ROLES].[SROLEGUID]
			,[ROLES].[ROWVERSION]
			,[ROLES].[LASTCHANGEDON]
			,[ROLES].[LASTCHANGEDBY]
			,'User Role Code Management Hidden Contact Added'
			,''
			,''
			,'User Role (' + [ROLES].[ID] + '), Code Management Hidden Contact (' + [CMCODECASECONTACTTYPE].[NAME] + ')'
			,'801F270F-912F-420A-91D6-82EBC3F351F3'
			,1
			,0
			,[CMCODECASECONTACTTYPE].[NAME]
	FROM	[inserted]
	INNER JOIN [ROLES] ON [inserted].[SROLEGUID] = [ROLES].[SROLEGUID]
	INNER JOIN [CMCODECASECONTACTTYPE] ON [inserted].[CONTACTTYPEID] = [CMCODECASECONTACTTYPE].[CMCODECASECONTACTTYPEID]
END