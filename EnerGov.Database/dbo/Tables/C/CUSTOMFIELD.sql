CREATE TABLE [dbo].[CUSTOMFIELD] (
    [GCUSTOMFIELD]             CHAR (36)     NOT NULL,
    [FKICUSTOMFIELDTYPE]       INT           NOT NULL,
    [SFIELDNAME]               NVARCHAR (50) NOT NULL,
    [SCUSTOMFIELD]             NVARCHAR (50) NOT NULL,
    [BISPICKLIST]              BIT           NULL,
    [BISWORKSHEET]             BIT           NULL,
    [FKGCUSTOMFIELDTEMPLATE]   CHAR (36)     NULL,
    [FKCUSTOMFIELDTABLE]       CHAR (36)     NULL,
    [FKCUSTOMFIELDTABLECOLREF] CHAR (36)     NULL,
    [COLUMNOPERATOR]           INT           NULL,
    CONSTRAINT [PK_CustomFieldInfo] PRIMARY KEY CLUSTERED ([GCUSTOMFIELD] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_CF_CFT] FOREIGN KEY ([FKCUSTOMFIELDTABLE]) REFERENCES [dbo].[CUSTOMFIELDTABLE] ([CUSTOMFIELDTABLEID]),
    CONSTRAINT [FK_CF_CFTCR] FOREIGN KEY ([FKCUSTOMFIELDTABLECOLREF]) REFERENCES [dbo].[CUSTOMFIELDTABLECOLUMNREF] ([CUSTOMFIELDTABLECOLUMNREFID]),
    CONSTRAINT [FK_CUSTOMFIELD_CFTEMPLATE] FOREIGN KEY ([FKGCUSTOMFIELDTEMPLATE]) REFERENCES [dbo].[CUSTOMFIELDTEMPLATE] ([GCUSTOMFIELDTEMPLATE]),
    CONSTRAINT [FK_CustomFields_CustomFieldTyp] FOREIGN KEY ([FKICUSTOMFIELDTYPE]) REFERENCES [dbo].[CUSTOMFIELDTYPE] ([ICUSTOMFIELDTYPE])
);


GO
CREATE NONCLUSTERED INDEX [IX_CUST_FLD_FLD_TYPE]
    ON [dbo].[CUSTOMFIELD]([GCUSTOMFIELD] ASC, [FKICUSTOMFIELDTYPE] ASC, [SFIELDNAME] ASC) WITH (FILLFACTOR = 80);


GO
CREATE NONCLUSTERED INDEX [IX_CUSTOMFIELD_FKCUSTOMFIELDTABLECOLREF]
    ON [dbo].[CUSTOMFIELD]([FKCUSTOMFIELDTABLECOLREF] ASC);


GO

CREATE TRIGGER [dbo].[TG_CUSTOMFIELD_INSERT] ON  [dbo].[CUSTOMFIELD]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	
	SELECT 
        [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID], 
        [CUSTOMFIELDTABLE].[ROWVERSION],
        GETUTCDATE(),
        [CUSTOMFIELDTABLE].[LASTCHANGEDBY],	
        'Custom Field Table, Column, Column Formula Added',
        '',
        '',       
		'Custom Field Table (' + [CUSTOMFIELDTABLE].[NAME] + '), Column ('+ ISNULL([CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],'[none]') +'), Column Formula ('+[inserted].[SFIELDNAME]+')',
		'490AC525-36DE-475D-8951-13C1F7066C51',
		1,
		0,
		[inserted].[SFIELDNAME]
    FROM [inserted]
	INNER JOIN [dbo].[CUSTOMFIELDTABLECOLUMNREF] ON [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLECOLUMNREFID] = [inserted].[FKCUSTOMFIELDTABLECOLREF]	
	INNER JOIN [dbo].[CUSTOMFIELDTABLE] ON [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLEID]	
	INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] WITH (NOLOCK) ON [CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDCOLUMNTEMPLATEID]
END
GO

CREATE TRIGGER [TG_CUSTOMFIELD_UPDATE] ON CUSTOMFIELD
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;	
    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	
	SELECT
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID],
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[ROWVERSION],
			GETUTCDATE(),
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[LASTCHANGEDBY],
			'Use Template',
			ISNULL(OLD_TEMPLATE.TEMPLATENAME, '[none]'),
			ISNULL(NEW_TEMPLATE.TEMPLATENAME, '[none]'),
			'Custom Field Column (' + [CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME] + ')',
			'8EEEE4B5-765C-44D1-8EC2-DC996FDA0152',
			2,
			0,
			[inserted].[SCUSTOMFIELD]
	FROM	[deleted]
			INNER JOIN [inserted] ON [deleted].[GCUSTOMFIELD] = [inserted].[GCUSTOMFIELD]
			INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] ON [dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[GCUSTOMFIELD] = [INSERTED].[GCUSTOMFIELD]
			LEFT OUTER JOIN [dbo].[CUSTOMFIELDTEMPLATE] AS OLD_TEMPLATE ON OLD_TEMPLATE.[GCUSTOMFIELDTEMPLATE] = [deleted].[FKGCUSTOMFIELDTEMPLATE]
			LEFT OUTER JOIN [dbo].[CUSTOMFIELDTEMPLATE] AS NEW_TEMPLATE ON NEW_TEMPLATE.[GCUSTOMFIELDTEMPLATE] = [inserted].[FKGCUSTOMFIELDTEMPLATE]			
	WHERE	ISNULL([deleted].[FKGCUSTOMFIELDTEMPLATE], '') <> ISNULL([inserted].[FKGCUSTOMFIELDTEMPLATE], '')
	
	UNION ALL
	SELECT 
			[CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID],
			[CUSTOMFIELDTABLE].[ROWVERSION],
			GETUTCDATE(),
			[CUSTOMFIELDTABLE].[LASTCHANGEDBY],
			'Display Name',			
			[deleted].[SFIELDNAME],
			[inserted].[SFIELDNAME],
			'Custom Field Table (' + [CUSTOMFIELDTABLE].[NAME] + '), Column ('+ ISNULL([CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],'[none]') +'), Column Formula ('+[deleted].[SFIELDNAME]+')',
			'490AC525-36DE-475D-8951-13C1F7066C51',
			2,
			0,
			[inserted].[SCUSTOMFIELD]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GCUSTOMFIELD] = [inserted].[GCUSTOMFIELD]			
			INNER JOIN [dbo].[CUSTOMFIELDTABLECOLUMNREF] ON [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLECOLUMNREFID] = [inserted].[FKCUSTOMFIELDTABLECOLREF]	
			INNER JOIN [dbo].[CUSTOMFIELDTABLE] ON [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLEID]	
			INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] WITH (NOLOCK) ON [CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDCOLUMNTEMPLATEID]
	WHERE	[deleted].[SFIELDNAME]<> [inserted].[SFIELDNAME]
	
	UNION ALL
	SELECT 
			[CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID],
			[CUSTOMFIELDTABLE].[ROWVERSION],
			GETUTCDATE(),
			[CUSTOMFIELDTABLE].[LASTCHANGEDBY],
			'Custom Field',			
			[deleted].[SCUSTOMFIELD],
			[inserted].[SCUSTOMFIELD],
			'Custom Field Table (' + [CUSTOMFIELDTABLE].[NAME] + '), Column ('+ ISNULL([CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],'[none]') +'), Column Formula ('+[deleted].[SFIELDNAME]+')',
			'490AC525-36DE-475D-8951-13C1F7066C51',
			2,
			0,
			[inserted].[SCUSTOMFIELD]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GCUSTOMFIELD] = [inserted].[GCUSTOMFIELD]			
			INNER JOIN [dbo].[CUSTOMFIELDTABLECOLUMNREF] ON [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLECOLUMNREFID] = [inserted].[FKCUSTOMFIELDTABLECOLREF]	
			INNER JOIN [dbo].[CUSTOMFIELDTABLE] ON [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLEID]	
			INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] WITH (NOLOCK) ON [CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDCOLUMNTEMPLATEID]
	WHERE	[deleted].[SCUSTOMFIELD] <> [inserted].[SCUSTOMFIELD]
	
	UNION ALL
	SELECT 
			[CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID],
			[CUSTOMFIELDTABLE].[ROWVERSION],
			GETUTCDATE(),
			[CUSTOMFIELDTABLE].[LASTCHANGEDBY],
			'Column Formula',			
			(SELECT [dbo].[GETENUMNAMEBYCOLUMNOPERATOR]([deleted].[COLUMNOPERATOR])),
			(SELECT [dbo].[GETENUMNAMEBYCOLUMNOPERATOR]([inserted].[COLUMNOPERATOR])),
			'Custom Field Table (' + [CUSTOMFIELDTABLE].[NAME] + '), Column ('+ ISNULL([CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],'[none]') +'), Column Formula ('+[deleted].[SFIELDNAME]+')',
			'490AC525-36DE-475D-8951-13C1F7066C51',
			2,
			0,
			[inserted].[SCUSTOMFIELD]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[GCUSTOMFIELD] = [inserted].[GCUSTOMFIELD]			
			INNER JOIN [dbo].[CUSTOMFIELDTABLECOLUMNREF] ON [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLECOLUMNREFID] = [inserted].[FKCUSTOMFIELDTABLECOLREF]	
			INNER JOIN [dbo].[CUSTOMFIELDTABLE] ON [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLEID]	
			INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] WITH (NOLOCK) ON [CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDCOLUMNTEMPLATEID]
	WHERE	[deleted].[COLUMNOPERATOR] <> [inserted].[COLUMNOPERATOR]			
END
GO

CREATE TRIGGER [dbo].[TG_CUSTOMFIELD_DELETE] ON  [dbo].[CUSTOMFIELD]
   AFTER DELETE
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	
	SELECT 
        [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID], 
        [CUSTOMFIELDTABLE].[ROWVERSION],
        GETUTCDATE(),
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),	
        'Custom Field Table, Column, Column Formula Deleted',
        '',
        '',       
		'Custom Field Table (' + [CUSTOMFIELDTABLE].[NAME] + '), Column ('+ ISNULL([CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],'[none]') +'), Column Formula ('+[deleted].[SFIELDNAME]+')',
		'490AC525-36DE-475D-8951-13C1F7066C51',
		3,
		0,
		[deleted].[SFIELDNAME]
    FROM [deleted]
	INNER JOIN [dbo].[CUSTOMFIELDTABLECOLUMNREF] ON [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLECOLUMNREFID] = [deleted].[FKCUSTOMFIELDTABLECOLREF]	
	INNER JOIN [dbo].[CUSTOMFIELDTABLE] ON [CUSTOMFIELDTABLE].[CUSTOMFIELDTABLEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDTABLEID]	
	INNER JOIN [dbo].[CUSTOMFIELDCOLUMNTEMPLATE] WITH (NOLOCK) ON [CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID] = [CUSTOMFIELDTABLECOLUMNREF].[CUSTOMFIELDCOLUMNTEMPLATEID]
END