CREATE TABLE [dbo].[CMCODEVIOLATIONPRIORITY] (
    [CMCODEVIOLATIONPRIORITYID]     CHAR (36)     NOT NULL,
    [NAME]                          NVARCHAR (50) NOT NULL,
    [VIOLATIONPRIORITYSYSTEMTYPEID] INT           NULL,
    [DEFAULTFLAG]                   BIT           DEFAULT ((0)) NOT NULL,
    [LASTCHANGEDBY]                 CHAR (36)     NULL,
    [LASTCHANGEDON]                 DATETIME      CONSTRAINT [DF_CMCodeViolationPriority_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]                    INT           CONSTRAINT [DF_CMCodeViolationPriority_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_CodeViolationPriority] PRIMARY KEY CLUSTERED ([CMCODEVIOLATIONPRIORITYID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CMCODEVIOLATIONPRIORITY_USERS] FOREIGN KEY ([LASTCHANGEDBY]) REFERENCES [dbo].[USERS] ([SUSERGUID]),
    CONSTRAINT [FK_CODEVIOPRI_SYSTYPE] FOREIGN KEY ([VIOLATIONPRIORITYSYSTEMTYPEID]) REFERENCES [dbo].[VIOLATIONPRIORITYSYSTEMTYPE] ([VIOLATIONPRIORITYSYSTEMTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [CMCODEVIOLATIONPRIORITY_IX_QUERY]
    ON [dbo].[CMCODEVIOLATIONPRIORITY]([CMCODEVIOLATIONPRIORITYID] ASC, [NAME] ASC);


GO

CREATE TRIGGER [dbo].[TG_CMCODEVIOLATIONPRIORITY_UPDATE] ON [dbo].[CMCODEVIOLATIONPRIORITY]
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[inserted].[CMCODEVIOLATIONPRIORITYID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Violation Priority (' + [inserted].[NAME] + ')',
			'AFF4FD9D-2C47-4774-AA79-C8ED1DD6549D',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].CMCODEVIOLATIONPRIORITYID = [inserted].CMCODEVIOLATIONPRIORITYID
	WHERE	[deleted].[NAME] <> [inserted].[NAME]
	UNION ALL
	SELECT
			[inserted].[CMCODEVIOLATIONPRIORITYID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Violation Priority System Type',
			ISNULL([VIOLATIONPRIORITYSYSTEMTYPE_DELETED].[NAME], '[none]'),			
			ISNULL([VIOLATIONPRIORITYSYSTEMTYPE_INSERTED].[NAME], '[none]'),
			'Violation Priority (' + [inserted].[NAME] + ')',
			'AFF4FD9D-2C47-4774-AA79-C8ED1DD6549D',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]			
			JOIN [inserted] ON [deleted].CMCODEVIOLATIONPRIORITYID = [inserted].CMCODEVIOLATIONPRIORITYID
			LEFT JOIN VIOLATIONPRIORITYSYSTEMTYPE VIOLATIONPRIORITYSYSTEMTYPE_DELETED WITH (NOLOCK) ON [deleted].VIOLATIONPRIORITYSYSTEMTYPEID = [VIOLATIONPRIORITYSYSTEMTYPE_DELETED].VIOLATIONPRIORITYSYSTEMTYPEID
			LEFT JOIN VIOLATIONPRIORITYSYSTEMTYPE VIOLATIONPRIORITYSYSTEMTYPE_INSERTED WITH (NOLOCK) ON [inserted].VIOLATIONPRIORITYSYSTEMTYPEID = [VIOLATIONPRIORITYSYSTEMTYPE_INSERTED].VIOLATIONPRIORITYSYSTEMTYPEID
	WHERE	ISNULL([deleted].[VIOLATIONPRIORITYSYSTEMTYPEID], '') <> ISNULL([inserted].[VIOLATIONPRIORITYSYSTEMTYPEID], '')	

	UNION ALL
	SELECT
			[inserted].[CMCODEVIOLATIONPRIORITYID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Default Flag',
			CASE WHEN [deleted].[DEFAULTFLAG] = 1 THEN 'Yes' ELSE 'No' END,
			CASE WHEN [inserted].[DEFAULTFLAG] = 1 THEN 'Yes' ELSE 'No' END,
			'Violation Priority (' + [inserted].[NAME] + ')',
			'AFF4FD9D-2C47-4774-AA79-C8ED1DD6549D',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].CMCODEVIOLATIONPRIORITYID = [inserted].CMCODEVIOLATIONPRIORITYID
	WHERE	[deleted].[DEFAULTFLAG] <> [inserted].[DEFAULTFLAG]
END
GO

CREATE TRIGGER [dbo].[TG_CMCODEVIOLATIONPRIORITY_INSERT] ON CMCODEVIOLATIONPRIORITY
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[inserted].[CMCODEVIOLATIONPRIORITYID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Violation Priority Added',
			'',
			'',
			'Violation Priority (' + [inserted].[NAME] + ')',
			'AFF4FD9D-2C47-4774-AA79-C8ED1DD6549D',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]	
END
GO


CREATE TRIGGER [dbo].[TG_CMCODEVIOLATIONPRIORITY_DELETE] ON CMCODEVIOLATIONPRIORITY
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[deleted].[CMCODEVIOLATIONPRIORITYID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Violation Priority Deleted',
			'',
			'',
			'Violation Priority (' + [deleted].[NAME] + ')',
			'AFF4FD9D-2C47-4774-AA79-C8ED1DD6549D',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END