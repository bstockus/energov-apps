CREATE TABLE [dbo].[CAFEEINPUTTRANSLATION] (
    [CAFEEINPUTTRANSLATIONID] CHAR (36)      NOT NULL,
    [CAFEETEMPLATEFEEINPUTID] CHAR (36)      NOT NULL,
    [PROPERTYVALUE]           NVARCHAR (MAX) NULL,
    [TRANSLATEDVALUE]         NVARCHAR (MAX) NULL,
    CONSTRAINT [PK_CAFeeInputTranslation] PRIMARY KEY CLUSTERED ([CAFEEINPUTTRANSLATIONID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_Translate_Input] FOREIGN KEY ([CAFEETEMPLATEFEEINPUTID]) REFERENCES [dbo].[CAFEETEMPLATEFEEINPUT] ([CAFEETEMPLATEFEEINPUTID])
);


GO

CREATE TRIGGER [dbo].[TG_CAFEEINPUTTRANSLATION_DELETE]  ON  [dbo].[CAFEEINPUTTRANSLATION]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[CAFEETEMPLATE].[CAFEETEMPLATEID],
		[CAFEETEMPLATE].[ROWVERSION],
		GETUTCDATE(),			
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Fee Template, Fee/Child Template, Fee Translation Deleted',
        '',
        '',       
		'Fee Template (' + [CAFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee/Child Template (' + [CAFEETEMPLATEFEE].[FEENAME] +'), Fee Input Translation (' + [CAFEETEMPLATEFEEINPUT].[INPUTFRIENDLYNAME] +')',
		'CCA2295B-072A-494B-9C11-91424F59BC16',
		3,
		0,
		[CAFEETEMPLATE].[CAFEETEMPLATENAME]
    FROM [deleted]
	INNER JOIN [dbo].[CAFEETEMPLATEFEEINPUT] ON [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEINPUTID] = [deleted].[CAFEETEMPLATEFEEINPUTID]
	INNER JOIN [dbo].[CAFEETEMPLATEFEE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEFEEID] = [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEID]
	INNER JOIN [dbo].[CAFEETEMPLATE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = [CAFEETEMPLATE].[CAFEETEMPLATEID]
END
GO

CREATE TRIGGER [dbo].[TG_CAFEEINPUTTRANSLATION_INSERT] ON  [dbo].[CAFEEINPUTTRANSLATION]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [CAFEETEMPLATE].[CAFEETEMPLATEID], 
        [CAFEETEMPLATE].[ROWVERSION],
        GETUTCDATE(),
        [CAFEETEMPLATE].[LASTCHANGEDBY],	
		'Fee Template, Fee/Child Template, Fee Translation Added',
        '',
        '',       
		'Fee Template (' + [CAFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee/Child Template (' + [CAFEETEMPLATEFEE].[FEENAME] +'), Fee Input Translation (' + [CAFEETEMPLATEFEEINPUT].[INPUTFRIENDLYNAME] +')',
		'CCA2295B-072A-494B-9C11-91424F59BC16',
		1,
		0,
		[CAFEETEMPLATE].[CAFEETEMPLATENAME]
    FROM [inserted]
	INNER JOIN [dbo].[CAFEETEMPLATEFEEINPUT] ON [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEINPUTID] = [inserted].[CAFEETEMPLATEFEEINPUTID]
	INNER JOIN [dbo].[CAFEETEMPLATEFEE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEFEEID] = [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEID]
	INNER JOIN [dbo].[CAFEETEMPLATE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = [CAFEETEMPLATE].[CAFEETEMPLATEID]
END
GO

CREATE TRIGGER [dbo].[TG_CAFEEINPUTTRANSLATION_UPDATE]   ON  [dbo].[CAFEEINPUTTRANSLATION] 
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[CAFEETEMPLATE].[CAFEETEMPLATEID],
			[CAFEETEMPLATE].[ROWVERSION],
			GETUTCDATE(),			
			[CAFEETEMPLATE].[LASTCHANGEDBY],
			'Property Value',			
			CASE WHEN [deleted].[PROPERTYVALUE] IS NULL THEN '[none]' ELSE CONVERT(NVARCHAR(MAX), [deleted].[PROPERTYVALUE]) END,
			CASE WHEN [inserted].[PROPERTYVALUE] IS NULL THEN '[none]' ELSE CONVERT(NVARCHAR(MAX), [inserted].[PROPERTYVALUE]) END,		
			'Fee Template (' + [CAFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee/Child Template (' + [CAFEETEMPLATEFEE].[FEENAME] +'), Fee Input Translation (' + [CAFEETEMPLATEFEEINPUT].[INPUTFRIENDLYNAME] +')',
			'CCA2295B-072A-494B-9C11-91424F59BC16',
			2,
			0,
			[CAFEETEMPLATE].[CAFEETEMPLATENAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CAFEEINPUTTRANSLATIONID] = [inserted].[CAFEEINPUTTRANSLATIONID]		
			INNER JOIN [dbo].[CAFEETEMPLATEFEEINPUT] ON [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEINPUTID] = [deleted].[CAFEETEMPLATEFEEINPUTID]
			INNER JOIN [dbo].[CAFEETEMPLATEFEE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEFEEID] = [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEID]
			INNER JOIN [dbo].[CAFEETEMPLATE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = [CAFEETEMPLATE].[CAFEETEMPLATEID]
	WHERE	ISNULL([deleted].[PROPERTYVALUE],'') <> ISNULL([inserted].[PROPERTYVALUE],'')

	UNION ALL
	
	SELECT 
			[CAFEETEMPLATE].[CAFEETEMPLATEID],
			[CAFEETEMPLATE].[ROWVERSION],
			GETUTCDATE(),			
			[CAFEETEMPLATE].[LASTCHANGEDBY],
			'Translate Value',			
			CASE WHEN [deleted].[TRANSLATEDVALUE] IS NULL THEN '[none]' ELSE CONVERT(NVARCHAR(MAX), [deleted].[TRANSLATEDVALUE]) END,
			CASE WHEN [inserted].[TRANSLATEDVALUE] IS NULL THEN '[none]' ELSE CONVERT(NVARCHAR(MAX), [inserted].[TRANSLATEDVALUE]) END,		
			'Fee Template (' + [CAFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee/Child Template (' + [CAFEETEMPLATEFEE].[FEENAME] +'), Fee Input Translation (' + [CAFEETEMPLATEFEEINPUT].[INPUTFRIENDLYNAME] +')',
			'CCA2295B-072A-494B-9C11-91424F59BC16',
			2,
			0,
			[CAFEETEMPLATE].[CAFEETEMPLATENAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CAFEEINPUTTRANSLATIONID] = [inserted].[CAFEEINPUTTRANSLATIONID]		
			INNER JOIN [dbo].[CAFEETEMPLATEFEEINPUT] ON [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEINPUTID] = [deleted].[CAFEETEMPLATEFEEINPUTID]
			INNER JOIN [dbo].[CAFEETEMPLATEFEE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEFEEID] = [CAFEETEMPLATEFEEINPUT].[CAFEETEMPLATEFEEID]
			INNER JOIN [dbo].[CAFEETEMPLATE] ON [CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = [CAFEETEMPLATE].[CAFEETEMPLATEID]
	WHERE	ISNULL([deleted].[TRANSLATEDVALUE],'') <> ISNULL([inserted].[TRANSLATEDVALUE],'')
END