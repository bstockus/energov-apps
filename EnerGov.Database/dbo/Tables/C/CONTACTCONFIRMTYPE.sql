CREATE TABLE [dbo].[CONTACTCONFIRMTYPE] (
    [CONTACTCONFIRMTYPEID]        CHAR (36) NOT NULL,
    [LANDMANAGEMENTCONTACTTYPEID] CHAR (36) NOT NULL,
    [MODULETYPE]                  INT       NOT NULL,
    [ISREQUIRE]                   BIT       DEFAULT ((0)) NOT NULL,
    [HASEDITRIGHT]                BIT       DEFAULT ((0)) NOT NULL,
    [CANCREATESUBRECORD]          BIT       DEFAULT ((1)) NOT NULL,
    [LASTCHANGEDBY]               CHAR (36) NULL,
    [LASTCHANGEDON]               DATETIME  CONSTRAINT [DF_CONTACTCONFIRMTYPE_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]                  INT       CONSTRAINT [DF_CONTACTCONFIRMTYPE_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_CONFIRMCONTACTTYPE] PRIMARY KEY CLUSTERED ([CONTACTCONFIRMTYPEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CONFIRMCTYPE_LANDCTYPE] FOREIGN KEY ([LANDMANAGEMENTCONTACTTYPEID]) REFERENCES [dbo].[LANDMANAGEMENTCONTACTTYPE] ([LANDMANAGEMENTCONTACTTYPEID])
);


GO

CREATE TRIGGER [dbo].[TG_CONTACTCONFIRMTYPE_UPDATE] ON [dbo].[CONTACTCONFIRMTYPE] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
		
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of CONTACTCONFIRMTYPE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END	

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Contact Type',
			[LANDMANAGEMENTCONTACTTYPE_DELETED].[NAME],
			[LANDMANAGEMENTCONTACTTYPE_INSERTED].[NAME],
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE_INSERTED].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE_INSERTED].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTCONFIRMTYPEID] = [inserted].[CONTACTCONFIRMTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] [LANDMANAGEMENTCONTACTTYPE_DELETED] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE_DELETED].[LANDMANAGEMENTCONTACTTYPEID] = [deleted].[LANDMANAGEMENTCONTACTTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] [LANDMANAGEMENTCONTACTTYPE_INSERTED] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE_INSERTED].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
	WHERE	[deleted].[LANDMANAGEMENTCONTACTTYPEID] <> [inserted].[LANDMANAGEMENTCONTACTTYPEID]

	UNION ALL
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Module Type',
			[CAPMODULES_DELETED].[NAME],
			[CAPMODULES_INSERTED].[NAME],
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTCONFIRMTYPEID] = [inserted].[CONTACTCONFIRMTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
			JOIN [CAPMODULES] [CAPMODULES_DELETED] WITH (NOLOCK) ON [CAPMODULES_DELETED].[CAPMODULESID] = [deleted].[MODULETYPE]
			JOIN [CAPMODULES] [CAPMODULES_INSERTED] WITH (NOLOCK) ON [CAPMODULES_INSERTED].[CAPMODULESID] = [inserted].[MODULETYPE]
	WHERE	[deleted].[MODULETYPE] <> [inserted].[MODULETYPE]
	
	UNION ALL
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Requires Confirmation',
			CASE [deleted].[ISREQUIRE] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISREQUIRE] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTCONFIRMTYPEID] = [inserted].[CONTACTCONFIRMTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
	WHERE	[deleted].[ISREQUIRE] <> [inserted].[ISREQUIRE]

	UNION ALL
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Has Editing Rights',
			CASE [deleted].[HASEDITRIGHT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[HASEDITRIGHT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTCONFIRMTYPEID] = [inserted].[CONTACTCONFIRMTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
	WHERE	[deleted].[HASEDITRIGHT] <> [inserted].[HASEDITRIGHT]

	UNION ALL
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Can Create Sub-Records',
			CASE [deleted].[CANCREATESUBRECORD] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[CANCREATESUBRECORD] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTCONFIRMTYPEID] = [inserted].[CONTACTCONFIRMTYPEID]
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
	WHERE	[deleted].[CANCREATESUBRECORD] <> [inserted].[CANCREATESUBRECORD]
END
GO

CREATE TRIGGER [dbo].[TG_CONTACTCONFIRMTYPE_INSERT] ON [dbo].[CONTACTCONFIRMTYPE]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of CONTACTCONFIRMTYPE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END

    INSERT INTO [HISTORYSYSTEMSETUP] 
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[CONTACTCONFIRMTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'CAP - Contact Management Contact Type Added',
			'',
			'',
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			1,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[inserted]	
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
END
GO

CREATE TRIGGER [dbo].[TG_CONTACTCONFIRMTYPE_DELETE] ON [dbo].[CONTACTCONFIRMTYPE]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[deleted].[CONTACTCONFIRMTYPEID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'CAP - Contact Management Contact Type Deleted',
			'',
			'',
			'CAP - Contact Management Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] + ')',
			'5CC0F829-8852-475F-B53B-B10ED4831222',
			3,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [deleted].[LANDMANAGEMENTCONTACTTYPEID]
END