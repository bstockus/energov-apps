CREATE TABLE [dbo].[CAFEECONSTANTVALUE] (
    [CAFEECONSTANTVALUEID] CHAR (36)      NOT NULL,
    [CAFEECONSTANTID]      CHAR (36)      NOT NULL,
    [VALUE]                NVARCHAR (MAX) NOT NULL,
    [CASCHEDULEID]         CHAR (36)      NULL,
    CONSTRAINT [PK_CAFeeConstantValue] PRIMARY KEY CLUSTERED ([CAFEECONSTANTVALUEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CAFeeConstValue_Const] FOREIGN KEY ([CAFEECONSTANTID]) REFERENCES [dbo].[CAFEECONSTANT] ([CAFEECONSTANTID]),
    CONSTRAINT [FK_CAFeeConstValue_Schedule] FOREIGN KEY ([CASCHEDULEID]) REFERENCES [dbo].[CASCHEDULE] ([CASCHEDULEID])
);


GO
CREATE NONCLUSTERED INDEX [CAFEECONSTANTVALUE_IX_CAFEECONSTANTID]
    ON [dbo].[CAFEECONSTANTVALUE]([CAFEECONSTANTID] ASC);


GO

CREATE TRIGGER [dbo].[TG_CAFEECONSTANTVALUE_UPDATE] ON [dbo].[CAFEECONSTANTVALUE] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
		(	[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT
			[CAFEECONSTANT].[CAFEECONSTANTID],
			[CAFEECONSTANT].[ROWVERSION],
			GETUTCDATE(),
			[CAFEECONSTANT].[LASTCHANGEDBY],
			'Fee Schedule',
			ISNULL([CASCHEDULE_DELETED].[NAME],'[none]'),
			ISNULL([CASCHEDULE_INSERTED].[NAME],'[none]'),
			'Fee Constant (' + [CAFEECONSTANT].[NAME] + '), Value (' + [inserted].[VALUE] + ')',
			'B19A0B8D-ED51-4A4E-8075-E8EE0DEDE22C',
			2,
			0,
			[inserted].[VALUE]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[CAFEECONSTANTVALUEID] = [deleted].[CAFEECONSTANTVALUEID]
			INNER JOIN CAFEECONSTANT ON [deleted].[CAFEECONSTANTID] = [CAFEECONSTANT].[CAFEECONSTANTID]
			LEFT JOIN CASCHEDULE CASCHEDULE_DELETED WITH (NOLOCK) ON [deleted].[CASCHEDULEID] = [CASCHEDULE_DELETED].[CASCHEDULEID]
			LEFT JOIN CASCHEDULE CASCHEDULE_INSERTED WITH (NOLOCK) ON [inserted].[CASCHEDULEID] = [CASCHEDULE_INSERTED].[CASCHEDULEID]
	WHERE	ISNULL([deleted].[CASCHEDULEID], '') <> ISNULL([inserted].[CASCHEDULEID], '')

UNION ALL
	SELECT
			[CAFEECONSTANT].[CAFEECONSTANTID],
			[CAFEECONSTANT].[ROWVERSION],
			GETUTCDATE(),
			[CAFEECONSTANT].[LASTCHANGEDBY],
			'Value',
			[deleted].[VALUE],
			[inserted].[VALUE],
			'Fee Constant (' + [CAFEECONSTANT].[NAME] + '), Value (' + [inserted].[VALUE] + ')',
			'B19A0B8D-ED51-4A4E-8075-E8EE0DEDE22C',
			2,
			0,
			[inserted].[VALUE]
	FROM	[deleted] INNER JOIN [inserted] ON [inserted].[CAFEECONSTANTVALUEID] = [deleted].[CAFEECONSTANTVALUEID]
			INNER JOIN CAFEECONSTANT ON [deleted].[CAFEECONSTANTID] = [CAFEECONSTANT].[CAFEECONSTANTID]
	WHERE	[deleted].[VALUE] <> [inserted].[VALUE]

END
GO

CREATE TRIGGER [TG_CAFEECONSTANTVALUE_INSERT] ON [CAFEECONSTANTVALUE]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
		(	[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT 
			[CAFEECONSTANT].[CAFEECONSTANTID],
			[CAFEECONSTANT].[ROWVERSION],
			GETUTCDATE(),
			[CAFEECONSTANT].[LASTCHANGEDBY],
			'Fee Constant - Fee Constant Value Added',
			'',
			'',
			'Fee Constant (' + [CAFEECONSTANT].[NAME] + '), Value (' + [inserted].[VALUE] + ')',
			'B19A0B8D-ED51-4A4E-8075-E8EE0DEDE22C',
			1,
			0,
			[inserted].[VALUE]
	FROM	[inserted]
			INNER JOIN CAFEECONSTANT ON [inserted].[CAFEECONSTANTID] = [CAFEECONSTANT].[CAFEECONSTANTID]
END
GO

CREATE TRIGGER [TG_CAFEECONSTANTVALUE_DELETE] ON [CAFEECONSTANTVALUE]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(		[ID],
			[ROWVERSION],
			[CHANGEDON],
			[CHANGEDBY],
			[FIELDNAME],
			[OLDVALUE],
			[NEWVALUE],
			[ADDITIONALINFO],
			[FORMID],
			[ACTION],
			[ISROOT],
			[RECORDNAME]
		)
	SELECT
			[CAFEECONSTANT].[CAFEECONSTANTID],
			[CAFEECONSTANT].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Fee Constant - Fee Constant Value Deleted',
			'',
			'',
			'Fee Constant (' + [CAFEECONSTANT].[NAME] + '), Value (' + [deleted].[VALUE] + ')',
			'B19A0B8D-ED51-4A4E-8075-E8EE0DEDE22C',
			3,
			0,
			[deleted].[VALUE]
	FROM	[deleted] 
			INNER JOIN CAFEECONSTANT ON [deleted].[CAFEECONSTANTID] = [CAFEECONSTANT].[CAFEECONSTANTID]			
END