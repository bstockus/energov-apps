CREATE TABLE [dbo].[CAFEETEMPLATECHILDXREF] (
    [CAFEETEMPLATECHILDXREFID] CHAR (36) NOT NULL,
    [PARENTFEETEMPLATEID]      CHAR (36) NOT NULL,
    [CHILDFEETEMPLATEID]       CHAR (36) NOT NULL,
    [CHILDORDER]               INT       NOT NULL,
    [CACONDITIONGROUPID]       CHAR (36) NULL,
    CONSTRAINT [PK_CAFeeTemplateChildXref] PRIMARY KEY CLUSTERED ([CAFEETEMPLATECHILDXREFID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CAFeeTmplChildXref_Child] FOREIGN KEY ([CHILDFEETEMPLATEID]) REFERENCES [dbo].[CAFEETEMPLATE] ([CAFEETEMPLATEID]),
    CONSTRAINT [FK_CAFeeTmplChildXref_Condition] FOREIGN KEY ([CACONDITIONGROUPID]) REFERENCES [dbo].[CACONDITIONGROUP] ([CACONDITIONGROUPID]),
    CONSTRAINT [FK_CAFeeTmplChildXref_Parent] FOREIGN KEY ([PARENTFEETEMPLATEID]) REFERENCES [dbo].[CAFEETEMPLATE] ([CAFEETEMPLATEID]),
    CONSTRAINT [UK_CAFeeTmplChildXref] UNIQUE NONCLUSTERED ([PARENTFEETEMPLATEID] ASC, [CHILDFEETEMPLATEID] ASC) WITH (FILLFACTOR = 90)
);


GO

CREATE TRIGGER [dbo].[TG_CAFEETEMPLATECHILDXREF_UPDATE] ON  [dbo].[CAFEETEMPLATECHILDXREF] 
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[PARENTFEETEMPLATE].[CAFEETEMPLATEID],
			[PARENTFEETEMPLATE].[ROWVERSION],
			GETUTCDATE(),
			[PARENTFEETEMPLATE].[LASTCHANGEDBY],
			'Child Order',
			CONVERT(NVARCHAR(MAX), [deleted].[CHILDORDER]),
			CONVERT(NVARCHAR(MAX), [inserted].[CHILDORDER]),
			'Fee Template (' + [PARENTFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee Template Child Ref (' + [CHILDFEETEMPLATE].[CAFEETEMPLATENAME] +')',			
			'CCA2295B-072A-494B-9C11-91424F59BC16',
			2,
			0,
			[PARENTFEETEMPLATE].[CAFEETEMPLATENAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CAFEETEMPLATECHILDXREFID] = [inserted].[CAFEETEMPLATECHILDXREFID]	
			INNER JOIN [dbo].[CAFEETEMPLATE] AS [PARENTFEETEMPLATE] ON [PARENTFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[PARENTFEETEMPLATEID]
			INNER JOIN [dbo].[CAFEETEMPLATE] AS [CHILDFEETEMPLATE] ON [CHILDFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[CHILDFEETEMPLATEID]
	WHERE	[deleted].[CHILDORDER] <> [inserted].[CHILDORDER]
		
	UNION ALL
	SELECT 
			[PARENTFEETEMPLATE].[CAFEETEMPLATEID],
			[PARENTFEETEMPLATE].[ROWVERSION],
			GETUTCDATE(),
			[PARENTFEETEMPLATE].[LASTCHANGEDBY],
			'Condition Group Type',	
			(CASE WHEN [CACONDITIONGROUP_DELETED].[ISORGROUP] IS NULL THEN '[none]' WHEN [CACONDITIONGROUP_DELETED].[ISORGROUP] = 1 THEN 'Is Group Yes' WHEN [CACONDITIONGROUP_DELETED].[ISORGROUP] = 0 THEN 'Is Group No' END),
			(CASE WHEN [CACONDITIONGROUP_INSERTED].[ISORGROUP] IS NULL THEN '[none]' WHEN [CACONDITIONGROUP_INSERTED].[ISORGROUP] = 1 THEN 'Is Group Yes' WHEN [CACONDITIONGROUP_INSERTED].[ISORGROUP] = 0 THEN 'Is Group No' END),
			'Fee Template (' + [PARENTFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee Template Child Ref (' + [CHILDFEETEMPLATE].[CAFEETEMPLATENAME] +')',
			'CCA2295B-072A-494B-9C11-91424F59BC16',
			2,
			0,
			[PARENTFEETEMPLATE].[CAFEETEMPLATENAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CAFEETEMPLATECHILDXREFID] = [inserted].[CAFEETEMPLATECHILDXREFID]	
			INNER JOIN [dbo].[CAFEETEMPLATE] AS [PARENTFEETEMPLATE] ON [PARENTFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[PARENTFEETEMPLATEID]
			INNER JOIN [dbo].[CAFEETEMPLATE] AS [CHILDFEETEMPLATE] ON [CHILDFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[CHILDFEETEMPLATEID]
			LEFT JOIN [dbo].[CACONDITIONGROUP] [CACONDITIONGROUP_DELETED] ON [deleted].[CACONDITIONGROUPID] = [CACONDITIONGROUP_DELETED].[CACONDITIONGROUPID]
			LEFT JOIN [dbo].[CACONDITIONGROUP] [CACONDITIONGROUP_INSERTED] ON [inserted].[CACONDITIONGROUPID] = [CACONDITIONGROUP_INSERTED].[CACONDITIONGROUPID]
	WHERE	ISNULL([deleted].[CACONDITIONGROUPID],'') <> ISNULL([inserted].[CACONDITIONGROUPID],'')			
END
GO

CREATE TRIGGER [dbo].[TG_CAFEETEMPLATECHILDXREF_INSERT] ON  [dbo].[CAFEETEMPLATECHILDXREF]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [PARENTFEETEMPLATE].[CAFEETEMPLATEID], 
        [PARENTFEETEMPLATE].[ROWVERSION],
        GETUTCDATE(),
        [PARENTFEETEMPLATE].[LASTCHANGEDBY],
        'Fee Template, Fee Template Child Ref Added',
        '',
        '',       
		'Fee Template (' + [PARENTFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee Template Child Ref (' + [CHILDFEETEMPLATE].[CAFEETEMPLATENAME] +')',
		'CCA2295B-072A-494B-9C11-91424F59BC16',
		1,
		0,
		[PARENTFEETEMPLATE].[CAFEETEMPLATENAME]
    FROM [inserted]
	INNER JOIN [dbo].[CAFEETEMPLATE] AS [PARENTFEETEMPLATE] ON [PARENTFEETEMPLATE].[CAFEETEMPLATEID] = [inserted].[PARENTFEETEMPLATEID]
	INNER JOIN [dbo].[CAFEETEMPLATE] AS [CHILDFEETEMPLATE] ON [CHILDFEETEMPLATE].[CAFEETEMPLATEID] = [inserted].[CHILDFEETEMPLATEID]
END
GO

CREATE TRIGGER [dbo].[TG_CAFEETEMPLATECHILDXREF_DELETE]  ON  [dbo].[CAFEETEMPLATECHILDXREF]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[PARENTFEETEMPLATE].[CAFEETEMPLATEID], 
        [PARENTFEETEMPLATE].[ROWVERSION],
        GETUTCDATE(),			
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Fee Template, Fee Template Child Ref Deleted',
        '',
        '',       
		'Fee Template (' + [PARENTFEETEMPLATE].[CAFEETEMPLATENAME] + '), Fee Template Child Ref (' + [CHILDFEETEMPLATE].[CAFEETEMPLATENAME] +')',
		'CCA2295B-072A-494B-9C11-91424F59BC16',
		3,
		0,
		[PARENTFEETEMPLATE].[CAFEETEMPLATENAME]
    FROM [deleted]
	INNER JOIN [dbo].[CAFEETEMPLATE] AS [PARENTFEETEMPLATE] ON [PARENTFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[PARENTFEETEMPLATEID]
	INNER JOIN [dbo].[CAFEETEMPLATE] AS [CHILDFEETEMPLATE] ON [CHILDFEETEMPLATE].[CAFEETEMPLATEID] = [deleted].[CHILDFEETEMPLATEID]	
END