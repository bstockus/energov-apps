CREATE TABLE [dbo].[CACPIREFERENCEDATE] (
    [CACPIREFERENCEDATEID]   CHAR (36)      NOT NULL,
    [CAMODULEID]             INT            NOT NULL,
    [DATEOBJECTTYPEID]       INT            NOT NULL,
    [DATEOBJECT]             NVARCHAR (255) NOT NULL,
    [DATEOBJECTFRIENDLYNAME] NVARCHAR (500) NOT NULL,
    [ISDEFAULT]              BIT            NOT NULL,
    [LASTCHANGEDBY]          CHAR (36)      NULL,
    [LASTCHANGEDON]          DATETIME       CONSTRAINT [DF_CACPIREFERENCEDATE_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]             INT            CONSTRAINT [DF_CACPIREFERENCEDATE_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_CACPIREFERENCEDATE] PRIMARY KEY CLUSTERED ([CACPIREFERENCEDATEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CACPIREFERENCEDATE_CAMODULE] FOREIGN KEY ([CAMODULEID]) REFERENCES [dbo].[CAMODULE] ([CAMODULEID]),
    CONSTRAINT [FK_CACPIREFERENCEDATE_TMPTYPE] FOREIGN KEY ([DATEOBJECTTYPEID]) REFERENCES [dbo].[CAFEETEMPLATEINPUTTYPE] ([CAFEETEMPLATEINPUTTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [CACPIREFERENCEDATE_IX_QUERY]
    ON [dbo].[CACPIREFERENCEDATE]([CACPIREFERENCEDATEID] ASC);


GO

CREATE TRIGGER [dbo].[TG_CACPIREFERENCEDATE_UPDATE] ON [dbo].[CACPIREFERENCEDATE]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;

	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of CACPIREFERENCEDATE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END


	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[CACPIREFERENCEDATEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Default Flag',
			CASE [deleted].[ISDEFAULT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			CASE [inserted].[ISDEFAULT] WHEN 1 THEN 'Yes' ELSE 'No' END,
			'Module (' + [CAMODULE].[NAME] + '), CPI Reference Date (' + [deleted].[DATEOBJECTFRIENDLYNAME] + ')',
			'D2215E33-94EF-4541-9CA6-DB5B9BF1BAE9',
			2,
			1,
			[inserted].[DATEOBJECTFRIENDLYNAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CACPIREFERENCEDATEID] = [inserted].[CACPIREFERENCEDATEID]
			INNER JOIN CAMODULE ON [deleted].[CAMODULEID] = [CAMODULE].[CAMODULEID]
	WHERE	[deleted].[ISDEFAULT] <> [inserted].[ISDEFAULT]	
	
END
GO

CREATE TRIGGER [dbo].[TG_CACPIREFERENCEDATE_INSERT] ON [dbo].[CACPIREFERENCEDATE]
   AFTER INSERT
AS 
BEGIN
	SET NOCOUNT ON;

	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of CACPIREFERENCEDATE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END

	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [inserted].[CACPIREFERENCEDATEID], 
        [inserted].[ROWVERSION],
        GETUTCDATE(),
        [inserted].[LASTCHANGEDBY],
        'CPI Reference Date Added',
        '',
        '',
        'Module (' + [CAMODULE].[NAME] + '), CPI Reference Date (' + [inserted].[DATEOBJECTFRIENDLYNAME] + ')',
		'D2215E33-94EF-4541-9CA6-DB5B9BF1BAE9',
		1,
		1,
		[inserted].[DATEOBJECTFRIENDLYNAME]
    FROM [inserted] 
	INNER JOIN CAMODULE ON [inserted].[CAMODULEID] = [CAMODULE].[CAMODULEID]
END
GO

CREATE TRIGGER [dbo].[TG_CACPIREFERENCEDATE_DELETE] ON [dbo].[CACPIREFERENCEDATE]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[deleted].[CACPIREFERENCEDATEID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'CPI Reference Date Deleted',
			'',
			'',
			'Module (' + [CAMODULE].[NAME] + '), CPI Reference Date (' + [deleted].[DATEOBJECTFRIENDLYNAME] + ')',
			'D2215E33-94EF-4541-9CA6-DB5B9BF1BAE9',
			3,
			1,
			[deleted].[DATEOBJECTFRIENDLYNAME]
	FROM	[deleted]
	INNER JOIN CAMODULE ON [deleted].[CAMODULEID] = [CAMODULE].[CAMODULEID]
END