CREATE TABLE [dbo].[CAMODULEDISCOUNTXREF] (
    [CAMODULEDISCOUNTXREFID] CHAR (36) NOT NULL,
    [CAMODULEID]             INT       NOT NULL,
    [CADISCOUNTID]           CHAR (36) NOT NULL,
    CONSTRAINT [PK_CAModuleDiscountXRef] PRIMARY KEY CLUSTERED ([CAMODULEDISCOUNTXREFID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_CAModDiscntXRef_Discount] FOREIGN KEY ([CADISCOUNTID]) REFERENCES [dbo].[CADISCOUNT] ([CADISCOUNTID]),
    CONSTRAINT [FK_CAModDiscntXRef_Module] FOREIGN KEY ([CAMODULEID]) REFERENCES [dbo].[CAMODULE] ([CAMODULEID])
);


GO
CREATE NONCLUSTERED INDEX [CAMODULEDISCOUNTXREF_IX_CADISCOUNTID]
    ON [dbo].[CAMODULEDISCOUNTXREF]([CAMODULEDISCOUNTXREFID] ASC);


GO


CREATE TRIGGER [dbo].[TG_CAMODULEDISCOUNTXREF_INSERT] ON [dbo].[CAMODULEDISCOUNTXREF]
   AFTER INSERT
AS 
BEGIN
	SET NOCOUNT ON;

	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [CADISCOUNT].[CADISCOUNTID], 
        [CADISCOUNT].[ROWVERSION],
        GETUTCDATE(),
        [CADISCOUNT].[LASTCHANGEDBY],
        'Discount Module Added',
        '',
        '',
        'Discount (' + [CADISCOUNT].[NAME] + '), Module (' + [CAMODULE].[NAME] + ')',
		'6E95A0E5-C492-4C95-B532-DE1630CE3F7C',
		1,
		0,
		[CAMODULE].[NAME]
    FROM [inserted]
	INNER JOIN [CADISCOUNT] ON [CADISCOUNT].[CADISCOUNTID] = [inserted].[CADISCOUNTID]
	INNER JOIN [CAMODULE] WITH (NOLOCK) ON [CAMODULE].[CAMODULEID] = [inserted].[CAMODULEID]
END
GO


CREATE TRIGGER [dbo].[TG_CAMODULEDISCOUNTXREF_UPDATE] ON [dbo].[CAMODULEDISCOUNTXREF]
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;

	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[CADISCOUNT].[CADISCOUNTID],
			[CADISCOUNT].[ROWVERSION],
			GETUTCDATE(),
			[CADISCOUNT].[LASTCHANGEDBY],
			'Module',
			[CAMODULE_DELETED].[NAME],
			[CAMODULE_INSERTED].[NAME],
			'Discount (' + [CADISCOUNT].[NAME] + '), Module (' + [CAMODULE_INSERTED].[NAME] + ')',
			'6E95A0E5-C492-4C95-B532-DE1630CE3F7C',
			2,
			0,
			[CAMODULE_INSERTED].[NAME]
	FROM	[deleted] JOIN [inserted] ON [deleted].[CAMODULEDISCOUNTXREFID] = [inserted].[CAMODULEDISCOUNTXREFID]
	INNER JOIN [CADISCOUNT] ON [CADISCOUNT].[CADISCOUNTID] = [inserted].[CADISCOUNTID]
	INNER JOIN [CAMODULE] CAMODULE_DELETED WITH (NOLOCK) ON [CAMODULE_DELETED].[CAMODULEID] = [deleted].[CAMODULEID]
	INNER JOIN [CAMODULE] CAMODULE_INSERTED WITH (NOLOCK) ON [CAMODULE_INSERTED].[CAMODULEID] = [inserted].[CAMODULEID]
	WHERE	[deleted].[CAMODULEID] <> [inserted].[CAMODULEID]
END
GO


CREATE TRIGGER [TG_CAMODULEDISCOUNTXREF_DELETE] ON [CAMODULEDISCOUNTXREF]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[CADISCOUNT].[CADISCOUNTID],
			[CADISCOUNT].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Discount Module Deleted',
			'',
			'',
			'Discount (' + [CADISCOUNT].[NAME] + '), Module (' + [CAMODULE].[NAME] + ')',
			'6E95A0E5-C492-4C95-B532-DE1630CE3F7C',
			3,
			0,
			[CAMODULE].[NAME]
	FROM	[deleted]	
	INNER JOIN [CADISCOUNT] ON [CADISCOUNT].[CADISCOUNTID] = [deleted].[CADISCOUNTID]
	INNER JOIN [CAMODULE] WITH (NOLOCK) ON [CAMODULE].[CAMODULEID] = [deleted].[CAMODULEID]
END