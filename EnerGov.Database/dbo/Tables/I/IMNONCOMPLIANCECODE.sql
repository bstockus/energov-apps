CREATE TABLE [dbo].[IMNONCOMPLIANCECODE] (
    [IMNONCOMPLIANCECODEID]      CHAR (36)       NOT NULL,
    [IMNONCOMPLIANCECATEGORYID]  CHAR (36)       NOT NULL,
    [DEFAULTRESPONSIBLEPARTYID]  INT             NULL,
    [DEFAULTNONCOMPLIANCERISKID] INT             NULL,
    [COMPLIANCEDAYSUNTILDUE]     INT             NOT NULL,
    [NAME]                       NVARCHAR (50)   NOT NULL,
    [DESCRIPTION]                NVARCHAR (2000) NULL,
    [LASTCHANGEDBY]              CHAR (36)       NULL,
    [LASTCHANGEDON]              DATETIME        CONSTRAINT [DF_IMNONCOMPLIANCECODE_LASTCHANGEDON] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]                 INT             CONSTRAINT [DF_IMNONCOMPLIANCECODE_ROWVERSION] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_IMNONCOMPLIANCECODE] PRIMARY KEY CLUSTERED ([IMNONCOMPLIANCECODEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_IMNONCOMPLCODE_RESP_PARTY] FOREIGN KEY ([DEFAULTRESPONSIBLEPARTYID]) REFERENCES [dbo].[IMNONCOMPLIANCERESPPARTY] ([IMNONCOMPLIANCERESPPARTYID]),
    CONSTRAINT [FK_IMNONCOMPLIANCECODE_CAT] FOREIGN KEY ([IMNONCOMPLIANCECATEGORYID]) REFERENCES [dbo].[IMNONCOMPLIANCECATEGORY] ([IMNONCOMPLIANCECATEGORYID]),
    CONSTRAINT [FK_IMNONCOMPLIANCECODE_RISK] FOREIGN KEY ([DEFAULTNONCOMPLIANCERISKID]) REFERENCES [dbo].[IMNONCOMPLIANCERISK] ([IMNONCOMPLIANCERISKID]),
    CONSTRAINT [FK_IMNONCOMPLIANCECODE_USERS] FOREIGN KEY ([LASTCHANGEDBY]) REFERENCES [dbo].[USERS] ([SUSERGUID])
);


GO


CREATE TRIGGER [dbo].[TG_IMNONCOMPLIANCECODE_DELETE] ON [dbo].[IMNONCOMPLIANCECODE]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[deleted].[IMNONCOMPLIANCECODEID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Noncompliance Code Deleted',
			'',
			'',
			'Noncompliance Code (' + [deleted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END
GO


CREATE TRIGGER [dbo].[TG_IMNONCOMPLIANCECODE_INSERT] ON [dbo].[IMNONCOMPLIANCECODE]
	AFTER INSERT
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Noncompliance Code Added',
			'',
			'',
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]
END
GO


CREATE TRIGGER [dbo].[TG_IMNONCOMPLIANCECODE_UPDATE] ON [dbo].[IMNONCOMPLIANCECODE]
	AFTER UPDATE
AS
BEGIN
	SET NOCOUNT ON

	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Code Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]
	UNION ALL
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Description',
			ISNULL([deleted].[DESCRIPTION], '[none]'),
			ISNULL([inserted].[DESCRIPTION], '[none]'),
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	WHERE	ISNULL([deleted].[DESCRIPTION], '') <> ISNULL([inserted].[DESCRIPTION], '')
	UNION ALL
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Compliance Days Until Due',
			CAST([deleted].[COMPLIANCEDAYSUNTILDUE] AS NVARCHAR(MAX)),
			CAST([inserted].[COMPLIANCEDAYSUNTILDUE] AS NVARCHAR(MAX)),
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	WHERE	[deleted].[COMPLIANCEDAYSUNTILDUE] <> [inserted].[COMPLIANCEDAYSUNTILDUE]
	UNION ALL
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Code Category',
			[COMPLIANCECATEGORY_DELETED].[NAME],
			[COMPLIANCECATEGORY_INSERTED].[NAME],
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	LEFT JOIN IMNONCOMPLIANCECATEGORY COMPLIANCECATEGORY_DELETED WITH (NOLOCK) ON [deleted].[IMNONCOMPLIANCECATEGORYID] = [COMPLIANCECATEGORY_DELETED].[IMNONCOMPLIANCECATEGORYID]
	LEFT JOIN IMNONCOMPLIANCECATEGORY COMPLIANCECATEGORY_INSERTED WITH (NOLOCK) ON [inserted].[IMNONCOMPLIANCECATEGORYID] = [COMPLIANCECATEGORY_INSERTED].[IMNONCOMPLIANCECATEGORYID]
	WHERE	[deleted].[IMNONCOMPLIANCECATEGORYID] <> [inserted].[IMNONCOMPLIANCECATEGORYID]
	UNION ALL
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Default Responsible Party',
			ISNULL([COMPLIANCERESPPARTY_DELETED].[NAME], '[none]'),
			ISNULL([COMPLIANCERESPPARTY_INSERTED].[NAME], '[none]'),
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	LEFT JOIN IMNONCOMPLIANCERESPPARTY COMPLIANCERESPPARTY_DELETED WITH (NOLOCK) ON [deleted].[DEFAULTRESPONSIBLEPARTYID] = [COMPLIANCERESPPARTY_DELETED].[IMNONCOMPLIANCERESPPARTYID]
	LEFT JOIN IMNONCOMPLIANCERESPPARTY COMPLIANCERESPPARTY_INSERTED WITH (NOLOCK) ON [inserted].[DEFAULTRESPONSIBLEPARTYID] = [COMPLIANCERESPPARTY_INSERTED].[IMNONCOMPLIANCERESPPARTYID]
	WHERE	ISNULL([deleted].[DEFAULTRESPONSIBLEPARTYID], '') <> ISNULL([inserted].[DEFAULTRESPONSIBLEPARTYID], '')
	UNION ALL
	SELECT
			[inserted].[IMNONCOMPLIANCECODEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Default Risk',
			ISNULL([COMPLIANCERISK_DELETED].[NAME], '[none]'),
			ISNULL([COMPLIANCERISK_INSERTED].[NAME], '[none]'),
			'Noncompliance Code (' + [inserted].[NAME] + ')',
			'9559D8EB-A281-4AE1-99A6-0E4E50388BB0',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted] JOIN [inserted]
	ON		[deleted].[IMNONCOMPLIANCECODEID] = [inserted].[IMNONCOMPLIANCECODEID]
	LEFT JOIN IMNONCOMPLIANCERISK COMPLIANCERISK_DELETED WITH (NOLOCK) ON [deleted].[DEFAULTNONCOMPLIANCERISKID] = [COMPLIANCERISK_DELETED].[IMNONCOMPLIANCERISKID]
	LEFT JOIN IMNONCOMPLIANCERISK COMPLIANCERISK_INSERTED WITH (NOLOCK) ON [inserted].[DEFAULTNONCOMPLIANCERISKID] = [COMPLIANCERISK_INSERTED].[IMNONCOMPLIANCERISKID]
	WHERE	ISNULL([deleted].[DEFAULTNONCOMPLIANCERISKID], '') <> ISNULL([inserted].[DEFAULTNONCOMPLIANCERISKID], '')
END