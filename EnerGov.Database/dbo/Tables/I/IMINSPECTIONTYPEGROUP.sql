CREATE TABLE [dbo].[IMINSPECTIONTYPEGROUP] (
    [IMINSPECTIONTYPEGROUPID]  CHAR (36)      NOT NULL,
    [NAME]                     NVARCHAR (50)  NOT NULL,
    [DESCRIPTION]              NVARCHAR (MAX) NULL,
    [IVRNUMBER]                INT            NOT NULL,
    [GROUPLIMITATION]          INT            DEFAULT ((0)) NOT NULL,
    [IMINSPECTIONSYSTEMTYPEID] INT            DEFAULT ((1)) NULL,
    [LASTCHANGEDBY]            CHAR (36)      NULL,
    [LASTCHANGEDON]            DATETIME       CONSTRAINT [DF_IMINSPECTIONTYPEGROUP_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]               INT            CONSTRAINT [DF_IMINSPECTIONTYPEGROUP_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_IMInspectionTypeGroup] PRIMARY KEY CLUSTERED ([IMINSPECTIONTYPEGROUPID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_IMINSPECTIONTYPEGROUP_IMINSPECTIONSYSTEMTYPE] FOREIGN KEY ([IMINSPECTIONSYSTEMTYPEID]) REFERENCES [dbo].[IMINSPECTIONSYSTEMTYPE] ([IMINSPECTIONSYSTEMTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [IMINSPECTIONTYPEGROUP_IX_QUERY]
    ON [dbo].[IMINSPECTIONTYPEGROUP]([IMINSPECTIONTYPEGROUPID] ASC, [NAME] ASC);


GO

CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPEGROUP_DELETE] ON [dbo].[IMINSPECTIONTYPEGROUP]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[deleted].[IMINSPECTIONTYPEGROUPID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Inspection Type Group Deleted',
			'',
			'',
			'Inspection Type Group (' + [deleted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END
GO

CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPEGROUP_UPDATE] ON [dbo].[IMINSPECTIONTYPEGROUP]
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
		
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of IMINSPECTIONZONE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END	

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[IMINSPECTIONTYPEGROUPID] = [inserted].[IMINSPECTIONTYPEGROUPID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]
	UNION ALL
		
	SELECT
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Description',
			ISNULL([deleted].[DESCRIPTION],'[none]'),
			ISNULL([inserted].[DESCRIPTION],'[none]'),
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[IMINSPECTIONTYPEGROUPID] = [inserted].[IMINSPECTIONTYPEGROUPID]
	WHERE	ISNULL([deleted].[DESCRIPTION], '') <> ISNULL([inserted].[DESCRIPTION], '')	
	UNION ALL

	SELECT 
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'IVR Number',
			CONVERT(NVARCHAR(MAX),[deleted].[IVRNUMBER]),
			CONVERT(NVARCHAR(MAX),[inserted].[IVRNUMBER]),
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].IMINSPECTIONTYPEGROUPID = [inserted].IMINSPECTIONTYPEGROUPID
	WHERE	[deleted].[IVRNUMBER] <> [inserted].[IVRNUMBER]
	UNION ALL

	SELECT 
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Group Limitation',
			CONVERT(NVARCHAR(MAX),[deleted].[GROUPLIMITATION]),
			CONVERT(NVARCHAR(MAX),[inserted].[GROUPLIMITATION]),
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].IMINSPECTIONTYPEGROUPID = [inserted].IMINSPECTIONTYPEGROUPID
	WHERE	[deleted].[GROUPLIMITATION] <> [inserted].[GROUPLIMITATION]
	UNION ALL

	SELECT 
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'System Type',
			ISNULL([IMINSPECTIONSYSTEMTYPE_DELETED].[NAME],'[none]'),
			ISNULL([IMINSPECTIONSYSTEMTYPE_INSERTED].[NAME],'[none]'),
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].IMINSPECTIONTYPEGROUPID = [inserted].IMINSPECTIONTYPEGROUPID
			LEFT OUTER JOIN IMINSPECTIONSYSTEMTYPE IMINSPECTIONSYSTEMTYPE_DELETED WITH (NOLOCK) ON [deleted].IMINSPECTIONSYSTEMTYPEID = [IMINSPECTIONSYSTEMTYPE_DELETED].IMINSPECTIONSYSTEMTYPEID
			LEFT OUTER JOIN IMINSPECTIONSYSTEMTYPE IMINSPECTIONSYSTEMTYPE_INSERTED WITH (NOLOCK) ON [inserted].IMINSPECTIONSYSTEMTYPEID = [IMINSPECTIONSYSTEMTYPE_INSERTED].IMINSPECTIONSYSTEMTYPEID
	WHERE	ISNULL([deleted].[IMINSPECTIONSYSTEMTYPEID],'') <> ISNULL([inserted].[IMINSPECTIONSYSTEMTYPEID],'')
END
GO
CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPEGROUP_INSERT] ON [dbo].[IMINSPECTIONTYPEGROUP]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of IMINSPECTIONZONE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[IMINSPECTIONTYPEGROUPID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Inspection Type Group Added',
			'',
			'',
			'Inspection Type Group (' + [inserted].[NAME] + ')',
			'1830179E-04A4-47A6-9C10-12FC54362AE9',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]	
END