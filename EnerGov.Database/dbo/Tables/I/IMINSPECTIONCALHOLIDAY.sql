CREATE TABLE [dbo].[IMINSPECTIONCALHOLIDAY] (
    [IMINSPECTIONCALHOLIDAYID] CHAR (36)      NOT NULL,
    [IMINSPECTIONCALENDARID]   CHAR (36)      NOT NULL,
    [NAME]                     NVARCHAR (100) NOT NULL,
    [HOLIDAYDATE]              DATETIME       NOT NULL,
    CONSTRAINT [PK_IMINSPECTIONCALHOLIDAY] PRIMARY KEY CLUSTERED ([IMINSPECTIONCALHOLIDAYID] ASC),
    CONSTRAINT [FK_IMINSPECTIONCALHOLIDAY_IMINSPECTIONCALENDAR] FOREIGN KEY ([IMINSPECTIONCALENDARID]) REFERENCES [dbo].[IMINSPECTIONCALENDAR] ([IMINSPECTIONCALENDARID]) ON DELETE CASCADE
);


GO
CREATE NONCLUSTERED INDEX [IMINSPECTIONCALHOLIDAY_IX_IMINSPECTIONCALENDARID]
    ON [dbo].[IMINSPECTIONCALHOLIDAY]([IMINSPECTIONCALENDARID] ASC);


GO

CREATE TRIGGER [TG_IMINSPECTIONCALHOLIDAY_INSERT] ON [IMINSPECTIONCALHOLIDAY]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME] 
    )

	SELECT
			[IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID],
			[IMINSPECTIONCALENDAR].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONCALENDAR].[LASTCHANGEDBY],
			'Inspection Calendar Holiday Added', 
			'',
			'',
			'Inspection Calendar (' + [IMINSPECTIONCALENDAR].[NAME] + '), Inspection Calendar Holiday (' + [inserted].[NAME] + ')',
			'540881BC-B7EC-4CAB-958C-D1820B6FEBAC',
			1,
			0,
			[inserted].[NAME]
	FROM	[inserted]	
	INNER JOIN [IMINSPECTIONCALENDAR]
      ON [IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID] = [inserted].[IMINSPECTIONCALENDARID]
END
GO

CREATE TRIGGER [TG_IMINSPECTIONCALHOLIDAY_UPDATE] ON [IMINSPECTIONCALHOLIDAY]
   AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME] 
    )

	SELECT
			[IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID],
			[IMINSPECTIONCALENDAR].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONCALENDAR].[LASTCHANGEDBY],
			'Holiday Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Inspection Calendar (' + [IMINSPECTIONCALENDAR].[NAME] + '), Inspection Calendar Holiday (' + [inserted].[NAME] + ')',
			'540881BC-B7EC-4CAB-958C-D1820B6FEBAC',
			2,
			0,
			[inserted].[NAME] 
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[IMINSPECTIONCALHOLIDAYID] = [inserted].[IMINSPECTIONCALHOLIDAYID]
			INNER JOIN [IMINSPECTIONCALENDAR]
      ON [IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID] = [inserted].[IMINSPECTIONCALENDARID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]	
	UNION ALL
	SELECT
			[IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID],
			[IMINSPECTIONCALENDAR].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONCALENDAR].[LASTCHANGEDBY],
			'Holiday Date',
			CONVERT(NVARCHAR(MAX), [deleted].[HOLIDAYDATE], 101),
			CONVERT(NVARCHAR(MAX), [inserted].[HOLIDAYDATE], 101),
			'Inspection Calendar (' + [IMINSPECTIONCALENDAR].[NAME] + '), Inspection Calendar Holiday (' + [inserted].[NAME] + ')',
			'540881BC-B7EC-4CAB-958C-D1820B6FEBAC',
			2,
			0,
			[inserted].[NAME] 
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[IMINSPECTIONCALHOLIDAYID] = [inserted].[IMINSPECTIONCALHOLIDAYID]
			INNER JOIN [IMINSPECTIONCALENDAR]
      ON [IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID] = [inserted].[IMINSPECTIONCALENDARID]
	WHERE	[deleted].[HOLIDAYDATE] <> [inserted].[HOLIDAYDATE]	
END
GO

CREATE TRIGGER [TG_IMINSPECTIONCALHOLIDAY_DELETE] ON [IMINSPECTIONCALHOLIDAY]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID],
			[IMINSPECTIONCALENDAR].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Inspection Calendar Holiday Deleted',
			'',
			'',
			'Inspection Calendar (' + [IMINSPECTIONCALENDAR].[NAME] + '), Inspection Calendar Holiday (' + [deleted].[NAME] + ')',
			'540881BC-B7EC-4CAB-958C-D1820B6FEBAC',
			3,
			0,
			[deleted].[NAME]
	FROM	[deleted]
	INNER JOIN [IMINSPECTIONCALENDAR]
      ON [IMINSPECTIONCALENDAR].[IMINSPECTIONCALENDARID] = [deleted].[IMINSPECTIONCALENDARID]
END