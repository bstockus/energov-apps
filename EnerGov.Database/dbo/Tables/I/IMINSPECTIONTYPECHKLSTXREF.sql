CREATE TABLE [dbo].[IMINSPECTIONTYPECHKLSTXREF] (
    [IMINSPECTIONTYPECHKLSTXREFID] CHAR (36) NOT NULL,
    [IMCHECKLISTID]                CHAR (36) NOT NULL,
    [IMINSPECTIONTYPEID]           CHAR (36) NOT NULL,
    [SORTORDER]                    INT       NOT NULL,
    [AUTOADD]                      BIT       NOT NULL,
    CONSTRAINT [PK_IMInspectionTypeChkistXRef] PRIMARY KEY CLUSTERED ([IMINSPECTIONTYPECHKLSTXREFID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_IMInspectionTypeChkistXRef_IMChecklist] FOREIGN KEY ([IMCHECKLISTID]) REFERENCES [dbo].[IMCHECKLIST] ([IMCHECKLISTID]),
    CONSTRAINT [FK_IMInspectionTypeChkistXRef_IMInspectionType] FOREIGN KEY ([IMINSPECTIONTYPEID]) REFERENCES [dbo].[IMINSPECTIONTYPE] ([IMINSPECTIONTYPEID])
);


GO
CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPECHKLSTXREF_UPDATE] ON [dbo].[IMINSPECTIONTYPECHKLSTXREF] 
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID],
			[IMINSPECTIONTYPE].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONTYPE].[LASTCHANGEDBY],
			'Sort Order',
			CONVERT(NVARCHAR(MAX),[deleted].[SORTORDER]),
			CONVERT(NVARCHAR(MAX),[inserted].[SORTORDER]),			
			'Inspection Type (' + [IMINSPECTIONTYPE].[NAME] + '), Check List Name ('+[IMCHECKLIST].[NAME] +')',
			'D6001D91-4891-41A9-8056-33EE10520A48',
			2,
			0,
			[IMCHECKLIST].[NAME]
	FROM	[deleted]
	JOIN [inserted] ON [deleted].[IMINSPECTIONTYPECHKLSTXREFID] = [inserted].[IMINSPECTIONTYPECHKLSTXREFID]
	INNER JOIN [IMINSPECTIONTYPE] ON [IMINSPECTIONTYPE].[IMINSPECTIONTYPEID] = [inserted].[IMINSPECTIONTYPEID]
	INNER JOIN [IMCHECKLIST] WITH (NOLOCK) ON [IMCHECKLIST].[IMCHECKLISTID] = [inserted].[IMCHECKLISTID]
	WHERE [deleted].[SORTORDER] <> [inserted].[SORTORDER]
	UNION ALL
	SELECT
			[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID],
			[IMINSPECTIONTYPE].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONTYPE].[LASTCHANGEDBY],
			'Auto Add Flag',
			CASE [deleted].[AUTOADD] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			CASE [inserted].[AUTOADD] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,			
			'Inspection Type (' + [IMINSPECTIONTYPE].[NAME] + '), Check List Name ('+ [IMCHECKLIST].[NAME] +')',
			'D6001D91-4891-41A9-8056-33EE10520A48',
			2,
			0,
			[IMCHECKLIST].[NAME]
	FROM	[deleted]
	JOIN [inserted] ON [deleted].[IMINSPECTIONTYPECHKLSTXREFID] = [inserted].[IMINSPECTIONTYPECHKLSTXREFID]
	INNER JOIN [IMINSPECTIONTYPE] ON [IMINSPECTIONTYPE].[IMINSPECTIONTYPEID] = [inserted].[IMINSPECTIONTYPEID]
	INNER JOIN [IMCHECKLIST] WITH (NOLOCK) ON [IMCHECKLIST].[IMCHECKLISTID] = [inserted].[IMCHECKLISTID]
	WHERE [deleted].[AUTOADD] <> [inserted].[AUTOADD]
END
GO
CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPECHKLSTXREF_INSERT] ON [dbo].[IMINSPECTIONTYPECHKLSTXREF]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;
	
    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID],
			[IMINSPECTIONTYPE].[ROWVERSION],
			GETUTCDATE(),
			[IMINSPECTIONTYPE].[LASTCHANGEDBY],
			'Inspection Type Check List Added',
			'',
			'',
			'Inspection Type (' + [IMINSPECTIONTYPE].[NAME] + '), Check List Name ('+[IMCHECKLIST].[NAME] +')',
			'D6001D91-4891-41A9-8056-33EE10520A48',
			1,
			0,
			[IMCHECKLIST].[NAME]
	FROM	[inserted]
	INNER JOIN [IMINSPECTIONTYPE] ON [IMINSPECTIONTYPE].[IMINSPECTIONTYPEID] = [inserted].[IMINSPECTIONTYPEID]
	INNER JOIN [IMCHECKLIST] WITH (NOLOCK) ON [IMCHECKLIST].[IMCHECKLISTID] = [inserted].[IMCHECKLISTID]
END
GO
CREATE TRIGGER [dbo].[TG_IMINSPECTIONTYPECHKLSTXREF_DELETE] ON [dbo].[IMINSPECTIONTYPECHKLSTXREF]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID],
			[IMINSPECTIONTYPE].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Inspection Type Check List Deleted',
			'',
			'',
			'Inspection Type (' + [IMINSPECTIONTYPE].[NAME] + '), Check List Name ('+[IMCHECKLIST].[NAME] +')',
			'D6001D91-4891-41A9-8056-33EE10520A48',
			3,
			0,
			[IMCHECKLIST].[NAME]
	FROM	[deleted]
	INNER JOIN [IMINSPECTIONTYPE] ON [IMINSPECTIONTYPE].[IMINSPECTIONTYPEID] = [deleted].[IMINSPECTIONTYPEID]
	INNER JOIN [IMCHECKLIST] WITH (NOLOCK) ON [IMCHECKLIST].[IMCHECKLISTID] = [deleted].[IMCHECKLISTID]
END