CREATE TABLE [dbo].[PMPERMITALLOWEDACTIVITY] (
    [PMPERMITALLOWEDACTIVITYID] CHAR (36) NOT NULL,
    [PMPERMITTYPEID]            CHAR (36) NOT NULL,
    [PMPERMITACTIVITYTYPEID]    CHAR (36) NOT NULL,
    CONSTRAINT [PK_PMPermitAllowedActivity] PRIMARY KEY CLUSTERED ([PMPERMITALLOWEDACTIVITYID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_PermAllowAct_PermActType] FOREIGN KEY ([PMPERMITACTIVITYTYPEID]) REFERENCES [dbo].[PMPERMITACTIVITYTYPE] ([PMPERMITACTIVITYTYPEID]),
    CONSTRAINT [FK_PermAllowAct_PermType] FOREIGN KEY ([PMPERMITTYPEID]) REFERENCES [dbo].[PMPERMITTYPE] ([PMPERMITTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [IX_PMPERMITALLOWEDACTIVITY_PMPERMITTYPEID]
    ON [dbo].[PMPERMITALLOWEDACTIVITY]([PMPERMITTYPEID] ASC);


GO
CREATE TRIGGER [dbo].[TG_PMPERMITALLOWEDACTIVITY_INSERT]
   ON  [dbo].[PMPERMITALLOWEDACTIVITY]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [PMPERMITTYPE].[PMPERMITTYPEID], 
        [PMPERMITTYPE].[ROWVERSION],
        GETUTCDATE(),
        [PMPERMITTYPE].[LASTCHANGEDBY],	
        'Permit Type Allowed Permit Activity Added',
        '',
        '',       
		'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Activity Type (' + [PMPERMITACTIVITYTYPE].[NAME] +')',
		'4976A4A9-20E8-4B8F-997D-CE244B540104',
		1,
		0,
		[PMPERMITACTIVITYTYPE].[NAME]
    FROM [inserted]
	INNER JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [inserted].[PMPERMITTYPEID]
	INNER JOIN [PMPERMITACTIVITYTYPE] WITH (NOLOCK) ON [PMPERMITACTIVITYTYPE].[PMPERMITACTIVITYTYPEID] = [inserted].[PMPERMITACTIVITYTYPEID]
END
GO
CREATE TRIGGER [dbo].[TG_PMPERMITALLOWEDACTIVITY_DELETE]  
    ON  [dbo].[PMPERMITALLOWEDACTIVITY]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[PMPERMITTYPE].[PMPERMITTYPEID],
		[PMPERMITTYPE].[ROWVERSION],
		GETUTCDATE(),			
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Permit Type Allowed Permit Activity Deleted',
        '',
        '',       
		'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Activity Type (' + [PMPERMITACTIVITYTYPE].[NAME] +')',
		'4976A4A9-20E8-4B8F-997D-CE244B540104',
		3,
		0,
		[PMPERMITACTIVITYTYPE].[NAME]
    FROM [deleted]
	INNER JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [deleted].[PMPERMITTYPEID]
	INNER JOIN [PMPERMITACTIVITYTYPE] WITH (NOLOCK) ON [PMPERMITACTIVITYTYPE].[PMPERMITACTIVITYTYPEID] = [deleted].[PMPERMITACTIVITYTYPEID]
END