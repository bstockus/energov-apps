CREATE TABLE [dbo].[PMCONTACTTYPEREF] (
    [CONTACTTYPEEXTID]            CHAR (36) NOT NULL,
    [LANDMANAGEMENTCONTACTTYPEID] CHAR (36) NOT NULL,
    [CONTACTTYPEGROUP]            INT       NULL,
    [ISREQUIRED]                  BIT       CONSTRAINT [DF_PMCONTACTTYPEREF_REQ] DEFAULT ((0)) NOT NULL,
    [OBJCLASSID]                  CHAR (36) NULL,
    [OBJTYPEID]                   CHAR (36) NULL,
    [OBJMODULEID]                 INT       NOT NULL,
    [ISDEFAULTONLINECONTACTTYPE]  BIT       DEFAULT ((0)) NOT NULL,
    CONSTRAINT [PK_PMCONTACTTYPEREF] PRIMARY KEY CLUSTERED ([CONTACTTYPEEXTID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_PMCONTACTTYPEREF_COTYPE] FOREIGN KEY ([LANDMANAGEMENTCONTACTTYPEID]) REFERENCES [dbo].[LANDMANAGEMENTCONTACTTYPE] ([LANDMANAGEMENTCONTACTTYPEID])
);


GO
CREATE TRIGGER [dbo].[TG_PMCONTACTTYPEREF_INSERT]
   ON  [dbo].[PMCONTACTTYPEREF]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [PMPERMITTYPE].[PMPERMITTYPEID], 
        [PMPERMITTYPE].[ROWVERSION],
        GETUTCDATE(),
        [PMPERMITTYPE].[LASTCHANGEDBY],	
        'Permit Type Contact Type Added',
        '',
        '',       
		'Permit Type (' + ISNULL([PMPERMITTYPE].[NAME],'[none]') + '), Work Class Name (' + ISNULL([PMPERMITWORKCLASS].[NAME],'[none]') +'), Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] +')',
		'4976A4A9-20E8-4B8F-997D-CE244B540104',
		1,
		0,
		[LANDMANAGEMENTCONTACTTYPE].[NAME]
    FROM [inserted]
	INNER JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
	LEFT JOIN [PMPERMITWORKCLASS] WITH (NOLOCK) ON [PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = [inserted].[OBJCLASSID]
	LEFT JOIN [PMPERMITTYPEWORKCLASS] WITH (NOLOCK) ON [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEWORKCLASSID] = [inserted].[OBJTYPEID]	
	LEFT JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEID]
END
GO
CREATE TRIGGER [dbo].[TG_PMCONTACTTYPEREF_UPDATE]
   ON  [dbo].[PMCONTACTTYPEREF] 
   AFTER UPDATE
AS 
BEGIN	
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
			[PMPERMITTYPE].[PMPERMITTYPEID],
			[PMPERMITTYPE].[ROWVERSION],
			GETUTCDATE(),
			[PMPERMITTYPE].[LASTCHANGEDBY],
			'Required Flag',
			CASE [deleted].[ISREQUIRED] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			CASE [inserted].[ISREQUIRED] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Work Class Name (' + ISNULL([PMPERMITWORKCLASS].[NAME],'[none]') +'), Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] +')',
			'4976A4A9-20E8-4B8F-997D-CE244B540104',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTTYPEEXTID] = [inserted].[CONTACTTYPEEXTID]
			INNER JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
			LEFT JOIN [PMPERMITWORKCLASS] WITH (NOLOCK) ON [PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = [inserted].[OBJCLASSID]
			LEFT JOIN [PMPERMITTYPEWORKCLASS] WITH (NOLOCK) ON [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEWORKCLASSID] = [inserted].[OBJTYPEID]
			LEFT JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEID]
	WHERE	[deleted].[ISREQUIRED] <> [inserted].[ISREQUIRED]
	UNION ALL
	SELECT 
			[PMPERMITTYPE].[PMPERMITTYPEID],
			[PMPERMITTYPE].[ROWVERSION],
			GETUTCDATE(),
			[PMPERMITTYPE].[LASTCHANGEDBY],
			'Default Online Contact Type Flag',
			CASE [deleted].[ISDEFAULTONLINECONTACTTYPE] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			CASE [inserted].[ISDEFAULTONLINECONTACTTYPE] WHEN 1 THEN 'Yes' WHEN 0 THEN 'No' END,
			'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Work Class Name (' + ISNULL([PMPERMITWORKCLASS].[NAME],'[none]') +'), Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] +')',
			'4976A4A9-20E8-4B8F-997D-CE244B540104',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTTYPEEXTID] = [inserted].[CONTACTTYPEEXTID]
			INNER JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
			LEFT JOIN [PMPERMITWORKCLASS] WITH (NOLOCK) ON [PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = [inserted].[OBJCLASSID]
			LEFT JOIN [PMPERMITTYPEWORKCLASS] WITH (NOLOCK) ON [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEWORKCLASSID] = [inserted].[OBJTYPEID]
			LEFT JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEID]
	WHERE	[deleted].[ISDEFAULTONLINECONTACTTYPE] <> [inserted].[ISDEFAULTONLINECONTACTTYPE]
	UNION ALL
	SELECT 
			[PMPERMITTYPE].[PMPERMITTYPEID],
			[PMPERMITTYPE].[ROWVERSION],
			GETUTCDATE(),
			[PMPERMITTYPE].[LASTCHANGEDBY],
			'Contact Type Set',
			ISNULL(CONVERT(NVARCHAR(MAX),[deleted].[CONTACTTYPEGROUP]),'[none]'),
			ISNULL(CONVERT(NVARCHAR(MAX),[inserted].[CONTACTTYPEGROUP]),'[none]'),
			'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Work Class Name (' + ISNULL([PMPERMITWORKCLASS].[NAME],'[none]') +'), Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] +')',
			'4976A4A9-20E8-4B8F-997D-CE244B540104',
			2,
			0,
			[LANDMANAGEMENTCONTACTTYPE].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[CONTACTTYPEEXTID] = [inserted].[CONTACTTYPEEXTID]
			INNER JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [inserted].[LANDMANAGEMENTCONTACTTYPEID]
			LEFT JOIN [PMPERMITWORKCLASS] WITH (NOLOCK) ON [PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = [inserted].[OBJCLASSID]
			LEFT JOIN [PMPERMITTYPEWORKCLASS] WITH (NOLOCK) ON [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEWORKCLASSID] = [inserted].[OBJTYPEID]
			LEFT JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEID]
	WHERE	ISNULL([deleted].[CONTACTTYPEGROUP],'') <> ISNULL([inserted].[CONTACTTYPEGROUP],'')
	
END
GO
CREATE TRIGGER [dbo].[TG_PMCONTACTTYPEREF_DELETE]  
    ON  [dbo].[PMCONTACTTYPEREF]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[PMPERMITTYPE].[PMPERMITTYPEID],
		[PMPERMITTYPE].[ROWVERSION],
		GETUTCDATE(),			
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Permit Type Contact Type Deleted',
        '',
        '',       
		'Permit Type (' + [PMPERMITTYPE].[NAME] + '), Work Class Name (' + ISNULL([PMPERMITWORKCLASS].[NAME],'[none]') +'), Contact Type (' + [LANDMANAGEMENTCONTACTTYPE].[NAME] +')',
		'4976A4A9-20E8-4B8F-997D-CE244B540104',
		3,
		0,
		[LANDMANAGEMENTCONTACTTYPE].[NAME]
    FROM [deleted]
	INNER JOIN [LANDMANAGEMENTCONTACTTYPE] WITH (NOLOCK) ON [LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [deleted].[LANDMANAGEMENTCONTACTTYPEID]
	LEFT JOIN [PMPERMITWORKCLASS] WITH (NOLOCK) ON [PMPERMITWORKCLASS].[PMPERMITWORKCLASSID] = [deleted].[OBJCLASSID]
	LEFT JOIN [PMPERMITTYPEWORKCLASS] WITH (NOLOCK) ON [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEWORKCLASSID] = [deleted].[OBJTYPEID]	
	LEFT JOIN [PMPERMITTYPE] ON [PMPERMITTYPE].[PMPERMITTYPEID] = [PMPERMITTYPEWORKCLASS].[PMPERMITTYPEID]
END