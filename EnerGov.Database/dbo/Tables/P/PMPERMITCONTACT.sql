CREATE TABLE [dbo].[PMPERMITCONTACT] (
    [PMPERMITCONTACTID]           CHAR (36) NOT NULL,
    [PMPERMITID]                  CHAR (36) NOT NULL,
    [LANDMANAGEMENTCONTACTTYPEID] CHAR (36) NOT NULL,
    [GLOBALENTITYID]              CHAR (36) NOT NULL,
    [ISBILLING]                   BIT       CONSTRAINT [DF_PMPermitContact_Billing] DEFAULT ((0)) NOT NULL,
    [CONTACTCONFIRMSTATUSID]      INT       DEFAULT ((0)) NOT NULL,
    CONSTRAINT [PK_PMPermitContact] PRIMARY KEY CLUSTERED ([PMPERMITCONTACTID] ASC) WITH (FILLFACTOR = 80),
    CONSTRAINT [FK_PermitContact_GlobalEntity] FOREIGN KEY ([GLOBALENTITYID]) REFERENCES [dbo].[GLOBALENTITY] ([GLOBALENTITYID]),
    CONSTRAINT [FK_PermitContact_LMContactType] FOREIGN KEY ([LANDMANAGEMENTCONTACTTYPEID]) REFERENCES [dbo].[LANDMANAGEMENTCONTACTTYPE] ([LANDMANAGEMENTCONTACTTYPEID]),
    CONSTRAINT [FK_PermitContact_PMPermit] FOREIGN KEY ([PMPERMITID]) REFERENCES [dbo].[PMPERMIT] ([PMPERMITID]),
    CONSTRAINT [FK_PMCONTACT_CONTACTCFMTYPE] FOREIGN KEY ([CONTACTCONFIRMSTATUSID]) REFERENCES [dbo].[CONTACTCONFIRMSTATUS] ([CONTACTCONFIRMSTATUSID])
);


GO
CREATE NONCLUSTERED INDEX [IX_PMPERMITCONTACT_ENTITY]
    ON [dbo].[PMPERMITCONTACT]([GLOBALENTITYID] ASC, [PMPERMITID] ASC) WITH (FILLFACTOR = 80);


GO
CREATE NONCLUSTERED INDEX [IX_PMPERMITCONTACT_PERMIT]
    ON [dbo].[PMPERMITCONTACT]([PMPERMITID] ASC, [GLOBALENTITYID] ASC, [LANDMANAGEMENTCONTACTTYPEID] ASC) WITH (FILLFACTOR = 80);


GO
CREATE NONCLUSTERED INDEX [PMPERMITCONTACT_PERMIT]
    ON [dbo].[PMPERMITCONTACT]([PMPERMITID] ASC) WITH (FILLFACTOR = 80);


GO
CREATE NONCLUSTERED INDEX [PMPERMITCONTACT_ENTITY]
    ON [dbo].[PMPERMITCONTACT]([GLOBALENTITYID] ASC, [ISBILLING] ASC, [PMPERMITID] ASC) WITH (FILLFACTOR = 80);


GO
CREATE TRIGGER [dbo].[TG_PMPERMITCONTACT_DELETE]
	ON [dbo].[PMPERMITCONTACT]
	AFTER DELETE
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @CaseType VARCHAR(50) = (SELECT dbo.UFN_GET_CASE_FROM_CONTEXT_INFO())
	-- Check if PENDINGCAPCONTACT Text is set in the Context info, if yes, then Insert the History Logs else return without inserting any logs	
	IF @CaseType = 'PENDINGCAPCONTACT'
	BEGIN
		INSERT INTO [HISTORYSYSTEMSETUP]
	(
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
		[GLOBALENTITY].[GLOBALENTITYID],
		[GLOBALENTITY].[ROWVERSION],
		GETUTCDATE(),
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Permit Contact Deleted',
		'',
		'',
		'Pending Cap Contact (' + 
				CASE WHEN [GLOBALENTITY].[ISCOMPANY] = 1 AND [GLOBALENTITY].[ISCONTACT]= 1 
					THEN [GLOBALENTITY].[GLOBALENTITYNAME]
				 WHEN [GLOBALENTITY].[ISCOMPANY] = 0 AND [GLOBALENTITY].[ISCONTACT] = 1 
					THEN [GLOBALENTITY].[LASTNAME] + COALESCE(', ' + [GLOBALENTITY].[FIRSTNAME], '')
				 WHEN [GLOBALENTITY].[ISCOMPANY] = 1 AND [GLOBALENTITY].[ISCONTACT] = 0 
					THEN [GLOBALENTITY].[GLOBALENTITYNAME]
		END + ')',
		'098DD4A2-D8A4-4047-AD1B-E3856C77F6BB',
		3,
		0,		
		CASE WHEN [GLOBALENTITY].[ISCOMPANY] = 1 AND [GLOBALENTITY].[ISCONTACT]= 1 
				THEN [GLOBALENTITY].[GLOBALENTITYNAME]
			 WHEN [GLOBALENTITY].[ISCOMPANY] = 0 AND [GLOBALENTITY].[ISCONTACT] = 1 
				THEN [GLOBALENTITY].[LASTNAME] + COALESCE(', ' + [GLOBALENTITY].[FIRSTNAME], '')
			WHEN [GLOBALENTITY].[ISCOMPANY] = 1 AND [GLOBALENTITY].[ISCONTACT] = 0 
				THEN [GLOBALENTITY].[GLOBALENTITYNAME] 
		END
	FROM [deleted]
	INNER JOIN [GLOBALENTITY] ON [deleted].[GLOBALENTITYID] = [GLOBALENTITY].[GLOBALENTITYID]
	END
END
GO

CREATE TRIGGER [dbo].[TG_PMPERMITCONTACT_UPDATE] ON [dbo].[PMPERMITCONTACT]
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
	DECLARE @CaseType VARCHAR(50) = (SELECT dbo.UFN_GET_CASE_FROM_CONTEXT_INFO())
	-- Check if PENDINGCAPCONTACT Text is set in the Context info, if yes, then Insert the History Logs else return without inserting any logs	
	IF @CaseType = 'PENDINGCAPCONTACT'
	BEGIN
		INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[GLOBALENTITY_INSERTED].[GLOBALENTITYID],
			[GLOBALENTITY_INSERTED].[ROWVERSION],
			GETUTCDATE(),
			[GLOBALENTITY_INSERTED].[LASTCHANGEDBY],
			'Globalentity Id',
			[GLOBALENTITY_DELETED].[GLOBALENTITYNAME],
			[GLOBALENTITY_INSERTED].[GLOBALENTITYNAME],
			'Pending Cap Contact (' + 
				 CASE WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 1 AND [GLOBALENTITY_INSERTED].[ISCONTACT]= 1 
					THEN [GLOBALENTITY_INSERTED].[GLOBALENTITYNAME]
				 WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 0 AND [GLOBALENTITY_INSERTED].[ISCONTACT] = 1 
					THEN [GLOBALENTITY_INSERTED].[LASTNAME] + COALESCE(', ' + [GLOBALENTITY_INSERTED].[FIRSTNAME], '')
				 WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 1 AND [GLOBALENTITY_INSERTED].[ISCONTACT] = 0 
					THEN [GLOBALENTITY_INSERTED].[GLOBALENTITYNAME] END + ')',
			'098DD4A2-D8A4-4047-AD1B-E3856C77F6BB',
			2,
			0,
			CASE WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 1 AND [GLOBALENTITY_INSERTED].[ISCONTACT]= 1 
					THEN [GLOBALENTITY_INSERTED].[GLOBALENTITYNAME]
				 WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 0 AND [GLOBALENTITY_INSERTED].[ISCONTACT] = 1 
					THEN [GLOBALENTITY_INSERTED].[LASTNAME] + COALESCE(', ' + [GLOBALENTITY_INSERTED].[FIRSTNAME], '')
				 WHEN [GLOBALENTITY_INSERTED].[ISCOMPANY] = 1 AND [GLOBALENTITY_INSERTED].[ISCONTACT] = 0 
					THEN [GLOBALENTITY_INSERTED].[GLOBALENTITYNAME] END
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[PMPERMITCONTACTID] = [inserted].[PMPERMITCONTACTID]
			INNER JOIN [GLOBALENTITY] GLOBALENTITY_DELETED ON [deleted].[GLOBALENTITYID] = [GLOBALENTITY_DELETED].[GLOBALENTITYID]
			INNER JOIN [GLOBALENTITY] GLOBALENTITY_INSERTED ON [inserted].[GLOBALENTITYID] = [GLOBALENTITY_INSERTED].[GLOBALENTITYID]
	WHERE	[deleted].[GLOBALENTITYID] <> [inserted].[GLOBALENTITYID]
	END
END