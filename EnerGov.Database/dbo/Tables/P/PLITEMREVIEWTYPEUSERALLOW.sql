CREATE TABLE [dbo].[PLITEMREVIEWTYPEUSERALLOW] (
    [PLITEMREVIEWTYPEUSERALLOWID] CHAR (36) NOT NULL,
    [PLITEMREVIEWTYPEID]          CHAR (36) NOT NULL,
    [USERID]                      CHAR (36) NOT NULL,
    CONSTRAINT [PK_PLItemReviewTypeUserAllow] PRIMARY KEY CLUSTERED ([PLITEMREVIEWTYPEUSERALLOWID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_PLItemReviewUserAllow_Type] FOREIGN KEY ([PLITEMREVIEWTYPEID]) REFERENCES [dbo].[PLITEMREVIEWTYPE] ([PLITEMREVIEWTYPEID]),
    CONSTRAINT [FK_PLTypeUserAllow_Users] FOREIGN KEY ([USERID]) REFERENCES [dbo].[USERS] ([SUSERGUID])
);


GO
CREATE NONCLUSTERED INDEX [IX_PLITEMREVIEWTYPEUSERALLOW_PLITEMREVIEWTYPEID]
    ON [dbo].[PLITEMREVIEWTYPEUSERALLOW]([PLITEMREVIEWTYPEID] ASC);


GO
CREATE TRIGGER [dbo].[TG_PLITEMREVIEWTYPEUSERALLOW_DELETE]  
    ON  [dbo].[PLITEMREVIEWTYPEUSERALLOW]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;
	INSERT INTO [HISTORYSYSTEMSETUP]
	(
		[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
	)
	SELECT
		[PLITEMREVIEWTYPE].[PLITEMREVIEWTYPEID], 
        [PLITEMREVIEWTYPE].[ROWVERSION],
		GETUTCDATE(),
		(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
		'Item Review Type Allowed User Deleted',
        '',
        '',
		'Item Review Type (' + [PLITEMREVIEWTYPE].[NAME] + '), Assigned To (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + ')',
		'86195D62-AE88-4A8D-A015-6CCD233B4D64',
		3,
		0,
		[PLITEMREVIEWTYPE].[NAME]
    FROM [deleted]
	INNER JOIN [PLITEMREVIEWTYPE] ON [PLITEMREVIEWTYPE].[PLITEMREVIEWTYPEID] = [deleted].[PLITEMREVIEWTYPEID]
	INNER JOIN [USERS] [USERS] WITH (NOLOCK) ON [USERS].[SUSERGUID] = [deleted].[USERID]
END
GO
CREATE TRIGGER [dbo].[TG_PLITEMREVIEWTYPEUSERALLOW_INSERT]
   ON  [dbo].[PLITEMREVIEWTYPEUSERALLOW]
   AFTER INSERT
AS 
BEGIN	
	SET NOCOUNT ON;	
	INSERT INTO [HISTORYSYSTEMSETUP]
    (
        [ID],
        [ROWVERSION],
        [CHANGEDON],
        [CHANGEDBY],
        [FIELDNAME],
        [OLDVALUE],
        [NEWVALUE],
        [ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT 
        [PLITEMREVIEWTYPE].[PLITEMREVIEWTYPEID],
        [PLITEMREVIEWTYPE].[ROWVERSION],
        GETUTCDATE(),
        [PLITEMREVIEWTYPE].[LASTCHANGEDBY],
        'Item Review Type Allowed User Added',
        '',
        '',
		'Item Review Type (' + [PLITEMREVIEWTYPE].[NAME] + '), Assigned To (' + [USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME], '') + ')',
		'86195D62-AE88-4A8D-A015-6CCD233B4D64',
		1,
		0,
		[PLITEMREVIEWTYPE].[NAME]
    FROM [inserted]
	INNER JOIN [PLITEMREVIEWTYPE] ON [PLITEMREVIEWTYPE].[PLITEMREVIEWTYPEID] = [inserted].[PLITEMREVIEWTYPEID]
	INNER JOIN [USERS] [USERS] WITH (NOLOCK) ON [USERS].[SUSERGUID] = [inserted].[USERID]
END