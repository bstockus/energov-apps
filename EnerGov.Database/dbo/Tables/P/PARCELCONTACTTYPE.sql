CREATE TABLE [dbo].[PARCELCONTACTTYPE] (
    [PARCELCONTACTTYPEID]       CHAR (36)      NOT NULL,
    [NAME]                      VARCHAR (50)   NOT NULL,
    [DESCRIPTION]               NVARCHAR (MAX) NULL,
    [PARCELCONTACTSYSTEMTYPEID] INT            NOT NULL,
    [LASTCHANGEDBY]             CHAR (36)      NULL,
    [LASTCHANGEDON]             DATETIME       CONSTRAINT [DF_PARCELCONTACTTYPE_LastChangedOn] DEFAULT (getutcdate()) NOT NULL,
    [ROWVERSION]                INT            CONSTRAINT [DF_PARCELCONTACTTYPE_RowVersion] DEFAULT ((1)) NOT NULL,
    CONSTRAINT [PK_PARCELCONTACTTYPEID] PRIMARY KEY CLUSTERED ([PARCELCONTACTTYPEID] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [FK_PARCELCONTACTTYPE_SYSTYP] FOREIGN KEY ([PARCELCONTACTSYSTEMTYPEID]) REFERENCES [dbo].[PARCELCONTACTSYSTEMTYPE] ([PARCELCONTACTSYSTEMTYPEID])
);


GO
CREATE NONCLUSTERED INDEX [PARCELCONTACTTYPE_IX_QUERY]
    ON [dbo].[PARCELCONTACTTYPE]([PARCELCONTACTTYPEID] ASC, [NAME] ASC);


GO

CREATE TRIGGER [dbo].[TG_PARCELCONTACTTYPE_DELETE] ON [dbo].[PARCELCONTACTTYPE]
   AFTER DELETE
AS 
BEGIN
	SET NOCOUNT ON;

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )

	SELECT
			[deleted].[PARCELCONTACTTYPEID],
			[deleted].[ROWVERSION],
			GETUTCDATE(),
			(SELECT dbo.UFN_GET_USERID_FROM_CONTEXT_INFO()),
			'Parcel Contact Type Deleted',
			'',
			'',
			'Parcel Contact Type (' + [deleted].[NAME] + ')',
			'8878FD3B-9F0F-482A-9B84-2DAF15A7EE88',
			3,
			1,
			[deleted].[NAME]
	FROM	[deleted]
END
GO

CREATE TRIGGER [dbo].[TG_PARCELCONTACTTYPE_INSERT] ON [dbo].[PARCELCONTACTTYPE]
   FOR INSERT
AS 
BEGIN
	SET NOCOUNT ON;
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of PARCELCONTACTTYPE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[PARCELCONTACTTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Parcel Contact Type Added',
			'',
			'',
			'Parcel Contact Type (' + [inserted].[NAME] + ')',
			'8878FD3B-9F0F-482A-9B84-2DAF15A7EE88',
			1,
			1,
			[inserted].[NAME]
	FROM	[inserted]	
END
GO

CREATE TRIGGER [dbo].[TG_PARCELCONTACTTYPE_UPDATE] ON [dbo].[PARCELCONTACTTYPE] 
	AFTER UPDATE
AS 
BEGIN
	SET NOCOUNT ON;
		
	-- Check if LASTCHANGEDBY contains VALID User Id and it Exists in USERS table, this is in replacement to foreign key reference of PARCELCONTACTTYPE table with USERS table.
	IF EXISTS (SELECT * FROM inserted 
		LEFT OUTER JOIN USERS WITH (NOLOCK) ON USERS.SUSERGUID = inserted.LASTCHANGEDBY
		WHERE inserted.LASTCHANGEDBY IS NOT NULL AND USERS.SUSERGUID IS NULL)
	BEGIN		
		RAISERROR ('The INSERT or UPDATE statement conflicted with the FOREIGN KEY to table USERS', 16, 0);
		ROLLBACK;
		RETURN;
	END	

    INSERT INTO [HISTORYSYSTEMSETUP]
    (	[ID],
		[ROWVERSION],
		[CHANGEDON],
		[CHANGEDBY],
		[FIELDNAME],
		[OLDVALUE],
		[NEWVALUE],
		[ADDITIONALINFO],
		[FORMID],
		[ACTION],
		[ISROOT],
		[RECORDNAME]
    )
	SELECT
			[inserted].[PARCELCONTACTTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Name',
			[deleted].[NAME],
			[inserted].[NAME],
			'Parcel Contact Type (' + [inserted].[NAME] + ')',
			'8878FD3B-9F0F-482A-9B84-2DAF15A7EE88',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[PARCELCONTACTTYPEID] = [inserted].[PARCELCONTACTTYPEID]
	WHERE	[deleted].[NAME] <> [inserted].[NAME]

	UNION ALL
	SELECT
			[inserted].[PARCELCONTACTTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'Description',
			ISNULL([deleted].[DESCRIPTION],'[none]'),
			ISNULL([inserted].[DESCRIPTION],'[none]'),
			'Parcel Contact Type (' + [inserted].[NAME] + ')',
			'8878FD3B-9F0F-482A-9B84-2DAF15A7EE88',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[PARCELCONTACTTYPEID] = [inserted].[PARCELCONTACTTYPEID]
	WHERE	ISNULL([deleted].[DESCRIPTION], '') <> ISNULL([inserted].[DESCRIPTION], '')
	UNION ALL
	SELECT
			[inserted].[PARCELCONTACTTYPEID],
			[inserted].[ROWVERSION],
			GETUTCDATE(),
			[inserted].[LASTCHANGEDBY],
			'System Type',
			[PARCELCONTACTSYSTEMTYPE_DELETED].[NAME],
			[PARCELCONTACTSYSTEMTYPE_INSERTED].[NAME],
			'Parcel Contact Type (' + [inserted].[NAME] + ')',
			'8878FD3B-9F0F-482A-9B84-2DAF15A7EE88',
			2,
			1,
			[inserted].[NAME]
	FROM	[deleted]
			JOIN [inserted] ON [deleted].[PARCELCONTACTTYPEID] = [inserted].[PARCELCONTACTTYPEID]
			JOIN PARCELCONTACTSYSTEMTYPE PARCELCONTACTSYSTEMTYPE_DELETED WITH (NOLOCK) ON [deleted].[PARCELCONTACTSYSTEMTYPEID] = [PARCELCONTACTSYSTEMTYPE_DELETED].[PARCELCONTACTSYSTEMTYPEID]
			JOIN PARCELCONTACTSYSTEMTYPE PARCELCONTACTSYSTEMTYPE_INSERTED WITH (NOLOCK) ON [inserted].[PARCELCONTACTSYSTEMTYPEID] = [PARCELCONTACTSYSTEMTYPE_INSERTED].[PARCELCONTACTSYSTEMTYPEID]
	WHERE	[deleted].[PARCELCONTACTSYSTEMTYPEID] <> [inserted].[PARCELCONTACTSYSTEMTYPEID]
END