CREATE PROCEDURE [gissetup].[USP_USERS_REVIEWERUSER_SEARCH]
(
	@SEARCH			AS NVARCHAR(255)	=	'',
	@PAGE_NUMBER	AS INT				=	1,
	@PAGE_SIZE		AS INT				=	10,
	@IS_ASCENDING	AS BIT				=	1,
	@ITEMREVIEWTYPE_ID	AS CHAR(36)     =   ''
)
AS

WITH RAW_DATA AS (
	SELECT		[dbo].[USERS].[SUSERGUID],
				[dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'') AS [FULLNAME],
				CASE @IS_ASCENDING	WHEN 1 THEN ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],''))
										   ELSE ROW_NUMBER() OVER(ORDER BY [dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'') DESC)
				END AS RowNumber,
				COUNT(1) OVER() AS TotalRows
	FROM		[dbo].[USERS]
				INNER JOIN [dbo].[USERDEPARTMENT] ON [dbo].[USERS].[SUSERGUID] = [dbo].[USERDEPARTMENT].[SUSERGUID]
				LEFT  JOIN [dbo].[PLITEMREVIEWTYPE] ON [dbo].[USERDEPARTMENT].[DEPARTMENTID] = [dbo].[PLITEMREVIEWTYPE].[DEPARTMENTID]
	WHERE		([dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'') LIKE '%'+ @SEARCH +'%' ) AND
				([dbo].[PLITEMREVIEWTYPE].[PLITEMREVIEWTYPEID] = @ITEMREVIEWTYPE_ID )
	GROUP BY	[dbo].[USERS].[SUSERGUID], [dbo].[USERS].[LNAME] + COALESCE(', ' + [USERS].[FNAME],'')
)

SELECT		* 
FROM		RAW_DATA
WHERE		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) 
			AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY	RowNumber