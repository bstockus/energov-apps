CREATE PROCEDURE [inspection].[USP_IMCHECKLIST_SEARCH_ALL]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@INSPECTIONTYPEID AS CHAR(36) = NULL,
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
	@SORT_FIELD AS NVARCHAR(MAX) = 'Name'
)
AS
WITH RAW_DATA AS (
	SELECT
		[dbo].[IMCHECKLIST].[IMCHECKLISTID],
		[dbo].[IMCHECKLIST].[NAME],
		[dbo].[IMCHECKLIST].[DESCRIPTION],
		[dbo].[IMCHECKLISTCATEGORY].[NAME] AS IMCHECKLISTCATEGORY,
		(CASE WHEN @IS_ASCENDING = 1 
			THEN 
			CASE 
				WHEN @SORT_FIELD = 'CategoryName' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLISTCATEGORY].[NAME] ASC)
				WHEN  @SORT_FIELD = 'Description' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLIST].[DESCRIPTION] ASC)
				ELSE ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLIST].[NAME] ASC) 
			END
			WHEN @IS_ASCENDING=0
			THEN 
			CASE 
				WHEN @SORT_FIELD = 'CategoryName' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLISTCATEGORY].[NAME] DESC)
				WHEN  @SORT_FIELD = 'Description' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLIST].[DESCRIPTION] DESC)
				ELSE ROW_NUMBER() OVER(ORDER BY [dbo].[IMCHECKLIST].[NAME] DESC) 
			END
			END) AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[IMCHECKLIST] 
	LEFT OUTER JOIN [dbo].[IMCHECKLISTCATEGORY] ON [dbo].[IMCHECKLISTCATEGORY].[IMCHECKLISTCATEGORYID] = [dbo].[IMCHECKLIST].[IMCHECKLISTCATEGORYID]
	LEFT OUTER JOIN [dbo].[IMINSPECTIONTYPECHKLSTXREF] on [dbo].[IMINSPECTIONTYPECHKLSTXREF].[IMCHECKLISTID] = [dbo].[IMCHECKLIST].[IMCHECKLISTID] AND [dbo].[IMINSPECTIONTYPECHKLSTXREF].[IMINSPECTIONTYPEID] = @INSPECTIONTYPEID
	WHERE (@INSPECTIONTYPEID IS NULL OR [dbo].[IMINSPECTIONTYPECHKLSTXREF].[IMCHECKLISTID] IS NOT NULL) AND (([dbo].[IMCHECKLIST].[NAME] LIKE '%'+ @SEARCH +'%') OR
		([dbo].[IMCHECKLIST].[DESCRIPTION] LIKE '%'+ @SEARCH +'%') OR
		([dbo].[IMCHECKLISTCATEGORY].[NAME] LIKE '%'+ @SEARCH +'%'))
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber