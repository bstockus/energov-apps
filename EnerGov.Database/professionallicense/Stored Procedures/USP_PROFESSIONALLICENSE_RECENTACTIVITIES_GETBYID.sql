CREATE PROCEDURE [professionallicense].[USP_PROFESSIONALLICENSE_RECENTACTIVITIES_GETBYID]
(
  @ID AS CHAR(36)
)
AS
BEGIN
SELECT TOP 15 * FROM
(
	SELECT ActivityDate = LASTRENEWALDATE,ProfessionalLicenseId = ILLICENSEID,IssuedBy = '',
		   ActivityId = '', ActivityNumber = '', ActivityName = '', ActivityType = '',
		   TransactionId = '', Amount = 0,ActionStepId = '',ActionStep = '',WorkflowStatusId = 0,WorkflowStatus = '' 
	FROM ILLICENSE
	WHERE ILLICENSEID = @ID AND LASTRENEWALDATE IS NOT NULL

	UNION ALL
	SELECT  ActivityDate = ISSUEDATE,ProfessionalLicenseId='',IssuedBy = Concat(USERS.FNAME, ' ', USERS.LNAME),
			ActivityId = '', ActivityNumber = '', ActivityName = '', ActivityType = '',
			TransactionId = '', Amount = 0,ActionStepId = '',ActionStep = '',WorkflowStatusId = 0,WorkflowStatus = '' 
	FROM ILLICENSE
		 INNER JOIN USERS ON ILLICENSE.ISSUEDBY = USERS.SUSERGUID
	WHERE ILLICENSEID = @ID AND ISSUEDATE IS NOT NULL

	UNION ALL
	SELECT * FROM (
		SELECT TOP 15 ActivityDate = CREATEDON, ProfessionalLicenseId='', IssuedBy = '' ,
			ActivityId = ILLICENSEACTIVITYID, ActivityNumber = ACTIVITYNUMBER, ActivityName = ACTIVITYNAME,ActivityType = ILLICENSEACTIVITYTYPE.NAME,
			TransactionId = '', Amount = 0,ActionStepId = '',ActionStep = '',WorkflowStatusId = 0,WorkflowStatus = ''
		FROM ILLICENSEACTIVITY
			INNER JOIN ILLICENSEACTIVITYTYPE ON ILLICENSEACTIVITY.ILLICENSEACTIVITYTYPEID = ILLICENSEACTIVITYTYPE.ILLICENSEACTIVITYTYPEID
		WHERE ILLICENSEACTIVITY.ILLICENSEID = @ID
		ORDER BY CREATEDON DESC
	) AS activities

	UNION ALL
	SELECT * FROM (
		SELECT DISTINCT TOP 15 ActivityDate = PAYMENTDATE,ProfessionalLicenseId='',IssuedBy = '',
			ActivityId = '', ActivityNumber = '', ActivityName = '', ActivityType = '',
			TransactionId = CATRANSACTION.CATRANSACTIONID, Amount = CATRANSACTIONINVOICE.PAIDAMOUNT,ActionStepId = '',ActionStep = '',WorkflowStatusId = 0,WorkflowStatus = ''
		FROM CATRANSACTION
			INNER JOIN CATRANSACTIONFEE on CATRANSACTION.CATRANSACTIONID = CATRANSACTIONFEE.CATRANSACTIONID
			INNER JOIN CATRANSACTIONINVOICE on CATRANSACTIONFEE.CATRANSACTIONID = CATRANSACTIONINVOICE.CATRANSACTIONID
			INNER JOIN CATRANSACTIONPAYMENT ON CATRANSACTIONPAYMENT.CATRANSACTIONID = CATRANSACTIONINVOICE.CATRANSACTIONID
			INNER JOIN CAINVOICE on CATRANSACTIONINVOICE.CAINVOICEID = CAINVOICE.CAINVOICEID
			INNER JOIN CAINVOICEFEE on CAINVOICE.CAINVOICEID = CAINVOICEFEE.CAINVOICEID
			INNER JOIN CACOMPUTEDFEE on CAINVOICEFEE.CACOMPUTEDFEEID = CACOMPUTEDFEE.CACOMPUTEDFEEID
			INNER JOIN ILLICENSEFEE on CACOMPUTEDFEE.CACOMPUTEDFEEID = ILLICENSEFEE.CACOMPUTEDFEEID
		WHERE ILLICENSEFEE.ILLICENSEID = @ID
		ORDER BY PAYMENTDATE DESC
	)AS payments

	UNION ALL
	SELECT * FROM (
		SELECT DISTINCT TOP 15 ActivityDate = ILLICENSEWFACTIONSTEP.LASTCHANGEDON,ProfessionalLicenseId='',IssuedBy = '',
			ActivityId = '', ActivityNumber = '', ActivityName = '', ActivityType = '',
			TransactionId = '', Amount = 0, ActionStepId = ILLICENSEWFACTIONSTEP.ILLICENSEWFACTIONSTEPID,ActionStep = ILLICENSEWFACTIONSTEP.NAME,
			WorkflowStatusId = ILLICENSEWFACTIONSTEP.WORKFLOWSTATUSID,
			WorkflowStatus = WORKFLOWSTATUS.NAME
		FROM ILLICENSEWFSTEP
			INNER JOIN ILLICENSEWFACTIONSTEP on ILLICENSEWFSTEP.ILLICENSEWFSTEPID = ILLICENSEWFACTIONSTEP.ILLICENSEWFSTEPID
			INNER JOIN WORKFLOWSTATUS ON ILLICENSEWFSTEP.WORKFLOWSTATUSID = WORKFLOWSTATUS.WORKFLOWSTATUSID
		WHERE ILLICENSEWFSTEP.ILLICENSEID = @ID AND ILLICENSEWFACTIONSTEP.WORKFLOWSTATUSID <>0  AND ILLICENSEWFACTIONSTEP.WORKFLOWSTATUSID <> 3
		ORDER BY ILLICENSEWFACTIONSTEP.LASTCHANGEDON DESC
	)AS workFlowActivities
) AS recentActivities
ORDER BY ActivityDate DESC

END