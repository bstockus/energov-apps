CREATE PROCEDURE [energovnotifications].[USP_TASK_SEARCH]
(
	@SEARCH			AS NVARCHAR(MAX)	=	'',
	@PAGE_NUMBER	AS INT				=	1,
	@PAGE_SIZE		AS INT				=	10,
	@IS_ASCENDING	AS BIT				=	1,
	@USERID CHAR(36),
	@ISTASKCOMPLETED AS BIT				=	NULL,
	@SORTBYCOLUMNNAME AS NVARCHAR(50)	= 'taskSubject',
	@ASSIGNEDTOLIST AS [dbo].[RecordIDs] READONLY,
	@CREATEDBYLIST AS [dbo].[RecordIDs] READONLY,
	@ISTASKCREATEDBYME AS BIT			=	0
)
AS

DECLARE @ISNULLRECORDEXIST AS BIT  = 0
IF EXISTS(SELECT RECORDID FROM @ASSIGNEDTOLIST WHERE RECORDID is null) 
BEGIN
	SET @ISNULLRECORDEXIST = 1
END;

WITH RAW_DATA AS (
	SELECT
	[dbo].[TASK].[SUBJECT],
	[dbo].[TASKSTATUS].[TASKSTATUSNAME],
	ASSIGNEDUSERS.ASSIGNEDUSERNAME,
	(COALESCE([dbo].[USERS].[FNAME] + ' ', '') + [dbo].[USERS].[LNAME]) AS CREATEDBYUSER,
	[dbo].[TASK].DUEDATE,
	CASENUMBERS.CASENUMBER,
	CASENUMBERS.CASEID,
	[dbo].[TASK].[TASKID],
    [dbo].[SYSTEMMODULE].[MODULENAME],
	[dbo].[TASK].[FORMID],
	[dbo].[TASKTYPE].[NAME] AS TASKTYPENAME,	
	[dbo].[TASK].[STARTDATE],
	[dbo].[TASK].[COMPLETEDDATE],	
	[dbo].[TASK].[UNIQUERECORDID] AS RECORDID,
	[dbo].[TASKSTATUS].[ISCOMPLETED],

	CASE @IS_ASCENDING WHEN 1 THEN
		CASE WHEN @SORTBYCOLUMNNAME = 'taskSubject' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[TASK].[SUBJECT] ASC)
				 ELSE ROW_NUMBER() OVER
				 (
					ORDER BY
						CASE
							WHEN @SORTBYCOLUMNNAME = 'taskStatus' THEN [dbo].[TASKSTATUS].[TASKSTATUSNAME]
							WHEN @SORTBYCOLUMNNAME = 'caseNumber' THEN CASENUMBERS.CASENUMBER
							WHEN @SORTBYCOLUMNNAME = 'dueDate' THEN CONVERT(NVARCHAR(MAX),[dbo].[TASK].[DUEDATE], 120)
						END
					ASC,
					[dbo].[TASK].[SUBJECT] ASC
				)
			END
		ELSE
			CASE WHEN @SORTBYCOLUMNNAME = 'taskSubject' THEN ROW_NUMBER() OVER(ORDER BY [dbo].[TASK].[SUBJECT] DESC)
				 ELSE ROW_NUMBER() OVER
				 (
					ORDER BY
						CASE
							WHEN @SORTBYCOLUMNNAME = 'taskStatus' THEN [dbo].[TASKSTATUS].[TASKSTATUSNAME]
							WHEN @SORTBYCOLUMNNAME = 'caseNumber' THEN CASENUMBERS.CASENUMBER
							WHEN @SORTBYCOLUMNNAME = 'dueDate' THEN CONVERT(NVARCHAR(MAX),[dbo].[TASK].[DUEDATE], 120)
						END
					DESC,
					[dbo].[TASK].[SUBJECT] DESC
				)
			END
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[TASK]
	JOIN [dbo].[USERS] ON [dbo].[TASK].[CREATEDBYID] = [dbo].[USERS].[SUSERGUID]
	JOIN [dbo].[TASKSTATUS] ON [dbo].[TASK].[TASKSTATUSID] = [dbo].[TASKSTATUS].[TASKSTATUSID]
	JOIN [dbo].[TASKTYPE] ON [dbo].[TASK].[TASKTYPEID] = [dbo].[TASKTYPE].[TASKTYPEID]
	LEFT JOIN [dbo].[FORMS] ON [dbo].[TASK].[FORMID] = [dbo].[FORMS].[SFORMSGUID]
    LEFT JOIN [dbo].[SYSTEMMODULE] ON [dbo].[FORMS].[MODULE_ID] = [dbo].[SYSTEMMODULE].[SYSTEMMODULEID]
	JOIN [dbo].[TASKPRIORITY] ON [dbo].[TASK].[TASKPRIORITYID] = [dbo].[TASKPRIORITY].[TASKPRIORITYID]
	LEFT JOIN (
			SELECT  TASKID
				   ,STUFF((SELECT ', ' + (COALESCE([dbo].[USERS].[FNAME] + ' ', '') + [dbo].[USERS].[LNAME])
					 FROM [dbo].[TASKUSER]
					 JOIN [dbo].[USERS] ON USERID = [dbo].[USERS].[SUSERGUID]
					 where TASKID = T1.TASKID
					 ORDER BY (COALESCE([dbo].[USERS].[FNAME] + ' ', '') + [dbo].[USERS].[LNAME])
					 FOR XML PATH(''), TYPE)
					.value('.','NVARCHAR(MAX)'),1,2,' ') ASSIGNEDUSERNAME
			FROM [dbo].[TASKUSER] T1
			WHERE (@ISTASKCREATEDBYME = 1 OR T1.USERID = @USERID)
			GROUP BY TASKID
		) ASSIGNEDUSERS ON [dbo].[TASK].[TASKID] = ASSIGNEDUSERS.TASKID
	JOIN (
		SELECT DISTINCT [dbo].[TASK].[TASKID]
		FROM 
			[dbo].[TASK] LEFT JOIN [dbo].[TASKUSER] ON [dbo].[TASK].TASKID = [dbo].[TASKUSER].TASKID 
		WHERE (
			(SELECT COUNT(1) FROM @ASSIGNEDTOLIST) = 0 
			OR ([dbo].[TASKUSER].USERID IN (SELECT [RECORDID] FROM @ASSIGNEDTOLIST)) 
			OR (@ISNULLRECORDEXIST = 1 AND [dbo].[TASKUSER].USERID IS NULL)
			)) 
			ASSIGNEDUSERTASKS ON [dbo].[TASK].[TASKID] = ASSIGNEDUSERTASKS.TASKID
	OUTER APPLY (
		SELECT TOP 1 
			CASEID, CASENUMBER
		FROM [dbo].[GETCASENUMBERBYRECORD]([dbo].[TASK].UNIQUERECORDID)
		) AS CASENUMBERS
	WHERE (@ISTASKCOMPLETED IS NULL OR [dbo].[TASKSTATUS].ISCOMPLETED = @ISTASKCOMPLETED) 
			AND ([dbo].[TASK].[SUBJECT] LIKE '%'+ @SEARCH +'%')			
			AND ((SELECT COUNT(1) FROM @CREATEDBYLIST) = 0 OR [dbo].[USERS].[SUSERGUID] IN (SELECT [RECORDID] FROM @CREATEDBYLIST))			
			AND ((@ISTASKCREATEDBYME = 1 AND [dbo].[TASK].[CREATEDBYID] = @USERID)
			OR (@ISTASKCREATEDBYME = 0 AND ASSIGNEDUSERNAME IS NOT NULL AND ASSIGNEDUSERTASKS.TASKID IS NOT NULL))
)

SELECT		* 
FROM		RAW_DATA
WHERE		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) 
			AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY	RowNumber