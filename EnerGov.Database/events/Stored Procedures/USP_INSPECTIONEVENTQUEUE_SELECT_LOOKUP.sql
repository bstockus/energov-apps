CREATE PROCEDURE [events].[USP_INSPECTIONEVENTQUEUE_SELECT_LOOKUP]
(
	@BATCHSIZE AS INT = 1000
)
AS  
BEGIN

DECLARE @RESULT_DATA AS TABLE 
(
	[INSPECTIONEVENTQUEUEID] BIGINT, 
	[INSPECTIONEVENTTYPEID] INT,
	[CREATEDDATE] DATETIME,
	[IMINSPECTIONID] CHAR(36),
	[INSPECTIONNUMBER] NVARCHAR(50),
	[INSPECTIONTYPENAME] NVARCHAR(50),
	[INSPECTIONSTATUSNAME] NVARCHAR(50),
	[INDICATESSUCCESS] BIT,
	[INVIOLATIONFLAG] BIT,
	[PARTIALPASS] BIT,
	[CANCELLEDFLAG] BIT,
	[SCHEDULEDSTARTDATE] DATETIME,
	[ACTUALENDDATE] DATETIME,
	[ISREINSPECTION] BIT,
	[ADDRESSLINE1] NVARCHAR(200),
	[ADDRESSLINE2] NVARCHAR(200),
	[ADDRESSLINE3] NVARCHAR(200),
	[STREETTYPE] NVARCHAR(50),
	[PREDIRECTION] NVARCHAR(30),
	[POSTDIRECTION] NVARCHAR(30),
	[UNITORSUITE] NVARCHAR(20),
	[CITY] NVARCHAR(50),
	[COUNTRYTYPE] INT,
	[PROVINCE] NVARCHAR(50),
	[STATE] NVARCHAR(50),
	[POSTALCODE] NVARCHAR(50),
	[COUNTRY] NVARCHAR(50),
	[PARCELNUMBER] NVARCHAR(50),
	[TAXNUMBER] NVARCHAR(50),
	[LASTCHANGEDBYNAME] NVARCHAR(100),
	[INSPECTIONCOMMENTS] NVARCHAR(80)
);

WITH RAW_DATA AS (
	SELECT TOP(@BATCHSIZE)
        [INSPECTIONEVENTQUEUE].[INSPECTIONEVENTQUEUEID],
		[INSPECTIONEVENTQUEUE].[INSPECTIONEVENTTYPEID],
		[INSPECTIONEVENTQUEUE].[CREATEDDATE],
		[IMINSPECTION].[IMINSPECTIONID],
		[IMINSPECTION].[INSPECTIONNUMBER],
		[IMINSPECTIONTYPE].[NAME] INSPECTIONTYPENAME,
		[IMINSPECTIONSTATUS].[STATUSNAME] INSPECTIONSTATUSNAME,
		[IMINSPECTIONSTATUS].[INDICATESSUCCESS],
		[IMINSPECTIONSTATUS].[INVIOLATIONFLAG],
		[IMINSPECTIONSTATUS].[PARTIALPASS],
		[IMINSPECTIONSTATUS].[CANCELLEDFLAG],
		[IMINSPECTION].[SCHEDULEDSTARTDATE],
		[IMINSPECTION].[ACTUALENDDATE],
		[IMINSPECTION].[ISREINSPECTION],
		[MAILINGADDRESS].[ADDRESSLINE1],
		[MAILINGADDRESS].[ADDRESSLINE2],
		[MAILINGADDRESS].[ADDRESSLINE3],
		[MAILINGADDRESS].[STREETTYPE],
		[MAILINGADDRESS].[PREDIRECTION],
		[MAILINGADDRESS].[POSTDIRECTION],
		[MAILINGADDRESS].[UNITORSUITE],
		[MAILINGADDRESS].[CITY],
		[MAILINGADDRESS].[COUNTRYTYPE],
		[MAILINGADDRESS].[PROVINCE],
		[MAILINGADDRESS].[STATE],
		[MAILINGADDRESS].[POSTALCODE],
		[MAILINGADDRESS].[COUNTRY],
		[PARCEL].[PARCELNUMBER],
		[PARCEL].[TAXNUMBER],
		CASE WHEN COALESCE([USERS].[FNAME], '') = ''
			 THEN CASE WHEN COALESCE([USERS].[LNAME], '') = '' 
					   THEN ''
					   ELSE [USERS].[LNAME] END
			 ELSE CASE WHEN COALESCE([USERS].[LNAME], '') = ''
						THEN [USERS].[FNAME]
						ELSE [USERS].[LNAME] + ', ' + [USERS].[FNAME] END
		END AS LASTCHANGEDBYNAME,
		SUBSTRING([IMINSPECTION].[COMMENTS], 1, 80) AS INSPECTIONCOMMENTS

		FROM [dbo].[INSPECTIONEVENTQUEUE]
		INNER JOIN [dbo].[IMINSPECTION] ON [dbo].[INSPECTIONEVENTQUEUE].[IMINSPECTIONID] = [dbo].[IMINSPECTION].[IMINSPECTIONID]
		INNER JOIN [dbo].[IMINSPECTIONTYPE] ON [dbo].[IMINSPECTION].[IMINSPECTIONTYPEID] = [dbo].[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID]
		INNER JOIN [dbo].[IMINSPECTIONSTATUS] ON [dbo].[IMINSPECTION].[IMINSPECTIONSTATUSID] = [dbo].[IMINSPECTIONSTATUS].[IMINSPECTIONSTATUSID]
		LEFT JOIN [dbo].[IMINSPECTIONADDRESS] ON [dbo].[IMINSPECTION].[IMINSPECTIONID] = [dbo].[IMINSPECTIONADDRESS].[IMINSPECTIONID] AND [dbo].[IMINSPECTIONADDRESS].[MAIN] = 1
		LEFT JOIN [dbo].[MAILINGADDRESS] ON [dbo].[IMINSPECTIONADDRESS].[MAILINGADDRESSID] = [dbo].[MAILINGADDRESS].[MAILINGADDRESSID]
		LEFT JOIN [dbo].[IMINSPECTIONPARCEL] ON [dbo].[IMINSPECTION].[IMINSPECTIONID] = [dbo].[IMINSPECTIONPARCEL].[IMINSPECTIONID] AND [dbo].[IMINSPECTIONPARCEL].[MAIN] = 1
		LEFT JOIN [dbo].[PARCEL] ON [dbo].[IMINSPECTIONPARCEL].[PARCELID] = [dbo].[PARCEL].[PARCELID]
		LEFT JOIN [dbo].[USERS] ON [dbo].[INSPECTIONEVENTQUEUE].[INSPECTIONLASTCHANGEDBY] = [dbo].[USERS].[SUSERGUID]
	WHERE [dbo].[INSPECTIONEVENTQUEUE].[PROCESSEDDATE] IS NULL
	AND [IMINSPECTION].[IMINSPECTIONLINKID] = 0
	ORDER BY [dbo].[INSPECTIONEVENTQUEUE].[CREATEDDATE]
)

INSERT INTO @RESULT_DATA
SELECT * FROM RAW_DATA

SELECT * FROM @RESULT_DATA

SELECT 
[IMINSPECTIONCONTACT].[IMINSPECTIONID],
[IMINSPECTIONCONTACT].[ISBILLING],
[GLOBALENTITY].[GLOBALENTITYID],
[GLOBALENTITY].[GLOBALENTITYNAME],
[GLOBALENTITY].[FIRSTNAME],
[GLOBALENTITY].[MIDDLENAME],
[GLOBALENTITY].[LASTNAME],
[LANDMANAGEMENTCONTACTTYPE].[NAME] AS [CONTACTTYPENAME],
[LANDMANAGEMENTCONTACTSYSTEMTYP].[NAME] AS [SYSTEMCONTACTTYPENAME]

FROM @RESULT_DATA AS [RESULTDATA] 
INNER JOIN [dbo].[IMINSPECTIONCONTACT] WITH(NOLOCK) ON [dbo].[IMINSPECTIONCONTACT].[IMINSPECTIONID] = [RESULTDATA].[IMINSPECTIONID]
INNER JOIN [dbo].[GLOBALENTITY] WITH(NOLOCK) ON [dbo].[GLOBALENTITY].[GLOBALENTITYID] = [dbo].[IMINSPECTIONCONTACT].[GLOBALENTITYID]
INNER JOIN [dbo].[LANDMANAGEMENTCONTACTTYPE] WITH(NOLOCK) ON [dbo].[LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTTYPEID] = [dbo].[IMINSPECTIONCONTACT].[CONTACTTYPEID]
INNER JOIN [dbo].[LANDMANAGEMENTCONTACTSYSTEMTYP] WITH(NOLOCK) ON [dbo].[LANDMANAGEMENTCONTACTSYSTEMTYP].[LANDMANAGEMENTCONTACTSYSTEMTYP] = [dbo].[LANDMANAGEMENTCONTACTTYPE].[LANDMANAGEMENTCONTACTSYSTEMTYP]

UNION ALL

SELECT 
[IMINSPECTIONCONTACT].[IMINSPECTIONID],
[IMINSPECTIONCONTACT].[ISBILLING],
[GLOBALENTITY].[GLOBALENTITYID],
[GLOBALENTITY].[GLOBALENTITYNAME],
[GLOBALENTITY].[FIRSTNAME],
[GLOBALENTITY].[MIDDLENAME],
[GLOBALENTITY].[LASTNAME],
[CMCODECASECONTACTTYPE].NAME AS [CONTACTTYPENAME],
[CMCONTACTTYPESYSTEM].NAME AS [SYSTEMCONTACTTYPENAME]

FROM @RESULT_DATA AS [RESULTDATA] 
INNER JOIN [dbo].[IMINSPECTIONCONTACT] WITH(NOLOCK) ON [dbo].[IMINSPECTIONCONTACT].[IMINSPECTIONID] = [RESULTDATA].[IMINSPECTIONID]
INNER JOIN [dbo].[GLOBALENTITY] WITH(NOLOCK) ON [dbo].[GLOBALENTITY].[GLOBALENTITYID] = [dbo].[IMINSPECTIONCONTACT].[GLOBALENTITYID]
INNER JOIN [dbo].[CMCODECASECONTACTTYPE] WITH(NOLOCK) ON [dbo].[CMCODECASECONTACTTYPE].[CMCODECASECONTACTTYPEID] = [dbo].[IMINSPECTIONCONTACT].[CONTACTTYPEID]
INNER JOIN [dbo].[CMCONTACTTYPESYSTEM] WITH(NOLOCK) ON [dbo].[CMCONTACTTYPESYSTEM].[CMCONTACTTYPESYSTEMID] = [dbo].[CMCODECASECONTACTTYPE].[CMCONTACTTYPESYSTEMID]

UNION ALL

SELECT 
[IMINSPECTIONCONTACT].[IMINSPECTIONID],
[IMINSPECTIONCONTACT].[ISBILLING],
[GLOBALENTITY].[GLOBALENTITYID],
[GLOBALENTITY].[GLOBALENTITYNAME],
[GLOBALENTITY].[FIRSTNAME],
[GLOBALENTITY].[MIDDLENAME],
[GLOBALENTITY].[LASTNAME],
[BLCONTACTTYPE].[NAME] AS [CONTACTTYPENAME],
[BLCONTACTTYPESYSTEM].[NAME] AS [SYSTEMCONTACTTYPENAME]

FROM @RESULT_DATA AS [RESULTDATA] 
INNER JOIN [dbo].[IMINSPECTIONCONTACT] WITH(NOLOCK) ON [dbo].[IMINSPECTIONCONTACT].[IMINSPECTIONID] = [RESULTDATA].[IMINSPECTIONID]
INNER JOIN [dbo].[GLOBALENTITY] WITH(NOLOCK) ON [dbo].[GLOBALENTITY].[GLOBALENTITYID] = [dbo].[IMINSPECTIONCONTACT].[GLOBALENTITYID]
INNER JOIN [dbo].[BLCONTACTTYPE] WITH(NOLOCK) ON [dbo].[BLCONTACTTYPE].[BLCONTACTTYPEID] = [dbo].[IMINSPECTIONCONTACT].[CONTACTTYPEID]
INNER JOIN [dbo].[BLCONTACTTYPESYSTEM] WITH(NOLOCK) ON [dbo].[BLCONTACTTYPESYSTEM].[BLCONTACTTYPESYSTEMID] = [dbo].[BLCONTACTTYPE].[BLCONTACTTYPESYSTEMID]

UNION ALL

SELECT 
[IMINSPECTIONCONTACT].[IMINSPECTIONID],
[IMINSPECTIONCONTACT].[ISBILLING],
[GLOBALENTITY].[GLOBALENTITYID],
[GLOBALENTITY].[GLOBALENTITYNAME],
[GLOBALENTITY].[FIRSTNAME],
[GLOBALENTITY].[MIDDLENAME],
[GLOBALENTITY].[LASTNAME],
[RPCONTACTTYPE].[NAME] AS [CONTACTTYPENAME],
[RPCONTACTTYPESYSTEM].[NAME] AS [SYSTEMCONTACTTYPENAME]

FROM @RESULT_DATA AS [RESULTDATA] 
INNER JOIN [dbo].[IMINSPECTIONCONTACT] WITH(NOLOCK) ON [dbo].[IMINSPECTIONCONTACT].[IMINSPECTIONID] = [RESULTDATA].[IMINSPECTIONID]
INNER JOIN [dbo].[GLOBALENTITY] WITH(NOLOCK) ON [dbo].[GLOBALENTITY].[GLOBALENTITYID] = [dbo].[IMINSPECTIONCONTACT].[GLOBALENTITYID]
INNER JOIN [dbo].[RPCONTACTTYPE] WITH(NOLOCK) ON [dbo].[RPCONTACTTYPE].[RPCONTACTTYPEID] = [dbo].[IMINSPECTIONCONTACT].[CONTACTTYPEID]
INNER JOIN [dbo].[RPCONTACTTYPESYSTEM] WITH(NOLOCK) ON [dbo].[RPCONTACTTYPESYSTEM].[RPCONTACTTYPESYSTEMID] = [dbo].[RPCONTACTTYPE].[RPCONTACTTYPESYSTEMID]

END