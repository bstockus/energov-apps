CREATE PROCEDURE [events].[USP_INSPECTIONEVENTQUEUE_INSPECTIONCODECASE_SELECT_LOOKUP]
(
	@BATCHSIZE AS INT = 1000
)
AS

DECLARE @RESULT_DATA AS TABLE 
(
	[INSPECTIONEVENTQUEUEID] BIGINT, 
	[INSPECTIONEVENTTYPEID] INT,
	[CREATEDDATE] DATETIME,
	[IMINSPECTIONID] CHAR(36),
	[INSPECTIONNUMBER] NVARCHAR(50),
	[INSPECTIONTYPENAME] NVARCHAR(50),
	[INSPECTIONSTATUSNAME] NVARCHAR(50),
	[INDICATESSUCCESS] BIT,
	[INVIOLATIONFLAG] BIT,
	[PARTIALPASS] BIT,
	[CANCELLEDFLAG] BIT,
	[SCHEDULEDSTARTDATE] DATETIME,
	[ACTUALENDDATE] DATETIME,
	[ISREINSPECTION] BIT,
	[ADDRESSLINE1] NVARCHAR(200),
	[ADDRESSLINE2] NVARCHAR(200),
	[ADDRESSLINE3] NVARCHAR(200),
	[STREETTYPE] NVARCHAR(50),
	[PREDIRECTION] NVARCHAR(30),
	[POSTDIRECTION] NVARCHAR(30),
	[UNITORSUITE] NVARCHAR(20),
	[CITY] NVARCHAR(50),
	[COUNTRYTYPE] INT,
	[PROVINCE] NVARCHAR(50),
	[STATE] NVARCHAR(50),
	[POSTALCODE] NVARCHAR(50),
	[COUNTRY] NVARCHAR(50),
	[PARCELNUMBER] NVARCHAR(50),
	[TAXNUMBER] NVARCHAR(50),
	[LASTCHANGEDBYNAME] NVARCHAR(100),
	[INSPECTIONCOMMENTS] NVARCHAR(80),
	[CMCODECASEID] CHAR(36),
	[CASENUMBER] NVARCHAR(50),
	[CMCASETYPENAME] NVARCHAR(50),
	[CMCODECASESTATUSNAME] NVARCHAR(50),
	[EMERGENCY] BIT,
	[CMCODECASESTATUSSUCCESSFLAG] BIT,
	[CMCODECASESTATUSFAILUREFLAG] BIT,
	[CMCODECASESTATUSCANCELLEDFLAG] BIT,
	[LASTCHANGEDON] DATETIME,
	[OPENEDDATE] DATETIME,
	[CLOSEDDATE] DATETIME,
	[DESCRIPTION] NVARCHAR(80)
);

WITH RAW_DATA AS (
	SELECT TOP(@BATCHSIZE)
		[INSPECTIONEVENTQUEUE].[INSPECTIONEVENTQUEUEID],
		[INSPECTIONEVENTQUEUE].[INSPECTIONEVENTTYPEID],
		[INSPECTIONEVENTQUEUE].[CREATEDDATE],
		[IMINSPECTION].[IMINSPECTIONID],
		[IMINSPECTION].[INSPECTIONNUMBER],
		[IMINSPECTIONTYPE].[NAME] INSPECTIONTYPENAME,
		[IMINSPECTIONSTATUS].[STATUSNAME] INSPECTIONSTATUSNAME,
		[IMINSPECTIONSTATUS].[INDICATESSUCCESS],
		[IMINSPECTIONSTATUS].[INVIOLATIONFLAG],
		[IMINSPECTIONSTATUS].[PARTIALPASS],
		[IMINSPECTIONSTATUS].[CANCELLEDFLAG],
		[IMINSPECTION].[SCHEDULEDSTARTDATE],
		[IMINSPECTION].[ACTUALENDDATE],
		[IMINSPECTION].[ISREINSPECTION],
		[MAILINGADDRESS].[ADDRESSLINE1],
		[MAILINGADDRESS].[ADDRESSLINE2],
		[MAILINGADDRESS].[ADDRESSLINE3],
		[MAILINGADDRESS].[STREETTYPE],
		[MAILINGADDRESS].[PREDIRECTION],
		[MAILINGADDRESS].[POSTDIRECTION],
		[MAILINGADDRESS].[UNITORSUITE],
		[MAILINGADDRESS].[CITY],
		[MAILINGADDRESS].[COUNTRYTYPE],
		[MAILINGADDRESS].[PROVINCE],
		[MAILINGADDRESS].[STATE],
		[MAILINGADDRESS].[POSTALCODE],
		[MAILINGADDRESS].[COUNTRY],
		[PARCEL].[PARCELNUMBER],
		[PARCEL].[TAXNUMBER],
		CASE WHEN COALESCE([USERS].[FNAME], '') = ''
			 THEN CASE WHEN COALESCE([USERS].[LNAME], '') = '' 
					   THEN ''
					   ELSE [USERS].[LNAME] END
			 ELSE CASE WHEN COALESCE([USERS].[LNAME], '') = ''
						THEN [USERS].[FNAME]
						ELSE [USERS].[LNAME] + ', ' + [USERS].[FNAME] END
		END AS LASTCHANGEDBYNAME,
		SUBSTRING([IMINSPECTION].[COMMENTS], 1, 80) AS INSPECTIONCOMMENTS,
		[CMCODECASE].[CMCODECASEID],
		[CMCODECASE].[CASENUMBER],
		[CMCASETYPE].[NAME] CMCASETYPENAME,
		[CMCODECASESTATUS].[NAME] CMCODECASESTATUSNAME,
		[CMCODECASE].[EMERGENCY],
		[CMCODECASESTATUS].[SUCCESSFLAG] CMCODECASESTATUSSUCCESSFLAG,
		[CMCODECASESTATUS].[FAILUREFLAG] CMCODECASESTATUSFAILUREFLAG,
		[CMCODECASESTATUS].[CANCELLEDFLAG] CMCODECASESTATUSCANCELLEDFLAG,
		[CMCODECASE].[LASTCHANGEDON],
		[CMCODECASE].[OPENEDDATE],
		[CMCODECASE].[CLOSEDDATE],
		SUBSTRING([CMCODECASE].[DESCRIPTION], 1, 80) AS [DESCRIPTION]

		FROM [dbo].[INSPECTIONEVENTQUEUE]
		INNER JOIN [dbo].[IMINSPECTION] ON [dbo].[INSPECTIONEVENTQUEUE].[IMINSPECTIONID] = [dbo].[IMINSPECTION].[IMINSPECTIONID]
		INNER JOIN [dbo].[IMINSPECTIONTYPE] ON [dbo].[IMINSPECTION].[IMINSPECTIONTYPEID] = [dbo].[IMINSPECTIONTYPE].[IMINSPECTIONTYPEID]
		INNER JOIN [dbo].[IMINSPECTIONSTATUS] ON [dbo].[IMINSPECTION].[IMINSPECTIONSTATUSID] = [dbo].[IMINSPECTIONSTATUS].[IMINSPECTIONSTATUSID]
		INNER JOIN [dbo].[CMCODECASE] ON [dbo].[IMINSPECTION].[LINKID] = [dbo].[CMCODECASE].[CMCODECASEID]
		INNER JOIN [dbo].[CMCASETYPE] ON [dbo].[CMCODECASE].[CMCASETYPEID] = [dbo].[CMCASETYPE].[CMCASETYPEID]
		INNER JOIN [dbo].[CMCODECASESTATUS] ON [dbo].[CMCODECASE].[CMCODECASESTATUSID] = [dbo].[CMCODECASESTATUS].[CMCODECASESTATUSID]
		LEFT JOIN [dbo].[IMINSPECTIONADDRESS] ON [dbo].[IMINSPECTION].[IMINSPECTIONID] = [dbo].[IMINSPECTIONADDRESS].[IMINSPECTIONID] AND [dbo].[IMINSPECTIONADDRESS].[MAIN] = 1
		LEFT JOIN [dbo].[MAILINGADDRESS] ON [dbo].[IMINSPECTIONADDRESS].[MAILINGADDRESSID] = [dbo].[MAILINGADDRESS].[MAILINGADDRESSID]
		LEFT JOIN [dbo].[IMINSPECTIONPARCEL] ON [dbo].[IMINSPECTION].[IMINSPECTIONID] = [dbo].[IMINSPECTIONPARCEL].[IMINSPECTIONID] AND [dbo].[IMINSPECTIONPARCEL].[MAIN] = 1
		LEFT JOIN [dbo].[PARCEL] ON [dbo].[IMINSPECTIONPARCEL].[PARCELID] = [dbo].[PARCEL].[PARCELID]
		LEFT JOIN [dbo].[USERS] ON [dbo].[INSPECTIONEVENTQUEUE].[INSPECTIONLASTCHANGEDBY] = [dbo].[USERS].[SUSERGUID]
	WHERE [dbo].[INSPECTIONEVENTQUEUE].[PROCESSEDDATE] IS NULL
	AND [IMINSPECTION].[IMINSPECTIONLINKID] = 3
	ORDER BY [dbo].[INSPECTIONEVENTQUEUE].[CREATEDDATE]
)

INSERT INTO @RESULT_DATA
SELECT * 
	FROM RAW_DATA

SELECT * FROM @RESULT_DATA

DECLARE @CMCODECASEIDs RecordIDs
INSERT INTO @CMCODECASEIDs(RECORDID)
SELECT DISTINCT [CMCODECASEID] 
FROM @RESULT_DATA

EXEC [events].[USP_EVENTQUEUE_GET_CODECASECONTACT] @UNIQUEIDS = @CMCODECASEIDs