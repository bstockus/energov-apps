CREATE PROCEDURE [events].[USP_TRANSACTIONEVENTQUEUE_SELECT_LOOKUP]
(
	@BATCHSIZE AS INT = 1000
)
AS


DECLARE @RESULT_DATA AS TABLE 
(
	[TRANSACTIONEVENTQUEUEID] BIGINT, 
	[TRANSACTIONEVENTTYPEID] INT,
	[CREATEDDATE] DATETIME,
	[CATRANSACTIONID] CHAR(36),
	[RECEIPTNUMBER] NVARCHAR(50),
	[PAYMENTDATE] DATETIME,
	[OFFICENAME] NVARCHAR(100),
	[TRANSACTIONTYPE] NVARCHAR(50),
	[TRANSACTIONSTATUS] NVARCHAR(50),
	[PAYMENTAMOUNT] MONEY,
	[EXTERNALBATCHNBR] NVARCHAR(50),
	[LASTCHANGEDFIRSTNAME] NVARCHAR(30),
	[LASTCHANGEDLASTNAME] NVARCHAR(30),
	[FIRSTNAME] NVARCHAR(30),
	[LASTNAME] NVARCHAR(30),
	[DEPOSITREFNBR] NVARCHAR(50),
	[PAYMENTMETHOD] NVARCHAR(50),
	[PAYMENTNOTE] NVARCHAR(80),
	[SUPPLEMENTALDATA] NVARCHAR(100),
	[NOTE] NVARCHAR(80),
	[ORIGINALRECEIPTNUMBER] NVARCHAR(50),
	[PARENTTRANSACTIONID] CHAR(36),
	[GLOBALENTITYID] CHAR(36)
);

WITH RAW_DATA AS (
	SELECT TOP(@BATCHSIZE)
		[dbo].[TRANSACTIONEVENTQUEUE].[TRANSACTIONEVENTQUEUEID],
		[dbo].[TRANSACTIONEVENTQUEUE].[TRANSACTIONEVENTTYPEID],
		[dbo].[TRANSACTIONEVENTQUEUE].[CREATEDDATE],
		[dbo].[CATRANSACTION].[CATRANSACTIONID],
		[dbo].[CATRANSACTION].[RECEIPTNUMBER],
		[dbo].[CATRANSACTIONPAYMENT].[PAYMENTDATE],
		[dbo].[OFFICE].[NAME] [OFFICENAME],
		[dbo].[CATRANSACTIONTYPE].[NAME] [TRANSACTIONTYPE],
		[dbo].[CATRANSACTIONSTATUS].[NAME] [TRANSACTIONSTATUS],
		[dbo].[CATRANSACTIONPAYMENT].[PAYMENTAMOUNT],
		[dbo].[CATRANSACTION].[EXTERNALBATCHNBR],
		[dbo].[USERS].[FNAME] [FIRSTNAME],
		[dbo].[USERS].[LNAME] [LASTNAME],
		[LASTUSER].[FNAME] [LASTCHANGEDFIRSTNAME],
		[LASTUSER].[LNAME] [LASTCHANGEDLASTNAME],
		[dbo].[CATRANSACTION].[DEPOSITREFNBR],
		[dbo].[CAPAYMENTMETHOD].[NAME] [PAYMENTMETHOD],
		SUBSTRING([dbo].[CATRANSACTIONPAYMENT].[PAYMENTNOTE], 1, 80) [PAYMENTNOTE],
		[dbo].[CAPAYMENTMETHOD].[NAME] + ': ' + [SUPPLEMENTALDATA] [SUPPLEMENTALDATA],
		SUBSTRING([dbo].[CATRANSACTION].[NOTE], 1, 80) [NOTE],
		[PARENTTRANSACTION].[RECEIPTNUMBER] [ORIGINALRECEIPTNUMBER],
		[dbo].[CATRANSACTION].[PARENTTRANSACTIONID],
		[dbo].[CATRANSACTION].[GLOBALENTITYID]

	FROM [dbo].[TRANSACTIONEVENTQUEUE] WITH (NOLOCK)
	INNER JOIN [dbo].[CATRANSACTION] WITH (NOLOCK) ON [dbo].[TRANSACTIONEVENTQUEUE].[CATRANSACTIONID] = [dbo].[CATRANSACTION].[CATRANSACTIONID]
	INNER JOIN [dbo].[CATRANSACTIONPAYMENT] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[CATRANSACTIONID] = [dbo].[CATRANSACTIONPAYMENT].[CATRANSACTIONID]
	INNER JOIN [dbo].[CAPAYMENTMETHOD] WITH (NOLOCK) ON [dbo].[CAPAYMENTMETHOD].[CAPAYMENTMETHODID] = [dbo].[CATRANSACTIONPAYMENT].[CAPAYMENTMETHODID]
	INNER JOIN [dbo].[CATRANSACTIONSTATUS] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[CATRANSACTIONSTATUSID] = [dbo].[CATRANSACTIONSTATUS].[CATRANSACTIONSTATUSID]
	INNER JOIN [dbo].[CATRANSACTIONTYPE] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[CATRANSACTIONTYPEID] = [dbo].[CATRANSACTIONTYPE].[CATRANSACTIONTYPEID]
	INNER JOIN [dbo].[USERS] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[CREATEDBY] = [dbo].[USERS].[SUSERGUID]
	INNER JOIN [dbo].[USERS] [LASTUSER] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[LASTCHANGEDBY] = [LASTUSER].[SUSERGUID]
	LEFT OUTER JOIN [dbo].[OFFICE] WITH (NOLOCK) ON [dbo].[CATRANSACTION].[OFFICEID] = [dbo].[OFFICE].[OFFICEID]
	LEFT OUTER JOIN [dbo].[CATRANSACTION] [PARENTTRANSACTION] WITH (NOLOCK) ON [PARENTTRANSACTION].[CATRANSACTIONID] = [dbo].[CATRANSACTION].[PARENTTRANSACTIONID]

	WHERE [dbo].[TRANSACTIONEVENTQUEUE].[PROCESSEDDATE] IS NULL
	ORDER BY [dbo].[TRANSACTIONEVENTQUEUE].[CREATEDDATE]
)
INSERT INTO @RESULT_DATA
SELECT * 
FROM RAW_DATA


-- Transaction Payment/Reversal Data
SELECT * FROM @RESULT_DATA


-- Transaction Billing Contact (should be only 1 for a transaction, though there could be multiple on the associated invoice)
--For now, we will keep this as a subset, even though the initial implementation is for 1 record per transaction.
SELECT 
	[RESULTDATA].[CATRANSACTIONID],
	[dbo].[GLOBALENTITY].[GLOBALENTITYID],
	[dbo].[GLOBALENTITY].[FIRSTNAME],
	[dbo].[GLOBALENTITY].[LASTNAME],
	[dbo].[GLOBALENTITY].[MIDDLENAME],
	[dbo].[GLOBALENTITY].[GLOBALENTITYNAME]
FROM @RESULT_DATA [RESULTDATA]
INNER JOIN [dbo].[GLOBALENTITY] WITH (NOLOCK) ON [RESULTDATA].[GLOBALENTITYID] = [dbo].[GLOBALENTITY].[GLOBALENTITYID]


-- Transaction Misc Fees
SELECT 
	[dbo].[CATRANSACTIONMISCFEE].[CATRANSACTIONID],
	[dbo].[CAMISCFEE].[FEENAME],
	COALESCE([dbo].[CATRANSACTIONMISCFEE].[PAIDAMOUNT], [dbo].[CATRANSACTIONMISCFEE].[REFUNDAMOUNT], 0.00) [AMOUNTAPPLIED],
	[dbo].[CAMISCFEE].[AMOUNT] [FEETOTAL],
	[dbo].[CAMISCFEE].[AMOUNT] - [CAMISCFEE].[PAIDAMOUNT] [AMOUNTDUE],
	[dbo].[CAINVOICE].[CAINVOICEID],
	[dbo].[CAINVOICE].[INVOICENUMBER]
FROM @RESULT_DATA [RESULTDATA]
INNER JOIN [dbo].[CATRANSACTIONMISCFEE] WITH (NOLOCK) ON [RESULTDATA].[CATRANSACTIONID] = [dbo].[CATRANSACTIONMISCFEE].[CATRANSACTIONID]
INNER JOIN [dbo].[CAMISCFEE] WITH (NOLOCK) ON [dbo].[CATRANSACTIONMISCFEE].[CAMISCFEEID] = [dbo].[CAMISCFEE].[CAMISCFEEID]
INNER JOIN [dbo].[CATRANSACTIONINVOICE] WITH (NOLOCK) ON [dbo].[CATRANSACTIONINVOICE].[CATRANSACTIONID] = [RESULTDATA].[CATRANSACTIONID]
INNER JOIN [dbo].[CAINVOICE] WITH (NOLOCK) ON [dbo].[CAINVOICE].[CAINVOICEID] = [dbo].[CATRANSACTIONINVOICE].[CAINVOICEID]


-- Transaction Fees
SELECT 
	[dbo].[CATRANSACTIONFEE].[CATRANSACTIONID],
	[dbo].[INVOICEMODULEFEEVIEW].[FEENAME],
	COALESCE([dbo].[CATRANSACTIONFEE].[PAIDAMOUNT], [dbo].[CATRANSACTIONFEE].[REFUNDAMOUNT], 0.00) [AMOUNTAPPLIED],
	[dbo].[INVOICEMODULEFEEVIEW].[COMPUTEDAMOUNT] FEETOTAL,
	[dbo].[INVOICEMODULEFEEVIEW].[COMPUTEDAMOUNT] - [INVOICEMODULEFEEVIEW].[AMOUNTPAIDTODATE] [AMOUNTDUE],
	[dbo].[CAINVOICE].[CAINVOICEID],
	[dbo].[CAINVOICE].[INVOICENUMBER],
	[dbo].[INVOICEMODULEFEEVIEW].[ENTITYNUMBER],
	[dbo].[INVOICEMODULEFEEVIEW].[ENTITYID],
	[dbo].[INVOICEMODULEFEEVIEW].[CAENTITYID]
FROM @RESULT_DATA [RESULTDATA]
INNER JOIN [dbo].[CATRANSACTIONFEE] WITH (NOLOCK) ON [RESULTDATA].[CATRANSACTIONID] = [dbo].[CATRANSACTIONFEE].[CATRANSACTIONID]
INNER JOIN [dbo].[INVOICEMODULEFEEVIEW] WITH (NOLOCK) ON [dbo].[CATRANSACTIONFEE].[CACOMPUTEDFEEID] = [dbo].[INVOICEMODULEFEEVIEW].[CACOMPUTEDFEEID]
INNER JOIN [dbo].[CATRANSACTIONINVOICE] WITH (NOLOCK) ON [dbo].[CATRANSACTIONINVOICE].[CATRANSACTIONID] = [RESULTDATA].[CATRANSACTIONID]
INNER JOIN [dbo].[CAINVOICE] WITH (NOLOCK) ON [dbo].[CAINVOICE].[CAINVOICEID] = [dbo].[CATRANSACTIONINVOICE].[CAINVOICEID]

-- Accounts (for now, it is implemented only for 'Fee payment' event)
SELECT 
	[dbo].[CATRANSACTIONACCOUNT].[CATRANSACTIONID],
	[dbo].[GLOBALENTITYACCOUNT].[ACCOUNTNUMBER],
	[dbo].[GLOBALENTITYACCOUNT].[NAME],
	[dbo].[CATRANSACTIONACCOUNT].[AMOUNT] [AMOUNTAPPLIED],
	[dbo].[GLOBALENTITYACCOUNT].[BALANCE] [CURRENTBALANCE]
FROM @RESULT_DATA [RESULTDATA]
INNER JOIN [dbo].[CATRANSACTIONACCOUNT] WITH (NOLOCK) ON [RESULTDATA].[CATRANSACTIONID] = [dbo].[CATRANSACTIONACCOUNT].[CATRANSACTIONID] AND [CATRANSACTIONACCOUNT].CAPAYMENTTYPEID = 4
INNER JOIN [dbo].[GLOBALENTITYACCOUNT] WITH (NOLOCK) ON [dbo].[GLOBALENTITYACCOUNT].[GLOBALENTITYACCOUNTID] = [dbo].[CATRANSACTIONACCOUNT].[ENTITYACCOUNTID]