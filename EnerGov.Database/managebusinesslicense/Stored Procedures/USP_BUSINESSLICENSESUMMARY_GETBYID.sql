CREATE PROCEDURE [managebusinesslicense].[USP_BUSINESSLICENSESUMMARY_GETBYID]
(
	@LICENSEID CHAR(36)
)
AS
BEGIN
	DECLARE @LICENSELIST AS RecordIDs, @RECENTINVOICEDATE DATE, @BALANCEDUE MONEY, @BUSINESSBALANCEDUE MONEY
	INSERT INTO @LICENSELIST (RECORDID)
	VALUES(@LICENSEID)

		SET @RECENTINVOICEDATE = [managebusinesslicense].[UFN_GET_RECENTINVOICEDATE_GETBY_LICENSEID](@LICENSEID)
		SET @BALANCEDUE = [managebusinesslicense].[UFN_GET_LICENSE_BALANCE_DUE](@LICENSEID)
		SET @BUSINESSBALANCEDUE = [managebusinesslicense].[UFN_GET_BUSINESS_BALANCE_DUE](@LICENSEID)


	SELECT  [BLLICENSE].[APPLIEDDATE] ,		
			[BLLICENSECLASS].[NAME] AS [BLLICENSECLASSNAME],
			[BLGLOBALENTITYEXTENSION].[BLGLOBALENTITYEXTENSIONID] ,
			[BLGLOBALENTITYEXTENSION].[COMPANYNAME],
			[BLLICENSE].[ISSUEDBY],
			[USERS].[FNAME] AS [ISSUEDBYFNAME],
			[USERS].[LNAME] AS [ISSUEDBYLNAME],
			[BLGLOBALENTITYEXTENSION].[DBA] ,
			[BLLICENSE].[DESCRIPTION] ,
			[BLLICENSE].[EXPIRATIONDATE],
			[BLLICENSE].[REPORTEDRECEIPTS],
			[BLLICENSE].[ALLOWEDDEDUCTIONAMOUNT],
			[BLLICENSE].[LASTRENEWALDATE] ,			
			[BLLICENSE].[BLLICENSEID],	
			[BLLICENSE].[LICENSENUMBER],
			[BLLICENSESTATUS].[NAME] AS [BLLICENSESTATUSNAME],	
			[BLLICENSE].[TAXYEAR],
			[BLLICENSETYPE].[NAME] AS [BLLICENSETYPENAME],		
			[BLGLOBALENTITYEXTENSION].[OPENDATE],
			[BLLICENSE].[ISSUEDDATE],
			[BLLICENSE].[ROWVERSION],
			[DISTRICT_BLGLOBALENTITYEXTENSION].[NAME] AS [BUSINESSDISTRICTNAME],
			[DISTRICT_BLLICENSE].[NAME] AS [LICENSEDISTRICTNAME],
			[BLEXTLOCATION].[NAME] AS [LOCATIONNAME],
			[BLEXTSTATUS].[NAME] AS [STATUSNAME],
			[BLEXTCOMPANYTYPE].[NAME]  AS [COMPANYTYPE],
			@RECENTINVOICEDATE AS RECENTINVOICEDATE,  
			@BALANCEDUE AS BALANCEDUE,  
			@BUSINESSBALANCEDUE AS BUSINESSBALANCEDUE,  
			[BLLICENSE].[NUMBEROFUNITS],
			[BLGLOBALENTITYEXTENSION].[BUSINESSPHONE],
			[BLLICENSE].[ISSUEDDATE] AS LICENSEISSUEDATE,
			[BLGLOBALENTITYEXTENSION].[ISSUEDDATE] AS BUSINESSISSUEDDATE,
			[BLLICENSE].[PROPERTYNAME],
			[BLLICENSE].[LASTINSPECTIONDATE]
	FROM [dbo].[BLLICENSE]
		INNER JOIN [dbo].[BLGLOBALENTITYEXTENSION] ON [BLLICENSE].[BLGLOBALENTITYEXTENSIONID] = [BLGLOBALENTITYEXTENSION].[BLGLOBALENTITYEXTENSIONID]
		INNER JOIN [dbo].[BLLICENSECLASS] ON [BLLICENSE].[BLLICENSECLASSID] = [BLLICENSECLASS].BLLICENSECLASSID 
		INNER JOIN [dbo].[BLLICENSETYPE] ON [BLLICENSE].[BLLICENSETYPEID] = [BLLICENSETYPE].[BLLICENSETYPEID]
		INNER JOIN [dbo].[BLLICENSESTATUS] ON [BLLICENSE].[BLLICENSESTATUSID] = [BLLICENSESTATUS].[BLLICENSESTATUSID]
		LEFT JOIN [dbo].[USERS] ON [BLLICENSE].[ISSUEDBY] = [USERS].[SUSERGUID]
		LEFT JOIN [dbo].[DISTRICT]  AS [DISTRICT_BLGLOBALENTITYEXTENSION] ON [BLGLOBALENTITYEXTENSION].[DISTRICTID]= [DISTRICT_BLGLOBALENTITYEXTENSION].[DISTRICTID]
		LEFT JOIN [dbo].[DISTRICT]  AS [DISTRICT_BLLICENSE] ON [BLLICENSE].[DISTRICTID]= [DISTRICT_BLLICENSE].[DISTRICTID]
		LEFT JOIN [dbo].[BLEXTCOMPANYTYPE] ON [BLGLOBALENTITYEXTENSION].[BLEXTCOMPANYTYPEID] = [BLEXTCOMPANYTYPE].[BLEXTCOMPANYTYPEID] 
		LEFT JOIN [dbo].[BLEXTLOCATION] ON [BLEXTLOCATION].[BLEXTLOCATIONID] = [BLGLOBALENTITYEXTENSION].[BLEXTLOCATIONID]  
		LEFT JOIN [dbo].[BLEXTSTATUS] ON  [BLEXTSTATUS].[BLEXTSTATUSID] = [BLGLOBALENTITYEXTENSION].[BLEXTSTATUSID]
	WHERE [BLLICENSE].[BLLICENSEID] = @LICENSEID
				
	EXEC  [managebusinesslicense].[USP_BLCONTACTTYPE_GETBYBLLICENSEID] @LICENSEID
	EXEC  [managebusinesslicense].[USP_ZONE_GETBYBLLICENSEID] @LICENSEID
	EXEC [managebusinesslicense].[USP_BLLICENSEADDRESS_GETBYIDS] @LICENSELIST
	EXEC [managebusinesslicense].[USP_BLLICENSECONTACT_GETBYIDS] @LICENSELIST
	EXEC [managebusinesslicense].[USP_BLLICENSE_PREVIOUSLICENSES_GETBYID] @LICENSEID	
	EXEC [managebusinesslicense].[USP_BLEXTBUSINESSTYPE_GETBY_BLLICENSEID] @LICENSELIST
END