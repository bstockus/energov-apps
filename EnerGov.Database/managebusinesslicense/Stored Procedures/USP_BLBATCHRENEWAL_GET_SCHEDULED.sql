CREATE PROCEDURE [managebusinesslicense].[USP_BLBATCHRENEWAL_GET_SCHEDULED]
(
	@SEARCH			AS NVARCHAR(MAX)	=	'',
	@PAGE_NUMBER	AS INT				=	1,
	@PAGE_SIZE		AS INT				=	10,
	@IS_ASCENDING	AS BIT				=	1,
	@COMPLETE		AS BIT,
	@MODULEID INT
)
AS
BEGIN

	WITH RAW_DATA AS 
	(
		SELECT	[dbo].[BLBATCHRENEWAL].[BLBATCHRENEWALID], 
				[dbo].[BLBATCHRENEWAL].[BATCHRENEWALNUMBER], 
				[dbo].[BLLICENSETYPEMODULE].[BLLICENSETYPEMODULEID] AS MODULEID, 
				[dbo].[BLLICENSETYPEMODULE].[NAME] AS MODULENAME, 
				[dbo].[BLBATCHRENEWAL].[PROCESSEDDATE],
				BatchRecords.TOTALCOUNT, 
				BatchRecords.PROCESSEDCOUNT,
				BatchRecords.[FAILEDCOUNT], 
				[dbo].[USERS].[FNAME], 
				[dbo].[USERS].[LNAME], 
				[dbo].[BLBATCHRENEWAL].[CREATEDATE],
				ROW_NUMBER() OVER(ORDER BY [dbo].[BLBATCHRENEWAL].[CREATEDATE] DESC) AS RowNumber,
				COUNT(1) OVER() AS TotalRows
		FROM [dbo].[BLBATCHRENEWAL]
		JOIN [managebusinesslicense].[UDF_GET_BULKRENEWAL_COUNTS]() BatchRecords ON [dbo].[BLBATCHRENEWAL].[BLBATCHRENEWALID] = BatchRecords.[BLBATCHRENEWALID]
		LEFT JOIN [dbo].[USERS] ON [dbo].[BLBATCHRENEWAL].[CREATEDBY] = [dbo].[USERS].[SUSERGUID]
		LEFT JOIN [dbo].[BLLICENSETYPEMODULE] ON [dbo].[BLBATCHRENEWAL].[BLLICENSETYPEMODULEID] = [dbo].[BLLICENSETYPEMODULE].[BLLICENSETYPEMODULEID]
		WHERE 
		@MODULEID = [dbo].[BLBATCHRENEWAL].[BLLICENSETYPEMODULEID] AND
		((@COMPLETE IS NULL) OR (@COMPLETE = 1 AND [dbo].[BLBATCHRENEWAL].[PROCESSEDDATE] IS NOT NULL) OR (@COMPLETE = 0 AND [dbo].[BLBATCHRENEWAL].[PROCESSEDDATE] IS NULL)) AND
		((@SEARCH IS NULL OR ([dbo].[BLBATCHRENEWAL].[BATCHRENEWALNUMBER] LIKE '%' + @SEARCH + '%')) OR
			(@SEARCH IS NULL OR (([dbo].[USERS].[FNAME] LIKE '%' + @SEARCH + '%') OR ([dbo].[USERS].[LNAME] LIKE '%' + @SEARCH + '%') OR (([dbo].[USERS].[FNAME] + ' ' + [dbo].[USERS].[LNAME]) LIKE '%' + @SEARCH + '%')))
		)
	)
	
	SELECT		* 
	FROM		RAW_DATA
	WHERE		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) 
				AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
	ORDER BY	RowNumber
	
END