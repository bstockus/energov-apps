CREATE PROCEDURE [managebusinesslicense].[USP_BLLICENSE_PREVIOUSLICENSES_GETBYID]
(
	@LICENSEID CHAR(36)
)
AS
BEGIN
	;WITH CTE_LICENCES (BLLICENSEPARENTID) AS (
			SELECT
				CAST([BLLICENSE].[BLLICENSEPARENTID] AS CHAR(36))
			FROM [dbo].[BLLICENSE]
				WHERE [dbo].[BLLICENSE].[BLLICENSEID] = @LICENSEID AND [BLLICENSEPARENTID] IS NOT NULL
			UNION ALL
			SELECT
				CAST([INNER_LICENCE].[BLLICENSEPARENTID] AS CHAR(36))
			FROM [dbo].[BLLICENSE] [INNER_LICENCE]
				INNER JOIN [CTE_LICENCES] [EXAMINED_LICENCES] ON [EXAMINED_LICENCES].[BLLICENSEPARENTID] = [INNER_LICENCE].[BLLICENSEID]
				WHERE [INNER_LICENCE].[BLLICENSEPARENTID] IS NOT NULL
	)
	SELECT
		[BLLICENSE].[BLLICENSEID],
		[BLLICENSE].[LICENSENUMBER],
		[BLLICENSE].[ISSUEDDATE],
		[BLLICENSE].[APPLIEDDATE],
		[BLLICENSE].[BLLICENSEPARENTID],
		COALESCE([BLGLOBALENTITYEXTENSION].[COMPANYNAME], [GLOBALENTITY].[GLOBALENTITYNAME]) AS COMPANYNAME
	FROM [CTE_LICENCES]
		INNER JOIN [dbo].[BLLICENSE] ON [BLLICENSE].[BLLICENSEID] = [CTE_LICENCES].[BLLICENSEPARENTID]
		INNER JOIN [dbo].[BLGLOBALENTITYEXTENSION] ON [BLLICENSE].[BLGLOBALENTITYEXTENSIONID] = [BLGLOBALENTITYEXTENSION].[BLGLOBALENTITYEXTENSIONID]
		LEFT OUTER JOIN [GLOBALENTITY] ON [GLOBALENTITY].[GLOBALENTITYID] = [BLGLOBALENTITYEXTENSION].[GLOBALENTITYID]
		ORDER BY
			 [BLLICENSE].[APPLIEDDATE] ASC
	OPTION (MAXRECURSION 0)
END