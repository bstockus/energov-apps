CREATE PROCEDURE [globalsetup].[USP_TIMETRACKINGTYPE_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
	@FILTERBYMODULEID AS INT = 0,
	@MODULEIDS AS [dbo].[IntegerIds] READONLY
)
AS
SET NOCOUNT ON;
WITH RAW_DATA AS (
	SELECT
		[dbo].[TIMETRACKINGTYPE].[TIMETRACKINGTYPEID],
		[dbo].[TIMETRACKINGTYPE].[NAME],
		[dbo].[TIMETRACKINGMODULE].[NAME] AS MODULENAME,
		[dbo].[TIMETRACKINGMODULE].[MODULEID],
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [dbo].[TIMETRACKINGTYPE].[NAME])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [dbo].[TIMETRACKINGTYPE].[NAME] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[TIMETRACKINGTYPE]
	INNER JOIN [dbo].[TIMETRACKINGMODULE] ON [dbo].[TIMETRACKINGTYPE].[MODULEID] = [dbo].[TIMETRACKINGMODULE].[MODULEID]
	WHERE ([dbo].[TIMETRACKINGTYPE].[NAME] LIKE '%'+ @SEARCH +'%')
		AND ([dbo].[TIMETRACKINGMODULE].[MODULEID] IN (SELECT [ID] FROM @MODULEIDS))
		AND (@FILTERBYMODULEID = 0 OR [dbo].[TIMETRACKINGTYPE].[MODULEID] = @FILTERBYMODULEID)
		AND ([dbo].[TIMETRACKINGTYPE].[BILLABLE] = 1)
		AND ([dbo].[TIMETRACKINGTYPE].[ACTIVE] = 1)
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber