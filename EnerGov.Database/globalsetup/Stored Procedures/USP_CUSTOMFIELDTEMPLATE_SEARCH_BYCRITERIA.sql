CREATE PROCEDURE [globalsetup].[USP_CUSTOMFIELDTEMPLATE_SEARCH_BYCRITERIA]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@CUSTOMFIELDLAYOUTCONTROLTYPE AS INT = 2,
	@ALLOWMULTIPLESELECTIONS AS BIT = 0,
	@ISACTIVE AS BIT = NULL,
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
	@IGNORE_ALLOWMULTIPLESELECTION_FLAG AS BIT = 0
)
AS
WITH RAW_DATA AS (
	SELECT		
		[dbo].[CUSTOMFIELDTEMPLATE].[GCUSTOMFIELDTEMPLATE],		
		[dbo].[CUSTOMFIELDTEMPLATE].[FKCUSTOMFIELDLAYOUTCONTROLTYPE],		
		[dbo].[CUSTOMFIELDTEMPLATE].[TEMPLATENAME],
		[dbo].[CUSTOMFIELDTEMPLATE].[SDEFAULTVALUE],
		[dbo].[CUSTOMFIELDTEMPLATE].[SFIELDTIP],
		[dbo].[CUSTOMFIELDTEMPLATE].[BALLOWMULTIPLESELECTIONS],
		[dbo].[CUSTOMFIELDTEMPLATE].[ISACTIVE],
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [TEMPLATENAME])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [TEMPLATENAME] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[CUSTOMFIELDTEMPLATE]
	WHERE (@ISACTIVE IS NULL OR  [dbo].[CUSTOMFIELDTEMPLATE].[ISACTIVE] = @ISACTIVE) 			
			AND [dbo].[CUSTOMFIELDTEMPLATE].[FKCUSTOMFIELDLAYOUTCONTROLTYPE] = @CUSTOMFIELDLAYOUTCONTROLTYPE
			AND (@IGNORE_ALLOWMULTIPLESELECTION_FLAG = 1 OR [dbo].[CUSTOMFIELDTEMPLATE].[BALLOWMULTIPLESELECTIONS] = @ALLOWMULTIPLESELECTIONS)
			AND [dbo].[CUSTOMFIELDTEMPLATE].[TEMPLATENAME] LIKE '%'+ @SEARCH +'%'
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber