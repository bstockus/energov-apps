CREATE PROCEDURE [globalsetup].[USP_CAFEE_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1,
	@CACOMPUTATIONTYPEID AS INT = 0,
	@ENTITY AS INT = NULL
)
AS
SET NOCOUNT ON;
WITH RAW_DATA AS (
	SELECT
		[dbo].[CAFEE].[CAFEEID],
		[dbo].[CAFEE].[NAME],
		[dbo].[CAFEE].[DESCRIPTION],
		[dbo].[CAFEE].[CACOMPUTATIONTYPEID],
		[dbo].[CAFEE].[NOTES],
		[dbo].[CAFEE].[ARFEECODE],
		[dbo].[JURISDICTION].[NAME] AS JURISDICTIONNAME,
		[dbo].[CACOMPUTATIONTYPE].[NAME] AS COMPUTATIONTYPENAME,
		CASE (SELECT TOP 1 1 FROM CAFEESETUP 
			WHERE [dbo].[CAFEESETUP].[CAFEEID] = [dbo].[CAFEE].[CAFEEID] AND [dbo].[CAFEESETUP].[CAPRORATIONMODELID] = 2) WHEN 1 THEN
				CONVERT(BIT,1)
			ELSE
				CONVERT(BIT,0)
			END AS [ISPRORATIONBYDATE],
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [dbo].[CAFEE].[NAME])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [dbo].[CAFEE].[NAME] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM [dbo].[CAFEE]	
	INNER JOIN [dbo].[CACOMPUTATIONTYPE] ON [dbo].[CACOMPUTATIONTYPE].[CACOMPUTATIONTYPEID] = [dbo].[CAFEE].[CACOMPUTATIONTYPEID]
	LEFT JOIN [dbo].[JURISDICTION] on [dbo].[JURISDICTION].[JURISDICTIONID] = [dbo].[CAFEE].[JURISDICTIONID]
	LEFT JOIN [dbo].[CAMODULEFEEXREF] ON [dbo].[CAMODULEFEEXREF].[CAFEEID] = [dbo].[CAFEE].[CAFEEID] AND @ENTITY IS NOT NULL
	LEFT JOIN [dbo].[CAENTITY] ON [dbo].[CAENTITY].[CAMODULEID] = [dbo].[CAMODULEFEEXREF].[CAMODULEID] AND @ENTITY IS NOT NULL
	WHERE ([dbo].[CAFEE].[NAME] LIKE '%'+ @SEARCH +'%' OR [dbo].[CAFEE].[DESCRIPTION] LIKE '%'+ @SEARCH +'%')
		  AND (@CACOMPUTATIONTYPEID = 0 OR [dbo].[CAFEE].[CACOMPUTATIONTYPEID] = @CACOMPUTATIONTYPEID)
		  AND (@ENTITY IS NULL OR [dbo].[CAMODULEFEEXREF].[CAMODULEID] IS NULL OR @ENTITY=[dbo].[CAENTITY].[CAENTITYID])
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber