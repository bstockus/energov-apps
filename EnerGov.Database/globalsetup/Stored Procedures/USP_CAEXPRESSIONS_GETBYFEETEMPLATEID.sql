CREATE PROCEDURE [globalsetup].[USP_CAEXPRESSIONS_GETBYFEETEMPLATEID]
	@CAFEETEMPLATEID CHAR(36)
AS
BEGIN
SET NOCOUNT ON
	
	;WITH CTE_CONDITIONGROUPS (CACONDITIONGROUPID) AS (
		SELECT	[dbo].[CAFEETEMPLATEFEE].[CACONDITIONGROUPID] 
		FROM	[dbo].[CAFEETEMPLATEFEE]
		WHERE	[dbo].[CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = @CAFEETEMPLATEID AND [dbo].[CAFEETEMPLATEFEE].[CACONDITIONGROUPID] IS NOT NULL
		UNION ALL
		SELECT	[dbo].[CAFEETEMPLATEDISCOUNT].[CACONDITIONGROUPID] 
		FROM	[dbo].[CAFEETEMPLATEDISCOUNT]
		WHERE	[dbo].[CAFEETEMPLATEDISCOUNT].[CAFEETEMPLATEID] = @CAFEETEMPLATEID AND [dbo].[CAFEETEMPLATEDISCOUNT].[CACONDITIONGROUPID] IS NOT NULL
		UNION ALL		
		SELECT
				CAST([INNER_CACONDITIONGROUP].[CACONDITIONGROUPID] AS CHAR(36))
		FROM	[dbo].[CACONDITIONGROUP] [INNER_CACONDITIONGROUP]
		INNER JOIN CTE_CONDITIONGROUPS [EXAMINED_CONDITIONGROUPS] ON [EXAMINED_CONDITIONGROUPS].[CACONDITIONGROUPID] = [INNER_CACONDITIONGROUP].[PARENTCONDITIONGROUPID]
	),
	CTE_EXPRESSIONS (CAEXPRESSIONID) AS (
		SELECT	[dbo].[CAFEETEMPLATEFEE].[CAEXPRESSIONID] 
		FROM	[dbo].[CAFEETEMPLATEFEE]
		WHERE	[dbo].[CAFEETEMPLATEFEE].[CAFEETEMPLATEID] = @CAFEETEMPLATEID AND [dbo].[CAFEETEMPLATEFEE].[CAEXPRESSIONID] IS NOT NULL
		UNION ALL
		SELECT	[dbo].[CAFEETEMPLATEDISCOUNT].[CAEXPRESSIONID] 
		FROM	[dbo].[CAFEETEMPLATEDISCOUNT]
		WHERE	[dbo].[CAFEETEMPLATEDISCOUNT].[CAFEETEMPLATEID] = @CAFEETEMPLATEID AND [dbo].[CAFEETEMPLATEDISCOUNT].[CAEXPRESSIONID] IS NOT NULL
		UNION ALL
		SELECT	[dbo].[CACONDITION].[COMPAREEXPRESSIONID]
		FROM	[CTE_CONDITIONGROUPS]
		INNER JOIN [dbo].[CACONDITION] ON [dbo].[CACONDITION].[CACONDITIONGROUPID] = [CTE_CONDITIONGROUPS].[CACONDITIONGROUPID]
		UNION ALL
		SELECT
			CAST([INNER_EXPRESSION].CAEXPRESSIONID AS CHAR(36))
		FROM [dbo].CAEXPRESSION [INNER_EXPRESSION]
			INNER JOIN [CTE_EXPRESSIONS] [EXAMINED_EXPRESSIONS] ON [EXAMINED_EXPRESSIONS].[CAEXPRESSIONID] = [INNER_EXPRESSION].[PARENTEXPRESSIONID]
	)

	SELECT	[dbo].[CAEXPRESSION].[CAEXPRESSIONID],
			[dbo].[CAEXPRESSION].[CAEXPRESSIONTYPE],
			[dbo].[CAEXPRESSION].[RETURNTYPE],
			[dbo].[CAEXPRESSION].[CONSTANTVALUE],
			[dbo].[CAEXPRESSION].[VARIABLETYPEID],
			[dbo].[CAEXPRESSION].[VARIABLEINPUTNAME],
			[dbo].[CAEXPRESSION].[VARIABLEINPUTID],
			[dbo].[CAEXPRESSION].[PARENTEXPRESSIONID],
			[dbo].[CAEXPRESSION].[PARAMETERINDEX] 
	FROM	[CTE_EXPRESSIONS]
	INNER JOIN [dbo].CAEXPRESSION ON [dbo].[CAEXPRESSION].[CAEXPRESSIONID] = [CTE_EXPRESSIONS].[CAEXPRESSIONID]
	ORDER BY [dbo].[CAEXPRESSION].[PARAMETERINDEX]
END