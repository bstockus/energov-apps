CREATE PROCEDURE [globalsetup].[USP_CUSTOMFIELDTABLE_COLUMN_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
BEGIN
	SET NOCOUNT ON;
	WITH RAW_DATA AS (
		SELECT 
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[CUSTOMFIELDCOLUMNTEMPLATEID],
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME],
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[SDESCRIPTION],
			[dbo].[CUSTOMFIELD].[SFIELDNAME] AS CUSTOMFIELDNAME,
			[dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[FKCUSTOMFIELDLAYOUTCONTROLTYPE] AS CUSTOMFIELDCONTROLTYPE,
			[dbo].[CUSTOMFIELD].[FKICUSTOMFIELDTYPE] AS CUSTOMFIELDTYPE,
			CASE @IS_ASCENDING WHEN 1 THEN
				ROW_NUMBER() OVER(ORDER BY [dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME])
			ELSE
				ROW_NUMBER() OVER(ORDER BY [dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME] DESC)
			END AS RowNumber,
			COUNT(1) OVER() AS TotalRows
		    FROM [dbo].[CUSTOMFIELDCOLUMNTEMPLATE]
		INNER JOIN [dbo].[CUSTOMFIELD] ON [dbo].[CUSTOMFIELD].[GCUSTOMFIELD] = [dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[GCUSTOMFIELD]
		WHERE [dbo].[CUSTOMFIELDCOLUMNTEMPLATE].[SCOLUMNNAME] LIKE '%'+ @SEARCH +'%'
	)

	SELECT * 
	FROM RAW_DATA
	WHERE
		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
		RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
	ORDER BY 
		RowNumber

END