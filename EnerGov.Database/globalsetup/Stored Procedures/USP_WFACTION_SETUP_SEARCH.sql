CREATE PROCEDURE [globalsetup].[USP_WFACTION_SETUP_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
BEGIN
	WITH RAW_DATA AS (
		SELECT
			[dbo].[WFACTION].[WFACTIONID],
			[dbo].[WFACTION].[NAME],			
			[dbo].[WFACTION].[DESCRIPTION],
			[dbo].[WFACTIONTYPE].[NAME] as ACTIONTYPENAME,
			[dbo].[WORKFLOWCOMPLETETYPE].[NAME] as COMPLETETYPENAME,
			CASE @IS_ASCENDING WHEN 1 THEN
				ROW_NUMBER() OVER(ORDER BY [dbo].[WFACTION].[NAME])
			ELSE
				ROW_NUMBER() OVER(ORDER BY [dbo].[WFACTION].[NAME] DESC)
			END AS RowNumber,
			COUNT(1) OVER() AS TotalRows
		FROM [dbo].[WFACTION]
		INNER JOIN [dbo].[WFACTIONTYPE] on [dbo].[WFACTIONTYPE].[WFACTIONTYPEID] = [dbo].[WFACTION].[WFACTIONTYPEID]
		LEFT JOIN [dbo].[WORKFLOWCOMPLETETYPE] on [dbo].[WORKFLOWCOMPLETETYPE].[WORKFLOWCOMPLETETYPEID] = [dbo].[WFACTION].[WORKFLOWCOMPLETETYPEID]
		WHERE ([dbo].[WFACTION].[NAME] LIKE '%'+ @SEARCH +'%' or [dbo].[WFACTION].[DESCRIPTION] LIKE '%'+ @SEARCH +'%')
	)
	
	SELECT * 
	FROM RAW_DATA
	WHERE
		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
		RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
	ORDER BY 
		RowNumber

END