CREATE PROCEDURE [globalsetup].[USP_CUSTOMFIELD_IMPORT_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@CUSTOMFIELDLAYOUTCONTROLTYPE AS INT,
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
SET NOCOUNT ON;
WITH RAW_DATA AS (
	SELECT 
		GROUPED_DATA.CUSTOMFIELDID, 
		GROUPED_DATA.SCUSTOMFIELD,
		[dbo].[CUSTOMFIELD].[SFIELDNAME],
		CASE @IS_ASCENDING WHEN 1 THEN
			ROW_NUMBER() OVER(ORDER BY [GROUPED_DATA].[SCUSTOMFIELD])
		ELSE
			ROW_NUMBER() OVER(ORDER BY [GROUPED_DATA].[SCUSTOMFIELD] DESC)
		END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM
		(SELECT 
			MAX([dbo].[CUSTOMFIELD].[GCUSTOMFIELD]) AS CUSTOMFIELDID,  -- DEAL WITH DUPLICATES ACROSS MODULES
			[dbo].[CUSTOMFIELD].[SCUSTOMFIELD]
		FROM [dbo].[CUSTOMFIELD]
		INNER JOIN [dbo].[CUSTOMFIELDOBJECT] ON [dbo].[CUSTOMFIELDOBJECT].[GCUSTOMFIELD] = [dbo].[CUSTOMFIELD].[GCUSTOMFIELD]
		WHERE
			[dbo].[CUSTOMFIELDOBJECT].[FKCUSTOMFIELDLAYOUTCONTROLTYPE] = @CUSTOMFIELDLAYOUTCONTROLTYPE AND 
			([dbo].[CUSTOMFIELDOBJECT].[BFOOTER] = 0 OR [dbo].[CUSTOMFIELDOBJECT].[BFOOTER] IS NULL) AND
			[dbo].[CUSTOMFIELD].[SCUSTOMFIELD] LIKE '%'+ @SEARCH +'%'
		GROUP BY [dbo].[CUSTOMFIELD].[SCUSTOMFIELD]) AS GROUPED_DATA
		INNER JOIN [dbo].[CUSTOMFIELD] ON [dbo].[CUSTOMFIELD].[GCUSTOMFIELD] = GROUPED_DATA.CUSTOMFIELDID
)

SELECT * 
FROM RAW_DATA
WHERE
	RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
	RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY 
	RowNumber