CREATE PROCEDURE [globalsetup].[USP_SERVICETASKHISTORY_SEARCH_BYCRITERIA]
(
	@SEARCH			AS NVARCHAR(MAX)	=	'',
	@PAGE_NUMBER	AS INT				=	1,
	@PAGE_SIZE		AS INT				=	10,
	@IS_ASCENDING	AS BIT				=	1,
	@SERVICETASKID	INT,
	@STARTDATETIME	DATE				= NULL,
	@ENDDATETIME	DATE				= NULL,
	@RESULTFILTER AS [dbo].[RecordNames] READONLY
)
AS
WITH RAW_DATA AS (	
	SELECT 		
		[dbo].[SERVICETASKHISTORY].[SERVICETASKHISTORYID],		
		[dbo].[SERVICETASKHISTORY].[STARTTIME],
		[dbo].[SERVICETASKHISTORY].[ENDTIME],
		[dbo].[SERVICETASKHISTORY].[AREDETAILSMONITORED],
		[dbo].[SERVICETASKHISTORY].[RESULT],
		[dbo].[SERVICETASKHISTORY].[ERROR],
		CASE @IS_ASCENDING WHEN 1 THEN	
		ROW_NUMBER() OVER(ORDER BY [dbo].[SERVICETASKHISTORY].[ENDTIME])
	   ELSE 
	    ROW_NUMBER() OVER(ORDER BY [dbo].[SERVICETASKHISTORY].[ENDTIME] DESC)
	   END AS RowNumber,
		COUNT(1) OVER() AS TotalRows
	FROM 
		[dbo].[SERVICETASKHISTORY] 
	WHERE 
		[dbo].[SERVICETASKHISTORY].[SERVICETASKID] = @SERVICETASKID  
	AND 
		(@STARTDATETIME IS NULL OR [dbo].[SERVICETASKHISTORY].[STARTTIME] >= @STARTDATETIME )
	AND 
		(@ENDDATETIME IS NULL OR [dbo].[SERVICETASKHISTORY].[STARTTIME] < @ENDDATETIME)
	AND
		((SELECT COUNT(1) FROM @RESULTFILTER) = 0 OR COALESCE([SERVICETASKHISTORY].[RESULT],'') IN (SELECT Name FROM @RESULTFILTER))
)

SELECT		* 
FROM		RAW_DATA
WHERE		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) 
			AND RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
ORDER BY	RowNumber