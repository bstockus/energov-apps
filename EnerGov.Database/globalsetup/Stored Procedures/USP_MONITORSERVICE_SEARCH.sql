CREATE PROCEDURE [globalsetup].[USP_MONITORSERVICE_SEARCH]
(
	@SEARCH AS NVARCHAR(MAX) = '',
	@PAGE_NUMBER AS INT = 1,
	@PAGE_SIZE AS INT = 10,
	@IS_ASCENDING AS BIT = 1
)
AS
BEGIN
WITH RAW_DATA AS (

SELECT [dbo].[SERVICETASK].[SERVICETASKID],
	   [dbo].[SERVICETASK].[NAME],
	  CASE [dbo].[SERVICETASK].[SERVICETASKID] 
		  WHEN 20 -- Send Emails
		  THEN (SELECT Count(*) FROM [dbo].[EMAILQUEUE] WHERE [dbo].[EMAILQUEUE].[DATESENT] IS NULL)
		  WHEN 4  -- Workflow Post Processing
		  THEN (SELECT COUNT(1) FROM [dbo].[WORKFLOWPOSTPROCESS] WHERE [dbo].[WORKFLOWPOSTPROCESS].[PROCESSEDDATE] IS NULL AND [dbo].[WORKFLOWPOSTPROCESS].[RESULTLOG] IS NULL AND [dbo].[WORKFLOWPOSTPROCESS].[WORKFLOWPOSTPROCESSTYPEID] IS NOT NULL )
		  WHEN 5  -- Workflow Action Post Processing
		  THEN  (SELECT COUNT(1) FROM [dbo].[WORKFLOWACTIONPOSTPROCESS] WHERE [dbo].[WORKFLOWACTIONPOSTPROCESS].[PROCESSEDDATE] IS NULL)
	  END as QUEUELENGTH,
	  SERVICETASKHISTORY.ENDTIME as LASTRUN,
	  SERVICETASKHISTORY.RESULT as LASTRUNRESULT,
	   CASE @IS_ASCENDING WHEN 1 THEN	
		ROW_NUMBER() OVER(ORDER BY [dbo].[SERVICETASK].[NAME])
	   ELSE 
	    ROW_NUMBER() OVER(ORDER BY [dbo].[SERVICETASK].[NAME] DESC)
	   END AS RowNumber,
	   COUNT(1) OVER() AS TotalRows
	FROM [dbo].[SERVICETASK]
	OUTER APPLY (SELECT TOP 1 [dbo].[SERVICETASKHISTORY].[ENDTIME],[dbo].[SERVICETASKHISTORY].[RESULT] FROM [dbo].[SERVICETASKHISTORY] WHERE [dbo].[SERVICETASKHISTORY].[SERVICETASKID]=[dbo].[SERVICETASK].[SERVICETASKID]  ORDER BY [dbo].[SERVICETASKHISTORY].[ENDTIME] DESC) AS SERVICETASKHISTORY
	WHERE [dbo].[SERVICETASK].[ISMONITORINGSUPPORTED]=1 AND [dbo].[SERVICETASK].[NAME] LIKE '%'+@SEARCH+'%' )

SELECT * 
	FROM RAW_DATA
	WHERE
		RowNumber > @PAGE_SIZE * (@PAGE_NUMBER - 1) AND 
		RowNumber <= @PAGE_SIZE * @PAGE_NUMBER
	ORDER BY 
		RowNumber
END