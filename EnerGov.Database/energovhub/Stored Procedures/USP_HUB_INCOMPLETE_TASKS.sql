CREATE PROCEDURE [energovhub].[USP_HUB_INCOMPLETE_TASKS]
	@USERID CHAR(36)
AS
BEGIN

	SELECT
	[dbo].[TASK].[TASKID],
	[dbo].[TASKTYPE].[NAME] AS TASKTYPENAME,
	[dbo].[TASK].[SUBJECT],
	[dbo].[TASK].TASKTEXT,
	[dbo].[TASK].STARTDATE,
	[dbo].[TASK].DUEDATE,
	[dbo].[TASK].COMPLETEDDATE,
	[dbo].[TASKSTATUS].TASKSTATUSNAME,
	[dbo].[TASK].FORMID,
	[dbo].[TASK].UNIQUERECORDID AS RECORDID,
	(ISNULL([dbo].[USERS].FNAME, '') + ' ' + ISNULL([dbo].[USERS].LNAME, '')) AS CREATEDBYUSER,
	ASSIGNEDUSERS.ASSIGNEDUSERNAME,
	CASENUMBERS.CASEID,
	CASENUMBERS.CASENUMBER,
	[dbo].[TASKPRIORITY].TASKPRIORITYNAME,
	[dbo].[TASK].PERCENTCOMPLETE,
	[dbo].[SYSTEMMODULE].MODULENAME
	FROM [dbo].[TASK]
	JOIN [dbo].[USERS] ON [dbo].[TASK].[CREATEDBYID] = [dbo].[USERS].SUSERGUID
	JOIN [dbo].[TASKSTATUS] ON [dbo].[TASK].TASKSTATUSID = [dbo].[TASKSTATUS].TASKSTATUSID
	JOIN [dbo].[TASKTYPE] ON [dbo].[TASK].TASKTYPEID = [dbo].[TASKTYPE].TASKTYPEID
	LEFT JOIN [dbo].[FORMS] ON [dbo].[TASK].FORMID = [dbo].[FORMS].SFORMSGUID
    LEFT JOIN [dbo].[SYSTEMMODULE] ON [dbo].[FORMS].MODULE_ID = [dbo].[SYSTEMMODULE].SYSTEMMODULEID
	JOIN [dbo].[TASKPRIORITY] ON [dbo].[TASK].TASKPRIORITYID = [dbo].[TASKPRIORITY].TASKPRIORITYID
	JOIN (
			SELECT  TASKID
				   ,STUFF((SELECT ', ' + (ISNULL([dbo].[USERS].FNAME, '') + ' ' + ISNULL([dbo].[USERS].LNAME, ''))
					 FROM [dbo].[TASKUSER]
					 JOIN [dbo].[USERS] ON USERID = [dbo].[USERS].SUSERGUID
					 where TASKID = T1.TASKID
					 FOR XML PATH(''), TYPE)
					.value('.','NVARCHAR(MAX)'),1,2,' ') ASSIGNEDUSERNAME
			FROM [dbo].[TASKUSER] T1 where T1.USERID = @USERID
			GROUP BY TASKID
		) ASSIGNEDUSERS ON [dbo].[TASK].TASKID = ASSIGNEDUSERS.TASKID
	OUTER APPLY (
		SELECT TOP 1 
			CASEID, CASENUMBER
		FROM [dbo].[GETCASENUMBERBYRECORD]([dbo].[TASK].UNIQUERECORDID)
		) AS CASENUMBERS
	WHERE [dbo].[TASKSTATUS].ISCOMPLETED = 0
	ORDER BY [dbo].[TASK].DUEDATE ASC
END