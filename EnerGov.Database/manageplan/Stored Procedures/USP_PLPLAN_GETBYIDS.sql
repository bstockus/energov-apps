CREATE PROCEDURE [manageplan].[USP_PLPLAN_GETBYIDS]
(
	@PLPLANLIST RecordIDs READONLY,
	@USERID AS CHAR(36) = NULL
)
AS
BEGIN	
	SELECT 
		[USERS].[FNAME],
		[USERS].[LNAME],
		[PLPLAN].[PLPLANID],
		[PLPLAN].[PLANNUMBER],
		[PLPLANSTATUS].[NAME] AS [PLANSTATUSNAME],
		[PLPLANTYPE].[PLANNAME],
		[PLPLANWORKCLASS].[NAME] AS [WORKCLASSNAME],
		[PLPLAN].[APPROVALEXPIREDATE],
		[DISTRICT].[NAME] AS [DISTRICTNAME],
		[PLPLAN].[EXPIREDATE],
		[PLPLAN].[SQUAREFEET],
		[PLPLAN].[VALUE],
		[PLPLAN].[COMPLETEDATE],
		[PLPLAN].[PLPLANSTATUSCHANGEDDATE],
		[PLPLAN].[DESCRIPTION] ,
		[PLPLAN].[APPLICATIONDATE],
		[PLPLAN].[ASSIGNEDTO],
		[PLPLAN].[ROWVERSION],
		[manageplan].[UFN_GET_PLAN_BALANCE_DUE](PLPLAN.PLPLANID) AS BALANCEDUE,
		(SELECT TOP 1 PRPROJECT.NAME 
		FROM PRPROJECTPLAN
		INNER JOIN PRPROJECT ON PRPROJECT.PRPROJECTID = PRPROJECTPLAN.PRPROJECTID
		WHERE PRPROJECTPLAN.PLPLANID = PLPLAN.PLPLANID) AS PROJECTNAME,
		(SELECT TOP 1 PARCEL.PARCELNUMBER		
			FROM PLPLANPARCEL
			INNER JOIN PARCEL ON PARCEL.PARCELID = PLPLANPARCEL.PARCELID
			WHERE PLPLANPARCEL.PLPLANID = PLPLAN.PLPLANID
			ORDER BY PLPLANPARCEL.MAIN DESC) AS PARCELNUMBER,
		[PLPLANTYPE].[PLPLANTYPEID],
		[PLPLANWORKCLASS].[PLPLANWORKCLASSID]
	FROM [dbo].[PLPLAN] 
INNER JOIN [dbo].[PLPLANTYPE] ON [PLPLAN].[PLPLANTYPEID] = [PLPLANTYPE].[PLPLANTYPEID]
INNER JOIN [dbo].[PLPLANWORKCLASS] ON [PLPLAN].[PLPLANWORKCLASSID] = [PLPLANWORKCLASS].[PLPLANWORKCLASSID]
INNER JOIN [dbo].[DISTRICT] ON [PLPLAN].[DISTRICTID] = [DISTRICT].[DISTRICTID]
LEFT OUTER JOIN [dbo].[USERS] ON [PLPLAN].[ASSIGNEDTO] = [USERS].[SUSERGUID]
INNER JOIN [dbo].[PLPLANSTATUS] ON [PLPLAN].[PLPLANSTATUSID] = [PLPLANSTATUS].[PLPLANSTATUSID]
INNER JOIN @PLPLANLIST [PLPLANLIST] ON [PLPLAN].[PLPLANID] = [PLPLANLIST].[RECORDID]
LEFT OUTER JOIN [dbo].[RECENTHISTORYPLAN] ON [RECENTHISTORYPLAN].[PLANID] = [PLPLAN].[PLPLANID] AND [RECENTHISTORYPLAN].[USERID] = @USERID
ORDER BY [RECENTHISTORYPLAN].[LOGGEDDATETIME] DESC
	
EXEC [common].[USP_PLPLANADDRESS_GETBYIDS] @PLPLANLIST
EXEC [manageplan].[USP_PLPLANCONTACT_GETBYIDS] @PLPLANLIST
END